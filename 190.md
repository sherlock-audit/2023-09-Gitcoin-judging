Low Corduroy Cow

medium

# `Allo.allocate()` missing the `percentFee`
## Summary

`Allo.allocate()` missing the `percentFee`, As a result, users can transfer funds to the strategy without paying `percentFee`.

## Vulnerability Detail

As we can see, if user call the `fundPool()` to allocate the fund, the user need to pay the `feeAmount` to the contract

```solidity
    function _fundPool(uint256 _amount, uint256 _poolId, IStrategy _strategy) internal {
        uint256 feeAmount;
        uint256 amountAfterFee = _amount;

        Pool storage pool = pools[_poolId];
        address _token = pool.token;

        if (percentFee > 0) {
            feeAmount = (_amount * percentFee) / getFeeDenominator();
            amountAfterFee -= feeAmount;

            _transferAmountFrom(_token, TransferData({from: msg.sender, to: treasury, amount: feeAmount}));
        }

        _transferAmountFrom(_token, TransferData({from: msg.sender, to: address(_strategy), amount: amountAfterFee}));
        _strategy.increasePoolAmount(amountAfterFee);

        emit PoolFunded(_poolId, amountAfterFee, feeAmount);
    }
```
According to the description, if function `batchAllocate()` is called, the user also needs to call function `fundPool()` to allocate funds, and the user also needs to pay fees. 
```solidity
    /// @notice Allocate to multiple pools
    /// @dev The encoded data will be specific to a given strategy requirements, reference the strategy
    ///      implementation of allocate(). Please note that this is not a 'payable' function, so if you
    ///      want to send funds to the strategy, you must send the funds using 'fundPool()'.
    /// @param _poolIds IDs of the pools
    /// @param _datas encoded data unique to the strategy for that pool
    function batchAllocate(uint256[] calldata _poolIds, bytes[] memory _datas) external nonReentrant {
        uint256 numPools = _poolIds.length;

        // Reverts if the length of _poolIds does not match the length of _datas with 'MISMATCH()' error
        if (numPools != _datas.length) revert MISMATCH();

        // Loop through the _poolIds & _datas and call the internal _allocate() function
        for (uint256 i; i < numPools;) {
            _allocate(_poolIds[i], _datas[i]);
            unchecked {
                ++i;
            }
        }
    }
```
However, through the allocate function, users can directly allocate funds to the strategy contract without paying fees.This will cause the protocol to lose fees
```solidity
    function allocate(uint256 _poolId, bytes memory _data) external payable nonReentrant {
        _allocate(_poolId, _data);
    }

    function _allocate(uint256 _poolId, bytes memory _data) internal {
        pools[_poolId].strategy.allocate{value: msg.value}(_data, msg.sender);
    }
```



## Impact

`Allo.allocate()` missing the `percentFee`, As a result, users can transfer funds to the strategy without paying `percentFee`.


## Code Snippet

https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/core/Allo.sol#L352

## Tool used

Manual Review

## Recommendation

Change to:
```solidity
    function allocate(uint256 _poolId, bytes memory _data) external payable nonReentrant {
        uint256 feeAmount;
        uint256 amountAfterFee = msg.value;
        if (percentFee > 0) {
            feeAmount = (msg.value * percentFee) / getFeeDenominator();
            amountAfterFee -= feeAmount;

            _transferAmountFrom(_token, TransferData({from: msg.sender, to: treasury, amount: feeAmount}));
        }
        pools[_poolId].strategy.allocate{value: amountAfterFee}(_data, msg.sender);

    }
```