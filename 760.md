Mini Garnet Squirrel

medium

# `useRegistryAnchor` is set as `true` in `RFPSimpleStrategy.sol` will brick the `_registerRecipient` function
## Summary
In the `RFPSimpleStrategy.sol` contract, if the `useRegistryAnchor` flag is initialized as `true`, the `_registerRecipient `function becomes unusable because the `recipientAddress` variable is not set, leading to a revert condition in the contract in the [line](https://github.com/sherlock-audit/2023-09-Gitcoin/blob/6430c8004017e96ae2f5aac365bdefd0b6eeea72/allo-v2/contracts/strategies/rfp-simple/RFPSimpleStrategy.sol#L362)

## Vulnerability Detail

The vulnerability arises from the contract's initialization with the `useRegistryAnchor` flag set to `true`. When this flag is enabled, the `_registerRecipient` function expects data that includes the recipient's address. However, since the `useRegistryAnchor` flag is true, the recipient's address is not provided when calling this function. This results in the recipientAddress variable not being set, leading to a revert condition in the following line:
```solidity
if (recipientAddress == address(0)) revert RECIPIENT_ERROR(recipientId);

```
As a result, the `_registerRecipient` function becomes unusable when the `useRegistryAnchor` flag is set to `true`.
## Impact
This vulnerability makes it impossible to register new recipients when the `RFPSimpleStrategy.sol` contract is initialized with `useRegistryAnchor` set to `true`. It effectively bricks the `_registerRecipient` function, preventing the addition of new recipients to the strategy.
## Code Snippet
```solidity
function _registerRecipient(bytes memory _data, address _sender)
        internal
        override
        onlyActivePool
        returns (address recipientId)
    {
        bool isUsingRegistryAnchor;
        address recipientAddress;
        address registryAnchor;
        uint256 proposalBid;
        Metadata memory metadata;

        // Decode '_data' depending on the 'useRegistryAnchor' flag
        if (useRegistryAnchor) {
            /// @custom:data when 'true' -> (address recipientId, uint256 proposalBid, Metadata metadata)
            (recipientId, proposalBid, metadata) = abi.decode(_data, (address, uint256, Metadata));

            // If the sender is not a profile member this will revert
            if (!_isProfileMember(recipientId, _sender)) revert UNAUTHORIZED();
        } else {
            //  @custom:data when 'false' -> (address recipientAddress, address registryAnchor, uint256 proposalBid, Metadata metadata)
            (recipientAddress, registryAnchor, proposalBid, metadata) =
                abi.decode(_data, (address, address, uint256, Metadata));

            // Check if the registry anchor is valid so we know whether to use it or not
            isUsingRegistryAnchor = registryAnchor != address(0);

            // Ternerary to set the recipient id based on whether or not we are using the 'registryAnchor' or '_sender'
            recipientId = isUsingRegistryAnchor ? registryAnchor : _sender;

            // Checks if the '_sender' is a member of the profile 'anchor' being used and reverts if not
            if (isUsingRegistryAnchor && !_isProfileMember(recipientId, _sender)) revert UNAUTHORIZED();
        }

        // Check if the metadata is required and if it is, check if it is valid, otherwise revert
        if (metadataRequired && (bytes(metadata.pointer).length == 0 || metadata.protocol == 0)) {
            revert INVALID_METADATA();
        }

        if (proposalBid > maxBid) {
            // If the proposal bid is greater than the max bid this will revert
            revert EXCEEDING_MAX_BID();
        } else if (proposalBid == 0) {
            // If the proposal bid is 0, set it to the max bid
            proposalBid = maxBid;
        }

        // If the recipient address is the zero address this will revert
        if (recipientAddress == address(0)) revert RECIPIENT_ERROR(recipientId);

        // Get the recipient
        Recipient storage recipient = _recipients[recipientId];

        if (recipient.recipientStatus == Status.None) {
            // If the recipient status is 'None' add the recipient to the '_recipientIds' array
            _recipientIds.push(recipientId);
            emit Registered(recipientId, _data, _sender);
        } else {
            emit UpdatedRegistration(recipientId, _data, _sender);
        }

        // update the recipients data
        recipient.recipientAddress = recipientAddress;
        recipient.useRegistryAnchor = isUsingRegistryAnchor ? true : recipient.useRegistryAnchor;
        recipient.proposalBid = proposalBid;
        recipient.recipientStatus = Status.Pending;
    }
```
## Tool used

Manual Review

## Recommendation
To fix this issue, you should provide the recipient's address when calling the `_registerRecipient `function, even when `useRegistryAnchor` is set to `true`. Alternatively, you can adjust the contract's logic to handle the case where `useRegistryAnchor` is true and recipient addresses are obtained from the registry.