Clumsy Pecan Jay

high

# Incorrect caching of previous credits in `_qv_allocate` result in amplified voting result

Quadratic voting base strategy `_qv_allocate` allows a voter to vote multiple times. 
On each call to `_qv_allocate` the previous credits plus new credits are used to calculate the added votes.
However - both the OLD and NEW credits are ADDED to the cache of credits which results in amplified previous credits when calculating the votes.

## Vulnerability Detail
https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/qv-base/QVBaseStrategy.sol#L506
```solidity
    function _qv_allocate(
        Allocator storage _allocator,
        Recipient storage _recipient,
        address _recipientId,
        uint256 _voiceCreditsToAllocate,
        address _sender
    ) internal onlyActiveAllocation {
----------
        // get the previous values
        uint256 creditsCastToRecipient = _allocator.voiceCreditsCastToRecipient[_recipientId];
        uint256 votesCastToRecipient = _allocator.votesCastToRecipient[_recipientId];

        // get the total credits and calculate the vote result
        uint256 totalCredits = _voiceCreditsToAllocate + creditsCastToRecipient;
        uint256 voteResult = _sqrt(totalCredits * 1e18);

        // update the values
        voteResult -= votesCastToRecipient;
        totalRecipientVotes += voteResult;
        _recipient.totalVotesReceived += voteResult;

        _allocator.voiceCreditsCastToRecipient[_recipientId] += totalCredits;  
        _allocator.votesCastToRecipient[_recipientId] += voteResult;
----------

    }
```

The above code takes two previous (cached) values:
* creditsCastToRecipient - previous credits used by the allocator for the recipient
* votesCastToRecipient - previous votes the allocator generated for the recipient

The the *NEW* credits along with the *CACHED* credits are stored in `totalCredit` and used to calculate the total votes 
```solidity
uint256 totalCredits = _voiceCreditsToAllocate + creditsCastToRecipient;
``` 
`totalCredit` is then used to calculate the new votes.

At the end of the function `totalCredit` is appended to the `voiceCreditsCastToRecipient` map to store it for the next allocation
```solidity
_allocator.voiceCreditsCastToRecipient[_recipientId] += totalCredits; 
```

The above snippet is incorrect since `voiceCreditsCastToRecipient` already holds the *CACHED* value.

Consider `voiceCreditsCastToRecipient` value is `x` and `_voiceCreditsToAllocate` is `y`:
1. `totalCredit = x + y`
2.  The caching is `x = x + totalCredit` = `x = x + x +y` - which is incorrect (should be `x = x + y`)

Lets take the following examples which should be identical:
1.`Allocator` calls `_qv_allocate` with `12` credits - this results in `_sqrt(12 * 1e18)` (3464101615) votes.
2. `Allocator` calls `_qv_allocate` `3` times with `4` credits (12/3)  - this results in `_sqrt(16 * 1e18)` (4000000000) votes

## Impact

The Incorrect voting mechanism is always applies for innocent allocators and malicious allocators.
This will result in increased votes which leads to increased payout (loss of funds)

## Code Snippet

I created the following POC with the above example.

Add the following test to `QVSimpleStrategy.t.sol`
```solidity
    function _sqrt(uint256 x) internal pure returns (uint256 y) {
        uint256 z = (x + 1) / 2;
        y = x;
        while (z < y) {
            y = z;
            z = (x / z + z) / 2;
        }
    }
    function test_allocateIncorrectCalculation() public virtual {
        // Setup recipient and allocator
        address recipientId = __register_accept_recipient();
        address allocator = randomAddress();
        vm.prank(pool_manager2());
        qvSimpleStrategy().addAllocator(allocator);

        // Fast-forward to after allocation starts
        vm.warp(allocationStartTime + 10);

        // Generate data for allocating 4 votes to recipient
        uint256 votes = 4;
        bytes memory allocateData = __generateAllocation(recipientId, votes);

        vm.startPrank(address(allo()));

        // Perform 3 allocations of 4 votes
        qvStrategy().allocate(allocateData, allocator);
        qvStrategy().allocate(allocateData, allocator);
        qvStrategy().allocate(allocateData, allocator);

        // Get recipient
        QVBaseStrategy.Recipient memory recipientAfterVotes = qvStrategy().getRecipient(recipientId);
        
        // Validate that the total votes recieved is not equal 3 allocations of votes (4)
        assertNotEq(recipientAfterVotes.totalVotesReceived, _sqrt(votes * 3 * 1e18));
        // Validate that the total votes received is equal to 4 allocates of votes (4)
        assertEq(recipientAfterVotes.totalVotesReceived, _sqrt(votes * 4 * 1e18));
    }
```

To execute the test run the command:
```solidity
forge test --match-test "test_allocateIncorrectCalculation" -v 
```

Expected output:
```solidity
Running 1 test for test/foundry/strategies/QVSimpleStrategy.t.sol:QVSimpleStrategyTest
[PASS] test_allocateIncorrectCalculation() (gas: 355987)
Test result: ok. 1 passed; 0 failed; 0 skipped; finished in 11.85ms
 
Ran 1 test suites: 1 tests passed, 0 failed, 0 skipped (1 total tests)
```
## Tool used

Manual Review

## Recommendation

in `_qv_allocate` swap:
```solidity
_allocator.voiceCreditsCastToRecipient[_recipientId] += totalCredits;
```
With:
```solidity
_allocator.voiceCreditsCastToRecipient[_recipientId] += _voiceCreditsToAllocate;
```