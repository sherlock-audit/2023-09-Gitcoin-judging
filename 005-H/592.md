Sneaky Tan Hippo

high

# Voice credits cast are incorrectly updated for recipients, allowing allocators to game voting to increase votes for their chosen recipient
## Summary

The logic of the QVBaseStrategy contract `_qv_allocate` function updates the total number of credits cast to a recipient each time a given allocator has allocated voice credits to them, to allow for the correct voting calculation (e.g. applying sqrt to the total credits cast). However, the update each time voice credits are cast to a recipient from the same allocator is not implemented correctly, and will allow allocators to game the logic to cast more votes for a given recipient then they should be allowed to.

## Vulnerability Detail

The `_qv_allocate` function in the QVBaseStrategy contract contains the logic which will actually update the total votes for a given recipient, based on the `_voiceCreditsToAllocate` amount from `_allocator` in this function call. This function is defined as follows: 
```solidity
function _qv_allocate(
    Allocator storage _allocator,
    Recipient storage _recipient,
    address _recipientId,
    uint256 _voiceCreditsToAllocate,
    address _sender
) internal onlyActiveAllocation {
    // check the `_voiceCreditsToAllocate` is > 0
    if (_voiceCreditsToAllocate == 0) revert INVALID();

    // get the previous values
    uint256 creditsCastToRecipient = _allocator.voiceCreditsCastToRecipient[_recipientId];
    uint256 votesCastToRecipient = _allocator.votesCastToRecipient[_recipientId];

    // get the total credits and calculate the vote result
    uint256 totalCredits = _voiceCreditsToAllocate + creditsCastToRecipient;
    uint256 voteResult = _sqrt(totalCredits * 1e18);

    // update the values
    voteResult -= votesCastToRecipient;
    totalRecipientVotes += voteResult;
    _recipient.totalVotesReceived += voteResult;

    _allocator.voiceCreditsCastToRecipient[_recipientId] += totalCredits; // @issue
    _allocator.votesCastToRecipient[_recipientId] += voteResult;

    // emit the event with the vote results
    emit Allocated(_recipientId, voteResult, _sender);
}
```

Notice the following line: `_allocator.voiceCreditsCastToRecipient[_recipientId] += totalCredits;`, where `totalCredits` is defined to be the sum of the previous total amount of credits given to this recipient + the current amount being given now. The issue is clear, that the addition of `totalCredits` is too much. Let's consider the case where an allocator gives 100 credits in the following ways: (1) 100 credits in one call, (2) 50 credits over two calls.

1. 100 credits/1 call: `_allocator.voiceCreditsCastToRecipient[_recipientId] = 100`
2. 50 credits/2 calls: `_allocator.voiceCreditsCastToRecipient[_recipientId] = 50 + 100 = 150`

In total there is an increase of 50% of the total credits for that recipient when you simply split the vote over two calls. This allows the allocator to game this voting in favor of their desired recipient.

## Impact

Allocators are able to game the logic in `_qv_allocate` to cast more votes for a given recipient then they should be allowed to.

## Code Snippet

Referenced lines of code:
https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/qv-base/QVBaseStrategy.sol#L506-L534

## Tool used

Manual Review

## Recommendation

Update the `_qv_allocate` function logic in the QVBaseStrategy contract to the following:
```solidity
- _allocator.voiceCreditsCastToRecipient[_recipientId] += totalCredits;
+ _allocator.voiceCreditsCastToRecipient[_recipientId] = totalCredits;
```