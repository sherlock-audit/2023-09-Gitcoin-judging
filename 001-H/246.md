Sneaky Amethyst Robin

high

# RFPSimpleStrategy recipients can frontrun distribution with a re-registration to increase proposalBid and receive more tokens

Missing authorization logic allows recipients to control the `poolActive` state of the RFPSimpleStrategy contract such they can frontrun distribution by re-registering to increase the payout they receive.

## Vulnerability Detail

`RFPSimpleStrategy.setPoolActive` lacks authorization logic, allowing anyone to set the `poolActive` flag. This allows for an already registered recipient to frontrun a pool manager call to `distribute` with a call to `registerRecipient`, increasing their `proposalBid` such that when the `distribute` call proceeds, they will receive a greater payout than the pool manager had previously approved. `_distribute` lacks validation that the recipient's status is still `Accepted`.

## Impact

Previously approved recipients can increase the amount of tokens they're due to receive immediately before receiving them, without pool manager approval.

## Code Snippet

https://github.com/allo-protocol/allo-v2/blob/851571c27df5c16f6586ece2a1cb6fd0acf04ec9/contracts/strategies/rfp-simple/RFPSimpleStrategy.sol#L219
```solidity
// @audit anyone can call this
function setPoolActive(bool _flag) external {
    _setPoolActive(_flag);
    emit PoolActive(_flag);
}
```

https://github.com/allo-protocol/allo-v2/blob/851571c27df5c16f6586ece2a1cb6fd0acf04ec9/contracts/strategies/rfp-simple/RFPSimpleStrategy.sol#L417
```solidity
// @audit lacks validation of recipient status
function _distribute(address[] memory, bytes memory, address _sender)
    internal
    virtual
    override
    onlyInactivePool
    onlyPoolManager(_sender)
{
    // check to make sure there is a pending milestone
    if (upcomingMilestone >= milestones.length) revert INVALID_MILESTONE();

    IAllo.Pool memory pool = allo.getPool(poolId);
    Milestone storage milestone = milestones[upcomingMilestone];
    Recipient memory recipient = _recipients[acceptedRecipientId];

    // @audit if only taking a percentage of the proposal bid, 
    //        why do we need to validate the full proposal bid is less than poolAmount?
    // make sure has enough funds to distribute based on the proposal bid
    if (recipient.proposalBid > poolAmount) revert NOT_ENOUGH_FUNDS();

    // Calculate the amount to be distributed for the milestone
    uint256 amount = (recipient.proposalBid * milestone.amountPercentage) / 1e18;

    // Get the pool, subtract the amount and transfer to the recipient
    poolAmount -= amount;
    _transferAmount(pool.token, recipient.recipientAddress, amount);

    // Set the milestone status to 'Accepted'
    milestone.milestoneStatus = Status.Accepted;

    // Increment the upcoming milestone
    upcomingMilestone++;

    // Emit events for the milestone and the distribution
    emit MilestoneStatusChanged(upcomingMilestone, Status.Accepted);
    emit Distributed(acceptedRecipientId, recipient.recipientAddress, amount, _sender);
}
```

## Tool used

Manual Review

## Recommendation

`setPoolActive` should be an `onlyPoolManager` protected function. Additionally, there should be validation that the recipient status is `Accepted` in `_distribute`.