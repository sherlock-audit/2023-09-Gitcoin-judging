Damaged Cornflower Turkey

medium

# Rejected milestones can still be paid out
There is a design flaw in `RFPSimpleStrategy`. There is nothing that checks if a milestone is rejected during the `distribute` function. This can lead to rejected milestones still being paid out.
## Vulnerability Detail
In `RFPSimpleStrategy.sol`, a pool manager can decide to reject a pending milestone submitted by the `acceptedRecipientId` as [commented](https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/rfp-simple/RFPSimpleStrategy.sol#L280) by the team:
```javascript
/// @notice Reject pending milestone submmited by the acceptedRecipientId.
```

But, there is nothing that checks if a milestone has been accepted or rejected when calling the `distribute` function in `RFPSimpleStrategy.sol`.  
```javascript
// no checks
    function _distribute(address[] memory, bytes memory, address _sender)
        internal
        virtual
        override
        onlyInactivePool
        onlyPoolManager(_sender)
    {
        // check to make sure there is a pending milestone
        if (upcomingMilestone >= milestones.length) revert INVALID_MILESTONE();

        IAllo.Pool memory pool = allo.getPool(poolId);
        Milestone storage milestone = milestones[upcomingMilestone];
        Recipient memory recipient = _recipients[acceptedRecipientId];

        // make sure has enough funds to distribute based on the proposal bid
        if (recipient.proposalBid > poolAmount) revert NOT_ENOUGH_FUNDS();

        // Calculate the amount to be distributed for the milestone
        uint256 amount = (recipient.proposalBid * milestone.amountPercentage) / 1e18;

        // Get the pool, subtract the amount and transfer to the recipient
        poolAmount -= amount;
        _transferAmount(pool.token, recipient.recipientAddress, amount);

        // Set the milestone status to 'Accepted'
        milestone.milestoneStatus = Status.Accepted;

        // Increment the upcoming milestone
        upcomingMilestone++;

        // Emit events for the milestone and the distribution
        emit MilestoneStatusChanged(upcomingMilestone, Status.Accepted);
        emit Distributed(acceptedRecipientId, recipient.recipientAddress, amount, _sender);
    }
```

This means that the poolManager can distribute the funds to the `acceptedRecipientId` even though the milestone has been rejected.

POC:
```javascript
source:test/foundry/strategies/RFPSimpleStrategy.t.sol

    // put this in test/foundry/strategies/RFPSimpleStrategy.t.sol
    // run using:
    // forge test --match-contract RFPSimpleStrategy --match-test test_distributeRejectedMilestone -vvvv
    function test_distributeRejectedMilestone() public {
        // Set milestones and submit upcoming milestones
        __register_setMilestones_allocate_submitUpcomingMilestone();

        // Reject upcoming milestone
        // Expect emit that changes status to rejected
        // Assert that upcoming milestone == rejected
        vm.expectEmit();
        emit MilestoneStatusChanged(0, IStrategy.Status.Rejected);
        vm.prank(pool_admin());
        strategy.rejectMilestone(0);
        RFPSimpleStrategy.Milestone memory milestone = strategy.getMilestone(0);
        assertEq(uint8(milestone.milestoneStatus), uint8(IStrategy.Status.Rejected));
        
        // Give the admin 1e19 native tokens 
        vm.deal(pool_admin(), 1e19);
        vm.prank(pool_admin());

        // Fund the pool with 1e19 native tokens
        allo().fundPool{value: 1e19}(poolId, 1e19);
        
        // Distribute the funds to the acceptedRecipientId while its a rejected milestone
        vm.prank(address(allo()));
        strategy.distribute(new address[](0), "", pool_admin());
}
```

## Impact
A rejected milestone should not be paid out. When a payout occurs for a rejected milestone, these funds will be lost forever.

## Code Snippet
https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/rfp-simple/RFPSimpleStrategy.sol#L283-L290

https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/rfp-simple/RFPSimpleStrategy.sol#L417-L450
## Tool used
Manual Review
## Recommendation
Add a check that reverts when the upcoming `milestone` has a rejected status.
```diff
source: contracts/strategies/rfp-simple/RFPSimpleStrategy.sol
L430:
+ if (milestone.milestoneStatus == Status.Rejected) revert();
```