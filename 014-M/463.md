Furry Cider Panda

medium

# In DonationVotingMerkleDistributionBaseStrategy._registerRecipient, the status returned by _getUintRecipientStatus(recipientId) is always the status of the first recipient

[[recipientToStatusIndexes](https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/donation-voting-merkle-base/DonationVotingMerkleDistributionBaseStrategy.sol#L194)](https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/donation-voting-merkle-base/DonationVotingMerkleDistributionBaseStrategy.sol#L194) is `mapping(address => uint256)`, which means `'recipientId' => 'statusIndex'`. **In solidity, uninitialized variables are all 0**. If `_recipientId` is a new id, then `recipientToStatusIndexes[_recipientId]` must be 0. Then `(rowIndex, colIndex, statusesBitMap[rowIndex])` returned by [[_getStatusRowColumn(newRecipientId)](https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/donation-voting-merkle-base/DonationVotingMerkleDistributionBaseStrategy.sol#L833-L840)](https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/donation-voting-merkle-base/DonationVotingMerkleDistributionBaseStrategy.sol#L833-L840) must be equal to `(0, 0, statusesBitMap[0])`. [[statusesBitMap](https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/donation-voting-merkle-base/DonationVotingMerkleDistributionBaseStrategy.sol#L190)](https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/donation-voting-merkle-base/DonationVotingMerkleDistributionBaseStrategy.sol#L190) is `mapping (uint256 => uint256)`, `statusesBitMap[0]` represents row 0, `statusesBitMap[1]` represents row 1, and so on. `uint256` represents the status of 64 ids, 4 bits for each status.

The current implementation of [[_registerRecipient](https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/donation-voting-merkle-base/DonationVotingMerkleDistributionBaseStrategy.sol#L580-L600)](https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/donation-voting-merkle-base/DonationVotingMerkleDistributionBaseStrategy.sol#L580-L600) will cause the following problems:

After the first recipientId is successfully registered, it occupies the status of `(row0, col0)`, whose value is modified to `Status.Pending`. Subsequent recipientIds will use the status of the first recipientId, which means that all recipientIds share the status of `(row0, col0)`.

## Vulnerability Detail

```solidity
File: contracts\strategies\donation-voting-merkle-base\DonationVotingMerkleDistributionBaseStrategy.sol
819:     function _getUintRecipientStatus(address _recipientId) internal view returns (uint8 status) {
820:         // Get the column index and current row
821:->       (, uint256 colIndex, uint256 currentRow) = _getStatusRowColumn(_recipientId);
822: 
823:         // Get the status from the 'currentRow' shifting by the 'colIndex'
824:         status = uint8((currentRow >> colIndex) & 15);
825: 
826:         // Return the status
827:         return status;
828:     }
......
833:     function _getStatusRowColumn(address _recipientId) internal view returns (uint256, uint256, uint256) {
834:->       uint256 recipientIndex = recipientToStatusIndexes[_recipientId];
835: 
836:         uint256 rowIndex = recipientIndex / 64; // 256 / 4
837:         uint256 colIndex = (recipientIndex % 64) * 4;
838: 
839:         return (rowIndex, colIndex, statusesBitMap[rowIndex]);
840:     }
```

L834, if `_recipientId` is a new id, then `recipientIndex` must be 0, which causes L839 to return `(0, 0, statusesBitMap[0])`.

L824, `status` is equal to the first 4 bit value of `statusesBitMap[0]`, which is the status of column 0.

Let's go back to the flow of the `_registerRecipient` function:

```solidity
File: contracts\strategies\donation-voting-merkle-base\DonationVotingMerkleDistributionBaseStrategy.sol
528:     function _registerRecipient(bytes memory _data, address _sender)
......   
580:->       uint8 currentStatus = _getUintRecipientStatus(recipientId);
581: 
582:         if (currentStatus == uint8(Status.None)) {
583:             // recipient registering new application
584:             recipientToStatusIndexes[recipientId] = recipientsCounter;	//@audit recipientsCounter starts from 0.
585:             _setRecipientStatus(recipientId, uint8(Status.Pending));
586: 
587:             bytes memory extendedData = abi.encode(_data, recipientsCounter);
588:             emit Registered(recipientId, extendedData, _sender);
589: 
590:             recipientsCounter++;
591:         } else {
592:             if (currentStatus == uint8(Status.Accepted)) {
593:                 // recipient updating accepted application
594:                 _setRecipientStatus(recipientId, uint8(Status.Pending));
595:             } else if (currentStatus == uint8(Status.Rejected)) {
596:                 // recipient updating rejected application
597:                 _setRecipientStatus(recipientId, uint8(Status.Appealed));
598:             }
599:             emit UpdatedRegistration(recipientId, _data, _sender, _getUintRecipientStatus(recipientId));
600:         }
601:     }
```

**Assume the first recipientId (A)**:

L580, `_getUintRecipientStatus(A)` returns the status of `(row0, col0)`, whose value is Status.None (0).

L583-590 block is executed. Since `recipientsCounter` has not been initialized (0), `recipientsToStatusIndexes[A] = recipientsCounter = 0`, and  `recipientsCounter++` is equal to 1. The status of `(row0, col0)` is the status of A, which is set to `Status.Pending` by [[_setRecipientStatus](https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/donation-voting-merkle-base/DonationVotingMerkleDistributionBaseStrategy.sol#L805-L814)](https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/donation-voting-merkle-base/DonationVotingMerkleDistributionBaseStrategy.sol#L805-L814).

**Assume the second recipientId (B)**:

L580, according to the previous analysis, the status returned is still the status of `(row0, col0)`, which is the status of A (equal to `Status.Pending`).

L592-599 block is executed.

Therefore, subsequent recipientIds execute the L592-599 block instead of the L583-590 block, which means that all recipientIds use the status of `(row0, col0)`.

## Impact

The recipientId that is actually registered is the first one, and the others are not registered successfully. Although other recipientIds can use the status of the first recipientId.

- Off-chain programs can only collect one `Registered` event and multiple `UpdatedRegistration` events. This makes the pool manager not know how to call [[reviewRecipients](https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/donation-voting-merkle-base/DonationVotingMerkleDistributionBaseStrategy.sol#L341)](https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/donation-voting-merkle-base/DonationVotingMerkleDistributionBaseStrategy.sol#L341) at all because the review process is done manually off-chain.
- If the status of the first recipient is set to `Status.Accepted`, the status of all recipients is also `Status.Accepted`.
- `_registerRecipient` function does not work as expected, which severely breaks the functionality of the DonationVotingMerkleDistributionBaseStrategy contract. `registerRecipient` is a very important phrase of the protocol.

## Code Snippet

https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/donation-voting-merkle-base/DonationVotingMerkleDistributionBaseStrategy.sol#L580

https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/donation-voting-merkle-base/DonationVotingMerkleDistributionBaseStrategy.sol#L833-L840

## Tool used

Manual Review

## Recommendation

It is recommended to initialize `recipientsCounter` to 1 in the constructor, 0 cannot be used. In this way, the status of `(row0,col0)` is always `Status.None` (0). The new recipientId will execute the L583-590 block.