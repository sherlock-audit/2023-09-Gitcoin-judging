Sneaky Amethyst Robin

high

# Can steal ETH from Allo via batchAllocate
## Summary

`Allo.batchAllocate` references `msg.value` as the amount to transfer to a pool in a loop within the same context, causing the actual amount transferred during the course of the transaction to be `msg.value` * num iterations.

## Vulnerability Detail

`Allo._allocate` calls a given pool's strategy's `allocate` function, providing the value as `msg.value` as transferred by the caller. The problem is that `Allo.batchAllocate` simply calls `_allocate` in a loop within the same context, resulting in the same, total `msg.value` to be provided for each individual allocation. 

An attacker can very easily drain Allo by creating a custom (or even cloneable) strategy which they can simply withdraw from and calling `batchAllocate` with many iterations to the same pool. All iterations except for the initial one will be paid for by ETH held by Allo.

## Impact

Attackers can drain Allo.sol of its native currency balance.

## Code Snippet

https://github.com/allo-protocol/allo-v2/blob/851571c27df5c16f6586ece2a1cb6fd0acf04ec9/contracts/core/Allo.sol#L362
```solidity
function batchAllocate(uint256[] calldata _poolIds, bytes[] memory _datas) external nonReentrant {
    uint256 numPools = _poolIds.length;

    // Reverts if the length of _poolIds does not match the length of _datas with 'MISMATCH()' error
    if (numPools != _datas.length) revert MISMATCH();

    // Loop through the _poolIds & _datas and call the internal _allocate() function
    for (uint256 i; i < numPools;) {
        _allocate(_poolIds[i], _datas[i]);
        unchecked {
            ++i;
        }
    }
}
```

https://github.com/allo-protocol/allo-v2/blob/851571c27df5c16f6586ece2a1cb6fd0acf04ec9/contracts/core/Allo.sol#L492
```solidity
function _allocate(uint256 _poolId, bytes memory _data) internal {
    pools[_poolId].strategy.allocate{value: msg.value}(_data, msg.sender);
}
```

## Tool used

Manual Review

## Recommendation

`msg.value` must not be reused within the same context. Instead `Allo._allocate` should pass the `value` as a parameter defined amount if it is called iteratively within the same message context.