Damaged Cornflower Turkey

high

# Allocator can allocate more voiceCredits than maxVoiceCreditsPerAllocator.
## Summary
`allocator.voiceCredits` gets never updated resulting in every allocator being able to vote more than `maxVoiceCreditsPerAllocator`.
## Vulnerability Detail
The specs [define](https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/qv-simple/README.md?plain=1#L47) `maxVoiceCreditsPerAllocator` as:
```solidity
Maximum voice credits that can be allocated by a single allocator.
```

This is supposed to be checked in `QVSimpleStrategy._allocate`:
```javascript
source:contracts/strategies/qv-simple/QVSimpleStrategy.sol

function _allocate(bytes memory _data, address _sender) internal virtual override {
	//.. omitted code
        if (!_hasVoiceCreditsLeft(voiceCreditsToAllocate, allocator.voiceCredits)) revert INVALID();
	//.. omitted code
    }
```

The problem is that `allocator.voiceCredits` is not updated after the voiceCredits have been casted so this will always be set to `0`.

This results in the `QVSimpleStrategy._hasVoiceCreditsLeft()` function to always pass if `voiceCreditsToAllocate + 0 <= maxVoiceCreditsPerAllocator`
```javascript
source:contracts/strategies/qv-simple/QVSimpleStrategy.sol

function _hasVoiceCreditsLeft(uint256 _voiceCreditsToAllocate, uint256 _allocatedVoiceCredits)
        internal
        view
        override
        returns (bool)
    {
        return _voiceCreditsToAllocate + _allocatedVoiceCredits <= maxVoiceCreditsPerAllocator;
    }
```

Here's a POC that showcases an allocator being able to allocate 100 voiceCredits for multiple times while the `maxVoiceCreditsPerAllocator` is set to 100.
```javascript
source: test/foundry/strategies/QVSimpleStrategy.t.sol

    // put this in test/foundry/strategies/QVSimpleStrategy.t.sol
    // run using:
    // forge test --match-contract QVSimpleStrategy --match-test test_alloMoreThanMax -vvvv
    function test_alloMoreThanMax() public {
        // Register a recipient
        address recipientId = __register_accept_recipient();

        // Create a random allocator address
        address allocator = randomAddress();

        // Add an allocator
        vm.prank(pool_manager2());
        qvSimpleStrategy().addAllocator(allocator);

        // Warp into the time window where allocation is possible
        vm.warp(allocationStartTime + 10);
        
        // Verify that maxVoiceCreditsPerAllocator == 100
        assertEq(qvSimpleStrategy().maxVoiceCreditsPerAllocator(), 100);

        // Create allocation data
        bytes memory allocateData = __generateAllocation(recipientId, 100);

        vm.startPrank(address(allo()));
        // Keep calling .allocate multiple times with 100 voiceCredits.
        // This should not be possible because maxVoiceCreditsPerAllocator == 100
        // However, all calls will succeed.
        qvSimpleStrategy().allocate(allocateData, allocator);
        qvSimpleStrategy().allocate(allocateData, allocator);
        qvSimpleStrategy().allocate(allocateData, allocator);
        vm.stopPrank();
    }
```

With the current implementation, consider the following scenario:
1. Participant and allocator work together.
2. An allocator can spam allocate votes to a participant.
3. Since the payout happens based on the amount of votes, the participant will end up stealing the majority of the funds, sharing it with the allocator.

## Impact
Allocator and participant can easily game the system to get a majority payout.

## Code Snippet
https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/qv-simple/QVSimpleStrategy.sol#L107-L124

https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/qv-base/QVBaseStrategy.sol#L506-L534
## Tool used
Manual Review
## Recommendation
Add the following line to `QVBaseStrategy._qv_allocate()`:
```diff
function _qv_allocate() {
// .. omitted code
L529	_allocator.voiceCreditsCastToRecipient[_recipientId] += totalCredits;
L530    _allocator.votesCastToRecipient[_recipientId] += voteResult;
+	    _allocator.voiceCredits += _voiceCreditsToAllocate;
// .. omitted code
}
```
This will ensure the allocator struct of the allocator is correctly updated after casting his voiceCredits.