Energetic Berry Llama

medium

# `RFPSimpleStrategy::setMilestones()` doesn't revert even if the milestones are already set
`RFPSimpleStrategy::setMilestones()` function should revert if the milestones already set but it doesn't revert, which might cause additional milestones to be set and total milestones percentage to be more than 100% 

## Vulnerability Detail
`RFPSimpleStrategy` distributes funds according to milestones. These milestones are set before the distribution and every milestone has an `amountPercentage`. The total `amountPercentage` of the milestones has to be 100%. The recipient proves that they accomplished necessary tasks for the milestone and get funds according to that milestone's `amountPercentage`. In the end, 100% of the funds will be distributed when all of the milestones are done.

The milestones can only be set once and `setMilestone` should revert if the milestones are already set. But it doesn't revert and lets the milestones be set again until the funds of the first milestone are distributed. Here is the code snippet:  
[https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/rfp-simple/RFPSimpleStrategy.sol#L228C1-L228C69](https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/rfp-simple/RFPSimpleStrategy.sol#L228C1-L228C69)

```solidity
    /// @notice Set the milestones for the acceptedRecipientId.
    /// @dev 'msg.sender' must be a pool manager to set milestones. Emits 'MilestonesSet' event
    /// @param _milestones Milestone[] The milestones to be set
    function setMilestones(Milestone[] memory _milestones) external onlyPoolManager(msg.sender) {
-->     if (upcomingMilestone != 0) revert MILESTONES_ALREADY_SET(); //@audit upcomingMilestone is not updated in this function. It is still 0. It will be updated in distribute

        uint256 totalAmountPercentage;

        // Loop through the milestones and add them to the milestones array
        uint256 milestonesLength = _milestones.length;
        for (uint256 i; i < milestonesLength;) {
            totalAmountPercentage += _milestones[i].amountPercentage;
            milestones.push(_milestones[i]);

            unchecked {
                i++;
            }
        }

        // Check if the all milestone amount percentage totals to 1e18(100%)
        if (totalAmountPercentage != 1e18) revert INVALID_MILESTONE();

        emit MilestonesSet();
    }
```

As you can see above, this check is the reason for the issue:  
`if (upcomingMilestone != 0) revert MILESTONES_ALREADY_SET()`

It checks if the `upcomingMilestone` is 0 or not. But the problem is `upcomingMilestone` is not updated when setting milestones. It will be 0 until the first milestone is distributed, and this function will not revert even when the milestones are already set.

## Coded PoC
I added a view function to make things easier for testing. It returns the `milestones` array, which is much simpler to work instead of working with tupples in the foundry. This is the additional view function:

```solidity
  //@audit add a view function to make test easier
  function getPublicMilestonesArray() external view returns (Milestone[] memory) {
      return milestones;
  }
```

The PoC is below. You can use the exact same setup of the protocol to test the PoC.  
\- Copy the test below and paste it into the `RFPSimpleStrategy.t.sol` test file.  
\- use `forge test --match-test test_setMilestoneAgainAndAgainBeforeDistribute`

```solidity
function test_setMilestoneAgainAndAgainBeforeDistribute() public {
        RFPSimpleStrategy.Milestone[] memory milestones = new RFPSimpleStrategy.Milestone[](2);
        RFPSimpleStrategy.Milestone memory milestone = RFPSimpleStrategy.Milestone({
            metadata: Metadata({protocol: 1, pointer: "metadata"}),
            amountPercentage: 7e17,
            milestoneStatus: IStrategy.Status.Pending
        });
        RFPSimpleStrategy.Milestone memory milestone2 = RFPSimpleStrategy.Milestone({
            metadata: Metadata({protocol: 1, pointer: "metadata"}),
            amountPercentage: 3e17,
            milestoneStatus: IStrategy.Status.Pending
        });

        // Create the milestones array.
        milestones[0] = milestone;
        milestones[1] = milestone2;

        // Set milestones for the first time.
        vm.prank(address(pool_admin()));
        vm.expectEmit();
        emit MilestonesSet();
        strategy.setMilestones(milestones);

        // Check the milestones array length --> This getter function is added just to make things easier.
        RFPSimpleStrategy.Milestone[] memory milestoneArray = strategy.getPublicMilestonesArray();
        
        // State variable milestone array is set with 2 milestones (70% and 30%)
        assertEq(milestoneArray.length, 2);

        // Change the milestones array. 
        milestones[0] = milestone2;
        milestones[1] = milestone;

        // Set milestones again. Normally it should revert because it is already set. But it won't revert.
        vm.prank(address(pool_admin()));
        vm.expectEmit();
        emit MilestonesSet();
        strategy.setMilestones(milestones);

        // Check the array length again
        RFPSimpleStrategy.Milestone[] memory milestoneArrayAfterSecondTime = strategy.getPublicMilestonesArray();
        
        // State variable milestone array is updated with 2 more milestones. 
        // It has 4 milestones now. (70% - 30% - 30% - 70%).
        // Total milestones percentage is 200% at the moment.
        assertEq(milestoneArrayAfterSecondTime.length, 4);
    }
```

Here is the test results below:

```solidity
Running 1 test for test/foundry/strategies/RFPSimpleStrategy.t.sol:RFPSimpleStrategyTest
[PASS] test_setMilestoneAgainAndAgainBeforeDistribute() (gas: 446981)
Test result: ok. 1 passed; 0 failed; 0 skipped; finished in 9.33ms
 
Ran 1 test suites: 1 tests passed, 0 failed, 0 skipped (1 total tests)
```

## Impact
Total milestone `amountPercentage` will be higher than 100%, and milestones array length will be greater than the expected array length. This will cause some other functions depending on `milestones.length` not revert when they should revert. (e.g. [`submitUpcomingMilestone()`](https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/rfp-simple/RFPSimpleStrategy.sol#L260), [`_distribute()`](https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/rfp-simple/RFPSimpleStrategy.sol#L425))

## Code Snippet
[https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/rfp-simple/RFPSimpleStrategy.sol#L228C1-L228C69](https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/rfp-simple/RFPSimpleStrategy.sol#L228C1-L228C69)

```solidity
    /// @notice Set the milestones for the acceptedRecipientId.
    /// @dev 'msg.sender' must be a pool manager to set milestones. Emits 'MilestonesSet' event
    /// @param _milestones Milestone[] The milestones to be set
    function setMilestones(Milestone[] memory _milestones) external onlyPoolManager(msg.sender) {
-->     if (upcomingMilestone != 0) revert MILESTONES_ALREADY_SET(); //@audit upcomingMilestone is not updated in this function. It is still 0. It will be updated in distribute

        uint256 totalAmountPercentage;

        // Loop through the milestones and add them to the milestones array
        uint256 milestonesLength = _milestones.length;
        for (uint256 i; i < milestonesLength;) {
            totalAmountPercentage += _milestones[i].amountPercentage;
            milestones.push(_milestones[i]);

            unchecked {
                i++;
            }
        }

        // Check if the all milestone amount percentage totals to 1e18(100%)
        if (totalAmountPercentage != 1e18) revert INVALID_MILESTONE();

        emit MilestonesSet();
    }
```

## Tool used

Manual Review

## Recommendation
Instead of using `if (upcomingMilestone != 0)` to check if the milestones already set, create a bool and use it for the check.
For example:
```solidity
    function setMilestones(Milestone[] memory _milestones) external onlyPoolManager(msg.sender) {
+      if (isMilestonesSet) revert MILESTONES_ALREADY_SET();

        // skipping this part for brevity 

+     isMilestonesSet = true  
      emit MilestonesSet();
    }
```