Silly Carob Opossum

medium

# Recipient registration always fails when RFPSimpleStrategy uses registry anchor

There is an incorrect implementation of the `_registerRecipient` function in `RFPSimpleStrategy` when it uses registry anchor.  It's impossible to register a recipient, if the strategy is initialized with `useRegistryAnchor = true`. Additionally, in this case `useRegistryAnchor` param of the recipient itself is always set to `false`, although seems that the opposite should always be `true`.

## Vulnerability Detail

Recipient registration flow depends on the strategy `useRegistryAnchor` param.

```solidity
bool isUsingRegistryAnchor;
address recipientAddress;
address registryAnchor;
uint256 proposalBid;
Metadata memory metadata;

// Decode '_data' depending on the 'useRegistryAnchor' flag
if (useRegistryAnchor) {
    /// @custom:data when 'true' -> (address recipientId, uint256 proposalBid, Metadata metadata)
    (recipientId, proposalBid, metadata) = abi.decode(_data, (address, uint256, Metadata));

    // If the sender is not a profile member this will revert
    if (!_isProfileMember(recipientId, _sender)) revert UNAUTHORIZED();
} else {
    //  @custom:data when 'false' -> (address recipientAddress, address registryAnchor, uint256 proposalBid, Metadata metadata)
    (recipientAddress, registryAnchor, proposalBid, metadata) =
        abi.decode(_data, (address, address, uint256, Metadata));

    // Check if the registry anchor is valid so we know whether to use it or not
    isUsingRegistryAnchor = registryAnchor != address(0);

    // Ternerary to set the recipient id based on whether or not we are using the 'registryAnchor' or '_sender'
    recipientId = isUsingRegistryAnchor ? registryAnchor : _sender;

    // Checks if the '_sender' is a member of the profile 'anchor' being used and reverts if not
    if (isUsingRegistryAnchor && !_isProfileMember(recipientId, _sender)) revert UNAUTHORIZED();
}
```

In case of using registry anchor `recipientAddress` isn't initialized and check that it isn't the zero address always fails.

```solidity
// If the recipient address is the zero address this will revert
if (recipientAddress == address(0)) revert RECIPIENT_ERROR(recipientId);
```

`isUsingRegistryAnchor`  is also not initialized, and `recipient.useRegistryAnchor` set to default value (`false`).

```solidity
recipient.useRegistryAnchor = isUsingRegistryAnchor ? true : recipient.useRegistryAnchor;
```

## Impact

1. It's impossible to register a recipient.
2. In the current implementation, there is no impact that `recipient.useRegistryAnchor` param is set to the wrong value because it's unused. But strategies that are inherited from `RFPSimpleStrategy` and have logic that depends on this param may encounter unexpected results.

## POC

Change `useRegistryAnchor` to `true` in `RFPSimpleStrategyTest` setup.

```solidity
function setUp() public {
    ...
    useRegistryAnchor = true;
    ...
}
```

Add this test, run with `forge test --mc RFPSimpleStrategyTest --mt testPOC -vv`.

```solidity
function testPOC() external {
    address sender = profile1_owner();
    address anchor = profile1_anchor();

    Metadata memory metadata = Metadata({protocol: 1, pointer: "metadata"});
    bytes memory data = abi.encode(anchor, maxBid, metadata);

    vm.prank(address(allo()));
    vm.expectRevert(abi.encodeWithSelector(RECIPIENT_ERROR.selector, anchor));
    address recipientId = strategy.registerRecipient(data, sender);
}
```

After that implement some logic of setting `recipientAddress` when `RFPSimpleStrategy` contract uses registry anchor.

For example:
```solidity
recipientAddress = _sender;
```
or:
```solidity
recipientAddress = recipientId;
```

Add second test, run with `forge test --mc RFPSimpleStrategyTest --mt testPOC2 -vv`.

```solidity
function testPOC2() external {
    address sender = profile1_owner();
    address anchor = profile1_anchor();

    Metadata memory metadata = Metadata({protocol: 1, pointer: "metadata"});
    bytes memory data = abi.encode(anchor, maxBid, metadata);

    vm.prank(address(allo()));
    address recipientId = strategy.registerRecipient(data, sender);

    RFPSimpleStrategy.Recipient memory _recipient = strategy.getRecipient(recipientId);
    assertEq(_recipient.useRegistryAnchor, false); // but true expected
}
```

## Code Snippet

https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/rfp-simple/RFPSimpleStrategy.sol#L307-L380

## Tool used

Manual Review

## Recommendation

1. Add tests that cover different strategy settings.
3. Implement `recipientAddress` setting when `RFPSimpleStrategy` contract uses registry anchor.
4. Fix `recipient.useRegistryAnchor` value.
```solidity
recipient.useRegistryAnchor = (useRegistryAnchor || isUsingRegistryAnchor) ? true : recipient.useRegistryAnchor;
```