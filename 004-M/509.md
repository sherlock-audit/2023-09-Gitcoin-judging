Sleepy Shadow Horse

high

# Allocation of Voice Credits Implementation is done Wrongly
Mistake in allocation of Voice Credits at [L529](https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/qv-base/QVBaseStrategy.sol#L529) will allow excessive allocation from consecutive allocations.
## Vulnerability Detail
```solidity
 function _qv_allocate(
        Allocator storage _allocator,
        Recipient storage _recipient,
        address _recipientId,
        uint256 _voiceCreditsToAllocate,
        address _sender
    ) internal onlyActiveAllocation {
        // check the `_voiceCreditsToAllocate` is > 0
        if (_voiceCreditsToAllocate == 0) revert INVALID();

        // get the previous values
        uint256 creditsCastToRecipient = _allocator.voiceCreditsCastToRecipient[_recipientId];
        uint256 votesCastToRecipient = _allocator.votesCastToRecipient[_recipientId];

        // get the total credits and calculate the vote result
        uint256 totalCredits = _voiceCreditsToAllocate + creditsCastToRecipient;
        uint256 voteResult = _sqrt(totalCredits * 1e18);

        // update the values
        voteResult -= votesCastToRecipient;
        totalRecipientVotes += voteResult;
        _recipient.totalVotesReceived += voteResult;

>>>>>    _allocator.voiceCreditsCastToRecipient[_recipientId] += totalCredits;
        _allocator.votesCastToRecipient[_recipientId] += voteResult;

        // emit the event with the vote results
        emit Allocated(_recipientId, voteResult, _sender);
    }

```
from the arrow in the code above the correct assignment to update recipient voice credits should be with the incoming "_voiceCreditsToAllocate" parameter. "totalCredits" was used, this would have been correct if "=" was used instead of "+=" as "totalCredits" from it first declaration shows that  it has been been updated already with the incoming "_voiceCreditsToAllocate" parameter i.e 
`uint256 totalCredits = _voiceCreditsToAllocate + creditsCastToRecipient;`
so using "+=" for the update can be simply broken down as 
`_allocator.voiceCreditsCastToRecipient[_recipientId] =  creditsCastToRecipient + _voiceCreditsToAllocate + creditsCastToRecipient;`
doubling the value of voiceCreditsCastToRecipient in a linear homogeneous recurrence way instead of a simple addition at each update i.e 
`y1, 2y1+y2, 4y1+2y2+y3, 8y1+4y2+2y3+y4, ...,(2n−1y1+2n−2y2+2n−3y3+...+yn​)`
"y" represents the _voice Credits allocation at each allocation.
Important to note from the documentation description of [allocation functionality](https://github.com/allo-protocol/allo-v2/blob/main/contracts/strategies/qv-base/README.md)
"* Voice credits are allocated based on a quadratic voting algorithm." 
the Voice credits update in the code is a linear homogeneous recurrence and not quadratic but sponsor has confirmed that quadratic in this context is simply the relationship between  the square and square root interaction between Voice credits and Votes.
## Impact
The intended functionality of the allocation of Voice Credits would be affected & the allocation of  voteResult to Recipient would also be affected in extension as it is calculated using the value of Voice Credits. Recipients that has been allocated in little fragments of Voice Credits would have excessive Voice Credits and voteResult due to this error.
## Code Snippet
https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/qv-base/QVBaseStrategy.sol#L529
## Tool used
Foundry,
Manual Review

## Recommendation
```solidity
 function _qv_allocate(
        Allocator storage _allocator,
        Recipient storage _recipient,
        address _recipientId,
        uint256 _voiceCreditsToAllocate,
        address _sender
    ) internal onlyActiveAllocation {
        // check the `_voiceCreditsToAllocate` is > 0
        if (_voiceCreditsToAllocate == 0) revert INVALID();

        // get the previous values
        uint256 creditsCastToRecipient = _allocator.voiceCreditsCastToRecipient[_recipientId];
        uint256 votesCastToRecipient = _allocator.votesCastToRecipient[_recipientId];

        // get the total credits and calculate the vote result
        uint256 totalCredits = _voiceCreditsToAllocate + creditsCastToRecipient;
        uint256 voteResult = _sqrt(totalCredits * 1e18);

        // update the values
        voteResult -= votesCastToRecipient;
        totalRecipientVotes += voteResult;
        _recipient.totalVotesReceived += voteResult;

+++  _allocator.voiceCreditsCastToRecipient[_recipientId] = totalCredits;
---   _allocator.voiceCreditsCastToRecipient[_recipientId] += totalCredits;
        _allocator.votesCastToRecipient[_recipientId] += voteResult;

        // emit the event with the vote results
        emit Allocated(_recipientId, voteResult, _sender);
    }

```
as seen above "=" should be used instead of "+=" , another way is `_allocator.voiceCreditsCastToRecipient[_recipientId] += _voiceCreditsToAllocate;`  
though more readable it would be less gas efficient.