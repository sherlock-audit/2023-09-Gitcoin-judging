Blunt Carmine Lynx

medium

# Every milestone can be rejected

In **RfpSimpleStrategy** there is a mechanism to `rejectMilestones` function in case the `acceptedRecipient` doesn’t get certain results, but now it can be used to reject every milestone, regardless of whether it was submitted via the `submitUpcommingMilestone` function.

## Vulnerability Detail

Let’s look at a practical example:

1. A pool is created and **poolManager** sets n count of milestones through `setMilestone` function, adding them to milestones array, for example, 3 milestones, identified by numbers: 0, 1, 2

```solidity
src: [allo-v2/contract/strategies/rfp-simple/RfpSimpleStrategy.sol](https://github.com/sherlock-audit/2023-09-Gitcoin/blob/6430c8004017e96ae2f5aac365bdefd0b6eeea72/allo-v2/contracts/strategies/rfp-simple/RFPSimpleStrategy.sol#L224-L247) [[click for link](https://github.com/sherlock-audit/2023-09-Gitcoin/blob/6430c8004017e96ae2f5aac365bdefd0b6eeea72/allo-v2/contracts/strategies/rfp-simple/RFPSimpleStrategy.sol#L224-L247)]

/// @notice Set the milestones for the acceptedRecipientId.
/// @dev 'msg.sender' must be a pool manager to set milestones. Emits 'MilestonesSet' event
/// @param _milestones Milestone[] The milestones to be set
function setMilestones(Milestone[] memory _milestones) external onlyPoolManager(msg.sender) {
    if (upcomingMilestone != 0) revert MILESTONES_ALREADY_SET();

    uint256 totalAmountPercentage;

    // Loop through the milestones and add them to the milestones array
    uint256 milestonesLength = _milestones.length;
    for (uint256 i; i < milestonesLength;) {
        totalAmountPercentage += _milestones[i].amountPercentage;
        milestones.push(_milestones[i]);

        unchecked {
            i++;
        }
    }

    // Check if the all milestone amount percentage totals to 1e18(100%)
    if (totalAmountPercentage != 1e18) revert INVALID_MILESTONE();

    emit MilestonesSet();
}
```

1. The recipient registers himself through `registerRecipient` function and gets accepted from **poolManager** as a user who is eligible for a reward in `_allocate` .

```solidity
src: [allo-v2/contract/strategies/rfp-simple/RfpSimpleStrategy.sol#L395](https://github.com/sherlock-audit/2023-09-Gitcoin/blob/6430c8004017e96ae2f5aac365bdefd0b6eeea72/allo-v2/contracts/strategies/rfp-simple/RFPSimpleStrategy.sol#L395) [[click for link](https://github.com/sherlock-audit/2023-09-Gitcoin/blob/6430c8004017e96ae2f5aac365bdefd0b6eeea72/allo-v2/contracts/strategies/rfp-simple/RFPSimpleStrategy.sol#L395)]
acceptedRecipientId = abi.decode(_data, (address));
```

1. **acceptedRecipient** submits a milestone through `submitUpcomingMilestone` which will change the status of the milestone 0 to **Pending**, but if the **poolManager** is not satisfied with the results related to that milestone he can call `rejectMilestones` before **acceptedRecipient** calls `submitUpcomingMilestone` and reject milestone 0, which at the moment won not be in a Pending state. (Also, the manager can reject other milestones that in the future may be considered pending by the **acceptedRecipient**, which is undesirable behavior.)

```solidity
src: [allo-v2/contract/strategies/rfp-simple/RfpSimpleStrategy.sol](https://github.com/sherlock-audit/2023-09-Gitcoin/blob/6430c8004017e96ae2f5aac365bdefd0b6eeea72/allo-v2/contracts/strategies/rfp-simple/RFPSimpleStrategy.sol#L280-L290) [[click for link](https://github.com/sherlock-audit/2023-09-Gitcoin/blob/6430c8004017e96ae2f5aac365bdefd0b6eeea72/allo-v2/contracts/strategies/rfp-simple/RFPSimpleStrategy.sol#L280-L290)]
/// @notice Reject pending milestone submmited by the acceptedRecipientId.
/// @dev 'msg.sender' must be a pool manager to reject a milestone. Emits a 'MilestoneStatusChanged()' event.
/// @param _milestoneId ID of the milestone
function rejectMilestone(uint256 _milestoneId) external onlyPoolManager(msg.sender) {
    // Check if the milestone is already accepted
    if (milestones[_milestoneId].milestoneStatus == Status.Accepted) revert MILESTONE_ALREADY_ACCEPTED();

    milestones[_milestoneId].milestoneStatus = Status.Rejected;

    emit MilestoneStatusChanged(_milestoneId, milestones[_milestoneId].milestoneStatus);
}
```

1. But as we can see there is no relationship between acceptedRecipient and the milestone allowing the **poolManager** to pass any arbitrary `_milestoneId`, except to accepted ones - even milestones that are not with status of **Pending**. 
The function is intended to reject only the upcomingMilestone and move to the next one, if any.

> *Note: **All milestones will be set to Status.None* initially (Sponsor confirmed)**
> 

## Impact

See the explanation in the Vulnerability Details section.

## Code Snippet

https://github.com/sherlock-audit/2023-09-Gitcoin/blob/6430c8004017e96ae2f5aac365bdefd0b6eeea72/allo-v2/contracts/strategies/rfp-simple/RFPSimpleStrategy.sol#L249-L271

https://github.com/sherlock-audit/2023-09-Gitcoin/blob/6430c8004017e96ae2f5aac365bdefd0b6eeea72/allo-v2/contracts/strategies/rfp-simple/RFPSimpleStrategy.sol#L280-L290

## Tool used

Manual Review

## Recommendation

```diff
File: [allo-v2/contract/strategies/rfp-simple/RfpSimpleStrategy.sol](https://github.com/sherlock-audit/2023-09-Gitcoin/blob/6430c8004017e96ae2f5aac365bdefd0b6eeea72/allo-v2/contracts/strategies/rfp-simple/RFPSimpleStrategy.sol#L280-L290) 

/// @notice Reject pending milestone submmited by the acceptedRecipientId.
/// @dev 'msg.sender' must be a pool manager to reject a milestone. Emits a 'MilestoneStatusChanged()' event.
/// @param _milestoneId ID of the milestone
function rejectMilestone(uint256 _milestoneId) external onlyPoolManager(msg.sender) {
    // Check if the milestone is already accepted
    if (milestones[_milestoneId].milestoneStatus == Status.Accepted) revert MILESTONE_ALREADY_ACCEPTED();

+   if (milestones[_milestoneId].milestoneStatus != Status.Pending) revert INVALID_MILESTONE();

    milestones[_milestoneId].milestoneStatus = Status.Rejected;
+   // Prevet the upcomingMilestone to stuck if check its status in _distribute
+   upcomingMilestone++;

    emit MilestoneStatusChanged(_milestoneId, milestones[_milestoneId].milestoneStatus);
}
```