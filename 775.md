Hot Zinc Hippo

high

# Allocation can be set invalid by incorrect calculation for quadratic voting strategies in QVBaseStrategy.
## Summary
In line 529 of `QVBaseStrategy`, operator `+=` is used mistakenly instead of operator `=`. As a result, the allocation can be calculated incorrectly.

## Vulnerability Detail

In function `_qv_allocate` of `QVBaseStrategy`, an allocator allocate more credit with amount `_voiceCreditsToAllocate` to a recipient with `_recipientId`.

```solidity
    /// @notice Allocate voice credits to a recipient
    /// @dev This can only be called during active allocation period
    /// @param _allocator The allocator details
    /// @param _recipient The recipient details
    /// @param _recipientId The ID of the recipient
    /// @param _voiceCreditsToAllocate The voice credits to allocate to the recipient
    /// @param _sender The sender of the transaction
    function _qv_allocate(
        Allocator storage _allocator,
        Recipient storage _recipient,
        address _recipientId,
        uint256 _voiceCreditsToAllocate,
        address _sender
    ) internal onlyActiveAllocation {
        // check the `_voiceCreditsToAllocate` is > 0
        if (_voiceCreditsToAllocate == 0) revert INVALID();

        // get the previous values
        uint256 creditsCastToRecipient = _allocator.voiceCreditsCastToRecipient[_recipientId];
        uint256 votesCastToRecipient = _allocator.votesCastToRecipient[_recipientId];

        // get the total credits and calculate the vote result
        uint256 totalCredits = _voiceCreditsToAllocate + creditsCastToRecipient;
        uint256 voteResult = _sqrt(totalCredits * 1e18);

        // update the values
        voteResult -= votesCastToRecipient;
        totalRecipientVotes += voteResult;
        _recipient.totalVotesReceived += voteResult;

        _allocator.voiceCreditsCastToRecipient[_recipientId] += totalCredits;
        _allocator.votesCastToRecipient[_recipientId] += voteResult;

        // emit the event with the vote results
        emit Allocated(_recipientId, voteResult, _sender);
    }
```

As you can see from the code `creditsCastToRecipient = _allocator.voiceCreditsCastToRecipient[_recipientId]` in line 517 and `totalCredits = _voiceCreditsToAllocate + creditsCastToRecipient` in line 521 and `_allocator.voiceCreditsCastToRecipient[_recipientId] += totalCredits` in line 529, the value `_allocator.voiceCreditsCastToRecipient[_recipientId]` will be increased by `_allocator.voiceCreditsCastToRecipient[_recipientId] + _voiceCreditsToAllocate` instead of `_voiceCreditsToAllocate`, due to the use of operator `+=` instead of operator `=` in line 529.

As a result, if the allocator calls function `_qv_allocate` again, the value `_allocator.votesCastToRecipient[_recipientId]` and `totalRecipienttVotes` can be set invalid and the funds can be distributed incorrectly.

**Example:**
Suppose that `_allocator.voiceCreditsCastToRecipient[_recipientId] = 100` and the allocator wants to allocate `50` more to the recipient.
Then `creditsCastToRecipient = 100` in line 517 and `totalCredits = 50 + 100 = 150` in line 521 and `_allocator.voiceCreditsCastToRecipient[_recipientId]` will be `100 + 150 = 250` instead of correct value `150` in line 529. So it will be inflated by `100`.
As a result, if the allocator calls function `_qv_allocate` again, the value `_allocator.votesCastToRecipient[_recipientId]` and `totalRecipienttVotes` are calculated incorrectly.

## Impact
Allocations to recipients can be set invalid and funds can be distributed incorrectly.

## Code Snippet
https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/qv-base/QVBaseStrategy.sol#L529

## Tool used

Manual Review

## Recommendation
Replace the operator **+=** with **=** in `QVBaseStrategy.sol`#L529. 
