Dandy Arctic Buffalo

medium

# Recipients may be excluded despite receiving enough "Accepted" reviews in quadratic voting allocation strategies
## Summary
Recipients should have "Accepted" status at the end of the active registration period if they have received a sufficient number (`reviewThreshold`) of reviews from pool managers accepting the recipient's bid. However, in the case that the recipient also receives at least `reviewThreshold` rejections, acceptance is decided by a race condition regardless of the ratio of "Accepted" to "Rejected" reviews received.

## Vulnerability Detail
The Allo `QVSimpleStrategy` quadratic voting strategy (and any other based on `QVBaseStrategy` in the future) has/have a time period for users to register their bids to become recipients of pool funds. These bids are reviewed by pool managers during this registration period to accept or reject them from consideration in later voting and distribution of the pool's funds. At the end of the registration time period, a recipient's status becomes final and cannot be changed.  

There is a logic error in `QVBaseStrategy.reviewRecipients()` that can result in an unexpected final recipient status. When a recipient receives `reviewThreshold` number of reviews with the same status, their status changes from "Pending" to this new status. If a recipient gets at least `reviewThreshold` reviews with "Accepted" and also receives at least `reviewThreshold` reviews with "Rejected", the final status for the recipient is that of the last review. In other words, the outcome is determined by a race condition: what was the last review received before the registration period ended? 

As an example, take a pool with `QVSimpleStrategy` and `reviewThreshold` of 3. Alice registers her bid in this pool to become a pool recipient and receives 8 "Accepted" reviews - plenty to be accepted. However, she also receives 3 "Rejected" reviews. If the last review received before the end of registration is "Rejected", Alice's bid is excluded despite receiving sufficient "Accepted" reviews and far more than the rejections received.

There is currently no workaround that a pool manager can use to change a recipient's status to rectify an error caused by this issue.

## Impact
Recipients registered in a quadratic voting pool can be excluded despite receiving enough "Accepted" reviews (even if the majority received are "Accepted"), unfairly depriving them of any chance of receiving the funds distributed by the pool.

## Code Snippet
The [`QVBaseStrategy.reviewRecipients()` function](https://github.com/allo-protocol/allo-v2/blob/851571c27df5c16f6586ece2a1cb6fd0acf04ec9/contracts/strategies/qv-base/QVBaseStrategy.sol#L249C1-L288C6) can update `recipient.recipientStatus` from "Accepted" to "Rejected":
```solidity
    /// @notice Review recipient(s) application(s)
    /// @dev You can review multiple recipients at once or just one. This can only be called by a pool manager and
    ///      only during active registration.
    /// @param _recipientIds Ids of the recipients
    /// @param _recipientStatuses Statuses of the recipients
    function reviewRecipients(address[] calldata _recipientIds, Status[] calldata _recipientStatuses)
        external
        virtual
        onlyPoolManager(msg.sender)
        onlyActiveRegistration
    {
        // make sure the arrays are the same length
        uint256 recipientLength = _recipientIds.length;
        if (recipientLength != _recipientStatuses.length) revert INVALID();

        for (uint256 i; i < recipientLength;) {
            Status recipientStatus = _recipientStatuses[i];
            address recipientId = _recipientIds[i];

            // if the status is none or appealed then revert
            if (recipientStatus == Status.None || recipientStatus == Status.Appealed) {
                revert RECIPIENT_ERROR(recipientId);
            }

            reviewsByStatus[recipientId][recipientStatus]++;

            if (reviewsByStatus[recipientId][recipientStatus] >= reviewThreshold) {
                Recipient storage recipient = recipients[recipientId];
                recipient.recipientStatus = recipientStatus;

                emit RecipientStatusUpdated(recipientId, recipientStatus, address(0));
            }

            emit Reviewed(recipientId, recipientStatus, msg.sender);

            unchecked {
                ++i;
            }
        }
    }
```

## Tool used

Manual Review

## Recommendation
Once a recipient is accepted, don't allow further reviews to modify their stored `recipientStatus`.

Note that there is nothing preventing pool managers from reviewing a recipient multiple times, inflating the review count. While managers are trusted not to act maliciously, this should not be allowed.