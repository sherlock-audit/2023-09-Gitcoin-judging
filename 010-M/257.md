Little Cloth Coyote

medium

# Multiple Calls to `setMilestones()` Allowed Before Completing First `upcomingMilestone`
Pool Manager can `setMilestones()` multiple times before the first `upcomingMilestone` is completed. 

## Vulnerability Detail
The intended design of RFPSimpleStrategy aims to enable the pool manager to set milestones only once, with no option to add more unless a new pool is deployed. However, the current implementation of `setMilestones()` allows the pool manager to keep adding milestones before the first `upcomingMilestones` is completed.

```solidity
    function setMilestones(Milestone[] memory _milestones) external onlyPoolManager(msg.sender) {
        if (upcomingMilestone != 0) revert MILESTONES_ALREADY_SET();

        uint256 totalAmountPercentage;

        // Loop through the milestones and add them to the milestones array
        uint256 milestonesLength = _milestones.length;
        for (uint256 i; i < milestonesLength;) {
            totalAmountPercentage += _milestones[i].amountPercentage;
            milestones.push(_milestones[i]);

            unchecked {
                i++;
            }
        }

        // Check if the all milestone amount percentage totals to 1e18(100%)
        if (totalAmountPercentage != 1e18) revert INVALID_MILESTONE();

        emit MilestonesSet();
    }
```
Safety checks can be circumvented as long as the `totalAmountPercent` sums up to 1e18. This problem may arise unintentionally, and the pool manager may not be aware of it.

Consider the following scenario:

1. The pool manager initially adds a set of milestones, with percentages totaling 100%.
2. A second set of milestones is added before first `upcomingMilestone` is completed, which also sums up to 100%.
3. However, the pool lacks the funds required to `distribute()` the rewards for the completed second set of milestones

## Impact
It allows the pool manager to repeatedly set milestones before the completion of the first upcoming milestone. This could lead to unexpected behavior and may result in situations where there are insufficient funds to distribute for the milestones that have been added but not yet completed. It can potentially disrupt the intended flow of milestone management.

## Code Snippet
https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/rfp-simple/RFPSimpleStrategy.sol#L227
## Tool used

Manual Review

## Recommendation
```solidity
    function setMilestones(Milestone[] memory _milestones) external onlyPoolManager(msg.sender) {
-       if (upcomingMilestone != 0) revert MILESTONES_ALREADY_SET();
+       if(milestones.length != 0) revert MILESTONES_ALREADY_SET();
        uint256 totalAmountPercentage;

        // Loop through the milestones and add them to the milestones array
        uint256 milestonesLength = _milestones.length;
        for (uint256 i; i < milestonesLength;) {
            totalAmountPercentage += _milestones[i].amountPercentage;
            milestones.push(_milestones[i]);

            unchecked {
                i++;
            }
        }

        // Check if the all milestone amount percentage totals to 1e18(100%)
        if (totalAmountPercentage != 1e18) revert INVALID_MILESTONE();

        emit MilestonesSet();
    }
```