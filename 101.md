Petite Wooden Elephant

high

# Users will lose funds when creating pools due to the wrong baseFee payment implementation
## Summary
Users are required to send more ether than necessary when creating a pool, resulting in extra ethers becoming trapped within the Allo contract. These extra funds are neither allocated to the strategy nor returned to the user, effectively locking them within the contract and preventing withdrawal. Such unintended fund locking poses a significant risk, potentially leading to user dissatisfaction and erosion of trust in the platform.

## Vulnerability Detail
In the case of `baseFee > 0`, `_createPool` will perform some checks:
```solidity
            if ((_token == NATIVE && (baseFee + _amount >= msg.value)) || (_token != NATIVE && baseFee >= msg.value)) {
                revert NOT_ENOUGH_FUNDS();
            }
```
It means:
  - When `_token == NATIVE` then `msg.value` must be greater than (not even equal to) `baseFee + _amount`
  - When `_token != NATIVE` then `msg.value` must be greater than (not even equal to) `baseFee`

So when `baseFee > 0`, the profile owner should always send some extra ethers when creating a pool. These extra ethers are neither sent to the strategy nor returned to the user, resulting in them getting trapped in the Allo contract.

Let's say a profile owner wants to create a pool with a custom strategy, `NATIVE` as token type and 0 as `_amount`. Since `baseFee` is set to 1 ether, the profile owner should send more ether than the sum of `baseFee` (1 ether) and `_amount` ( 0 ), So they call the `createPoolWithCustomStrategy` with `msg.value` set to 2 ethers. The `createPoolWithCustomStrategy` function calls the `_createPool` function. Inside `_createPool` 1 ether is deducted from `msg.value` to pay the `baseFee` to the treasury and then the pool is created with 0 as `_amount`.
Now, the situation arises where 1 extra ether is trapped within the Allo contract and cannot be withdrawn by the profile owner.

## Impact
User funds being unintentionally locked in the contract can result in user dissatisfaction and potential loss of trust in the platform.

## Proof of Concept
Above scenario is implemented within this test.
Add the following test function to: `test/foundry/core/Allo.t.sol`
Run: `forge test --match-test test_ExtraEthWhileCreatePool`
```solidity
    function test_ExtraEthWhileCreatePool() public {
        uint256 baseFee = 1 ether;

        allo().updateBaseFee(baseFee);

        uint256 strategyBalanceBefore = address(strategy).balance;
        uint256 alloBalanceBefore = address(allo()).balance;

        vm.deal(address(pool_admin()), 2 ether);

        // Pool admin calls the createPoolWithCustomStrategy with _amount set to 0, and 1 ether as msg.value
        // to pay the baseFee but the Transaction will revert due to a NOT_ENOUGH_FUNDS error.
        vm.prank(pool_admin());
        vm.expectRevert(NOT_ENOUGH_FUNDS.selector);
        allo().createPoolWithCustomStrategy{value: 1 ether}(
            poolProfile_id(), strategy, "0x", NATIVE, 0, metadata, pool_managers()
        );

        // Pool admin calls the createPoolWithCustomStrategy again with _amount set to 0, and 2 ethers as msg.value
        // to cover the baseFee payment
        vm.prank(pool_admin());
        allo().createPoolWithCustomStrategy{value: 2 ether}(
            poolProfile_id(), strategy, "0x", NATIVE, 0, metadata, pool_managers()
        );

        // The balance of the strategy is 0 because _amount was set to 0
        assertEq(address(strategy).balance, strategyBalanceBefore);

        // Extra ether will be trapped within Allo contract.
        assertEq(address(allo()).balance, alloBalanceBefore + 1 ether);

        // Pool admin has lost 1 ether 
        assertEq(address(pool_admin()).balance, 0);
    }
```
## Code Snippet
https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/core/Allo.sol#L473

```solidity
        if (baseFee > 0) {
            // To prevent paying the baseFee from the Allo contract's balance
            // If _token is NATIVE, then baseFee + _amount should be >= than msg.value.
            // If _token is not NATIVE, then baseFee should be >= than msg.value.
            if ((_token == NATIVE && (baseFee + _amount >= msg.value)) || (_token != NATIVE && baseFee >= msg.value)) {
                revert NOT_ENOUGH_FUNDS();
            }
            _transferAmount(NATIVE, treasury, baseFee);
            emit BaseFeePaid(poolId, baseFee);
        }
```
## Tool used

Foundry
VSCode

## Recommendation
There are 2 ways to solve this bug:

1. Let users send ether equal to the amount needed for `baseFee` payment, so there will be no need to send extra ethers:
```diff
diff --git a/Allo.sol.orig b/Allo.sol
index 45ae82c..07cd690 100644
--- a/Allo.sol.orig
+++ b/Allo.sol
@@ -470,7 +470,7 @@ contract Allo is IAllo, Native, Transfer, Initializable, Ownable, AccessControl,
             // To prevent paying the baseFee from the Allo contract's balance
             // If _token is NATIVE, then baseFee + _amount should be >= than msg.value.
             // If _token is not NATIVE, then baseFee should be >= than msg.value.
-            if ((_token == NATIVE && (baseFee + _amount >= msg.value)) || (_token != NATIVE && baseFee >= msg.value)) {
+            if ((_token == NATIVE && (baseFee + _amount > msg.value)) || (_token != NATIVE && baseFee > msg.value)) {
                 revert NOT_ENOUGH_FUNDS();
             }
             _transferAmount(NATIVE, treasury, baseFee);
```
2. Return the extra ethers received
```diff
diff --git a/Allo.sol.orig b/Allo.sol
index 45ae82c..afd8b15 100644
--- a/Allo.sol.orig
+++ b/Allo.sol
@@ -481,6 +481,19 @@ contract Allo is IAllo, Native, Transfer, Initializable, Ownable, AccessControl,
             _fundPool(_amount, poolId, _strategy);
         }
 
+       // Sending back the extra ether
+        uint256 extraETH = msg.value - baseFee;
+        if(_token != NATIVE){
+            if(extraETH != 0){
+                _transferAmount(NATIVE, msg.sender, extraETH);
+            }
+        }else{
+            extraETH -= _amount;
+            if(extraETH != 0){
+                _transferAmount(NATIVE, msg.sender, extraETH);
+            }
+        }
+
         emit PoolCreated(poolId, _profileId, _strategy, _token, _amount, _metadata);
     }
 
```