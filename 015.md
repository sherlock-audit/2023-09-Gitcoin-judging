Brief Silver Porcupine

high

# Vote inflation due to incorrect accounting
## Summary
The **voiceCreditsCastToRecipient** for allocators in [QVBaseStrategy.sol](https://github.com/allo-protocol/allo-v2/blob/8a41a342a0de7a2d5d7dbc5395d1da44cb811348/contracts/strategies/qv-base/QVBaseStrategy.sol#L506-L534) is updated incorrectly, which leads to incorrect delegation of voting power.

## Vulnerability Detail

The **_qv_allocate** function in **QVBaseStrategy.sol** allocates votes from allocator to a recipient using quadratic voting. It then updates the **voiceCreditsCastToRecipient** for the current allocator and should save the total credits that the allocator has used for the given recipient. The issue arises when a given allocator decides to allocate voting credits to the same recipient more than one time.

The function uses a local variable **totalCredits** to store the amount of credits allocated up until now plus the amount of credits that are going to be used for allocation.

```jsx
     uint256 totalCredits = _voiceCreditsToAllocate + creditsCastToRecipient;
```

After that these totalCredits should be saved in **voiceCreditsCastToRecipient**, but instead the code adds them on top of the already casted credits

```jsx
        _allocator.voiceCreditsCastToRecipient[_recipientId] += totalCredits;
```

The next time this allocator votes, he will have more voting power due to the incorrect **totalCredits**

```jsx
 uint256 voteResult = _sqrt(totalCredits * 1e18);
```
## Impact

Allocators can cast more votes that allowed.

## Code Snippet

```jsx
    function _qv_allocate(
        Allocator storage _allocator,
        Recipient storage _recipient,
        address _recipientId,
        uint256 _voiceCreditsToAllocate,
        address _sender
    ) internal onlyActiveAllocation {
        // check the `_voiceCreditsToAllocate` is > 0
        if (_voiceCreditsToAllocate == 0) revert INVALID();

        // get the previous values
        uint256 creditsCastToRecipient = _allocator.voiceCreditsCastToRecipient[_recipientId];
        uint256 votesCastToRecipient = _allocator.votesCastToRecipient[_recipientId];

        // get the total credits and calculate the vote result
        uint256 totalCredits = _voiceCreditsToAllocate + creditsCastToRecipient;
        uint256 voteResult = _sqrt(totalCredits * 1e18);

        // update the values
        voteResult -= votesCastToRecipient;
        totalRecipientVotes += voteResult;
        _recipient.totalVotesReceived += voteResult;

        _allocator.voiceCreditsCastToRecipient[_recipientId] += totalCredits;
        _allocator.votesCastToRecipient[_recipientId] += voteResult;

        // emit the event with the vote results
        emit Allocated(_recipientId, voteResult, _sender);
    }
```

## Tool used

Manual Review

## Recommendation

Instead of adding new credits, update the old value with the current one: 

```jsx
        _allocator.voiceCreditsCastToRecipient[_recipientId] = totalCredits;
```
