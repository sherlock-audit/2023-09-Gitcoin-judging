Little Cloth Coyote

medium

# Lack of mechanic to skip rejected milestone
RFPSimpleStrategy and RFPCommitteeStrategy lack the mechanic to skip rejected milestones. Based on current design, each milestone must be paid out in order to move on to the next.

## Vulnerability Detail
Per sponsor, only accepted milestone should be distributed.
However, there's an issue with the `upcomingMilestone` tracking. It's incremented only after the `_distribute()` function is called. As a result, regardless of the actual milestone status, funds must be distributed before moving on to the next milestone (`_distribute()` also does not check the status before executing). This can potentially disable the pool if the `distribute()` function is never called by the pool manager.

```solidity
    function _distribute(address[] memory, bytes memory, address _sender)
        internal
        virtual
        override
        onlyInactivePool
        onlyPoolManager(_sender)
    {
        // check to make sure there is a pending milestone
        if (upcomingMilestone >= milestones.length) revert INVALID_MILESTONE();

        IAllo.Pool memory pool = allo.getPool(poolId);
        Milestone storage milestone = milestones[upcomingMilestone];
        Recipient memory recipient = _recipients[acceptedRecipientId];

        // make sure has enough funds to distribute based on the proposal bid
        if (recipient.proposalBid > poolAmount) revert NOT_ENOUGH_FUNDS();

        // Calculate the amount to be distributed for the milestone
        uint256 amount = (recipient.proposalBid * milestone.amountPercentage) / 1e18;

        // Get the pool, subtract the amount and transfer to the recipient
        poolAmount -= amount;
        _transferAmount(pool.token, recipient.recipientAddress, amount);

        // Set the milestone status to 'Accepted'
        milestone.milestoneStatus = Status.Accepted;

        // Increment the upcoming milestone
        upcomingMilestone++;

        // Emit events for the milestone and the distribution
        emit MilestoneStatusChanged(upcomingMilestone, Status.Accepted);
        emit Distributed(acceptedRecipientId, recipient.recipientAddress, amount, _sender);
    }
```
## Impact
Each milestone must be paid out regardless of the status.
## Code Snippet
https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/rfp-simple/RFPSimpleStrategy.sol#L417
## Tool used

Manual Review

## Recommendation
I suggest introducing two new functions, accessible only to pool managers, for incrementing the `upcomingMilestone` when they find it appropriate or for accepting the `upcomingMilestone`. Additionally, consider updating the `_distribute()` function to perform a status check before execution.
RFPSimpleStrategy:
```solidity
    function rejectCurrentMilestone() external virtual onlyActivePool onlyPoolManager(msg.sender){
        Milestone storage milestone = milestones[upcomingMilestone];
        //Should only be able to reject pending milestone
        if(milestone.milestoneStatus == Status.Accepted || milestone.milestoneStatus == Status.Rejected){
            revert();
        }else{
            milestone.milestoneStatus = Status.Rejected;
            upcomingMilestone++;
        }
    }
    function acceptUpcomingMilestone() external virtual onlyActivePool onlyPoolManager(msg.sender){
        Milestone storage milestone = milestones[upcomingMilestone];
        //Only able to accept pending milestone
        if(milestone.milestoneStatus == Status.Pending){
            milestone.milestoneStatus = Status.Accepted;
        }
    }
```
```solidity
    function _distribute(address[] memory, bytes memory, address _sender)
        internal
        virtual
        override
        onlyInactivePool
        onlyPoolManager(_sender)
    {
        // check to make sure there is a pending milestone
        if (upcomingMilestone >= milestones.length) revert INVALID_MILESTONE();

        IAllo.Pool memory pool = allo.getPool(poolId);
        Milestone storage milestone = milestones[upcomingMilestone];
        Recipient memory recipient = _recipients[acceptedRecipientId];
+       require(milestone.milestoneStatus == Status.Accepted);

        // make sure has enough funds to distribute based on the proposal bid
        if (recipient.proposalBid > poolAmount) revert NOT_ENOUGH_FUNDS();

        // Calculate the amount to be distributed for the milestone
        uint256 amount = (recipient.proposalBid * milestone.amountPercentage) / 1e18;

        // Get the pool, subtract the amount and transfer to the recipient
        poolAmount -= amount;
        _transferAmount(pool.token, recipient.recipientAddress, amount);

        // Set the milestone status to 'Accepted'
-       milestone.milestoneStatus = Status.Accepted;

        // Increment the upcoming milestone
        upcomingMilestone++;

        // Emit events for the milestone and the distribution
        emit MilestoneStatusChanged(upcomingMilestone, Status.Accepted);
        emit Distributed(acceptedRecipientId, recipient.recipientAddress, amount, _sender);
    }
```
RFPCommitteeStrategy:
```solidity
    uint256 public rejectCounter;
    uint256 public acceptCounter;

    function rejectCurrentMilestone() external override onlyActivePool onlyPoolManager(msg.sender) {
        Milestone storage milestone = milestones[upcomingMilestone];
        if(milestone.milestoneStatus == Status.Rejected || milestone.milestoneStatus == Status.Accepted){
            revert();
        }else{
            rejectCounter++;
        }
        if(rejectCounter >= voteThreshold){
            milestone.milestoneStatus == Status.Rejected;
            //increment upcomingMilestone
            upcomingMilestone++;
            //reset rejectCounter for next milestone 
            rejectCounter = 0;
        }
    }

    function acceptUpcomingMilestone() external override onlyActivePool onlyPoolManager(msg.sender){
        Milestone storage milestone = milestones[upcomingMilestone];
        if(milestone.milestoneStatus == Status.Rejected || milestone.milestoneStatus == Status.Accepted){
            revert();
        }else{
            acceptCounter++;
        }
        if(acceptCounter >= voteThreshold){
            milestone.milestoneStatus == Status.Accepted;
            //reset acceptCounter for next milestone
            acceptCounter = 0;
        }
    }
```