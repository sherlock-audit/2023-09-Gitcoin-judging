Special Eggplant Falcon

high

# RFPSimpleStrategy `_registerRecipient` always revert if `useRegistryAnchor` is true
## Summary
The RFPSimpleStrategy contract has a security issue related to the initialization of the `useRegistryAnchor` parameter. When `useRegistryAnchor` is set to true, the `_registerRecipient` function will always revert with the `RECIPIENT_ERROR` message due to the `recipientAddress` being initialized as address(0).

## Vulnerability Detail
Inside the `_registerRecipient` function, the input calldata `_data` is decoded into `(address recipientId, uint256 proposalBid, Metadata metadata)`. However, the variable `recipientAddress` remains as address(0) and is only properly decoded when `useRegistryAnchor` is set to false. 
Consequently, line 367 if `(recipientAddress == address(0))` revert `RECIPIENT_ERROR(recipientId)`; will always trigger a revert if `useRegistryAnchor` is true.

## Impact
1. When the `useRegistryAnchor` parameter is initialized as true, the `_registerRecipient` function will always revert with the `RECIPIENT_ERROR` message. This means that the intended functionality of registering recipients will be completely broken, rendering the contract unable to perform its core operations as expected.
2. Loss of Funds or Opportunities: The inability to register recipients can have financial implications. If the contract relies on registering recipients to execute certain actions or distribute funds, the security issue might prevent those actions from occurring. This could result in potential financial losses or missed opportunities for both the contract owner and recipients.

## Code Snippet
[RFPSimpleStrategy.sol - Line 367](https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/rfp-simple/RFPSimpleStrategy.sol#L367)
```solidity
    function _registerRecipient(bytes memory _data, address _sender)
        internal
        override
        onlyActivePool
        returns (address recipientId)
    {
        bool isUsingRegistryAnchor;
        address recipientAddress;
        address registryAnchor;
        uint256 proposalBid;
        Metadata memory metadata;

        // Decode '_data' depending on the 'useRegistryAnchor' flag
        if (useRegistryAnchor) {
            /// @custom:data when 'true' -> (address recipientId, uint256 proposalBid, Metadata metadata)
            (recipientId, proposalBid, metadata) = abi.decode(_data, (address, uint256, Metadata));
            // If the sender is not a profile member this will revert
            if (!_isProfileMember(recipientId, _sender)) revert UNAUTHORIZED();
        } else {
            //  @custom:data when 'false' -> (address recipientAddress, address registryAnchor, uint256 proposalBid, Metadata metadata)
            (recipientAddress, registryAnchor, proposalBid, metadata) =
                abi.decode(_data, (address, address, uint256, Metadata));

            // Check if the registry anchor is valid so we know whether to use it or not
            isUsingRegistryAnchor = registryAnchor != address(0);

            // Ternerary to set the recipient id based on whether or not we are using the 'registryAnchor' or '_sender'
            recipientId = isUsingRegistryAnchor ? registryAnchor : _sender;

            // Checks if the '_sender' is a member of the profile 'anchor' being used and reverts if not
            if (isUsingRegistryAnchor && !_isProfileMember(recipientId, _sender)) revert UNAUTHORIZED();
        }
        // Check if the metadata is required and if it is, check if it is valid, otherwise revert
        // If the recipient address is the zero address this will revert
        @audit-issue always revert if useRegistryAnchor=true
        if (recipientAddress == address(0)) revert RECIPIENT_ERROR(recipientId);
```
## Tool used

Manual Review

## Recommendation
To address this issue, proper initialization of the recipientAddress variable is required when useRegistryAnchor is set to true. This ensures that the function behaves as intended and does not unconditionally revert