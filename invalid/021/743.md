Energetic Berry Llama

high

# `Allo::_createPool()` expects more than the required `baseFee`, which will cause a loss of funds
## Summary
The code part that checks if the `msg.value` is enough to create a pool is incorrectly implemented and expects more than required. The function reverts if the exact amount is sent. It expects more than the exact amount but doesn't refund the excess amount. Every pool creation will cause a loss of funds to the creators.

## Vulnerability Detail
Allo protocol has two different fee types: `baseFee` and `percentFee`.  
`baseFee` is paid during pool creation and is the same for every pool, `percentFee` is paid during the funding of the pool and it depends on the funding amount. According to documentation, the protocol `baseFee` is 0 at the moment but it will be implemented in the future.

The issue is in the `_createPool()` function when the `baseFee > 0`. The function reverts if the exact amount of `baseFee` is paid.  
[https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/core/Allo.sol#L473C1-L475C14](https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/core/Allo.sol#L473C1-L475C14)

```solidity
file: Allo.sol
// inside the _createPool() function
 
       if (baseFee > 0) {
            // To prevent paying the baseFee from the Allo contract's balance
            // If _token is NATIVE, then baseFee + _amount should be >= than msg.value.
            // If _token is not NATIVE, then baseFee should be >= than msg.value.
-->         if ((_token == NATIVE && (baseFee + _amount >= msg.value)) || (_token != NATIVE && baseFee >= msg.value)) { //@audit it reverts when the baseFee is paid. It expects more than the baseFee
                revert NOT_ENOUGH_FUNDS();
            }
            _transferAmount(NATIVE, treasury, baseFee);
            emit BaseFeePaid(poolId, baseFee);
        }
```

Normally there are few options in this kind of situations.

1. Expect the user to send more than the required amount.  
    Use the necessary amount in the function.  
    Refund the excess amount.
    
2. Expect the user to send exactly the required amount  
    Revert if the sent amount and required amount don't match
    

But unfortunately, this function with the current implementation expects more than the required amount but doesn't refund the excess amount.

## Coded PoC

You can use the protocol's own setup to prove the PoC below  
\- Copy the code snippet and paste it into the Allo.t.sol test file.  
\- Run `forge test --match-test test_CanNotCreatePool_WhenPayingBaseFee`

```solidity
//@audit-issue Pay the exact baseFee amount to create the pool.
    function test_CanNotCreatePool_WhenPayingBaseFee() public {
        uint256 baseFee = 1e17;

        allo().updateBaseFee(baseFee);

        vm.deal(address(pool_admin()), 1e18);

        
        vm.prank(pool_admin());

        // Reverts when paying exact base fee amount even if you don't fund the pool
        vm.expectRevert(NOT_ENOUGH_FUNDS.selector);
        allo().createPoolWithCustomStrategy{value: 1e17}(
            poolProfile_id(), strategy, "0x", NATIVE, 0, metadata, pool_managers()
        );
    }
```

You can find the test result below:

```solidity
Running 1 test for test/foundry/core/Allo.t.sol:AlloTest
[PASS] test_CanNotCreatePool_WhenPayingBaseFee() (gas: 454512)
Test result: ok. 1 passed; 0 failed; 0 skipped; finished in 33.91ms
 
Ran 1 test suites: 1 tests passed, 0 failed, 0 skipped (1 total tests)
```

## Impact
It will cause loss of funds to the creators every time a pool is created

## Code Snippet
[https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/core/Allo.sol#L473C1-L475C14](https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/core/Allo.sol#L473C1-L475C14)

```solidity
file: Allo.sol
// inside the _createPool() function
 
       if (baseFee > 0) {
            // To prevent paying the baseFee from the Allo contract's balance
            // If _token is NATIVE, then baseFee + _amount should be >= than msg.value.
            // If _token is not NATIVE, then baseFee should be >= than msg.value.
-->         if ((_token == NATIVE && (baseFee + _amount >= msg.value)) || (_token != NATIVE && baseFee >= msg.value)) { //@audit it reverts when the baseFee is paid. It expects more than the baseFee
                revert NOT_ENOUGH_FUNDS();
            }
            _transferAmount(NATIVE, treasury, baseFee);
            emit BaseFeePaid(poolId, baseFee);
        }
```

## Tool used

Manual Review

## Recommendation
Change this:
```solidity
       if (baseFee > 0) {
            if ((_token == NATIVE && (baseFee + _amount >= msg.value)) || (_token != NATIVE && baseFee >= msg.value)) { 
                revert NOT_ENOUGH_FUNDS();
            }
        }
```

To this:
```solidity
       if (baseFee > 0) {
            if ((_token == NATIVE && (baseFee + _amount > msg.value)) || (_token != NATIVE && baseFee > msg.value)) { 
                revert NOT_ENOUGH_FUNDS();
            }
        }
```