Electric Tiger Bull

high

# [H-01] When calling `allo::allocate()` with `DonationVotingMerkleDistributionBaseStrategy` non-native tokens are not transferred from the allocator.
## Summary

When calling `allo::allocate()`  with  `DonationVotingMerkleDistributionBaseStrategy` non-native tokens are not transferred from the allocator. Which enables allocator to allocate funds to a recipient without actually sending tokens.

## Vulnerability Detail

When using [DonationVotingMerkleDistributionBaseStrategy.sol](https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/donation-voting-merkle-base/DonationVotingMerkleDistributionBaseStrategy.sol) as the strategy, users calling `allo::allocate` are able to allocate without actually sending tokens when non-native tokens are used.  

This is because [DonationVotingMerkleDistributionBaseStrategy::_allocate](https://github.com/sherlock-audit/2023-09-Gitcoin/blob/6430c8004017e96ae2f5aac365bdefd0b6eeea72/allo-v2/contracts/strategies/donation-voting-merkle-base/DonationVotingMerkleDistributionBaseStrategy.sol#L640C5-L664C6) only has logic to handle `NATIVE` tokens, in case of non-native tokens, there are no transfer of funds.

## Impact

Users are able to allocate tokens to themselves(if recipient) / or to others using allowed non-native tokens without actually sending tokens from their possession.

1. Suppose Alice is an allocator / recipient
2. Alice can call `allo::allocate()` where strategy is `DonationVotingMerkleDistributionBaseStrategy` and uses non-native tokens as one of allowed tokens.
3. Alice can call the allocate function and no tokens will be transferred from her.
4. Event is emitted as tokens allocated to Alice / recipient she is sending to.
5. Off chain mechanisms will add this emit to their bookkeeping and when distributing, tokens are badly distributed to `recipient` without actually sending any token at all.

Here is a coded PoC depicting this scenario:
This code can be pasted into the tests/strategies/DonationVotingMerkleDistributionBaseStrategy.t.sol

```javascript
//File::test/foundry/strategies/DonationVotingMerkleDistributionBaseStrategy.sol

 function testRevert_allocate_ERC20NotTransferred() public  { //@audit //POC 
        address recipientId = __register_accept_recipient();

        //Preparing permit data
        //Using allowedTokens[1] mockERC20
        DonationVotingMerkleDistributionBaseStrategy.Permit2Data memory permit2Data =
        DonationVotingMerkleDistributionBaseStrategy.Permit2Data({
            permit: ISignatureTransfer.PermitTransferFrom({
                permitted: ISignatureTransfer.TokenPermissions({token: allowedTokens[1], amount: 1e18}),
                nonce: 0,
                deadline: allocationStartTime + 10000
            }),
            signature: ""
        });

        // vm.expectRevert(INVALID.selector);

        vm.warp(allocationStartTime + 1);
        vm.deal(pool_admin(), 1e20);

        //Create user
        address Alice = makeAddr("Alice");
        vm.prank(Alice);
        mockERC20.mint(Alice, 1_000_000 * 1e18);

        //ERC20 Balance of ALice before allocating
        uint256 BalanceOfAliceBefore = mockERC20.balanceOf(Alice);

        //alice calls allocate
        allo().allocate(poolId, abi.encode(recipientId, permit2Data));

        //ERC20 Balance of ALice before allocating
        uint256 BalanceOfAliceAfter = mockERC20.balanceOf(Alice);

        //Balances are same because tokens are not transferred
        assertEq(BalanceOfAliceAfter, BalanceOfAliceBefore);
    }
```

I'm using the non-native tokens as the allowedTokens[1] mockERC20 already defined in the test script. We can see that even after allocating tokens to `recipientId` the mockERC20 `balance` of Alice hasn't changed at all.

## Code Snippet

There are no mechanism for non-native transfer in [DonationVotingMerkleDistributionBaseStrategy::_allocate](https://github.com/sherlock-audit/2023-09-Gitcoin/blob/6430c8004017e96ae2f5aac365bdefd0b6eeea72/allo-v2/contracts/strategies/donation-voting-merkle-base/DonationVotingMerkleDistributionBaseStrategy.sol#L640C5-L664C6).

```javascript
function _allocate(bytes memory _data, address _sender) internal virtual override onlyActiveAllocation {
        // Decode the '_data' to get the recipientId, amount and token
        (address recipientId, Permit2Data memory p2Data) = abi.decode(_data, (address, Permit2Data));

        uint256 amount = p2Data.permit.permitted.amount;
        address token = p2Data.permit.permitted.token;

        // If the recipient status is not 'Accepted' this will revert, the recipient must be accepted through registration
        if (Status(_getUintRecipientStatus(recipientId)) != Status.Accepted) {
            revert RECIPIENT_ERROR(recipientId);
        }

        // The token must be in the allowed token list and not be native token or zero address
        if (!allowedTokens[token] && !allowedTokens[address(0)]) {
            revert INVALID();
        }

        // If the token is native, the amount must be equal to the value sent, otherwise it reverts
        if (msg.value > 0 && token != NATIVE || token == NATIVE && msg.value != amount) {
            revert INVALID();
        }

        // Emit that the amount has been allocated to the recipient by the sender
        emit Allocated(recipientId, amount, token, _sender);
    }

```


## Tool used

Manual Review

## Recommendation

Add mechanism for handling non-native tokens. 

```javascript
//In File DonationVotingMerkleDistributionBaseStrategy::_allocate()

if(token != NATIVE ){
    token.safeTransferFrom(sender, address(this), amount);
}
```