Curved Chocolate Iguana

high

# Allo@fundPool can still increase strategies poolAmount for pools created with the address(0) as a token
## Summary
`Allo@fundPool` can still be called for pools that have `address(0)` as a token, triggering a `poolAmount` increase for the corresponding strategies even if no tokens are transferred.

## Vulnerability Detail
Anyone can call `Allo@fundPool` for any pool, even the ones with `address(0)` as a token. Since `SafeTransferLib@safeTransferFrom` does not fail if the token is `address(0)`, the corresponding strategy gets its `poolAmount` increased even if no tokens were transferred.

https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/core/libraries/Transfer.sol#L43

https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/core/Allo.sol#L517

## Impact
`poolAmount` for a pool strategy can be increased even if no tokens are transferred.

## Code Snippet
```solidity
    function testfundPoolAddress0() public {
        vm.prank(pool_admin());

        uint256 poolId = allo().createPoolWithCustomStrategy(
            poolProfile_id(), strategy, "0x", address(0), 0, metadata, pool_managers()
        );

        allo().fundPool(poolId, 1 ether);

        assertEq(IStrategy(strategy).getPoolAmount(), 0);
    }
```

## Tool used

Manual Review

## Recommendation
https://github.com/sherlock-audit/2023-09-Gitcoin-imsrybr0/issues/3 happens to fix this too as the `balanceOf` calls revert for the `address(0)`.

Alternatively / additionally, a check can be added to `Allo@_fundPool` to short circuit if the pool token is the `address(0)`
```solidity
    function _fundPool(uint256 _amount, uint256 _poolId, IStrategy _strategy) internal {
        uint256 feeAmount;
        uint256 amountAfterFee = _amount;

        Pool storage pool = pools[_poolId];
        address _token = pool.token;

        if (_token == address(0)) {
            return;
        }

        if (percentFee > 0) {
            feeAmount = (_amount * percentFee) / getFeeDenominator();
            amountAfterFee -= feeAmount;

            _transferAmountFrom(_token, TransferData({from: msg.sender, to: treasury, amount: feeAmount}));
        }

        _transferAmountFrom(_token, TransferData({from: msg.sender, to: address(_strategy), amount: amountAfterFee}));
        _strategy.increasePoolAmount(amountAfterFee);

        emit PoolFunded(_poolId, amountAfterFee, feeAmount);
    }
```

## Additional Comments
This may also impact other parts of the system like `DonationVotingMerkleDistributionBaseStrategy@withdraw` where `poolAmount` can be decreased even if no tokens are transferred.
