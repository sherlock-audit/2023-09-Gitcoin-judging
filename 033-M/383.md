Uneven Holographic Llama

medium

# Unprotected status transitions in reviewRecipients function at DonatingVotingMerkleDistributionBaseStrategy
## Summary
The reviewRecipients function in the provided contract allows arbitrary status transitions without checks, potentially leading to incorrect recipient statuses. This can disrupt the intended flow and logic of the application.

## Vulnerability Detail
The contract uses a bitmap representation to manage the statuses of recipients. Each recipient's status is represented using 4 bits, allowing for encoding of 5 defined statuses: none (0), pending (1), accepted (2), rejected (3), and appealed (4). The intended status flow is:
none -> pending
pending -> accepted or rejected
rejected -> appealed
accepted -> pending
appealed -> pending, accepted or rejected

However, the reviewRecipients function, which updates the statuses, does not enforce these intended transitions. It permits arbitrary changes in status. This means valid statuses like accepted can be inadvertently or deliberately reverted to none, or rejected can be changed to pending, both of which are unintended and potentially disruptive transitions.

Given the intricacy of the bitmap logic and the importance of recipient statuses, this absence of checks is risky. While the function is manager-controlled, the manager might by oversight disrupt the contract's state. The complexity of the logic further amplifies the chances of unintentional mistakes.

## Impact

The arbitrary status transitions can cause backend functions to return incorrect data, directly affecting frontend displays and possibly misleading users. This can lead users to believe that pool managers are trying to manipulate allocations or act in non-transparent ways. The potential mistrust and confusion created can undermine the credibility of the entire system.

## Code Snippet
https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/donation-voting-merkle-base/DonationVotingMerkleDistributionBaseStrategy.sol#L341-L360

```solidity
function reviewRecipients(ApplicationStatus[] memory statuses)
        external
        onlyActiveRegistration
        onlyPoolManager(msg.sender)
    {
        // Loop through the statuses and set the status
        for (uint256 i; i < statuses.length;) {
            uint256 rowIndex = statuses[i].index;
            uint256 fullRow = statuses[i].statusRow;

            statusesBitMap[rowIndex] = fullRow;

            // Emit that the recipient status has been updated with the values
            emit RecipientStatusUpdated(rowIndex, fullRow, msg.sender);

            unchecked {
                i++;
            }
        }
    }
```

Notice the lack of validation checks at the function, it overrides all statuses regardless of their initial state.

## Tool used

Manual Review

## Recommendation
In the current implementation of the reviewRecipients function, recipient statuses are updated directly without checks on the validity of these transitions.

Status Transition Validation:
Implement a helper function, isValidTransition, which verifies if a proposed status change is consistent with the allowed transitions based on the current status.
```solidity
function isValidTransition(uint256 currentStatus, uint256 newStatus) internal pure returns (bool) {
    if (currentStatus == 0) {
        return newStatus == 1;
    } else if (currentStatus == 1) {
        return newStatus == 2 || newStatus == 3;
    } else if (currentStatus == 2) {
        return newStatus == 1;
    } else if (currentStatus == 3) {
        return newStatus == 4;
    } else if (currentStatus == 4) {
        return newStatus == 1 || newStatus == 2 || newStatus == 3;
    }
    return false;
}
```
By incorporating this function into reviewRecipients, only legitimate status transitions are permitted. This reduces the risk of state disruptions.
Modified function: 
```solidity
function reviewRecipients(ApplicationStatus[] memory statuses)
    external
    onlyActiveRegistration
    onlyPoolManager(msg.sender)
{
    // Loop through the statuses and set the status
    for (uint256 i = 0; i < statuses.length; i++) {
        uint256 rowIndex = statuses[i].index;
        uint256 fullRow = statuses[i].statusRow;

        // Calculate column index for the given recipient in the bitmap
        uint256 colIndex = rowIndex * 4;

        // Retrieve current and new statuses
        uint256 currentStatus = (statusesBitMap[rowIndex] >> (256 - (colIndex + 4))) & 0xF;
        uint256 newStatus = (fullRow >> (256 - (colIndex + 4))) & 0xF;

        // Validate the status transition
        require(isValidTransition(currentStatus, newStatus), "Invalid status transition");

        // Update the status in the bitmap
        statusesBitMap[rowIndex] = fullRow;

        // Emit that the recipient status has been updated with the values
        emit RecipientStatusUpdated(rowIndex, fullRow, msg.sender);
    }
}
```
