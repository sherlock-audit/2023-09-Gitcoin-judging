Clumsy Pecan Jay

medium

# Funding using a fee-on-transfer token will prevent payments

Strategies funded through `Allo` allocate a higher amount of tokens to the pool then the actual balance of the strategy when fee-on-transfer tokens are used

## Vulnerability Detail
https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/core/Allo.sol#L502
```solidity
    function _fundPool(uint256 _amount, uint256 _poolId, IStrategy _strategy) internal {
        uint256 feeAmount;
        uint256 amountAfterFee = _amount;

        Pool storage pool = pools[_poolId];
        address _token = pool.token;

        if (percentFee > 0) {
            feeAmount = (_amount * percentFee) / getFeeDenominator();
            amountAfterFee -= feeAmount;

            _transferAmountFrom(_token, TransferData({from: msg.sender, to: treasury, amount: feeAmount}));
        }

        _transferAmountFrom(_token, TransferData({from: msg.sender, to: address(_strategy), amount: amountAfterFee}));
        _strategy.increasePoolAmount(amountAfterFee);

        emit PoolFunded(_poolId, amountAfterFee, feeAmount);
    }
```

As can be seen above - `increasePoolAmount` is called with `amountAfterFee` which does not include the fee taken from the `_transferAmountFrom` call.

This can prevent distribution for example on RFP strategies with a single milestone and a proposalBid of the pool amount:
```solidity
        // make sure has enough funds to distribute based on the proposal bid
        if (recipient.proposalBid > poolAmount) revert NOT_ENOUGH_FUNDS();

        // Calculate the amount to be distributed for the milestone
        uint256 amount = (recipient.proposalBid * milestone.amountPercentage) / 1e18;

        // Get the pool, subtract the amount and transfer to the recipient
        poolAmount -= amount;
        _transferAmount(pool.token, recipient.recipientAddress, amount);
```

`_transferAmount` will fail because the contract has insufficient tokens

## Impact

Loss of funds

## Code Snippet

## Tool used

Manual Review

## Recommendation

Consider changing the funding logic to check for the real balance of the strategy:
```solidity
        uint256 balanceBefore = IERC20Upgradeable(_token).balanceOf(address(_strategy));
        _transferAmountFrom(_token, TransferData({from: msg.sender, to: address(_strategy), amount: amountAfterFee}));
         uint256 balanceAfter = IERC20Upgradeable(_token).balanceOf(address(_strategy));
        _strategy.increasePoolAmount(balanceAfter - balanceBefore);
```