Immense Teal Penguin

medium

# QVSimpleStrategy contract is not functioning with native token as pool's token
QVSimpleStrategy contract is not functioning with native token as pool's token
## Vulnerability Detail
The QVSimpleStrategy contract doesn't have `receive()` function, which mean pool manager can't send fund through `Allo.fundPool()`, which mean it's impossible to call the function `QVSimpleStrategy.increasePoolAmount()`, which mean the `poolAmount` variable of QVSimpleStrategy will always be ZERO. Hence, it's impossible for recipient to receive funds

`Allo.fundPool()`:
```solidity
    function fundPool(uint256 _poolId, uint256 _amount) external payable nonReentrant {
        if (_amount == 0) revert NOT_ENOUGH_FUNDS();

        _fundPool(_amount, _poolId, pools[_poolId].strategy);
    }
```
`Allo._fundPool()`:
```solidity
    function _fundPool(uint256 _amount, uint256 _poolId, IStrategy _strategy) internal {
        ...

        _transferAmountFrom(_token, TransferData({from: msg.sender, to: address(_strategy), amount: amountAfterFee})); //<@@ because QVSimpleStrategy contract doesn't have receive(), this will be reverted
        _strategy.increasePoolAmount(amountAfterFee); //<@@ this is the only way to call to increasePoolAmount()

        ...
    }
```
`BaseStrategy.increasePoolAmount()`:
```solidity
    function increasePoolAmount(uint256 _amount) external override onlyAllo {
        _beforeIncreasePoolAmount(_amount);

        poolAmount += _amount; //<@@ this is the only way to increase poolAmount

        _afterIncreasePoolAmount(_amount);
    }
```
`QVBaseStrategy._getPayout()`:
```solidity
    function _getPayout(address _recipientId, bytes memory)
        internal
        view
        virtual
        override
        returns (PayoutSummary memory)
    {
        Recipient memory recipient = recipients[_recipientId];

        uint256 amount;
        if (!paidOut[_recipientId] && totalRecipientVotes != 0) {
            amount = poolAmount * recipient.totalVotesReceived / totalRecipientVotes; //<@@ because poolAmount always be ZERO, this will always be ZERO too
        }
        return PayoutSummary(recipient.recipientAddress, amount);
    }
```
`QVBaseStrategy._distribute()`:
```solidity
    function _distribute(address[] memory _recipientIds, bytes memory, address _sender)
        internal
        virtual
        override
        onlyPoolManager(_sender)
        onlyAfterAllocation
    {
        uint256 payoutLength = _recipientIds.length;
        for (uint256 i; i < payoutLength;) {
            address recipientId = _recipientIds[i];
            Recipient storage recipient = recipients[recipientId];

            PayoutSummary memory payout = _getPayout(recipientId, "");

            uint256 amount = payout.amount; //<@@ amount will always be ZERO

            if (paidOut[recipientId] || !_isAcceptedRecipient(recipientId) || amount == 0) {
                revert RECIPIENT_ERROR(recipientId);
            }

            IAllo.Pool memory pool = allo.getPool(poolId);
            _transferAmount(pool.token, recipient.recipientAddress, amount); //<@@ sending ZERO amount to recipient

            paidOut[recipientId] = true;

            emit Distributed(recipientId, recipient.recipientAddress, amount, _sender);
            unchecked {
                ++i;
            }
        }
    }
```
## Impact
This will make the QVSimpleStrategy contract unusable with native token as pool's token 
## Code Snippet
https://github.com/sherlock-audit/2023-09-Gitcoin/blob/6430c8004017e96ae2f5aac365bdefd0b6eeea72/allo-v2/contracts/core/Allo.sol#L339C1-L345C6

https://github.com/sherlock-audit/2023-09-Gitcoin/blob/6430c8004017e96ae2f5aac365bdefd0b6eeea72/allo-v2/contracts/core/Allo.sol#L502C1-L520C6

https://github.com/sherlock-audit/2023-09-Gitcoin/blob/6430c8004017e96ae2f5aac365bdefd0b6eeea72/allo-v2/contracts/strategies/BaseStrategy.sol#L153C1-L157C6

https://github.com/sherlock-audit/2023-09-Gitcoin/blob/6430c8004017e96ae2f5aac365bdefd0b6eeea72/allo-v2/contracts/strategies/qv-base/QVBaseStrategy.sol#L559C1-L574C6

https://github.com/sherlock-audit/2023-09-Gitcoin/blob/6430c8004017e96ae2f5aac365bdefd0b6eeea72/allo-v2/contracts/strategies/qv-base/QVBaseStrategy.sol#L436C1-L465C6
## Tool used

Manual Review

## Recommendation
Add receive() function to QVSimpleStrategy contract