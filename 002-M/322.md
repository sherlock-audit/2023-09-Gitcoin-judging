Rich Jade Wolf

medium

# Tokens with a fee-on-transfer mechanism(reflection tokens) will break the protocol
Some tokens, such as PAXG or even [USDT](https://etherscan.io/token/0xdac17f958d2ee523a2206206994597c13d831ec7#code#L177) (currently switched off), have a fee-on-transfer mechanism that can potentially disrupt the protocol or open up attack vectors. For example:

## Vulnerability Detail

1. Create a new pool where the `_token` argument is an address of a token with a built-in fee-on-transfer mechanism.
2. The value of the `_amount` argument is greater than 0.
3. After creating the pool, the balance of the strategy will equal `_amount - baseFee - transferFee`, but `poolAmount` equals `_amount - baseFee`.

## Impact
Some users may experience value loss when dealing with such tokens.  Medium, as such tokens are not common.

## Code Snippet
First, let's implement a simple ERC20 token with a built-in fee-on-transfer mechanism in `test/utils/MockERC20TransferFee.sol`.
```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import "@openzeppelin/contracts/token/ERC20/ERC20.sol";
import "@openzeppelin/contracts/access/Ownable.sol";

contract MockERC20TransferFee is ERC20, Ownable {
    uint256 public transferFee; // Fee in tokens
    address public feeCollector; // Address that collects the fee

    constructor(
        string memory name,
        string memory symbol,
        uint256 initialSupply,
        uint256 initialTransferFee
    ) ERC20(name, symbol) {
        _mint(msg.sender, initialSupply);
        transferFee = initialTransferFee;
        feeCollector = msg.sender;
    }

    // Set the transfer fee
    function setTransferFee(uint256 newFee) external onlyOwner {
        transferFee = newFee;
    }

    // Set the fee collector address
    function setFeeCollector(address newCollector) external onlyOwner {
        feeCollector = newCollector;
    }

    // Override the transferFrom function to include the fee
    function transferFrom(address sender, address recipient, uint256 amount) public virtual override returns (bool) {
        require(sender != address(0), "ERC20: transfer from the zero address");
        require(recipient != address(0), "ERC20: transfer to the zero address");
        require(amount <= balanceOf(sender), "ERC20: transfer amount exceeds balance");
        require(amount <= allowance(sender, msg.sender), "ERC20: transfer amount exceeds allowance");

        uint256 feeAmount = (amount * transferFee) / 1e18; // Calculate the fee amount

        _transfer(sender, feeCollector, feeAmount); // Transfer fee to the fee collector
        _transfer(sender, recipient, amount - feeAmount); // Transfer the remainder to the recipient

        return true;
    }
}
```

Now, import this contract into `test/foundry/core/Allo.t.sol`
```solidity
import {MockERC20TransferFee} from "../../utils/MockERC20TransferFee.sol";
```
Then add a new variable to the `AlloTest` contract.
```solidity
    MockERC20TransferFee public tokenWithTransferFee;
```
Create an instance in the `setUp()` function.
```solidity
        vm.prank(pool_admin());
        tokenWithTransferFee = new MockERC20TransferFee(
            "MockERC20TransferFee", "MOCK", 1e19, 1e16
        );
```

We can reproduce the vulnerability by writing a test.
```solidity
    function test_createPoolAttack() public {
        allo().addToCloneableStrategies(strategy);
        
        vm.prank(pool_admin());
        tokenWithTransferFee.approve(address(allo()), 1 ether);

        vm.prank(pool_admin());
        uint256 poolId = allo().createPool(
            poolProfile_id(),
            strategy,
            "0x",
            address(tokenWithTransferFee),
            1 ether,
            metadata,
            pool_managers()
        );
        
        IStrategy s = IStrategy(allo().getStrategy(poolId));

        assertEq(tokenWithTransferFee.balanceOf(address(s)), s.getPoolAmount());
    }
```

The test has failed.
```bash
  Error: a == b not satisfied [uint]
        Left: 980100000000000000
       Right: 99000000000000000
```

## Tool used
`forge test`

## Recommendation
Cache the balance of the strategy contract in the  [_fundPool](https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/core/Allo.sol#L502) function and subtract this value from the new balance after transferring tokens to the strategy contract. Use the result to increase the pool amount instead of relying on the `_amount` variable.