Expert Pecan Carp

medium

# `createPool` transaction reverts when the amount of ETH sent is the exact amount required.
`createPool` transaction reverts when the amount of ETH sent is the exact amount required. So user has to send more ETH than required for  the transaction to be successful due to the use of `>=` operator instead of `>` operator in
```solidity
baseFee + _amount >= msg.value
```

## Vulnerability Detail
The vulnerability arise from the logical operation on line 473 of the `_createPool(...)` function below.

```solidity
if ((_token == NATIVE && (baseFee + _amount >= msg.value)) || (_token != NATIVE && baseFee >= msg.value)) {
                revert NOT_ENOUGH_FUNDS();//@audit-issue condition >= wrong. No eth refund.
            }
```
File: https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/core/Allo.sol#L473

When `baseFee` is not zero or `_amount` is not zero and the amount of ETH sent with the transaction is the exact amount, the transaction will fail.

Issue scenario
1. Lets say `baseFee` is 1000 wei
2. And `_amount` parameter is 0 wei.
3. When Alice sends a `createPool` transaction with 1000 wei the transaction will fail.
4. For the above transaction to be successful, Alice has to send a transaction with at least `baseFee` + `_amount` + 1. which will be 1001 in this case.

## Impact
Loss of extra ETH sent with the `createPool` transaction.

## Code Snippet
File: https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/core/Allo.sol#L473

```solidity
function _createPool(
        bytes32 _profileId,
        IStrategy _strategy,
        bytes memory _initStrategyData,
        address _token,
        uint256 _amount,
        Metadata memory _metadata,
        address[] memory _managers
    ) internal returns (uint256 poolId) {
...
if (baseFee > 0) {
            // To prevent paying the baseFee from the Allo contract's balance
            // If _token is NATIVE, then baseFee + _amount should be >= than msg.value. //@audit comment not correct. add NOT.
            // If _token is not NATIVE, then baseFee should be >= than msg.value.
473 @>            if ((_token == NATIVE && (baseFee + _amount >= msg.value)) || (_token != NATIVE && baseFee >= msg.value)) {
                revert NOT_ENOUGH_FUNDS();//@audit-issue condition >= wrong. use > instead of >=.  No eth refund.
            }
            _transferAmount(NATIVE, treasury, baseFee);
            emit BaseFeePaid(poolId, baseFee);
        }
...
}
```
## Proof of Concept
Create a test file with the name Ding.t.sol in the `test/foundry/core` directory then copy and paste the below code snippet and run `forge test --match-path test/**/Ding.t.sol -vvv`

```solidity
// SPDX-License-Identifier: AGPL-3.0-only
pragma solidity ^0.8.19;

import "forge-std/Test.sol";

// Interfaces
import {IAllo} from "../../../contracts/core/interfaces/IAllo.sol";
import {IStrategy} from "../../../contracts/core/interfaces/IStrategy.sol";
// Core contracts
import {Allo} from "../../../contracts/core/Allo.sol";
import {Registry} from "../../../contracts/core/Registry.sol";
// Internal Libraries
import {Errors} from "../../../contracts/core/libraries/Errors.sol";
import {Metadata} from "../../../contracts/core/libraries/Metadata.sol";
import {Native} from "../../../contracts/core/libraries/Native.sol";
// Test libraries
import {AlloSetup} from "../shared/AlloSetup.sol";
import {RegistrySetupFull} from "../shared/RegistrySetup.sol";
import {TestStrategy} from "../../utils/TestStrategy.sol";
import {MockStrategy} from "../../utils/MockStrategy.sol";
import {MockERC20} from "../../utils/MockERC20.sol";

contract AlloTest is Test, AlloSetup, RegistrySetupFull, Native, Errors {
    
    address public strategy;
    MockERC20 public token;

    uint256 mintAmount = 1000000 * 10 ** 18;

    Metadata public metadata = Metadata({protocol: 1, pointer: "strategy pointer"});
    string public name;
    uint256 public nonce;

    function setUp() public {
        __RegistrySetupFull();
        __AlloSetup(address(registry()));

        token = new MockERC20();
        token.mint(local(), mintAmount);
        token.mint(allo_owner(), mintAmount);
        token.mint(pool_admin(), mintAmount);
        token.approve(address(allo()), mintAmount);

        vm.prank(pool_admin());
        token.approve(address(allo()), mintAmount);

        strategy = address(new MockStrategy(address(allo())));

        vm.startPrank(allo_owner());
        allo().transferOwnership(local());
        vm.stopPrank();
    }

    function test_createPool() public {
        allo().addToCloneableStrategies(strategy);
         uint newBaseFee = 1e16;
        //Note: baseFee is updated to 1e16 but creating pool with exactly 1e16 will fail.
         allo().updateBaseFee(newBaseFee);
         
        deal(address(pool_admin()), 1e20);
        vm.prank(pool_admin());
        //Expect that transaction will revert.
         vm.expectRevert(NOT_ENOUGH_FUNDS.selector);
        //Note: sending the exact newBaseFee as value will fail.
        // uint256 poolId = 
        allo().createPool{value: newBaseFee}(poolProfile_id(), strategy, "0x", NATIVE, 0, metadata, pool_managers());

        
    }
}
```
## Tool used
Manual Review

## Recommendation
Replace `>=` with `>` in the `if` condition of the `_createPool` internal function on line 473.