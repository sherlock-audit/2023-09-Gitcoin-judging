Clumsy Pecan Jay

medium

# Users cannot claim tokens in `DonationVotingMerkleDistributionVaultStrategy` if fee-on-transfer tokens are used.
## Summary

`DonationVotingMerkleDistributionVaultStrategy` allows anyone to vote by sending a token to the contract and the recipient can call `claim` to receive those tokens.
However, the accounting is incorrect if fee-on-transfer tokens are used. The recipient won't be able to claim those tokens and they will be permanently locked in the contract.

## Vulnerability Detail
https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/donation-voting-merkle-distribution-vault/DonationVotingMerkleDistributionVaultStrategy.sol#L70
```solidity
    function claim(Claim[] calldata _claims) external nonReentrant onlyAfterAllocation {
-------------
            Claim memory singleClaim = _claims[i];
            Recipient memory recipient = _recipients[singleClaim.recipientId];
            uint256 amount = claims[singleClaim.recipientId][singleClaim.token];
-------------
            address token = singleClaim.token;

            // Transfer the tokens to the recipient
            _transferAmount(token, recipient.recipientAddress, amount);
-------------
        }
    }
-------------
    function _afterAllocate(bytes memory _data, address _sender) internal override {
        // Decode the '_data' to get the recipientId, amount and token
        (address recipientId, Permit2Data memory p2Data) = abi.decode(_data, (address, Permit2Data));

        // Get the token address
        address token = p2Data.permit.permitted.token;
        uint256 amount = p2Data.permit.permitted.amount;
-------------
        } else {
            PERMIT2.permitTransferFrom(
                // The permit message.
                p2Data.permit,
                // The transfer recipient and amount.
                ISignatureTransfer.SignatureTransferDetails({to: address(this), requestedAmount: amount}),
                // Owner of the tokens and signer of the message.
                _sender,
                // The packed signature that was the result of signing
                // the EIP712 hash of `_permit`.
                p2Data.signature
            );
        }

        // Update the total payout amount for the claim
        claims[recipientId][token] += amount;
    }
```

In the above snippet you can see that on allocation `claims[recipientId][token] += amount;` is set.
In fee-on-transfer tokens the actual balance after fee will be less then `amount`.

Therefore - at `claim` function `_transferAmount(token, recipient.recipientAddress, amount);` will either:
1. Take funds from the contract balance which is allocated to another user
2. Revert because there are insufficient funds (taken by the above option)

## Impact

Accounting mismatch between the actual balance and allocated balance.
Users will receive other users funds or not receive funds at all.

## Code Snippet

## Tool used

Manual Review

## Recommendation

At allocation when `claims` is updated, update the actual balance added:
```solidity
        } else {
            uint256 balanceBefore = IERC20(token).balanceOf(address(this));
            PERMIT2.permitTransferFrom(
                // The permit message.
                p2Data.permit,
                // The transfer recipient and amount.
                ISignatureTransfer.SignatureTransferDetails({to: address(this), requestedAmount: amount}),
                // Owner of the tokens and signer of the message.
                _sender,
                // The packed signature that was the result of signing
                // the EIP712 hash of `_permit`.
                p2Data.signature
            );
            uint256 balanceAfter= IERC20(token).balanceOf(address(this));
            amount = balanceAfter - balanceBefore;
        }

        // Update the total payout amount for the claim
        claims[recipientId][token] += amount;
    }
```