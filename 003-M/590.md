Sneaky Tan Hippo

high

# Whitelisted allocators in QVSimpleStrategy can vote an infinite number of times (~inf voice credits), breaking the voting system

In QVSimpleStrategy, the intention is that allocators have the ability to assign votes (voice credits) across all their desired recipients, with the one caveat being that they cannot exceed `maxVoiceCreditsPerAllocator` in total. The issue is that the current implementation allows allocators to vote an infinite number of times, where they are able to vote up to `maxVoiceCreditsPerAllocator` every time they call the `_allocate` function. This breaks the entire voting system.

## Vulnerability Detail

When an allocator is voting for a recipient they will effectively call the `_allocate` function which is defined as follows:
```solidity
function _allocate(bytes memory _data, address _sender) internal virtual override {
    (address recipientId, uint256 voiceCreditsToAllocate) = abi.decode(_data, (address, uint256));

    // spin up the structs in storage for updating
    Recipient storage recipient = recipients[recipientId];
    Allocator storage allocator = allocators[_sender];

    // check that the sender can allocate votes
    if (!_isValidAllocator(_sender)) revert UNAUTHORIZED();

    // check that the recipient is accepted
    if (!_isAcceptedRecipient(recipientId)) revert RECIPIENT_ERROR(recipientId);

    // check that the recipient has voice credits left to allocate
    if (!_hasVoiceCreditsLeft(voiceCreditsToAllocate, allocator.voiceCredits)) revert INVALID();

    _qv_allocate(allocator, recipient, recipientId, voiceCreditsToAllocate, _sender);
}
```

To check whether the allocator still has voice credits left to vote with, they call `_hasVoiceCreditsLeft`, which is defined as follows:
```solidity
function _hasVoiceCreditsLeft(uint256 _voiceCreditsToAllocate, uint256 _allocatedVoiceCredits)
    internal
    view
    override
    returns (bool)
{
    return _voiceCreditsToAllocate + _allocatedVoiceCredits <= maxVoiceCreditsPerAllocator;
}
```

As can be seen in these two function calls, `allocator.voiceCredits` is referenced as the previous amount that the allocator has voted in the past. However, this is never updated with the new amount that they have voted this call (`voiceCreditsToAllocate`), meaning they can continue calling this function & voting/allocating up to `maxVoiceCreditsPerAllocator` to a recipient per call, cheating the voting system.

## Impact

Allocators are able to vote an infinite number of times (~inf voice credits), which breaks the entire functionality of the QVSimpleStrategy contract. 

## Code Snippet

Referenced lines of code:
https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/qv-simple/QVSimpleStrategy.sol#L107-L124
https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/qv-simple/QVSimpleStrategy.sol#L144-L151

## Tool used

Manual Review

## Recommendation

At the end of the call to `_allocate` in QVSimpleStrategy, `allocator.voiceCredits` should be updated using the following rule: `allocator.voiceCredits += voiceCreditsToAllocate;`.