Clever Metal Giraffe

high

# `QVBaseStrategy._qv_allocate` miscalculate `voiceCreditsCastToRecipient`

`QVBaseStrategy._qv_allocate` is used to calculate the votes for quadratic voting. And it updates `voiceCreditsCastToRecipient[_recipientId]` to record the total credits that the allocator cast to the recipient. However, it calculates the total credits incorrectly.

## Vulnerability Detail

`QVBaseStrategy._qv_allocate` calculates the votes for quadratic voting. And it updates `_allocator.voiceCreditsCastToRecipient[_recipientId]`
https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/qv-base/QVBaseStrategy.sol#L529
```solidity
    function _qv_allocate(
        Allocator storage _allocator,
        Recipient storage _recipient,
        address _recipientId,
        uint256 _voiceCreditsToAllocate,
        address _sender
    ) internal onlyActiveAllocation {
        // check the `_voiceCreditsToAllocate` is > 0
        if (_voiceCreditsToAllocate == 0) revert INVALID();

        // get the previous values
        uint256 creditsCastToRecipient = _allocator.voiceCreditsCastToRecipient[_recipientId];
        uint256 votesCastToRecipient = _allocator.votesCastToRecipient[_recipientId];

        // get the total credits and calculate the vote result
        uint256 totalCredits = _voiceCreditsToAllocate + creditsCastToRecipient;
        uint256 voteResult = _sqrt(totalCredits * 1e18);

        // update the values
        voteResult -= votesCastToRecipient;
        totalRecipientVotes += voteResult;
        _recipient.totalVotesReceived += voteResult;

        _allocator.voiceCreditsCastToRecipient[_recipientId] += totalCredits;
        _allocator.votesCastToRecipient[_recipientId] += voteResult;

        // emit the event with the vote results
        emit Allocated(_recipientId, voteResult, _sender);
    }
```

Suppose `_allocator.voiceCreditsCastToRecipient[_recipientId]` is 100e18 and `_voiceCreditsToAllocate` is also 100e18:
```solidity
creditsCastToRecipient = _allocator.voiceCreditsCastToRecipient[_recipientId] = 100e18
totalCredits = _voiceCreditsToAllocate + creditsCastToRecipient = 100e18 + 100e18 = 200e18
_allocator.voiceCreditsCastToRecipient[_recipientId] += totalCredits; // _allocator.voiceCreditsCastToRecipient[_recipientId] = 100e18 + 200e18 = 300e18
```

At the end, `_allocator.voiceCreditsCastToRecipient[_recipientId]` becomes 300e18. But the total credits should be 200e18. And the allocator gets higher `totalCredits` next time doing allocation.

## Impact

`_allocator.voiceCreditsCastToRecipient[_recipientId]` would be higher than the actual value. The allocator has more votes. 


## Code Snippet

https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/qv-base/QVBaseStrategy.sol#L529

## Tool used

Manual Review

## Recommendation

Correct the calculation of new `_allocator.voiceCreditsCastToRecipient[_recipientId]`
```diff
    function _qv_allocate(
        Allocator storage _allocator,
        Recipient storage _recipient,
        address _recipientId,
        uint256 _voiceCreditsToAllocate,
        address _sender
    ) internal onlyActiveAllocation {
        // check the `_voiceCreditsToAllocate` is > 0
        if (_voiceCreditsToAllocate == 0) revert INVALID();

        // get the previous values
        uint256 creditsCastToRecipient = _allocator.voiceCreditsCastToRecipient[_recipientId];
        uint256 votesCastToRecipient = _allocator.votesCastToRecipient[_recipientId];

        // get the total credits and calculate the vote result
        uint256 totalCredits = _voiceCreditsToAllocate + creditsCastToRecipient;
        uint256 voteResult = _sqrt(totalCredits * 1e18);

        // update the values
        voteResult -= votesCastToRecipient;
        totalRecipientVotes += voteResult;
        _recipient.totalVotesReceived += voteResult;

-       _allocator.voiceCreditsCastToRecipient[_recipientId] += totalCredits;
+       _allocator.voiceCreditsCastToRecipient[_recipientId] = totalCredits;
        _allocator.votesCastToRecipient[_recipientId] += voteResult;

        // emit the event with the vote results
        emit Allocated(_recipientId, voteResult, _sender);
    }
```