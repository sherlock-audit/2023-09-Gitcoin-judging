Dandy Arctic Buffalo

high

# Unlimited voting permitted by allocators manipulates distribution of funds in quadratic voting strategies
## Summary
Voice credits, which translate to votes for eligible fund recipients, can be granted without limit. This allows vote manipulation that results in skewed distribution of funds.

## Vulnerability Detail
The [QVBaseStrategy.sol](https://github.com/allo-protocol/allo-v2/blob/main/contracts/strategies/qv-base/QVBaseStrategy.sol) contract uses designated "allocator" accounts to distribute an allotment of "voice credits" among candidates, those credits translate to votes via a square root calculation, and the proportion of votes per recipient determines the share of funds that each eligible ("accepted") recipient receives from the funding pool. 

There is an [attempt to limit the voice credits per allocator](https://github.com/allo-protocol/allo-v2/blob/851571c27df5c16f6586ece2a1cb6fd0acf04ec9/contracts/strategies/qv-simple/QVSimpleStrategy.sol#L104), but credits can actually be allocated without limit due to a logic error:  during allocation, an allocator's `voiceCredits` counter is not updated to indicate the credits spent.

The only restriction on each allocator is that they cannot allocate more than `maxVoiceCreditsPerAllocator` in one transaction. But, they are free to call `allocate()` repeatedly with any value up to the maximum.

## Impact
An allocator can give unlimited votes and unduly influence the payout distribution to recipients. A sybil attack where the attacker uses 2 accounts to act as an accepted recipient and as an allocator (or collusion between an accepted recipient and allocator), can swing the vote and payout distribution drastically in favor of the attacker(s), and thereby steal funds from all other accepted recipients.

## Code Snippet
[QVBaseStrategy._qv_allocate() function](https://github.com/allo-protocol/allo-v2/blob/851571c27df5c16f6586ece2a1cb6fd0acf04ec9/contracts/strategies/qv-base/QVBaseStrategy.sol#L499-L534) does not increase `_allocator.voiceCredits`:
```solidity
    /// @notice Allocate voice credits to a recipient
    /// @dev This can only be called during active allocation period
    /// @param _allocator The allocator details
    /// @param _recipient The recipient details
    /// @param _recipientId The ID of the recipient
    /// @param _voiceCreditsToAllocate The voice credits to allocate to the recipient
    /// @param _sender The sender of the transaction
    function _qv_allocate(
        Allocator storage _allocator,
        Recipient storage _recipient,
        address _recipientId,
        uint256 _voiceCreditsToAllocate,
        address _sender
    ) internal onlyActiveAllocation {
        // check the `_voiceCreditsToAllocate` is > 0
        if (_voiceCreditsToAllocate == 0) revert INVALID();

        // get the previous values
        uint256 creditsCastToRecipient = _allocator.voiceCreditsCastToRecipient[_recipientId];
        uint256 votesCastToRecipient = _allocator.votesCastToRecipient[_recipientId];

        // get the total credits and calculate the vote result
        uint256 totalCredits = _voiceCreditsToAllocate + creditsCastToRecipient;
        uint256 voteResult = _sqrt(totalCredits * 1e18);

        // update the values
        voteResult -= votesCastToRecipient;
        totalRecipientVotes += voteResult;
        _recipient.totalVotesReceived += voteResult;

        _allocator.voiceCreditsCastToRecipient[_recipientId] += totalCredits;
        _allocator.votesCastToRecipient[_recipientId] += voteResult;

        // emit the event with the vote results
        emit Allocated(_recipientId, voteResult, _sender);
    }
```
So, [this check in QVSimpleStrategy](https://github.com/allo-protocol/allo-v2/blob/851571c27df5c16f6586ece2a1cb6fd0acf04ec9/contracts/strategies/qv-simple/QVSimpleStrategy.sol#L150C5-L150C5) will always be called with the uninitialized value 0 for `allocator.voiceCredits` and thereby permit repeated calls to grant credits without regard for past allocations:
```solidity
        // check that the recipient has voice credits left to allocate
        if (!_hasVoiceCreditsLeft(voiceCreditsToAllocate, allocator.voiceCredits)) revert INVALID();
.
.
.

    /// @notice Checks if the allocator has voice credits left
    /// @param _voiceCreditsToAllocate The voice credits to allocate
    /// @param _allocatedVoiceCredits The allocated voice credits
    /// @return true if the allocator has voice credits left
    function _hasVoiceCreditsLeft(uint256 _voiceCreditsToAllocate, uint256 _allocatedVoiceCredits)
        internal
        view
        override
        returns (bool)
    {
        return _voiceCreditsToAllocate + _allocatedVoiceCredits <= maxVoiceCreditsPerAllocator;
    }
```

## Tool used

Manual Review

## Recommendation
Add to `_qv_allocate()`:
```solidity
_allocator.voiceCredits += _voiceCreditsToAllocate;
```
Note that `QVBaseStrategyTest` uses `QVBaseStrategyTestMock` which has an implementation of `_hasVoiceCreditsLeft()` that always returns `true`. So, it is not currently capable of testing the `maxVoiceCreditsPerAllocator` limit.
