Glamorous Hazelnut Haddock

medium

# Registering new recipients will corrupt the status bitmap in `DonationVotingMerkleDistribution` strategies
## Summary
The status bitmap will be corrupted due to incorrect retrieval of the status for new recipients.

## Vulnerability Detail
When registering a recipient for `DonationVotingMerkleDistribution` strategies, `currentStatus` is retrieved using `_getUintRecipientStatus`. The `colIndex` and `currentRow` are retrieved using `_getStatusRowColumn`.
https://github.com/sherlock-audit/2023-09-Gitcoin/blob/6430c8004017e96ae2f5aac365bdefd0b6eeea72/allo-v2/contracts/strategies/donation-voting-merkle-base/DonationVotingMerkleDistributionBaseStrategy.sol#L833-L840
```solidity
    function _getStatusRowColumn(address _recipientId) internal view returns (uint256, uint256, uint256) {
        uint256 recipientIndex = recipientToStatusIndexes[_recipientId];

        uint256 rowIndex = recipientIndex / 64; // 256 / 4
        uint256 colIndex = (recipientIndex % 64) * 4;

        return (rowIndex, colIndex, statusesBitMap[rowIndex]);
    }
```
For new recipients, `recipientIndex = recipientToStatusIndexes[_recipientId] = 0`. Consequently, `rowIndex = colIndex = 0`, so `currentRow` (the third return value) will always be the first row in the bitmap.

Back in `_getUintRecipientStatus`, `status` is calculated as the first 4 (least significant) bits of `currentRow` shifted right by `colIndex` bits.
https://github.com/sherlock-audit/2023-09-Gitcoin/blob/6430c8004017e96ae2f5aac365bdefd0b6eeea72/allo-v2/contracts/strategies/donation-voting-merkle-base/DonationVotingMerkleDistributionBaseStrategy.sol#L819-L828
```solidity
    function _getUintRecipientStatus(address _recipientId) internal view returns (uint8 status) {
        // Get the column index and current row
        (, uint256 colIndex, uint256 currentRow) = _getStatusRowColumn(_recipientId);

        // Get the status from the 'currentRow' shifting by the 'colIndex'
        status = uint8((currentRow >> colIndex) & 15);

        // Return the status
        return status;
    }
```
Since `colIndex = 0` for a new recipient, `status` will always be the first 4 bits of `currentRow` (ie. that of the first recipient registered for the strategy). This means the perceived status of a new recipient will depend on the status of the first registered recipient (note that `recipientsCounter` used as the index of a recipient's status starts from 0).

Back in `_registerRecipient`, execution differs based on whether the recipient's status is `Status.None` (ie. not registered). For new recipients after the first registration, the second branch (for already registered recipients) will always be executed since the first registered recipient's status cannot be `Status.None` (without a pool manager explicitly setting it, which shouldn't happen).
https://github.com/sherlock-audit/2023-09-Gitcoin/blob/6430c8004017e96ae2f5aac365bdefd0b6eeea72/allo-v2/contracts/strategies/donation-voting-merkle-base/DonationVotingMerkleDistributionBaseStrategy.sol#L582-L601
```solidity
        if (currentStatus == uint8(Status.None)) {
            // recipient registering new application
            recipientToStatusIndexes[recipientId] = recipientsCounter;
            _setRecipientStatus(recipientId, uint8(Status.Pending));

            bytes memory extendedData = abi.encode(_data, recipientsCounter);
            emit Registered(recipientId, extendedData, _sender);

            recipientsCounter++;
        } else {
            if (currentStatus == uint8(Status.Accepted)) {
                // recipient updating accepted application
                _setRecipientStatus(recipientId, uint8(Status.Pending));
            } else if (currentStatus == uint8(Status.Rejected)) {
                // recipient updating rejected application
                _setRecipientStatus(recipientId, uint8(Status.Appealed));
            }
            emit UpdatedRegistration(recipientId, _data, _sender, _getUintRecipientStatus(recipientId));
        }
    }
```
In the second branch, `recipientToStatusIndexes` will not be set for the new `recipientId` (and `recipientsCounter` is not incremented). Consequently, their status will continue to depend on/point to the first registered recipient's status, which could result in them unintentionally being eligible for allocation. Otherwise, if the first registered recipient's status is `Accepted`, any attempt at reregistering will change the status of the first registered recipient to `Pending`, denying allocation.

## PoC
Modified from `test_reviewRecipients` in `DonationVotingMerkleDistributionBase.t.sol`. It demonstrates that subsequent registrations will modify the first registered recipient's status.
```solidity
function test_reviewRecipients() public {
        address recipientId = __register_accept_recipient();
        assertEq(strategy.statusesBitMap(0), 2);
        __register_recipient2();

        // first registered recipient's status changed to pending even though only second recipient's status should be changed
        assertEq(strategy.statusesBitMap(0), 1);
        IStrategy.Status recipientStatus = strategy.getRecipientStatus(recipientId);
        assertEq(uint8(recipientStatus), uint8(IStrategy.Status.Pending));
    }
```

## Impact
All queried statuses will depend on the first registered recipient, and all attempts to register new recipients after the first will always only update the first registered recipient's status. Anyone can allocate to any registered recipient if the first registered recipient is accepted, and no recipient can be allocated to if the first registered recipient is not accepted.

## Code Snippet
https://github.com/sherlock-audit/2023-09-Gitcoin/blob/6430c8004017e96ae2f5aac365bdefd0b6eeea72/allo-v2/contracts/strategies/donation-voting-merkle-base/DonationVotingMerkleDistributionBaseStrategy.sol#L833-L840
https://github.com/sherlock-audit/2023-09-Gitcoin/blob/6430c8004017e96ae2f5aac365bdefd0b6eeea72/allo-v2/contracts/strategies/donation-voting-merkle-base/DonationVotingMerkleDistributionBaseStrategy.sol#L819-L828

## Tool used

Manual Review

## Recommendation
Consider determining whether a `recipientId` is new by checking whether `recipient.recipientAddress = 0` before setting it.