Uneven Holographic Llama

high

# Flawed Allocation Logic Potentially Doubles Voice Credits to Recipients in QVBaseStrategy
The _qv_allocate function within the QVBaseStrategy contract contains a logical flaw that doubles the voiceCredits already cast (_allocator.voiceCreditsCastToRecipient[_recipientId]) to a recipient by an allocator whenever an allocator attemps to add more voice credits.  

## Vulnerability Detail
When allocating credits to a recipient, the _qv_allocate function call is the last call at the operations flow. It is crucial in ensuring the proper values are accounted for and state updates conform to the business logic.
The function first calculates totalCredits as the sum of _voiceCreditsToAllocate and creditsCastToRecipient (which is the previously allocated credits). 

```solidity
uint256 creditsCastToRecipient = _allocator.voiceCreditsCastToRecipient[_recipientId];
uint256 totalCredits = _voiceCreditsToAllocate + creditsCastToRecipient;
...
_allocator.voiceCreditsCastToRecipient[_recipientId] += totalCredits;

```

Later in the function, it then adds totalCredits to the initial creditsCastToRecipient, meaning the sum is:
$allocator.voiceCreditsCastToRecipient[recipientId] + voiceCreditsToAllocate + allocator.voiceCreditsCastToRecipient[recipientId]$

This approach effectively doubles the voice credits allocated to a recipient, as it sums the previously allocated credits with the new ones and then adds the voice credits allocated to a recipient again.

## Impact
This can allow users to allocate more voice credits than they should be able to, thereby potentially manipulating the outcome of a vote.

## Code Snippet
[From QVBaseStrategy.sol:](https://github.com/sherlock-audit/2023-09-Gitcoin/blob/6430c8004017e96ae2f5aac365bdefd0b6eeea72/allo-v2/contracts/strategies/qv-base/QVBaseStrategy.sol#L506-L534)
```solidity
 function _qv_allocate(
        Allocator storage _allocator,
        Recipient storage _recipient,
        address _recipientId,
        uint256 _voiceCreditsToAllocate,
        address _sender
    ) internal onlyActiveAllocation {
        // check the `_voiceCreditsToAllocate` is > 0
        if (_voiceCreditsToAllocate == 0) revert INVALID();

        // get the previous values
        uint256 creditsCastToRecipient = _allocator.voiceCreditsCastToRecipient[_recipientId];
        uint256 votesCastToRecipient = _allocator.votesCastToRecipient[_recipientId];

        // get the total credits and calculate the vote result
        uint256 totalCredits = _voiceCreditsToAllocate + creditsCastToRecipient;
        uint256 voteResult = _sqrt(totalCredits * 1e18);

        // update the values
        voteResult -= votesCastToRecipient;
        totalRecipientVotes += voteResult;
        _recipient.totalVotesReceived += voteResult;

        _allocator.voiceCreditsCastToRecipient[_recipientId] += totalCredits;
        _allocator.votesCastToRecipient[_recipientId] += voteResult;

        // emit the event with the vote results
        emit Allocated(_recipientId, voteResult, _sender);
    }
```

## Tool used

Manual Review

## Recommendation
Correct the logic to ensure that voice credits aren't being doubled. One potential fix is to set the voiceCreditsCastToRecipient by _voiceCreditsToAllocate to equal totalCredits instead of summing totalCredits to this value:
```solidity
_allocator.voiceCreditsCastToRecipient[_recipientId] = totalCredits;
```