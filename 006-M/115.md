Clumsy Pecan Jay

high

# RFP payment cannot be fulfilled because of a validation in `_distribute`
## Summary

The `_distribute` function checks if the `proposalBid` of the recipient is higher then the `poolAmount`.
However, in every distribution for a milestone the `poolAmount` is decreased while `proposalBid` is not. 
This puts the contract in an insolvant state that it cannot payout the rewards for the RFP even if there are sufficient funds.  

## Vulnerability Detail
https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/rfp-simple/RFPSimpleStrategy.sol#L417
```solidity
    function _distribute(address[] memory, bytes memory, address _sender)
        internal
        virtual
        override
        onlyInactivePool
        onlyPoolManager(_sender)
    {
-----
        // make sure has enough funds to distribute based on the proposal bid
        if (recipient.proposalBid > poolAmount) revert NOT_ENOUGH_FUNDS();
-----
        // Get the pool, subtract the amount and transfer to the recipient
        poolAmount -= amount;
-----
    }
```

As can be seen in the above `_distribute` function - every time it is called there is a check to see if `recipient.proposalBid > poolAmount)`.

* `recipient.proposalBid` - Stays the same. This is the original `proposalBid` of the accepted recipient.
* `poolAmount` - Decreases by the amount of distribution on every call to `_distribute`

Because `poolAmount` is decreased and `recipient.proposalBid` is not - after a few calls to distribute funds to accepted milestone submission it will not be possible anymore and the contract will not be able to pay for the submission.

## Impact

Protocol insolvency 

## Code Snippet

Add the following test to `RFPSimpleStrategy.t.sol`
```solidity
    function test_cannotDistributePayment() external {
        uint256 poolTotalBeforeFee = 100 ether;
        uint256 percentFee =  (poolTotalBeforeFee * 1e16) / 1e18;
        uint256 poolTotal = poolTotalBeforeFee - percentFee;

        // Fund pool with ~100 ether
        vm.deal(pool_admin(), 100 ether);
        vm.prank(pool_admin());
        allo().fundPool{value: poolTotalBeforeFee}(poolId, poolTotalBeforeFee);

        // Increase max bid to ~100 ether
        vm.prank(pool_admin());
        strategy.increaseMaxBid(poolTotal);

        // Register with proposalBid of ~100 ether
        address sender = recipient();
        Metadata memory metadata = Metadata({protocol: 1, pointer: "metadata"});
        bytes memory data = abi.encode(recipientAddress(), false, poolTotal, metadata);
        vm.prank(address(allo()));
        address recipientIdReceived = strategy.registerRecipient(data, sender);

        // Set Milestones (two)
        __setMilestones();

        // Allocate
        vm.prank(address(allo()));
        strategy.allocate(abi.encode(recipientIdReceived), address(pool_admin()));

        // Submit milestone #1
        vm.prank(recipient());
        strategy.submitUpcomingMilestone(Metadata({protocol: 1, pointer: "metadata"}));

        // Distribute
        vm.prank(address(allo()));
        strategy.distribute(new address[](0), "", pool_admin());

        // Submit milestone #2
        vm.prank(recipient());
        strategy.submitUpcomingMilestone(Metadata({protocol: 1, pointer: "metadata"}));

        // Distribute (this time will revert with NOT_ENOUGH_FUNDS)
        vm.prank(address(allo()));
        vm.expectRevert(NOT_ENOUGH_FUNDS.selector);
        strategy.distribute(new address[](0), "", pool_admin());
    }
```

To execute the test run the command:
```solidity
forge test --match-test "test_cannotDistributePayment" -v  
```

Expected output:
```solidity
Running 1 test for test/foundry/strategies/RFPSimpleStrategy.t.sol:RFPSimpleStrategyTest
[PASS] test_cannotDistributePayment() (gas: 611834)
Test result: ok. 1 passed; 0 failed; 0 skipped; finished in 4.77ms
 
Ran 1 test suites: 1 tests passed, 0 failed, 0 skipped (1 total tests)
```
## Tool used

Manual Review

## Recommendation

Either decrease `proposalBid` by the amount paid or track the previous amount paid in a new mapping and add it to the if statement
