Glamorous Hazelnut Haddock

medium

# `_registerRecipient` always reverts if `useRegistryAnchor = true` in `RFPSimpleStrategy`
`_registerRecipient` always reverts if `useRegistryAnchor = true` in `RFPSimpleStrategy` due to uninitialised `recipientAddress`.

## Vulnerability Detail
In `_registerRecipient`, if `useRegistryAnchor = true`, `recipientAddress` will remain `0` since it's only initialised in the other branch (and nowhere else).
https://github.com/sherlock-audit/2023-09-Gitcoin/blob/6430c8004017e96ae2f5aac365bdefd0b6eeea72/allo-v2/contracts/strategies/rfp-simple/RFPSimpleStrategy.sol#L314-L362
```solidity
    function _registerRecipient(bytes memory _data, address _sender)
        internal
        override
        onlyActivePool
        returns (address recipientId)
    {
        bool isUsingRegistryAnchor;
        address recipientAddress;
        address registryAnchor;
        uint256 proposalBid;
        Metadata memory metadata;

        // Decode '_data' depending on the 'useRegistryAnchor' flag
        if (useRegistryAnchor) {
            /// @custom:data when 'true' -> (address recipientId, uint256 proposalBid, Metadata metadata)
            (recipientId, proposalBid, metadata) = abi.decode(_data, (address, uint256, Metadata));

            // If the sender is not a profile member this will revert
            if (!_isProfileMember(recipientId, _sender)) revert UNAUTHORIZED();
        } else {
            //  @custom:data when 'false' -> (address recipientAddress, address registryAnchor, uint256 proposalBid, Metadata metadata)
            (recipientAddress, registryAnchor, proposalBid, metadata) =
                abi.decode(_data, (address, address, uint256, Metadata));

            // Check if the registry anchor is valid so we know whether to use it or not
            isUsingRegistryAnchor = registryAnchor != address(0);

            // Ternerary to set the recipient id based on whether or not we are using the 'registryAnchor' or '_sender'
            recipientId = isUsingRegistryAnchor ? registryAnchor : _sender;

            // Checks if the '_sender' is a member of the profile 'anchor' being used and reverts if not
            if (isUsingRegistryAnchor && !_isProfileMember(recipientId, _sender)) revert UNAUTHORIZED();
        }
        ...
        // If the recipient address is the zero address this will revert
        if (recipientAddress == address(0)) revert RECIPIENT_ERROR(recipientId);
```
Consequently, the function will always revert on the zero address check for `recipientAddress`.

## Impact
`_registerRecipient` always reverts if `useRegistryAnchor = true` in `RFPSimpleStrategy`. Since `useRegistryAnchor` can only be set on initialisation, registrations will be permanently bricked in this case necessitating redeployment and pool creation. 

## Code Snippet
https://github.com/sherlock-audit/2023-09-Gitcoin/blob/6430c8004017e96ae2f5aac365bdefd0b6eeea72/allo-v2/contracts/strategies/rfp-simple/RFPSimpleStrategy.sol#L314-L362

## Tool used

Manual Review

## Recommendation
Consider retrieving `recipientAddress` from `data`.