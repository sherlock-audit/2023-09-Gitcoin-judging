Digital Berry Horse

high

# Using ERC20 tokens with fees on transfer may result in a loss of funds
Using ERC20 tokens with fees on transfer may result in a loss of funds since the _poolAmount_ will be miscalculated
## Vulnerability Detail
When using _fundPool()_ in _Allo.sol_, _amountAfterFee_ is transferred to the strategy contract and added to the strategy's _poolAmount_. The problem with this is that when using an ERC20 that has fees on transfer (such as USDT: even if the fee is 0 at the moment, this could change anytime), the strategy's balance will mismatch the strategy's _poolAmount_, leading to unexpected behaviour. 

Example with loss of funds: a QV Strategy is created and funded. Since the balance of the strategy mismatches the _poolAmount_, the _distribute_ function will fail. Since there is no withdraw function, funds will be locked. When using tokens as USDT we could send manually more USDT to the contract in order to be able to _distribute_, but if we are using a special ERC20 token that may used for governance, a DAO or something special, this may be impossible, causing the complete loss of funds. 

Here is a PoC. 
Mock ERC20 with fees on transfer, copying the code from the real USDT contract:

      
      import "@openzeppelin/contracts/token/ERC20/ERC20.sol";
      
      contract MockUSDT is ERC20 {
          uint256 public maximumFee = 10000e18;
          uint256 public basisPointsRate = 100;
      
          mapping(address => uint) public balances;
      
          constructor() ERC20("FakeUSDT", "FUSDT") {}
      
          function mint(address to, uint256 amount) public  {
              balances[to] += amount;
              _mint(to, amount);
          }
      
          function transferFrom(address _from, address _to, uint _value) public override returns (bool) {
              // Check is not needed because sub(_allowance, _value) will already throw if this condition is not met
              // if (_value > _allowance) throw;
      
              uint fee = (_value * basisPointsRate) / 10000;
              if (fee > maximumFee) {
                  fee = maximumFee;
              }
              uint sendAmount = _value - fee;
              balances[_from] = balances[_from] - _value;
              balances[_to] = balances[_to] + sendAmount;
              return true;
          }
      
          function balanceOf(address user) public override view returns(uint256) {
              return balances[user];
          }
      }

Test showing that _distribute_ will fail in a QV Strategy, which don't have _withdraw()_ functions:

    function test_distributePoolWithER20WithFeesOnTransfer() public {
        // Create a pool for QVBaseStrategy with a token that has fees on transfer. FakeUSDT in this case
        address strategyWithUSDT = address(new QVBaseStrategyTestMock(address(allo()), "MockStrategyWithUSDT"));
        vm.startPrank(pool_admin());
        uint256 usdtPoolId = allo().createPoolWithCustomStrategy(
            poolProfile_id(),
            strategyWithUSDT,
            abi.encode(
                registryGating,
                metadataRequired,
                2,
                registrationStartTime,
                registrationEndTime,
                allocationStartTime,
                allocationEndTime
            ),
            address(fakeUSDT),
            0 ether,
            poolMetadata,
            pool_managers()
        );
        vm.stopPrank();

        vm.startPrank(allo_owner());
        allo().updatePercentFee(0); // 0% so we can see that fees are from the token itself
        vm.stopPrank();

        vm.prank(pool_admin());
        fakeUSDT.approve(address(allo()), mintAmount);
        vm.warp(registrationStartTime + 10);

        // register
        vm.startPrank(address(allo()));
        bytes memory data = __generateRecipientWithoutId(false);
        address recipientId = QVBaseStrategy(strategyWithUSDT).registerRecipient(data, recipient1());
        vm.stopPrank();
        // accept
        address[] memory recipientIds = new address[](1);
        recipientIds[0] = recipientId;
        IStrategy.Status[] memory Statuses = new IStrategy.Status[](1);
        Statuses[0] = IStrategy.Status.Accepted;
        vm.startPrank(pool_admin());
        QVBaseStrategy(strategyWithUSDT).reviewRecipients(recipientIds, Statuses);
        vm.stopPrank();
        vm.startPrank(pool_manager1());
        QVBaseStrategy(strategyWithUSDT).reviewRecipients(recipientIds, Statuses);
        vm.stopPrank();
        vm.warp(registrationEndTime + 10);

        uint256[] memory amounts = new uint256[](1);
        amounts[0] = 100e18; // fund amount: 100e18

        console.log("***** BALANCES BEFORE FUNDING POOL *****");
        console.log("Pool admin Fake USDT balance: %d", fakeUSDT.balanceOf(address(pool_admin())));
        console.log("Strategy balance: %d", fakeUSDT.balanceOf(strategyWithUSDT));
        console.log("Strategy pool amount: %d", QVBaseStrategy(strategyWithUSDT).getPoolAmount());
        console.log("****************************************");

        // fund pool
        vm.prank(pool_admin());
        allo().fundPool(usdtPoolId, 100e18); // We fund the pool with a token that has fees on transfer
        vm.warp(allocationStartTime + 10);
        bytes memory allocation = __generateAllocation(recipientId, 4);
        vm.prank(address(allo()));
        address usdtReceiver = makeAddr("USDT receiver");
        QVBaseStrategy(strategyWithUSDT).allocate(allocation, usdtReceiver);
        vm.warp(allocationEndTime + 10);

        console.log("***** BALANCES AFTER FUNDING POOL *****");
        console.log("Pool admin Fake USDT balance before distribute: %d", fakeUSDT.balanceOf(address(pool_admin())));
        console.log("Strategy balance: %d", fakeUSDT.balanceOf(strategyWithUSDT));
        console.log("Strategy pool amount: %d", QVBaseStrategy(strategyWithUSDT).getPoolAmount());
        console.log("****************************************");

        vm.prank(address(allo()));
        QVBaseStrategy(strategyWithUSDT).distribute(recipientIds, "", pool_admin());
        // The line above files because there is no sufficient balance
    }

Output of running the test:
[FAIL. Reason: TransferFailed()] test_distributePoolWithER20WithFeesOnTransfer() (gas: 3082153)
Logs:
  ***** BALANCES BEFORE FUNDING POOL *****
  Pool admin Fake USDT balance: 100000000000000000000
  Strategy balance: 0
  Strategy pool amount: 0
  ****************************************
  ***** BALANCES AFTER FUNDING POOL *****
  Pool admin Fake USDT balance before distribute: 0
  Strategy balance: 99000000000000000000
  Strategy pool amount: 100000000000000000000
  ****************************************
## Impact
ERC20 tokens that use fees on transfer may be locked forever in QV Strategy contracts
## Code Snippet
Fund pool:
https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/core/Allo.sol#L496-L520
Distribute function which uses _getPayout() for calculating the distribution amounts:
https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/qv-base/QVBaseStrategy.sol#L433C1-L465C6
_getPayout() that miscalculates the amount since the balance and the _poolAmount_ are not the same:
https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/qv-base/QVBaseStrategy.sol#L556C1-L574C6
## Tool used

Manual Review and Foundry

## Recommendation
Check the balance of the pool before and after transferring the funded tokens, and add that amount to _poolAmount_.