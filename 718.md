Energetic Berry Llama

medium

# `QVBaseStrategy::reviewRecipients()` doesn't check if the manager already reviewed the recipient
## Summary
`QVBaseStrategy::reviewRecipients()` doesn't check if the manager already reviewed the recipient, and the same manager can review the same recipient.

## Vulnerability Detail
Pool managers can accept or reject the pending recipient applications with the `reviewRecipients()` function in the QV strategy contracts. Recipients must reach the `reviewThreshold` to be accepted. The review counts of the recipients are tracked with `reviewsByStatus` mapping.

However, the `QVBaseStrategy::reviewRecipients()` function doesn't check if the pool manager already reviewed that recipient, and increments the review count in the `reviewsByStatus` mapping.

[https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/qv-base/QVBaseStrategy.sol#L254C1-L288C6](https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/qv-base/QVBaseStrategy.sol#L254C1-L288C6)

```solidity
file: QVBaseStrategy.sol
    function reviewRecipients(address[] calldata _recipientIds, Status[] calldata _recipientStatuses)
        external
        virtual
        onlyPoolManager(msg.sender)
        onlyActiveRegistration
    {
        // make sure the arrays are the same length
        uint256 recipientLength = _recipientIds.length;
        if (recipientLength != _recipientStatuses.length) revert INVALID();

        for (uint256 i; i < recipientLength;) {
            Status recipientStatus = _recipientStatuses[i];
            address recipientId = _recipientIds[i];

            // if the status is none or appealed then revert
            if (recipientStatus == Status.None || recipientStatus == Status.Appealed) { 
                revert RECIPIENT_ERROR(recipientId);
            }

            reviewsByStatus[recipientId][recipientStatus]++;

            if (reviewsByStatus[recipientId][recipientStatus] >= reviewThreshold) { 
                Recipient storage recipient = recipients[recipientId];
                recipient.recipientStatus = recipientStatus;

                emit RecipientStatusUpdated(recipientId, recipientStatus, address(0));
            }

            emit Reviewed(recipientId, recipientStatus, msg.sender);

            unchecked {
                ++i;
            }
        }
    }
```

As you can see above, there is no check in the function in terms of which manager reviewed which recipient.

## Coded PoC

You can prove the issue using the protocol's own test setup with this PoC below.  
\- Copy the snippet and paste it into the `QVBaseStrategy.t.sol` test file.  
\- run `forge test --match-test test_reviewRecipient_ReviewMultipleTimes_WithSameManager`

```solidity
    // Review the same recipient with the same manager.    
    function test_reviewRecipient_ReviewMultipleTimes_WithSameManager() public virtual {
        address recipientId = __register_recipient();

        // Create the status
        address[] memory recipientIds = new address[](1);
        recipientIds[0] = recipientId;
        IStrategy.Status[] memory Statuses = new IStrategy.Status[](1);
        Statuses[0] = IStrategy.Status.Accepted;

        // Accept two times with the same manager
        vm.startPrank(pool_manager1());
        qvStrategy().reviewRecipients(recipientIds, Statuses);
        qvStrategy().reviewRecipients(recipientIds, Statuses);

        // Status will be updated.
        assertEq(uint8(qvStrategy().getRecipientStatus(recipientId)), uint8(IStrategy.Status.Accepted));
        assertEq(qvStrategy().reviewsByStatus(recipientId, IStrategy.Status.Accepted), 2);
    }
```

The test result is shown below:

```solidity
Running 1 test for test/foundry/strategies/QVSimpleStrategy.t.sol:QVSimpleStrategyTest
[PASS] test_reviewRecipient_ReviewMultipleTimes_WithSameManager() (gas: 174977)
Test result: ok. 1 passed; 0 failed; 0 skipped; finished in 15.88ms
```

## Impact
The recipient can be reviewed multiple times by the same manager which can change the recipient status.

## Code Snippet
[https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/qv-base/QVBaseStrategy.sol#L254C1-L288C6](https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/qv-base/QVBaseStrategy.sol#L254C1-L288C6)

```solidity
file: QVBaseStrategy.sol
    function reviewRecipients(address[] calldata _recipientIds, Status[] calldata _recipientStatuses)
        external
        virtual
        onlyPoolManager(msg.sender)
        onlyActiveRegistration
    {
        // make sure the arrays are the same length
        uint256 recipientLength = _recipientIds.length;
        if (recipientLength != _recipientStatuses.length) revert INVALID();

        for (uint256 i; i < recipientLength;) {
            Status recipientStatus = _recipientStatuses[i];
            address recipientId = _recipientIds[i];

            // if the status is none or appealed then revert
            if (recipientStatus == Status.None || recipientStatus == Status.Appealed) { 
                revert RECIPIENT_ERROR(recipientId);
            }

            reviewsByStatus[recipientId][recipientStatus]++;

            if (reviewsByStatus[recipientId][recipientStatus] >= reviewThreshold) { 
                Recipient storage recipient = recipients[recipientId];
                recipient.recipientStatus = recipientStatus;

                emit RecipientStatusUpdated(recipientId, recipientStatus, address(0));
            }

            emit Reviewed(recipientId, recipientStatus, msg.sender);

            unchecked {
                ++i;
            }
        }
    }
```

## Tool used

Manual Review

## Recommendation
I would recommend adding a new mapping that checks if a manager reviewed the recipient or not, and using it in the function
