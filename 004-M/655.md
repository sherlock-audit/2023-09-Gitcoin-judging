Tart Citron Platypus

high

# `QVBaseStrategy._qv_allocate()` mistakenly included `_allocator.voiceCreditsCastToRecipient[_recipientId]`, causing `_recipient.totalVotesReceived` to increase.

## Vulnerability Detail

L529 should use `=` instead of `+=`, because the right side is a new value (from _voiceCreditsToAllocate + creditsCastToRecipient from L521), rather than an increment `_voiceCreditsToAllocate`.

This caused an overcount of `_recipient.totalVotesReceived` the next time based on L517 and L521.


## Impact

## Code Snippet

https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/qv-base/QVBaseStrategy.sol#L499-L534


## Tool used

Manual Review

## Recommendation

Consider changing to:

```diff
    /// @notice Allocate voice credits to a recipient
    /// @dev This can only be called during active allocation period
    /// @param _allocator The allocator details
    /// @param _recipient The recipient details
    /// @param _recipientId The ID of the recipient
    /// @param _voiceCreditsToAllocate The voice credits to allocate to the recipient
    /// @param _sender The sender of the transaction
    function _qv_allocate(
        Allocator storage _allocator,
        Recipient storage _recipient,
        address _recipientId,
        uint256 _voiceCreditsToAllocate,
        address _sender
    ) internal onlyActiveAllocation {
        // check the `_voiceCreditsToAllocate` is > 0
        if (_voiceCreditsToAllocate == 0) revert INVALID();

        // get the previous values
        uint256 creditsCastToRecipient = _allocator.voiceCreditsCastToRecipient[_recipientId];
        uint256 votesCastToRecipient = _allocator.votesCastToRecipient[_recipientId];

        // get the total credits and calculate the vote result
        uint256 totalCredits = _voiceCreditsToAllocate + creditsCastToRecipient;
        uint256 voteResult = _sqrt(totalCredits * 1e18);

        // update the values
        voteResult -= votesCastToRecipient;
        totalRecipientVotes += voteResult;
        _recipient.totalVotesReceived += voteResult;

-        _allocator.voiceCreditsCastToRecipient[_recipientId] += totalCredits;
+        _allocator.voiceCreditsCastToRecipient[_recipientId] = totalCredits;
        _allocator.votesCastToRecipient[_recipientId] += voteResult;

        // emit the event with the vote results
        emit Allocated(_recipientId, voteResult, _sender);
    }
```