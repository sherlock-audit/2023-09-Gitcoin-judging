Brief Mahogany Tiger

medium

# If the `baseFee` is configured and the pool's token is `NATIVE`, the pool creator is forced to send more tokens apart the tokens needed to fund the pool
## Summary

The pool's creator needs to send more tokens in the pool creation, apart the tokens needed to fund the pool, when the baseFee is configured and the pool's token is `NATIVE` causing that the `pool creator` to lose the surplus sent amount.

## Vulnerability Detail

The [Allo::_createPool()](https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/core/Allo.sol#L415) is used to create a pool. The function receives the [_amount](https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/core/Allo.sol#L420) parameter which is the token to be deposited into the pool. The problem is that when the `baseFee` is configured, the function validates that the user send more tokens than the needed, the [code line 473](https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/core/Allo.sol#L473C13-L473C120) evaluates that the user needs to send more `msg.value` when the token is native:

```solidity
File: Allo.sol
415:     function _createPool(
416:         bytes32 _profileId,
417:         IStrategy _strategy,
418:         bytes memory _initStrategyData,
419:         address _token,
420:         uint256 _amount,
421:         Metadata memory _metadata,
422:         address[] memory _managers
423:     ) internal returns (uint256 poolId) {
...
...
...
469:         if (baseFee > 0) {
470:             // To prevent paying the baseFee from the Allo contract's balance
471:             // If _token is NATIVE, then baseFee + _amount should be >= than msg.value.
472:             // If _token is not NATIVE, then baseFee should be >= than msg.value.
473:             if ((_token == NATIVE && (baseFee + _amount >= msg.value)) || (_token != NATIVE && baseFee >= msg.value)) {
474:                 revert NOT_ENOUGH_FUNDS();
475:             }
476:             _transferAmount(NATIVE, treasury, baseFee);
477:             emit BaseFeePaid(poolId, baseFee);
478:         }
479: 
480:         if (_amount > 0) {
481:             _fundPool(_amount, poolId, _strategy);
482:         }
483: 
484:         emit PoolCreated(poolId, _profileId, _strategy, _token, _amount, _metadata);
485:     }
```

I created a test where the pool's token is `NATIVE` and the `baseFee` is configured to `1e17`. Then the user wants to create the pool and using NATIVE tokens to deposit into the pool (`1e18`). The `createPoolWithCustomStrategy()` will be reverted by `NOT_ENOUGH_FUNDS` error. The user needs to send the 1e18 + 1e17 + a surplus amount in order to create the pool. That is incorrect because the user will lost the surplus amount:

```solidity
// File: test/foundry/core/Allo.t.sol:AlloTest
// $ forge test --match-test "test_createPoolWithBaseFeeAndAmount" -vvv
//
    function test_createPoolWithBaseFeeAndAmount() public {
        // Pool creator needs to send more tokens in the creation process.
        //
        //
        // Update baseFee to 1e17.
        uint256 baseFee = 1e17;
        allo().updateBaseFee(baseFee);
        //
        // Give to the pool_admin 10e18 tokens so he can have funds.
        vm.deal(address(pool_admin()), 10e18);
        //
        // Send 1e18 (amountToFund) to fund the pool + 1e17 as fees.
        // The transaction will be reverted by NOT_ENOUGH_FUNDS error.
        // That is incorrect because the user is obligated to send more funds and lose the surplus amount
        uint256 amountToFund = 1e18;
        vm.startPrank(pool_admin());
        vm.expectRevert(NOT_ENOUGH_FUNDS.selector);
        allo().createPoolWithCustomStrategy{value: amountToFund + baseFee}(
            poolProfile_id(),
            strategy,
            "0x",
            NATIVE,
            amountToFund,  // 1e18 amount to fund
            metadata,
            pool_managers()
        );
        //
        // Send a little more funds suplusAmount (1e10) in order to NOT get reverted by the creationPool
        uint256 suplusAmount = 1e10;
        allo().createPoolWithCustomStrategy{value: amountToFund + baseFee + suplusAmount}(
            poolProfile_id(),
            strategy,
            "0x",
            NATIVE,
            amountToFund,  // 1e18 amount to fund
            metadata,
            pool_managers()
        );
        //
        // pool_admin loses suplusAmount (1e10)
        assertEq(pool_admin().balance, 10e18 - amountToFund - baseFee - suplusAmount);
        //
        // Strategy balance is the amountToFund (1e18) - feeAmount
        uint256 feeAmt = (amountToFund * 1e16) / 1e18;
        assertEq(strategy.balance, amountToFund - feeAmt); 
        //
        // Allo contract has the surplus amount (1e10)
        assertEq(address(allo()).balance, suplusAmount);
        vm.stopPrank();
    }
```
I added the next line in the `MockStrategy.sol` contract:

```solidity
File: MockStrategy.sol
92:     receive() external payable {
93:     }
```

## Impact

User is forced to send more `NATIVE` tokens when the `baseFee` is configured causing the lost of the surplus sent amount. Users will lost `NATIVE` tokens.

## Code Snippet

- [Allo::_createPool()](https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/core/Allo.sol#L415)

## Tool used

Manual review

## Recommendation

Change the next validation so it is not necessary to send more tokens than the needed:

```diff
    function _createPool(
        bytes32 _profileId,
        IStrategy _strategy,
        bytes memory _initStrategyData,
        address _token,
        uint256 _amount,
        Metadata memory _metadata,
        address[] memory _managers
    ) internal returns (uint256 poolId) {
...
...
...
        if (baseFee > 0) {
            // To prevent paying the baseFee from the Allo contract's balance
            // If _token is NATIVE, then baseFee + _amount should be >= than msg.value.
            // If _token is not NATIVE, then baseFee should be >= than msg.value.
--          if ((_token == NATIVE && (baseFee + _amount >= msg.value)) || (_token != NATIVE && baseFee >= msg.value)) {
++          if ((_token == NATIVE && (baseFee + _amount > msg.value)) || (_token != NATIVE && baseFee > msg.value)) {
                revert NOT_ENOUGH_FUNDS();
            }
            _transferAmount(NATIVE, treasury, baseFee);
            emit BaseFeePaid(poolId, baseFee);
        }

        if (_amount > 0) {
            _fundPool(_amount, poolId, _strategy);
        }

        emit PoolCreated(poolId, _profileId, _strategy, _token, _amount, _metadata);
    }
```