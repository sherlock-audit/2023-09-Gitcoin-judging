Recumbent Citron Mustang

high

# Allocators can vote infinitely in QVSimpleStrategy
## Summary

In the QVSimpleStrategy the [`_allocate()`](https://github.com/sherlock-audit/2023-09-Gitcoin/blob/6430c8004017e96ae2f5aac365bdefd0b6eeea72/allo-v2/contracts/strategies/qv-simple/QVSimpleStrategy.sol#L107) function allows allocators to vote for a `recipient`. It checks that they have enough vote left but the storage variable used is never updated thus allowing them to vote infinitely.

## Vulnerability Detail

The [`_allocate()`](https://github.com/sherlock-audit/2023-09-Gitcoin/blob/6430c8004017e96ae2f5aac365bdefd0b6eeea72/allo-v2/contracts/strategies/qv-simple/QVSimpleStrategy.sol#L107) function allows allocators to vote for a `recipient`.

When casting the vote there is a check to make sure the allocator still has enough votes available. But this check will never revert as the vote already used are never saved in the `allocator.voiceCredits` storage variable, thus when checking it against `maxVoiceCreditsPerAllocator` it will always be less.

This result in allocators being able to cast vote up to `maxVoiceCreditsPerAllocator`value as much as they want.

## Impact

High. Allocators can vote infinitely and change the final result.

## Code Snippet

[Code with the error.](https://github.com/sherlock-audit/2023-09-Gitcoin/blob/6430c8004017e96ae2f5aac365bdefd0b6eeea72/allo-v2/contracts/strategies/qv-simple/QVSimpleStrategy.sol#L120C8-L121C101)

```solidity
 // check that the recipient has voice credits left to allocate
if (!_hasVoiceCreditsLeft(voiceCreditsToAllocate, allocator.voiceCredits)) revert INVALID();
```

Here is a poc that can be copy pasted in QVSimpleStrategy.t.sol.

```solidity
function test_allocate_multiple_times() public {
        //get recipient and allocator
        address recipientId = __register_accept_recipient();
        address allocator = randomAddress();

        //add allocator
        vm.prank(pool_manager2());
        qvSimpleStrategy().addAllocator(allocator);
        vm.warp(allocationStartTime + 10);

        //max vote is 100
        bytes memory allocateData = __generateAllocation(recipientId, 100);

        //vote a first time
        vm.startPrank(address(allo()));
        qvSimpleStrategy().allocate(allocateData, allocator);

        QVSimpleStrategy.Recipient memory _recipient = qvSimpleStrategy().getRecipient(recipientId);
        uint previousVote = _recipient.totalVotesReceived;

        //vote again even tho we shouldn't be able
        qvSimpleStrategy().allocate(allocateData, allocator);

        _recipient = qvSimpleStrategy().getRecipient(recipientId);
        uint newVote = _recipient.totalVotesReceived;

        //vote value for recipient increased
        assertLt(previousVote, newVote);
    }
```

## Tool used

Manual Review

## Recommendation

Update the storage variable `allocator.voiceCredits` when calling the `_allocate()` function.
