Fresh Indigo Platypus

high

# QVBaseStrategy._qv_allocate() does not calculate _allocator.voiceCreditsCastToRecipient[_recipientId] correctly, as a result, the votes for each recipient and thus payout for each recipient will not be calculated correctly.
## Summary

``QVBaseStrategy._qv_allocate()`` does not calculate ``_allocator.voiceCreditsCastToRecipient[_recipientId]`` correctly.  The main issue is at L517, the amount to increase should be ``_voiceCreditsToAllocate`` instead of ``totalCredits``. 

## Vulnerability Detail

``QVBaseStrategy._qv_allocate()`` will allocate a new amount of ``_voiceCreditsToAllocate`` to a recipient: 

[https://github.com/allo-protocol/allo-v2/blob/0b881ef4a0013d2809374c9ea69f4cf1288dfe62/contracts/strategies/qv-base/QVBaseStrategy.sol#L506-L534](https://github.com/allo-protocol/allo-v2/blob/0b881ef4a0013d2809374c9ea69f4cf1288dfe62/contracts/strategies/qv-base/QVBaseStrategy.sol#L506-L534)

While L517 retrieves the existing ``voiceCreditsCastToRecipient`` for  the recipient, L521 calculates the new total voice credits for the recipient. 

 The main issue is at L517, the amount to increase should be ``_voiceCreditsToAllocate`` instead of ``totalCredits``.  Alternatively, ``_allocator.voiceCreditsCastToRecipient[_recipientId]`` should be assigned with ``totalCredits`` instead of increasing it by ``totalCredits`` since ``totalCredits`` already includes the old value of ``_allocator.voiceCreditsCastToRecipient[_recipientId]``.

## Impact
``QVBaseStrategy._qv_allocate()`` does not calculate ``_allocator.voiceCreditsCastToRecipient[_recipientId]`` correctly, as a result, the votes for each recipient and thus payout for each recipient will not be calculated correctly. 

## Code Snippet

## Tool used
VScode

Manual Review

## Recommendation
We assign  ``_allocator.voiceCreditsCastToRecipient[_recipientId]`` with ``totalCredits`` instead of increasing it by ``totalCredits``:

```diff
function _qv_allocate(
        Allocator storage _allocator,
        Recipient storage _recipient,
        address _recipientId,
        uint256 _voiceCreditsToAllocate,
        address _sender
    ) internal onlyActiveAllocation {
        // check the `_voiceCreditsToAllocate` is > 0
        if (_voiceCreditsToAllocate == 0) revert INVALID();

        // get the previous values
        uint256 creditsCastToRecipient = _allocator.voiceCreditsCastToRecipient[_recipientId];
        uint256 votesCastToRecipient = _allocator.votesCastToRecipient[_recipientId];

        // get the total credits and calculate the vote result
        uint256 totalCredits = _voiceCreditsToAllocate + creditsCastToRecipient;
        uint256 voteResult = _sqrt(totalCredits * 1e18);

        // update the values
        voteResult -= votesCastToRecipient;
        totalRecipientVotes += voteResult;
        _recipient.totalVotesReceived += voteResult;

-        _allocator.voiceCreditsCastToRecipient[_recipientId] += totalCredits;
+       _allocator.voiceCreditsCastToRecipient[_recipientId] = totalCredits;
 
       _allocator.votesCastToRecipient[_recipientId] += voteResult;

        // emit the event with the vote results
        emit Allocated(_recipientId, voteResult, _sender);
    }
```