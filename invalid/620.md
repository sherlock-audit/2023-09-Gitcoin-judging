Orbiting Neon Wolf

medium

# `_createPool()` doesn't verify the existence of the token in the pool, which could potentially deceive users by displaying a counterfeit pool balance.
When a pool is created by profile owners or members using the `createPool` or `createPoolWithCustomStrategy` functions, they provide the pool's `token` address and the initial `amount` they wish to deposit into the pool. However, it's possible for them to input a non-contract address and a substantial `amount`, and the pool will still be created successfully. While this might not initially appear to be harmful, it's important to note that there are many token addresses that follow deterministic patterns. For instance, we can predict the address of the next Curve LP token to be generated or the next token to be minted by the Optimism Bridge. Consequently, profile owners or members can create a pool with a substantial amount (without actually transferring any tokens), and when the token contract is eventually deployed, they can claim a significant pool balance, potentially deceiving users.

## Vulnerability Detail
The functions `createPool` and `createPoolWithCustomStrategy` both invoke the internal function `_createPool()`. Under the hood, `_createPool()` subsequently calls `_fundPool`, which in turn utilizes the `_transferAmountFrom` function from the `Transfer` contract. This process employs Solady's `SafeTransferLib`. Notably, in situations where no return data is received, the function will pass and pool will be created with wrong amount.

```solidity
function safeTransferFrom(address token, address from, address to, uint256 amount) internal {
        /// @solidity memory-safe-assembly
        assembly {
            let m := mload(0x40) // Cache the free memory pointer.

            mstore(0x60, amount) // Store the `amount` argument.
            mstore(0x40, to) // Store the `to` argument.
            mstore(0x2c, shl(96, from)) // Store the `from` argument.
            // Store the function selector of `transferFrom(address,address,uint256)`.
            mstore(0x0c, 0x23b872dd000000000000000000000000)

            if iszero(
                and( // The arguments of `and` are evaluated from right to left.
                    // Set success to whether the call reverted, if not we check it either
                    // returned exactly 1 (can't just be non-zero data), or had no return data.
                    or(eq(mload(0x00), 1), iszero(returndatasize())),
                    call(gas(), token, 0, 0x1c, 0x64, 0x00, 0x20)
                )
            ) {
                // Store the function selector of `TransferFromFailed()`.
                mstore(0x00, 0x7939f424)
                // Revert with (offset, size).
                revert(0x1c, 0x04)
            }

            mstore(0x60, 0) // Restore the zero slot to zero.
            mstore(0x40, m) // Restore the free memory pointer.
        }
    }
```

## Impact
Profile owners or members can create pools with large balances without transferring any tokens to the pool

## Code Snippet
https://github.com/sherlock-audit/2023-09-Gitcoin/blob/6430c8004017e96ae2f5aac365bdefd0b6eeea72/allo-v2/contracts/core/Allo.sol#L480-L482
https://github.com/sherlock-audit/2023-09-Gitcoin/blob/6430c8004017e96ae2f5aac365bdefd0b6eeea72/allo-v2/contracts/core/libraries/Transfer.sol#L70-L93

## Tool used

Manual Review

## Recommendation
Add an explicit check in `_createPool` function that the token is a contract and not an EOA.

```diff
    function _createPool(
        bytes32 _profileId,
        IStrategy _strategy,
        bytes memory _initStrategyData,
        address _token,
        uint256 _amount,
        Metadata memory _metadata,
        address[] memory _managers
    ) internal returns (uint256 poolId) {
        ...
        if (_amount > 0) {
+            if(_token != NATIVE) {
+               uint size;
+               assembly {
+                   size := extcodesize(_token)
+               }
+               if(size == 0) {
+                    revert NOT_CONTRACT();
+               }
+            }
            _fundPool(_amount, poolId, _strategy);
        }
        ...
    }
```