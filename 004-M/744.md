Mini Garnet Squirrel

high

# Incorrect state update of variable `allocator.voiceCreditsCastToRecipient[_recipientId]`
The internal function` _qv_allocate` is responsible for allocating voice credits to recipients. However, there is a bug in the state update of the mapping `allocator.voiceCreditsCastToRecipient[_recipientId]`. Instead of updating it with `_voiceCreditsToAllocate`, the code incorrectly updates it with `totalCredits`, potentially leading to incorrect calculations of voice credits allocated to recipients.

## Vulnerability Detail
The bug is related to the internal function `_qv_allocate`, which is used to allocate voice credits to recipients within the contract. When the function updates the state variable `allocator.voiceCreditsCastToRecipient[_recipientId]`, it uses the ` totalCredits `variable for the update operation and `totalCredits ` is calculated by using previous values:
```solidity
// get the previous values
        uint256 creditsCastToRecipient = _allocator.voiceCreditsCastToRecipient[_recipientId];
        uint256 votesCastToRecipient = _allocator.votesCastToRecipient[_recipientId];

        // get the total credits and calculate the vote result
        uint256 totalCredits = _voiceCreditsToAllocate + creditsCastToRecipient;
// Incorrect state update
allocator.voiceCreditsCastToRecipient[_recipientId] += totalCredits;

```

## Impact

This bug can have a significant impact on the accuracy of voice credit allocation to recipients. By incorrectly updating the `voiceCreditsCastToRecipient` mapping, the contract may overestimate the total voice credits allocated to a recipient, leading to incorrect payouts or calculations based on the voice credits.

## Code Snippet
```solidity
function _qv_allocate(
        Allocator storage _allocator,
        Recipient storage _recipient,
        address _recipientId,
        uint256 _voiceCreditsToAllocate,
        address _sender
    ) internal onlyActiveAllocation {
        // check the `_voiceCreditsToAllocate` is > 0
        if (_voiceCreditsToAllocate == 0) revert INVALID();

        // get the previous values
        uint256 creditsCastToRecipient = _allocator.voiceCreditsCastToRecipient[_recipientId];
        uint256 votesCastToRecipient = _allocator.votesCastToRecipient[_recipientId];

        // get the total credits and calculate the vote result
        uint256 totalCredits = _voiceCreditsToAllocate + creditsCastToRecipient;
        uint256 voteResult = _sqrt(totalCredits * 1e18);

        // update the values
        voteResult -= votesCastToRecipient;
        totalRecipientVotes += voteResult;
        _recipient.totalVotesReceived += voteResult;

        _allocator.voiceCreditsCastToRecipient[_recipientId] += totalCredits;
        _allocator.votesCastToRecipient[_recipientId] += voteResult;

        // emit the event with the vote results
        emit Allocated(_recipientId, voteResult, _sender);
    }
```
https://github.com/sherlock-audit/2023-09-Gitcoin/blob/6430c8004017e96ae2f5aac365bdefd0b6eeea72/allo-v2/contracts/strategies/qv-base/QVBaseStrategy.sol#L529
## Tool used

Manual Review

## Recommendation
```solidity
allocator.voiceCreditsCastToRecipient[_recipientId] += _voiceCreditsToAllocate;
```
or should be `= totalCredits` instead of `+= totalCredits`