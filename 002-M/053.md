Urban Strawberry Monkey

medium

# Using fee-on-transfer ERC20 tokens leads to incorrect pool balance
## Summary
The `Allo` lacks support for fee-on-transfer tokens even though the contest guideline states it does.

When a pool is funded with a fee-on-transfer token, the actual number of tokens added to the pool strategy is lower than expected as `Allo` does not account for any charged fees. This discrepancy disrupts the accurate accounting of the pool and can lead to issues with allocations and distributions within the system.

## Vulnerability Detail
The internal `_fundPool()` function is used by `createPool()`, `createPoolWithCustomStrategy()` and `fundPool()` to perform pool funding either upon pool creation or subsequently.

The implementation of `_fundPool()` calculates the fee the Allo protocol charges and sends it to the protocol `treasury`. The remaining funds, known as `amountAfterFee`, get moved to the pool's strategy. Additionally, `amountAfterFee` plays a role in keeping track of the strategy's internal accounting by updating the pool balance via `_strategy.increasePoolAmount(amountAfterFee)`.:

https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/core/Allo.sol#L509-L517

When dealing with fee-on-transfer tokens like STA, PAXG, USDT, USDC, etc., the token protocol deducts a fee before sending the remaining amount to the recipient. This becomes problematic with the `Allo` contract because it doesn't account for this fee deduction, leading to inaccurate balance update in the pool strategy.

## Impact
The actual pool strategy balance is inaccurate (lower than expected) and this breaks subsequent activities dependant on it such as allocations, distributions, etc.

## Code Snippet
This is the problematic function, the actual vulnerability is on **line 517**.

https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/core/Allo.sol#L496-L520

## PoC
Here is a drop-in snippet of a PoC which demonstrates the issue and how the pool strategy's internal accounting is incorrectly calculated:

```solidity
// SPDX-License-Identifier: AGPL-3.0-only
pragma solidity ^0.8.19;

import "forge-std/Test.sol";

import {ERC20} from "openzeppelin-contracts/contracts/token/ERC20/ERC20.sol";
import {IAllo} from "../../../contracts/core/interfaces/IAllo.sol";
import {IStrategy} from "../../../contracts/core/interfaces/IStrategy.sol";
import {AlloTest} from "./Allo.t.sol";
import {MockStrategy} from "../../utils/MockStrategy.sol";

contract IncorrectPoolBalancePoC is AlloTest {

    function setUp() public override {
        super.setUp();
    }

    function test_incorrectPoolBalancePoC() public {
        // Deploy a simple fee-on-transfer token and mint 100 000 tokens to pool_admin()
        FeeOnTransferToken token = new FeeOnTransferToken(1_000_000 ether);
        token.mint(pool_admin(), 100_000 ether);
        console.log("\n=== Before Pool Creation ===");
        console.log("Pool admin balance:\t\t\t\t%s tokens", token.balanceOf(pool_admin()));
        
        // Create a new pool and fund it with all 100 000 tokens
        vm.startPrank(pool_admin());
        token.approve(address(allo()), type(uint256).max);
        uint256 poolId = allo().createPoolWithCustomStrategy(
            poolProfile_id(), strategy, "0x", address(token), 100_000 ether, metadata, pool_managers()
        );
        vm.stopPrank();

        IAllo.Pool memory poolWithBrokenBalance = allo().getPool(poolId);
        console.log("\n=== After Pool Creation ===");
        console.log("Pool admin balance:\t\t\t\t%s tokens", token.balanceOf(pool_admin()));
        console.log("Pool balance as per Strategy#getPoolAmount():\t%s tokens", poolWithBrokenBalance.strategy.getPoolAmount());
        console.log("Actual pool balance as per Token#balanceOf():\t%s tokens", token.balanceOf(address(poolWithBrokenBalance.strategy)));
    }
}

// Simplistic fee-on-transfer-token implementation
contract FeeOnTransferToken is ERC20 {
    uint256 private constant FEE_PERCENTAGE = 10;
    address private _owner;

    constructor(uint256 initialSupply) ERC20("FeeOnTransferToken", "FOTT") {
        _owner = msg.sender;
        _mint(_owner, initialSupply);
    }

    function transfer(address to, uint256 amount) public override returns (bool) {
        require(balanceOf(msg.sender) >= amount, "Insufficient balance");

        uint256 fee = amount * FEE_PERCENTAGE / 100;
        _transfer(msg.sender, to, amount - fee);
        _transfer(msg.sender, _owner, fee); // Send the fee to a designated address

        return true;
    }

    function transferFrom(address sender, address recipient, uint256 amount) public override returns (bool) {
        require(balanceOf(sender) >= amount, "Insufficient balance");

        uint256 fee = amount * FEE_PERCENTAGE / 100;
        _spendAllowance(sender, msg.sender, amount);
        _transfer(sender, recipient, amount - fee);
        _transfer(sender, _owner, fee);

        return true;
    }

    function mint(address account, uint256 amount) external {
        _mint(account, amount);
    }
}
```

## Tool used

Manual Review

## Recommendation

Transfer the tokens first and compare pre-/after token balances to compute the actual transferred amount, e.g.:
```solidity
uint256 poolBalanceBefore = _token.balanceOf(_strategy);
_transferAmountFrom(_token, TransferData({from: msg.sender, to: address(_strategy), amount: amountAfterFee})); 
uint256 poolBalanceAfter = _token.balanceOf(_strategy);
uint256 actualAmountDeposited = poolBalanceAfter - poolBalanceBefore;
_strategy.increasePoolAmount(actualAmountDeposited); 
```