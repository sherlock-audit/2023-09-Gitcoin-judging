Dandy Seafoam Shrimp

high

# Malicious user can manipulate the poolAmount leading to loss of funds for recipients.

Pools are designed so that they can be funded by any token the pool creator initiate them with. The pool fund is tracked via ```poolAmount``` in pool strategy contract, and this will be later used to distribute allocated funds to recipients addresses.However the pool amount can be manipulated and this will result in some recipient losing funds.
## Vulnerability Detail

Some tokens take a transfer fee (e.g. STA, PAXG), some do not currently charge a fee but may do so in the future (e.g. USDT, USDC).
The STA transfer fee was used to drain $500k from several balancer pools ([more details](https://medium.com/@1inch.exchange/balancer-hack-2020-a8f7131c980e)).

Using a token that take fee on transfer to create a pool and fund it, a pool creator may not know but the poolAmount will be manipulated by the token internal functionality (fee on transfer). Let's see a PoC to prove this:

First we implement a fee on transfer like token.
Copy and paste this code under a ERC20.sol file under test/utils
```solidity
// Copyright (C) 2017, 2018, 2019, 2020 dbrock, rain, mrchico, d-xo
// SPDX-License-Identifier: AGPL-3.0-only

pragma solidity >=0.6.12;

contract Math {
    // --- Math ---
    function add(uint x, uint y) internal pure returns (uint z) {
        require((z = x + y) >= x);
    }
    function sub(uint x, uint y) internal pure returns (uint z) {
        require((z = x - y) <= x);
    }
}

contract ERC20 is Math {
    // --- ERC20 Data ---
    string  public constant name = "Token";
    string  public constant symbol = "TKN";
    uint8   public decimals = 18;
    uint256 public totalSupply;

    mapping (address => uint)                      public balanceOf;
    mapping (address => mapping (address => uint)) public allowance;

    event Approval(address indexed src, address indexed guy, uint wad);
    event Transfer(address indexed src, address indexed dst, uint wad);

    // --- Init ---
    constructor(uint _totalSupply) public {
        totalSupply = _totalSupply;
        balanceOf[msg.sender] = _totalSupply;
        emit Transfer(address(0), msg.sender, _totalSupply);
    }

    // --- Token ---
    function transfer(address dst, uint wad) virtual public returns (bool) {
        return transferFrom(msg.sender, dst, wad);
    }
    function transferFrom(address src, address dst, uint wad) virtual public returns (bool) {
        require(balanceOf[src] >= wad, "insufficient-balance");
        if (src != msg.sender && allowance[src][msg.sender] != type(uint).max) {
            require(allowance[src][msg.sender] >= wad, "insufficient-allowance");
            allowance[src][msg.sender] = sub(allowance[src][msg.sender], wad);
        }
        balanceOf[src] = sub(balanceOf[src], wad);
        balanceOf[dst] = add(balanceOf[dst], wad);
        emit Transfer(src, dst, wad);
        return true;
    }
    function approve(address usr, uint wad) virtual public returns (bool) {
        allowance[msg.sender][usr] = wad;
        emit Approval(msg.sender, usr, wad);
        return true;
    }
}
```

Copy and paste the TransferFeeToken contract under another file under test/utils
```solidity
// Copyright (C) 2020 d-xo
// SPDX-License-Identifier: AGPL-3.0-only

pragma solidity >=0.6.12;

import {ERC20} from "./ERC20.sol";

contract TransferFeeToken is ERC20 {

    uint immutable fee;

    // --- Init ---
    constructor(uint _totalSupply, uint _fee) ERC20(_totalSupply)  {
        fee = _fee;
    }

    // --- Token ---
    function transferFrom(address src, address dst, uint wad) override public returns (bool) {
        require(balanceOf[src] >= wad, "insufficient-balance");
        if (src != msg.sender && allowance[src][msg.sender] != type(uint).max) {
            require(allowance[src][msg.sender] >= wad, "insufficient-allowance");
            allowance[src][msg.sender] = sub(allowance[src][msg.sender], wad);
        }

        balanceOf[src] = sub(balanceOf[src], wad);
        balanceOf[dst] = add(balanceOf[dst], sub(wad, fee));
        balanceOf[address(0)] = add(balanceOf[address(0)], fee);

        emit Transfer(src, dst, sub(wad, fee));
        emit Transfer(src, address(0), fee);

        return true;
    }
}
```

Now let's write a test to see how this affect the poolAmount.
Copy and paste this test under test/foundry/core/MyTests.t.sol

```solidity

// SPDX-License-Identifier: AGPL-3.0-only
pragma solidity ^0.8.19;

import "forge-std/Test.sol";

// Interfaces
import {IAllo} from "../../../contracts/core/interfaces/IAllo.sol";
import {IStrategy} from "../../../contracts/core/interfaces/IStrategy.sol";
// Core contracts
import {Allo} from "../../../contracts/core/Allo.sol";
import {Registry} from "../../../contracts/core/Registry.sol";
// Internal Libraries
import {Errors} from "../../../contracts/core/libraries/Errors.sol";
import {Metadata} from "../../../contracts/core/libraries/Metadata.sol";
import {Native} from "../../../contracts/core/libraries/Native.sol";
// Test libraries
import {AlloSetup} from "../shared/AlloSetup.sol";
import {RegistrySetupFull} from "../shared/RegistrySetup.sol";
import {TestStrategy} from "../../utils/TestStrategy.sol";
import {MockStrategy} from "../../utils/MockStrategy.sol";
import {MockERC20} from "../../utils/MockERC20.sol";
import {TransferFeeToken} from "../../utils/TransferFeeToken.sol";

contract MyTests is Test, AlloSetup, RegistrySetupFull, Native, Errors {
    event PoolCreated(
        uint256 indexed poolId,
        bytes32 indexed profileId,
        IStrategy strategy,
        address token,
        uint256 amount,
        Metadata metadata
    );
    event PoolMetadataUpdated(uint256 indexed poolId, Metadata metadata);
    event PoolFunded(uint256 indexed poolId, uint256 amount, uint256 fee);
    event BaseFeePaid(uint256 indexed poolId, uint256 amount);
    event TreasuryUpdated(address treasury);
    event PercentFeeUpdated(uint256 percentFee);
    event BaseFeeUpdated(uint256 baseFee);
    event RegistryUpdated(address registry);
    event StrategyApproved(address strategy);
    event StrategyRemoved(address strategy);

    error AlreadyInitialized();

    address public strategy;
    MockERC20 public token;

    uint256 mintAmount = 1000000 * 10 ** 18;

    Metadata public metadata = Metadata({protocol: 1, pointer: "strategy pointer"});
    string public name;
    uint256 public nonce;

    function setUp() public {
        __RegistrySetupFull();
        __AlloSetup(address(registry()));

        token = new MockERC20();
        token.mint(local(), mintAmount);
        token.mint(allo_owner(), mintAmount);
        token.mint(pool_admin(), mintAmount);
        token.approve(address(allo()), mintAmount);

        vm.prank(pool_admin());
        token.approve(address(allo()), mintAmount);

        strategy = address(new MockStrategy(address(allo())));

        vm.startPrank(allo_owner());
        allo().transferOwnership(local());
        vm.stopPrank();
    }



function test_createPoolWith_feeToken() public {

        uint256 baseFee = 1e17;
        uint256 newFee = 1e17;
        
        
        allo().updateBaseFee(baseFee);
        allo().updatePercentFee(newFee);
        vm.deal(address(pool_admin()), 1e18);
        
        vm.startPrank(pool_admin());
        // create a new tranferfee token with 100 fee on transfer and 1_000_000 totalSupply
        TransferFeeToken feeToken = new TransferFeeToken(1000000 * 1e18 , 100);
        assertEq(feeToken.balanceOf(pool_admin()), 1000000 *1e18  );
        // approve allo to spend the feetransfer token
        feeToken.approve(address(allo()), 1000000);
        
        // create a new pool with feetransfer token and funds it with 10000 feetoken
        // good path is baseFee get sent to treasury (1e17 native)
        // percentfee get sent to treasury (10% of 10000 = 1000)
        // and the amount after percent fee get sent to the pool strategy (9000)
        // but let's see how the 100 fee on token transfer will affect all this and manipulate the poolAmount
        allo().createPoolWithCustomStrategy{value: 1e18}(
            poolProfile_id(), strategy, "0x", address(feeToken), 10000, metadata, pool_managers()
        );
    }
}
```

Now run the test : forge test --match-path test/foundry/core/MyTests.t.sol -vvvvv

This test pass , and the output we are interest in is the following:


 ├─ [0] allo_treasury::fallback{value: 100000000000000000}()
    │   │   └─ ← ()
    │   ├─ emit BaseFeePaid(poolId: 1, amount: 100000000000000000 [1e17])
    │   ├─ [51265] TransferFeeToken::transferFrom(pool_admin: [0x6E1FEb7a47a8A2C71E61BA8e3c4D5243CB392019], allo_treasury: [0x11CDD8c4b40352E593942e66b1ccA5DC28E391B4], 1000)
    │   │   ├─ emit Transfer(src: pool_admin: [0x6E1FEb7a47a8A2C71E61BA8e3c4D5243CB392019], dst: allo_treasury: [0x11CDD8c4b40352E593942e66b1ccA5DC28E391B4], wad: 900)
    │   │   ├─ emit Transfer(src: pool_admin: [0x6E1FEb7a47a8A2C71E61BA8e3c4D5243CB392019], dst: 0x0000000000000000000000000000000000000000, wad: 100)
    │   │   └─ ← true
    │   ├─ [29365] TransferFeeToken::transferFrom(pool_admin: [0x6E1FEb7a47a8A2C71E61BA8e3c4D5243CB392019], MockStrategy: [0xF62849F9A0B5Bf2913b396098F7c7019b51A820a], 9000)
    │   │   ├─ emit Transfer(src: pool_admin: [0x6E1FEb7a47a8A2C71E61BA8e3c4D5243CB392019], dst: MockStrategy: [0xF62849F9A0B5Bf2913b396098F7c7019b51A820a], wad: 8900)
    │   │   ├─ emit Transfer(src: pool_admin: [0x6E1FEb7a47a8A2C71E61BA8e3c4D5243CB392019], dst: 0x0000000000000000000000000000000000000000, wad: 100)
    │   │   └─ ← true
    │   ├─ [22607] MockStrategy::increasePoolAmount(9000)
    │   │   └─ ← ()
    │   ├─ emit PoolFunded(poolId: 1, amount: 9000, fee: 1000)
    │   ├─ emit PoolCreated(poolId: 1, profileId: 0xb3c8f868e0a570b26fec0dcc52d639611aeb5fe816154b3054f3eb4402def182, strategy: MockStrategy: [0xF62849F9A0B5Bf2913b396098F7c7019b51A820a], token: TransferFeeToken: [0x26CEA884b83572cbb5ac873bc73542F5C093b93e], amount: 10000 [1e4], metadata: (1, strategy pointer))
    │   └─ ← 1
    └─ ← ()


We can see that the base fee ( 1e17 in ETH ) have been correctly paid.
instead of the treasury receiving a fee of 1000 feeToken , it received 900 feeToken ( This is because 100 transfer fee have been charged by feeToken contract and sent to 0 address ).
Instead of the pool strategy receiving amount after fee (9000 fee tokens), it receive 8900 fee token as 100 was charged by feeToken contract. The pool funds is increased by 9000 fee token instead of the correct 8900 fee token.
This situation lead to poolAmount being incorrect and because of that some recipients will not receive their allocated funds when a pool manager calls ```strategy.distribute()``` to distribute allocated funds.
When ```strategy.distribute()``` is called for example in RFPSimpleStrategy it deduct amount for the milestone from total pool Funds tracked by the state variable ```poolAmount```  and send the deducted amount to recipient, a recipient will not get his allocated funds 

## Impact

Lost of funds for the recipient

## Code Snippet

https://github.com/allo-protocol/allo-v2/blob/8a41a342a0de7a2d5d7dbc5395d1da44cb811348/contracts/core/Allo.sol#L144-L161

https://github.com/allo-protocol/allo-v2/blob/main/contracts/core/Allo.sol#L415-L485

https://github.com/allo-protocol/allo-v2/blob/8a41a342a0de7a2d5d7dbc5395d1da44cb811348/contracts/core/Allo.sol#L502-L520

https://github.com/allo-protocol/allo-v2/blob/8a41a342a0de7a2d5d7dbc5395d1da44cb811348/contracts/strategies/rfp-simple/RFPSimpleStrategy.sol#L417-L450
## Tool used

VsCode
Manual Review

## Recommendation

Instead of increasing the pool Amount by amount sent into the pool, account the total pool amount with the pool strategy token balance.