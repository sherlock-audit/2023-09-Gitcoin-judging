Cuddly Pewter Shark

medium

# `useRegistryAnchor == true` breakes the logics of `RFPSimpleStrategy._registerRecipient()`
There's no way to execute `Allo.registerRecipient()` when `useRegistryAnchor == true` in `RFPSimpleStrategy`.

## Vulnerability Detail
`RFPSimpleStrategy._registerRecipient()` is intended to be possible to execute in two different ways based on `useRegistryAnchor` variable that is set during the initialization of a strategy. But in the case with `useRegistryAnchor == true` the memory variable `recipientAddress` will always be equal to zero and the function will revert because of this later.

```solidity
...
address recipientAddress;
...

// Decode '_data' depending on the 'useRegistryAnchor' flag
if (useRegistryAnchor) {
    /// @custom:data when 'true' -> (address recipientId, uint256 proposalBid, Metadata metadata)
    (recipientId, proposalBid, metadata) = abi.decode(_data, (address, uint256, Metadata));

    // If the sender is not a profile member this will revert
    if (!_isProfileMember(recipientId, _sender)) revert UNAUTHORIZED();
} else {
    //  @custom:data when 'false' -> (address recipientAddress, address registryAnchor, uint256 proposalBid, Metadata metadata)
    (recipientAddress, registryAnchor, proposalBid, metadata) =
        abi.decode(_data, (address, address, uint256, Metadata));

    // Check if the registry anchor is valid so we know whether to use it or not
    isUsingRegistryAnchor = registryAnchor != address(0);

    // Ternerary to set the recipient id based on whether or not we are using the 'registryAnchor' or '_sender'
    recipientId = isUsingRegistryAnchor ? registryAnchor : _sender;

    // Checks if the '_sender' is a member of the profile 'anchor' being used and reverts if not
    if (isUsingRegistryAnchor && !_isProfileMember(recipientId, _sender)) revert UNAUTHORIZED();
}

...

// If the recipient address is the zero address this will revert
if (recipientAddress == address(0)) revert RECIPIENT_ERROR(recipientId);
```


## PoC
```solidity
// SPDX-License-Identifier: AGPL-3.0-only
pragma solidity ^0.8.19;

import "forge-std/Test.sol";

// Strategy contracts
import {RFPSimpleStrategy} from "../../../contracts/strategies/rfp-simple/RFPSimpleStrategy.sol";
// Internal libraries
import {Errors} from "../../../contracts/core/libraries/Errors.sol";
import {Metadata} from "../../../contracts/core/libraries/Metadata.sol";
import {Native} from "../../../contracts/core/libraries/Native.sol";
// Test libraries
import {AlloSetup} from "../shared/AlloSetup.sol";
import {RegistrySetupFull} from "../shared/RegistrySetup.sol";

contract AuditTest is Test, RegistrySetupFull, AlloSetup, Native, Errors {
    bool public useRegistryAnchor;
    RFPSimpleStrategy public strategy;

    function setUp() public {
        __RegistrySetupFull();
        __AlloSetup(address(registry()));

        useRegistryAnchor = true;

        strategy = new RFPSimpleStrategy(address(allo()), "RFPSimpleStrategy");

        vm.prank(pool_admin());
        allo().createPoolWithCustomStrategy(
            poolProfile_id(),
            address(strategy),
            abi.encode(1e18, useRegistryAnchor, true),
            NATIVE,
            0,
            Metadata({protocol: 1, pointer: "PoolMetadata"}),
            pool_managers()
        );
    }

    function test_audit_revertRegisterRecipient() public {
        Metadata memory metadata = Metadata({protocol: 1, pointer: "metadata"});

        bytes memory data = abi.encode(poolProfile_anchor(), 1e18, metadata);

        vm.prank(address(allo()));
        vm.expectRevert(abi.encodeWithSelector(RECIPIENT_ERROR.selector, poolProfile_anchor()));
        strategy.registerRecipient(data, pool_manager1()); // will revert
    }
}
```

## Impact
The strategy is unusable in this case. 

## Code Snippet
https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/rfp-simple/RFPSimpleStrategy.sol#L320-L346
https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/rfp-simple/RFPSimpleStrategy.sol#L362

## Tool used
Manual Review

## Recommendation
Review the logic of the function