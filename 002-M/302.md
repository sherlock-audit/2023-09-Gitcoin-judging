Curved Chocolate Iguana

high

# Incorrect strategy poolAmount for pools using fee on transfer tokens
When using a token that has fees on transfers for a pool, funding that pool leads to an incorrect increase of its strategy `poolAmount`.

## Vulnerability Detail
When funding a pool, `Allo` instructs the strategy for that pool to increase its `poolAmount` by the amount minus `Allo` fees (if any).

https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/core/Allo.sol#L517

When the pool uses a token that has fees on transfer, the amount transferred will be lower than the original one, leading to a discrepancy between the strategy `poolAmount` and its actual balance.

## Impact
Any logic in strategies relying on `poolAmount`.

## Code Snippet
```solidity
    function testfundPoolFeeOnTransfer() public {
        TransferFeeToken fotToken = new TransferFeeToken(10 ether, 0.01 ether);
        fotToken.approve(address(allo()), 1 ether);

        vm.prank(pool_admin());
        uint256 poolId = allo().createPoolWithCustomStrategy(
            poolProfile_id(), strategy, "0x", address(fotToken), 0, metadata, pool_managers()
        );
        
        allo().fundPool(poolId, 1 ether);

        assertEq(IStrategy(strategy).getPoolAmount(), fotToken.balanceOf(strategy));
    }
```

## Tool used

Manual Review

## Recommendation
Compute the amount to increase the `poolAmount` by from the balance of the strategy before and after the transfer.

```solidity
    function _fundPool(uint256 _amount, uint256 _poolId, IStrategy _strategy) internal {
        uint256 feeAmount;
        uint256 amountAfterFee = _amount;

        Pool storage pool = pools[_poolId];
        address _token = pool.token;

        if (percentFee > 0) {
            feeAmount = (_amount * percentFee) / getFeeDenominator();
            amountAfterFee -= feeAmount;

            _transferAmountFrom(_token, TransferData({from: msg.sender, to: treasury, amount: feeAmount}));
        }

        uint256 strategyBalanceBefore = _token == NATIVE ? address(_strategy).balance : IERC20Upgradeable(_token).balanceOf(address(_strategy));
        _transferAmountFrom(_token, TransferData({from: msg.sender, to: address(_strategy), amount: amountAfterFee}));
        uint256 strategyBalanceAfter =  _token == NATIVE ? address(_strategy).balance : IERC20Upgradeable(_token).balanceOf(address(_strategy));

        _strategy.increasePoolAmount(strategyBalanceAfter - strategyBalanceBefore);

        emit PoolFunded(_poolId, amountAfterFee, feeAmount);
    }
```