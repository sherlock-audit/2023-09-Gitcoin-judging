Silly Carob Opossum

medium

# QVBaseStrategy single manager can change recipient's review status even if reviewThreshold > 1
## Summary

1. Managers can call the `reviewRecipients` function multiple times with the same status, increasing the status review count each time. If count becomes equal to `reviewThreshold`, status changes.
2. If recipient's status changes again, for example, if the recipient appeals, the review count for the previous status will remain equal to `reviewThreshold`. In this case, only one review from any manager needs to change the status back.

## Vulnerability Detail

The `reviewRecipients` function doesn't have checks that manager has already reviewed the recipient.

Review count is stored for each status.

```solidity
// recipientId -> status -> count
mapping(address => mapping(Status => uint256)) public reviewsByStatus;
```

Every time manager reviews a recipient, status review count increases.

```solidity
reviewsByStatus[recipientId][recipientStatus]++;
```

If review count becomes equal to `reviewThreshold`, status changes.

```solidity
if (reviewsByStatus[recipientId][recipientStatus] >= reviewThreshold) {
    Recipient storage recipient = recipients[recipientId];
    recipient.recipientStatus = recipientStatus;

    emit RecipientStatusUpdated(recipientId, recipientStatus, address(0));
}
```

The status may change again if recipient updates the application.

```solidity
if (currentStatus == Status.Accepted) {
    // recipient updating accepted application
    recipient.recipientStatus = Status.Pending;
} else if (currentStatus == Status.Rejected) {
    // recipient updating rejected application
    recipient.recipientStatus = Status.Appealed;
}
```

The review count for the previous status, in this case, does not change and remains equal to `reviewThreshold`. Calling `reviewRecipients` function again with the previous status will change it back.


## Impact

Recipient's status can be changed by a single manager. This may not even be done intentionally. For example, two managers did a review and rejected the recipient. The recipient appealed. Then, if at least one manager rejects the recipient again, the status will change immediately. This manager may not even know that the other one did not review it. And vice versa, the second manager may not know that the recipient was under review again.

## POC

Add these tests to `QVSimpleStrategyTest`, run with `forge test --mc QVSimpleStrategyTest --mt testPOC -vv`.

```solidity
function testPOC1() external {
    address recipientId = __register_recipient();

    address[] memory recipientIds = new address[](1);
    recipientIds[0] = recipientId;
    IStrategy.Status[] memory statuses = new IStrategy.Status[](1);
    statuses[0] = IStrategy.Status.Accepted;

    // Single manager makes review twice and changes the recipient's status
    vm.startPrank(pool_manager1());
    for (uint256 i; i < qvStrategy().reviewThreshold(); i++) {
        qvStrategy().reviewRecipients(recipientIds, statuses);
    }
    vm.stopPrank();

    assertEq(uint8(qvStrategy().getRecipientStatus(recipientId)), uint8(IStrategy.Status.Accepted));
}

function testPOC2() external {
    /**
     * Recipient is registered, the admin and pool manager review it
     * and change the recipient's status to Rejected
     */
    address recipientId = __register_reject_recipient();

    // The recipient updates info and changes status to Appealed
    __register_recipient();

    IStrategy.Status newStatus = qvStrategy().getRecipientStatus(recipientId);
    assertEq(uint8(IStrategy.Status.Appealed), uint8(newStatus));

    // Now it needs only one review to change status back to Rejected
    address[] memory recipientIds = new address[](1);
    recipientIds[0] = recipientId;
    IStrategy.Status[] memory statuses = new IStrategy.Status[](1);
    statuses[0] = IStrategy.Status.Rejected;

    vm.prank(pool_manager1());
    qvStrategy().reviewRecipients(recipientIds, statuses);

    assertEq(uint8(qvStrategy().getRecipientStatus(recipientId)), uint8(IStrategy.Status.Rejected));
}
```

## Code Snippet

https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/qv-base/QVBaseStrategy.sol#L249-L288

## Tool used

Manual Review

## Recommendation

Every time recipient's status changes, the review count of the previous status needs to be reset.

```solidity
/// @notice Call this function everywhere the recipient's status needs to be changed.
function _updateRecipientStatus(address recipientId, Status newStatus) internal {
    Recipient storage recipient = recipients[recipientId];

    Status oldStatus = recipient.recipientStatus;
    reviewsByStatus[recipientId][oldStatus] = 0;

    recipient.recipientStatus = newStatus;

    emit RecipientStatusUpdated(recipientId, newStatus, address(0));
}
```
To avoid multiple reviews by the same manager, we need to store result of the review. For this purpose, we can use additional mapping of review status by every manager.

```solidity
// recipientId -> reviewer ->  status
mapping(address => mapping(address => Status)) public reviewsByReviewers;
```

But the problem is that when the recipient’s status changes, we also need to reset the review result of each manager. It's a bit harder to do. Instead, we can use a status nonce that will increment every time the status changes. The result of a review will be associated with the nonce, so we won’t have to reset the results of previous reviews.

```solidity
/// @notice The details of the recipient
struct Recipient {
    // slot 0
    uint256 totalVotesReceived;
    // slot 1
    bool useRegistryAnchor;
    address recipientAddress;
    Metadata metadata;
    Status recipientStatus;
    uint256 statusNonce;  // <--
}

...

// recipientId -> reviewer -> statusNonce -> status
mapping(address => mapping(address => mapping(uint256 => Status))) public reviewsByReviewers;  // <--

...

function reviewRecipients(address[] calldata _recipientIds, Status[] calldata _recipientStatuses)
    external
    virtual
    onlyPoolManager(msg.sender)
    onlyActiveRegistration
{
    // make sure the arrays are the same length
    uint256 recipientLength = _recipientIds.length;
    if (recipientLength != _recipientStatuses.length) revert INVALID();

    for (uint256 i; i < recipientLength;) {
        Status recipientStatus = _recipientStatuses[i];
        address recipientId = _recipientIds[i];

        // if the status is none or appealed then revert
        if (recipientStatus == Status.None || recipientStatus == Status.Appealed) {
            revert RECIPIENT_ERROR(recipientId);
        }

        if (reviewsByReviewers[recipientId][msg.sender][statusNonce] == recipientStatus) {  // <--
            revert RECIPIENT_HAS_ALREADY_BEEN_REVIEWED(recipientId);
        }

        reviewsByStatus[recipientId][recipientStatus]++;
        reviewsByReviewers[recipientId][msg.sender][statusNonce] = recipientStatus;  // <--

        if (reviewsByStatus[recipientId][recipientStatus] >= reviewThreshold) {
            _updateRecipientStatus(recipientId, recipientStatus);  // <--
        }

        emit Reviewed(recipientId, recipientStatus, msg.sender);

        unchecked {
            ++i;
        }
    }
}

function _updateRecipientStatus(address recipientId, Status newStatus) internal {
    Recipient storage recipient = recipients[recipientId];
    Status oldStatus = recipient.recipientStatus;

    reviewsByStatus[recipientId][oldStatus] = 0;

    recipient.recipientStatus = newStatus;
    recipient.statusNonce++;  // <--

    emit RecipientStatusUpdated(recipientId, newStatus, address(0));
}

...

function _registerRecipient(bytes memory _data, address _sender)
...
   if (currentStatus == Status.None) {
        // recipient registering new application
        recipient.recipientStatus = Status.Pending;
        emit Registered(recipientId, _data, _sender);
    } else {
        if (currentStatus == Status.Accepted) {
            // recipient updating accepted application
            _updateRecipientStatus(recipientId, Status.Pending);  // <--
        } else if (currentStatus == Status.Rejected) {
            // recipient updating rejected application
            _updateRecipientStatus(recipientId, Status.Appealed);  // <--
        }

        // emit the new status with the '_data' that was passed in
        emit UpdatedRegistration(recipientId, _data, _sender, recipient.recipientStatus);
    }
}
```