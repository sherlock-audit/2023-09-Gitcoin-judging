Bumpy Charcoal Squid

high

# `RFPSimpleStrategy::_distribute` function: rejected milestones can be executed

`RFPSimpleStrategy::_distribute` function: rejected milestones can be executed

## Vulnerability Detail

- In `RFPSimpleStrategy` strategy contract: the pool manager can set a list of milestones of payments to be distributed to the selected accepted recipient (`acceptedRecipientId`) later, and this operation can be done only once via `setMilestones` function.

- The `acceptedRecipientId` can submit a milestone: in order to get the next milestone payment when this milestone is distributed; and this sets the milestone status from `None` to `Pending`.

- Then the pool manager can either reject the submitted milestone by calling `rejectMilestone` function where it sets the status of the milestone from `Pending` to `Rejected`; or can leave the status as it is (`Pending`).

- But when the `_duistribute` function is called for the nextMilestone; it doesn't check if the status of the milestone is **Rejected**.

## Impact

So this will result in distributing the **Rejected** milestone payment for the accepted recipient.

## Code Snippet

[RFPSimpleStrategy::submitUpcomingMilestone function](https://github.com/sherlock-audit/2023-09-Gitcoin/blob/6430c8004017e96ae2f5aac365bdefd0b6eeea72/allo-v2/contracts/strategies/rfp-simple/RFPSimpleStrategy.sol#L253-L271)

```solidity
    function submitUpcomingMilestone(Metadata calldata _metadata) external {
        // Check if the 'msg.sender' is the 'acceptedRecipientId' and is a member of the 'Profile'
        if (acceptedRecipientId != msg.sender && !_isProfileMember(acceptedRecipientId, msg.sender)) {
            revert UNAUTHORIZED();
        }

        // Check if the upcoming milestone is in fact upcoming
        if (upcomingMilestone >= milestones.length) revert INVALID_MILESTONE();

        // Get the milestone and update the metadata and status
        Milestone storage milestone = milestones[upcomingMilestone];
        milestone.metadata = _metadata;

        // Set the milestone status to 'Pending' to indicate that the milestone is submitted
        milestone.milestoneStatus = Status.Pending;

        // Emit event for the milestone
        emit MilstoneSubmitted(upcomingMilestone);
    }
```

[RFPSimpleStrategy::rejectMilestone function](https://github.com/sherlock-audit/2023-09-Gitcoin/blob/6430c8004017e96ae2f5aac365bdefd0b6eeea72/allo-v2/contracts/strategies/rfp-simple/RFPSimpleStrategy.sol#L283-L290)

```solidity
    function rejectMilestone(uint256 _milestoneId) external onlyPoolManager(msg.sender) {
        // Check if the milestone is already accepted
        if (milestones[_milestoneId].milestoneStatus == Status.Accepted) revert MILESTONE_ALREADY_ACCEPTED();

        milestones[_milestoneId].milestoneStatus = Status.Rejected;

        emit MilestoneStatusChanged(_milestoneId, milestones[_milestoneId].milestoneStatus);
    }
```

[RFPSimpleStrategy::\_distribute function](https://github.com/sherlock-audit/2023-09-Gitcoin/blob/6430c8004017e96ae2f5aac365bdefd0b6eeea72/allo-v2/contracts/strategies/rfp-simple/RFPSimpleStrategy.sol#L417-L450)

```solidity
function _distribute(address[] memory, bytes memory, address _sender)
        internal
        virtual
        override
        onlyInactivePool
        onlyPoolManager(_sender)
    {
        // check to make sure there is a pending milestone
        if (upcomingMilestone >= milestones.length) revert INVALID_MILESTONE();

        IAllo.Pool memory pool = allo.getPool(poolId);
        Milestone storage milestone = milestones[upcomingMilestone];
        Recipient memory recipient = _recipients[acceptedRecipientId];

        // make sure has enough funds to distribute based on the proposal bid
        if (recipient.proposalBid > poolAmount) revert NOT_ENOUGH_FUNDS();

        // Calculate the amount to be distributed for the milestone
        uint256 amount = (recipient.proposalBid * milestone.amountPercentage) / 1e18;

        // Get the pool, subtract the amount and transfer to the recipient
        poolAmount -= amount;
        _transferAmount(pool.token, recipient.recipientAddress, amount);

        // Set the milestone status to 'Accepted'
        milestone.milestoneStatus = Status.Accepted;

        // Increment the upcoming milestone
        upcomingMilestone++;

        // Emit events for the milestone and the distribution
        emit MilestoneStatusChanged(upcomingMilestone, Status.Accepted);
        emit Distributed(acceptedRecipientId, recipient.recipientAddress, amount, _sender);
    }
```

## Tool used

Manual Review

## Recommendation

Check the milestone status before distribution; if it's set to `Rejected`, then `_distribute` function won't execute this milestone and moves to the next one for execution, and add a mechanism to enable executing the `Rejected` milestones again when their statuses are updated (impelemnt another mechanism to update the milestone statuses by the pool manager).