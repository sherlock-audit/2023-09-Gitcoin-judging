Little Cloth Coyote

high

# `distribute()` can be front-run by toggling pool status to steal funds
Missing modifier in `setPoolActive()` allows anyone to toggle status of pool. Profile members can front-run `distribute()` and steal funds. 

## Vulnerability Detail
The `setPoolActive()` function lacks the necessary onlyPoolManager modifier. According to NatSpec, only the pool manager should have access to this function.

```solidity
    /// @notice Toggle the status between active and inactive.
    /// @dev 'msg.sender' must be a pool manager to close the pool. Emits a 'PoolActive()' event.
    /// @param _flag The flag to set the pool to active or inactive
    function setPoolActive(bool _flag) external {
        _setPoolActive(_flag);
        emit PoolActive(_flag);
    }
```
This exploit can occur in both RFPSimpleStrategy and RFPSimpleStrategy. Once `upcomingMilestone` is completed and `acceptedRecipientId` is selected by pool managers through `allocate()`, a profile member can increase funding by front-running `distribute()`.  This involves swiftly changing the pool's status to active, re-registering `acceptedRecipientId` using `registerRecipient()` on Allo.sol with increased `recipient.proposalBid`, and deactivate the pool all within a single transaction.
```solidity
    function _registerRecipient(bytes memory _data, address _sender)
        internal
        override
        onlyActivePool
        returns (address recipientId)
    {
        bool isUsingRegistryAnchor;
        address recipientAddress;
        address registryAnchor;
        uint256 proposalBid;
        Metadata memory metadata;

        // Decode '_data' depending on the 'useRegistryAnchor' flag
        if (useRegistryAnchor) {
            /// @custom:data when 'true' -> (address recipientId, uint256 proposalBid, Metadata metadata)
            (recipientId, proposalBid, metadata) = abi.decode(_data, (address, uint256, Metadata));

            // If the sender is not a profile member this will revert
            if (!_isProfileMember(recipientId, _sender)) revert UNAUTHORIZED();
        } else {
            //  @custom:data when 'false' -> (address recipientAddress, address registryAnchor, uint256 proposalBid, Metadata metadata)
            (recipientAddress, registryAnchor, proposalBid, metadata) =
                abi.decode(_data, (address, address, uint256, Metadata));

            // Check if the registry anchor is valid so we know whether to use it or not
            isUsingRegistryAnchor = registryAnchor != address(0);

            // Ternerary to set the recipient id based on whether or not we are using the 'registryAnchor' or '_sender'
            recipientId = isUsingRegistryAnchor ? registryAnchor : _sender;

            // Checks if the '_sender' is a member of the profile 'anchor' being used and reverts if not
            if (isUsingRegistryAnchor && !_isProfileMember(recipientId, _sender)) revert UNAUTHORIZED();
        }

        // Check if the metadata is required and if it is, check if it is valid, otherwise revert
        if (metadataRequired && (bytes(metadata.pointer).length == 0 || metadata.protocol == 0)) {
            revert INVALID_METADATA();
        }

        if (proposalBid > maxBid) {
            // If the proposal bid is greater than the max bid this will revert
            revert EXCEEDING_MAX_BID();
        } else if (proposalBid == 0) {
            // If the proposal bid is 0, set it to the max bid
            proposalBid = maxBid;
        }

        // If the recipient address is the zero address this will revert
        if (recipientAddress == address(0)) revert RECIPIENT_ERROR(recipientId);

        // Get the recipient
        Recipient storage recipient = _recipients[recipientId];

        if (recipient.recipientStatus == Status.None) {
            // If the recipient status is 'None' add the recipient to the '_recipientIds' array
            _recipientIds.push(recipientId);
            emit Registered(recipientId, _data, _sender);
        } else {
            emit UpdatedRegistration(recipientId, _data, _sender);
        }

        // update the recipients data
        recipient.recipientAddress = recipientAddress;
        recipient.useRegistryAnchor = isUsingRegistryAnchor ? true : recipient.useRegistryAnchor;
        recipient.proposalBid = proposalBid;
        recipient.recipientStatus = Status.Pending;
    }
```
`amount` is calculated by newly updated `recipient.proposalBid`, `acceptedRecipientId` will receive more funds than originally intended when `_distribute()` is executed.
```solidity
    function _distribute(address[] memory, bytes memory, address _sender)
        internal
        virtual
        override
        onlyInactivePool
        onlyPoolManager(_sender)
    {
        // check to make sure there is a pending milestone
        if (upcomingMilestone >= milestones.length) revert INVALID_MILESTONE();

        IAllo.Pool memory pool = allo.getPool(poolId);
        Milestone storage milestone = milestones[upcomingMilestone];
        Recipient memory recipient = _recipients[acceptedRecipientId];

        // make sure has enough funds to distribute based on the proposal bid
        if (recipient.proposalBid > poolAmount) revert NOT_ENOUGH_FUNDS();

        // Calculate the amount to be distributed for the milestone
        uint256 amount = (recipient.proposalBid * milestone.amountPercentage) / 1e18;

        // Get the pool, subtract the amount and transfer to the recipient
        poolAmount -= amount;
        _transferAmount(pool.token, recipient.recipientAddress, amount);

        // Set the milestone status to 'Accepted'
        milestone.milestoneStatus = Status.Accepted;

        // Increment the upcoming milestone
        upcomingMilestone++;

        // Emit events for the milestone and the distribution
        emit MilestoneStatusChanged(upcomingMilestone, Status.Accepted);
        emit Distributed(acceptedRecipientId, recipient.recipientAddress, amount, _sender);
    }
```
Other key functions such as `distribute()`, `allocate()`, and `registerRecipient()`, each equipped with status modifiers, are susceptible to disruption by toggling of the pool status.

## Impact
Unauthorized individuals can grief the pool. 

## Code Snippet
https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/rfp-simple/RFPSimpleStrategy.sol#L219 
## Tool used

Manual Review

## Recommendation
```solidity
-   function setPoolActive(bool _flag) external {
+   function setPoolActive(bool _flag) external onlyPoolManager(msg.sender) {
        _setPoolActive(_flag);
        emit PoolActive(_flag);
    }
```
Side Note:

As last line of defense, I also recommend checking `_recipients[acceptedRecipientId].recipientStatus == Status.Accepted` in `_distribute()`