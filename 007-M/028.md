Brief Silver Porcupine

medium

# Managers can vote for the same recepient many times
Pool managers in **QVBaseStrategy.sol** can vote multiple times for the same recipient.

## Vulnerability Detail
In **QVBaseStrategy.sol**, after recepients has been proposed, pool managers can review them. Reviewing means voting on what the status of the recipient should be. When a recipient has passed the needed threshold, his status is set. However, [the function](https://github.com/allo-protocol/allo-v2/blob/8a41a342a0de7a2d5d7dbc5395d1da44cb811348/contracts/strategies/qv-base/QVBaseStrategy.sol#L273-L275) updates the counter for the recipient's votes on each iteration without checking if the current manager has not already voted for the recipient. This means that a single pool manager can call the function with the same recipient **reviewThreshold** times and change its status.

## Impact
A single pool manager can vote for a certain recipient as many times as he desires.

## Code Snippet
```jsx
function reviewRecipients(address[] calldata _recipientIds, Status[] calldata _recipientStatuses)
        external
        virtual
        onlyPoolManager(msg.sender)
        onlyActiveRegistration
    {
        // make sure the arrays are the same length
        uint256 recipientLength = _recipientIds.length;
        if (recipientLength != _recipientStatuses.length) revert INVALID();

        for (uint256 i; i < recipientLength;) {
            Status recipientStatus = _recipientStatuses[i];
            address recipientId = _recipientIds[i];

            // if the status is none or appealed then revert
            if (recipientStatus == Status.None || recipientStatus == Status.Appealed) {
                revert RECIPIENT_ERROR(recipientId);
            }

            reviewsByStatus[recipientId][recipientStatus]++;

            if (reviewsByStatus[recipientId][recipientStatus] >= reviewThreshold) {
                Recipient storage recipient = recipients[recipientId];
                recipient.recipientStatus = recipientStatus;

                emit RecipientStatusUpdated(recipientId, recipientStatus, address(0));
            }

            emit Reviewed(recipientId, recipientStatus, msg.sender);

            unchecked {
                ++i;
            }
        }
 }

```

## Tool used

Manual Review

## Recommendation

Add a mapping that tracks if a manager has voted for a certain recipient.

```jsx
  mapping (address manager => mapping(address recipient => bool voted)) managerHasVoted;
```

Record a new vote count for the recipient **only if** the manager votes for him for the first time.

```jsx
  if (!managerHasVoted[msg.sender][recipientId]) {
       reviewsByStatus[recipientId][recipientStatus]++;
  }
```

Update the mapping when a manager makes a review of a recipient.

```jsx
managerHasVoted[msg.sender][recipientId] = true;
```