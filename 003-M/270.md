Uneven Holographic Llama

high

# Repeated Vote Allocation Due to Missing Update of Allocator's Total Voice Credits in _qv_allocate Function.
The _qv_allocate function in the QVBaseStrategy contract does not properly update the allocator's total voice credits, allowing for repeated allocations.

## Vulnerability Detail
The function _qv_allocate is responsible for allocating voice credits to a recipient. During this allocation, the function updates the number of votes and voice credits an allocator has allocated to a particular recipient. However, the function does not update the total voice credits used by the allocator, which means an allocator can allocate infinite voice credits.

In QVSimpleStrategy, the _allocate internal function allocates votes to a recipient. Before doing this allocation,
 the _hasVoiceCreditsLeft function is called to check if an allocator has enough voice credits left. This check relies on the _allocator.voiceCredits value, which should represent the total voice credits already used by the allocator. 
However, since _qv_allocate doesn't update this value when called, the check in _hasVoiceCreditsLeft becomes meaningless, as it won't account for votes already processed - allowing the allocator to allocate more voice credits than their limit.

## Impact

Even though the pool managers are trustworthy, the system doesn't really stop them from mistakenly sending duplicate transactions. This will distort voice credits distribution and create untruthy allocations.

## Code Snippet

Notice _allocate checks if the allocator still has voice credits left before doing the allocation.
[_allocate:](https://github.com/sherlock-audit/2023-09-Gitcoin/blob/6430c8004017e96ae2f5aac365bdefd0b6eeea72/allo-v2/contracts/strategies/qv-simple/QVSimpleStrategy.sol#L107-L124)
```solidity
    function _allocate(bytes memory _data, address _sender) internal virtual override {
        (address recipientId, uint256 voiceCreditsToAllocate) = abi.decode(_data, (address, uint256));

        // spin up the structs in storage for updating
        Recipient storage recipient = recipients[recipientId];
        Allocator storage allocator = allocators[_sender];

        // check that the sender can allocate votes
        if (!_isValidAllocator(_sender)) revert UNAUTHORIZED();

        // check that the recipient is accepted
        if (!_isAcceptedRecipient(recipientId)) revert RECIPIENT_ERROR(recipientId);

        // check that the recipient has voice credits left to allocate
        if (!_hasVoiceCreditsLeft(voiceCreditsToAllocate, allocator.voiceCredits)) revert INVALID();

        _qv_allocate(allocator, recipient, recipientId, voiceCreditsToAllocate, _sender);
    }
```
_hasVoiceCreditsLeft utilizes the allocator's already accounted voiceCredits fetched from the allocators mapping in order to compute whether the newly allocated credits + the already allocated credits are not bigger than the maximum amount allowed per allocator.
[_hasVoiceCreditsLeft](https://github.com/sherlock-audit/2023-09-Gitcoin/blob/6430c8004017e96ae2f5aac365bdefd0b6eeea72/allo-v2/contracts/strategies/qv-simple/QVSimpleStrategy.sol#L144-L151)
```solidity
function _hasVoiceCreditsLeft(uint256 _voiceCreditsToAllocate, uint256 _allocatedVoiceCredits)
        internal
        view
        override
        returns (bool)
    {
        return _voiceCreditsToAllocate + _allocatedVoiceCredits <= maxVoiceCreditsPerAllocator;
    }
```
Passed the _hasVoiceCreditsLeft condition, the last call is _qv_allocate - in which it's expected to update allocator.voiceCredits by summing the new _voiceCreditsToAllocate. However this step doesn't happen.

[_qv_allocate:](https://github.com/sherlock-audit/2023-09-Gitcoin/blob/6430c8004017e96ae2f5aac365bdefd0b6eeea72/allo-v2/contracts/strategies/qv-base/QVBaseStrategy.sol#L506-L534)
```solidity
 function _qv_allocate(
        Allocator storage _allocator,
        Recipient storage _recipient,
        address _recipientId,
        uint256 _voiceCreditsToAllocate,
        address _sender
    ) internal onlyActiveAllocation {
        // check the `_voiceCreditsToAllocate` is > 0
        if (_voiceCreditsToAllocate == 0) revert INVALID();

        // get the previous values
        uint256 creditsCastToRecipient = _allocator.voiceCreditsCastToRecipient[_recipientId];
        uint256 votesCastToRecipient = _allocator.votesCastToRecipient[_recipientId];

        // get the total credits and calculate the vote result
        uint256 totalCredits = _voiceCreditsToAllocate + creditsCastToRecipient;
        uint256 voteResult = _sqrt(totalCredits * 1e18);

        // update the values
        voteResult -= votesCastToRecipient;
        totalRecipientVotes += voteResult;
        _recipient.totalVotesReceived += voteResult;

        _allocator.voiceCreditsCastToRecipient[_recipientId] += totalCredits;
        _allocator.votesCastToRecipient[_recipientId] += voteResult;

        // emit the event with the vote results
        emit Allocated(_recipientId, voteResult, _sender);
    }
```

## Tool used

Manual Review

## Recommendation
Update the _qv_allocate function to properly increment the total voice credits used by the allocator.
```solidity
 function _qv_allocate(
        Allocator storage _allocator,
        Recipient storage _recipient,
        address _recipientId,
        uint256 _voiceCreditsToAllocate,
        address _sender
    ) internal onlyActiveAllocation {
       ...
       _allocator.voiceCredits += _voiceCreditsToAllocate;
...
    }
```