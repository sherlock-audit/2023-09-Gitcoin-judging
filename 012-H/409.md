Atomic Ultraviolet Mole

high

# The ````Anchor```` contract is broken entirely and any funds sent to it would be locked for ever
The ````Anchor```` contract is a core component of  ````Allo```` protocol, but it is broken entirely due to incorrect using of ````solady````'s ````CREATE3```` library. And any funds sent to instances of ````Anchor```` would be locked for ever.

## Vulnerability Detail
Calls to ````execute()```` would always revert on L72, as ````msg.sender```` on L56 is not the ````registry````.
```solidity
File: contracts\core\Anchor.sol
27: contract Anchor {
...
33:     Registry public immutable registry;
...
36:     bytes32 public immutable profileId;
37: 
...
55:     constructor(bytes32 _profileId) {
56:         registry = Registry(msg.sender);
57:         profileId = _profileId;
58:     }
...
70:     function execute(address _target, uint256 _value, bytes memory _data) external returns (bytes memory) {
71:         // Check if the caller is the owner of the profile and revert if not
72:         if (!registry.isOwnerOfProfile(profileId, msg.sender)) revert UNAUTHORIZED();
73: 
74:         // Check if the target address is the zero address and revert if it is
75:         if (_target == address(0)) revert CALL_FAILED();
76: 
77:         // Call the target address and return the data
78:         (bool success, bytes memory data) = _target.call{value: _value}(_data);
79: 
80:         // Check if the call was successful and revert if not
81:         if (!success) revert CALL_FAILED();
82: 
83:         return data;
84:     }
...
87:     receive() external payable {}
88: }

```

Let's analyze the call stack while creating ````Anchor```` instances:
(1) ````CREATE3.deploy()```` is called by the ````registry```` on L350 of ````Registry.sol````
```solidity
File: contracts\core\Registry.sol
335:     function _generateAnchor(bytes32 _profileId, string memory _name) internal returns (address anchor) {
336:         bytes32 salt = keccak256(abi.encodePacked(_profileId, _name));
337: 
338:         address preCalculatedAddress = CREATE3.getDeployed(salt);
...
341:         if (preCalculatedAddress.code.length > 0) {
...
345:         } else {
347:             bytes memory creationCode = abi.encodePacked(type(Anchor).creationCode, abi.encode(_profileId));
348: 
349:             // Use CREATE3 to deploy the anchor contract
350:             anchor = CREATE3.deploy(salt, creationCode, 0);
351:         }
352:     }

```

(2) Next, let's dive deeper on the implementation of ````CREATE3.deploy()````, please pay attention on L072, an intermediate helper  contract ````proxy```` is created firstly. Then, on L094~L102, ````creationCode```` of ````Anchor```` contact is passed to the proxy. Finally, the proxy creates ````Anchor```` instance.
```solidity
File: lib\solady\src\utils\CREATE3.sol
048:     uint256 private constant _PROXY_BYTECODE = 0x67363d3d37363d34f03d5260086018f3;
...
063:     function deploy(bytes32 salt, bytes memory creationCode, uint256 value)
064:         internal
065:         returns (address deployed)
066:     {
067:         /// @solidity memory-safe-assembly
068:         assembly {
069:             // Store the `_PROXY_BYTECODE` into scratch space.
070:             mstore(0x00, _PROXY_BYTECODE)
071:             // Deploy a new contract with our pre-made bytecode via CREATE2.
072:             let proxy := create2(0, 0x10, 0x10, salt)
073: 
074:             // If the result of `create2` is the zero address, revert.
075:             if iszero(proxy) {
076:                 // Store the function selector of `DeploymentFailed()`.
077:                 mstore(0x00, 0x30116425)
078:                 // Revert with (offset, size).
079:                 revert(0x1c, 0x04)
080:             }
081: 
082:             // Store the proxy's address.
083:             mstore(0x14, proxy)
084:             // 0xd6 = 0xc0 (short RLP prefix) + 0x16 (length of: 0x94 ++ proxy ++ 0x01).
085:             // 0x94 = 0x80 + 0x14 (0x14 = the length of an address, 20 bytes, in hex).
086:             mstore(0x00, 0xd694)
087:             // Nonce of the proxy contract (1).
088:             mstore8(0x34, 0x01)
089: 
090:             deployed := keccak256(0x1e, 0x17)
091: 
092:             // If the `call` fails, revert.
093:             if iszero(
094:                 call(
095:                     gas(), // Gas remaining.
096:                     proxy, // Proxy's address.
097:                     value, // Ether value.
098:                     add(creationCode, 0x20), // Start of `creationCode`.
099:                     mload(creationCode), // Length of `creationCode`.
100:                     0x00, // Offset of output.
101:                     0x00 // Length of output.
102:                 )
103:             ) {
104:                 // Store the function selector of `InitializationFailed()`.
105:                 mstore(0x00, 0x19b991a8)
106:                 // Revert with (offset, size).
107:                 revert(0x1c, 0x04)
108:             }
109: 
110:             // If the code size of `deployed` is zero, revert.
111:             if iszero(extcodesize(deployed)) {
112:                 // Store the function selector of `InitializationFailed()`.
113:                 mstore(0x00, 0x19b991a8)
114:                 // Revert with (offset, size).
115:                 revert(0x1c, 0x04)
116:             }
117:         }
118:     }

```

Therefore, while executing ````constructor()```` of ````Anchor````, the call stack is as
```solidity
Registry._generateAnchor() ---> proxy.fallback() ---> Anchor.constructor()
```
The ````msg.sender```` retrieved in ````constructor()```` of ````Anchor```` is ````proxy````, not ````registry````.



## Impact
(1) As a core component, the ````Anchor```` contract is broken entirely
(2) As shown on L87, the contract is designed to receive funds, but any funds sent to it would be locked for ever
```solidity
File: contracts\core\Anchor.sol
27: contract Anchor {
...
87:     receive() external payable {}
}
```

## Code Snippet
https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/core/Anchor.sol#L56

## Tool used

Manual Review

## Recommendation
Passing ````registry```` as parameter of ````constructor()```` of ````Anchor```` contract.