Passive Golden Skunk

high

# ETH can be stolen when using the fundPool() function

## Vulnerability Detail

```solidity
function fundPool(uint256 _poolId, uint256 _amount) external payable nonReentrant {
        // if amount is 0, revert with 'NOT_ENOUGH_FUNDS()' error
        if (_amount == 0) revert NOT_ENOUGH_FUNDS();

        // Call the internal fundPool() function
        _fundPool(_amount, _poolId, pools[_poolId].strategy);
    }
```
When using the fundPool() function with native eth, anyone can steal contract funds. 

```solidity
function _fundPool(uint256 _amount, uint256 _poolId, IStrategy _strategy) internal {
        uint256 feeAmount;
        uint256 amountAfterFee = _amount;

        Pool storage pool = pools[_poolId];
        address _token = pool.token;

        if (percentFee > 0) {
            feeAmount = (_amount * percentFee) / getFeeDenominator();
            amountAfterFee -= feeAmount;

            _transferAmountFrom(_token, TransferData({from: msg.sender, to: treasury, amount: feeAmount})); 
        }

        _transferAmountFrom(_token, TransferData({from: msg.sender, to: address(_strategy), amount: amountAfterFee})); //@audit 
        _strategy.increasePoolAmount(amountAfterFee);

        emit PoolFunded(_poolId, amountAfterFee, feeAmount);
    }
```
The _fundPool() function uses the _transferAmountFrom() function to send the fee amount to the treasury and amountAfterFee to the strategy.

```solidity
function _transferAmountFrom(address _token, TransferData memory _transferData) internal returns (bool) {
        uint256 amount = _transferData.amount;
        if (_token == NATIVE) {
            // Native Token
            if (msg.value < amount) revert AMOUNT_MISMATCH();

            SafeTransferLib.safeTransferETH(_transferData.to, amount);
        } else {
            SafeTransferLib.safeTransferFrom(_token, _transferData.from, _transferData.to, amount);
        }
        return true;
    }
```
When the token is native token , the function checks the transferData.amount and send it. 

Let's say someone uses the fundPool() function with msg.value = 9e18 and amount = 10e18.
Let's say the fee is 10 percent. So [amountAfterFee](https://github.com/sherlock-audit/2023-09-Gitcoin/blob/6430c8004017e96ae2f5aac365bdefd0b6eeea72/allo-v2/contracts/core/Allo.sol#L511) will be equal to 10e18 - 1e18 = 9e18.
The msg.value checks for both feeAmount and amountAfterFee will be passed, and the user can fund the pool more than it should.
## Impact
ETH can be stolen from the contract.
## Code Snippet
https://github.com/sherlock-audit/2023-09-Gitcoin/blob/6430c8004017e96ae2f5aac365bdefd0b6eeea72/allo-v2/contracts/core/Allo.sol#L502-L520
## Tool used

Manual Review

## Recommendation
If percentFee > 0 , msg.value needs to be greater than _amount + feeAmount.