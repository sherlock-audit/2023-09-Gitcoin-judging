Suave Peanut Panda

high

# An allocator can game the fairness of vote calculations by providing batches of relatively small amount of votes for one recipient instead of voting in full with his vote credits
## Summary
`QVBaseStrategy` utilizes the quadratic voting functionality by using and updating `voiceCreditsCastToRecipient` and `votesCastToRecipient` variables per each allocator. In its `_qv_allocate()`function it takes the square root of `totalCredits` (**the square root of total voice credits casted to this recipient from this allocator plus the voice credits an allocator wishes to allocate now**), then subtracts from the result the total votes casted and updates the votes casted accordingly. It also updates the `voiceCreditsCastToRecipient` variable by adding the `totalCredits`. However, due to the fact that this update accounts for `voiceCreditsCastToRecipient` twice, it is possible for an allocator to game the fairness of the system by allocating votes in small batches.
## Vulnerability Detail
In `_qv_allocate()` function
```solidity
        // get the previous values
        uint256 creditsCastToRecipient = _allocator.voiceCreditsCastToRecipient[_recipientId];
        uint256 votesCastToRecipient = _allocator.votesCastToRecipient[_recipientId];

        // get the total credits and calculate the vote result
        uint256 totalCredits = _voiceCreditsToAllocate + creditsCastToRecipient;
        uint256 voteResult = _sqrt(totalCredits * 1e18);

        // update the values
        voteResult -= votesCastToRecipient;
        totalRecipientVotes += voteResult;
        _recipient.totalVotesReceived += voteResult;

        _allocator.voiceCreditsCastToRecipient[_recipientId] += totalCredits;
        _allocator.votesCastToRecipient[_recipientId] += voteResult;
```
The vulnerable line here is 
```solidity
_allocator.voiceCreditsCastToRecipient[_recipientId] += totalCredits;
```
Since `totalCredits` includes noy-yet-updated `_allocator.voiceCreditsCastToRecipient[_recipientId]`, it is the same as 
```solidity
_allocator.voiceCreditsCastToRecipient[_recipientId] = _allocator.voiceCreditsCastToRecipient[_recipientId] + _allocator.voiceCreditsCastToRecipient[_recipientId] + _voiceCreditsToAllocate
```
Now, with that in mind let's consider the following scenario:
Alice wishes to allocate credits to Bob. The total amount of voice credits she has is 900 (totalAmount). If Alice were to allocate her totalAmount in one transaction, Bob would receive 30 votes. But Alice knows about this vulnerability and split allocation in three transactions, with `_voiceCreditsToAllocate` being 100, 300 and 500 for each function call.
* In the first call `totalCredits` are only 100 since `creditsCastToRecipient` is zero, so: `_recipient.totalVotesReceived = 10`, `_allocator.voiceCreditsCastToRecipient[_recipientId] = 100`;
* In the second call `totalCredits = 300 + 100 = 400` and the `voteResult = 20 - 10 = 10` , so: `_recipient.totalVotesReceived = 10 + 10 = 20`, `_allocator.voiceCreditsCastToRecipient[_recipientId] = 100 + 400 = 500`;
* In the third call `totalCredits = 1000`, `voteResult = 100 - 20 = 80`, `_recipient.totalVotesReceived = 20 + 80 = 100`, `_allocator.voiceCreditsCastToRecipient[_recipientId] = 500 + 1000 = 1500`

Now, instead of receiving 30 votes for 900 voice credits, Bob gets 100 votes for the same amount of voice credits, meaning that he will receive more tokens during distribution, since the actual amount to distribute is depended on `totalRecipientVotes` 
```solidity
            amount = poolAmount * recipient.totalVotesReceived / totalRecipientVotes;
```
It means that every other recipient for that distribution (bearing in mind that only Bob received more that he is entitled to) will receive less tokens, hence Bob will receive more.

It should be noted that it could even be more detrimental if Alice were to split her allocation in more that three batches.
## Impact
The fairness of quadratic voting is broken, one could gain advantage by utilizing such a flaw in the system at the expense of other users.
## Code Snippet
https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/qv-base/QVBaseStrategy.sol#L506-L534
https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/qv-base/QVBaseStrategy.sol#L529
https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/qv-base/QVBaseStrategy.sol#L559-L574
https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/qv-base/QVBaseStrategy.sol#L436-L449
## Tool used

Manual Review

## Recommendation
Instead of adding `totalCredits` to `voiceCreditsCastToRecipient`, just add `_voiceCreditsToAllocate`:
```solidity
_allocator.voiceCreditsCastToRecipient[_recipientId] += _voiceCreditsToAllocate;
```
