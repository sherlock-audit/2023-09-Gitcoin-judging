Original Navy Donkey

high

# allocator voiceCreditsCastToRecipient value calculate twice  resulting in an incorrect calculation
## Summary
When calculating voiceCreditsCastToRecipient, the double accumulation resulted in an incorrect calculation

## Vulnerability Detail
https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/qv-base/QVBaseStrategy.sol#L506#L534

the value of `creditsCastToRecipient` means previous amount of `voiceCreditsCastToRecipient` ,  Its value had already been accumulated into totalCredits
```solidity
uint256 totalCredits = _voiceCreditsToAllocate + creditsCastToRecipient;
```
 but here we can see it was added again, leading to an incorrect calculation
```solidity
_allocator.voiceCreditsCastToRecipient[_recipientId] += totalCredits;//@audit <-------------------  voiceCreditsCastToRecipient sum twice.
```

and i write a test POC:
add a function to QVBaseStrategy to read data:
```solidity
    function getAllocator(address a,address r) external view returns (uint256) {
        return (allocators[a].voiceCreditsCastToRecipient)[r];
    }
```

```solidity
    function testAllocate() public {
        address recipientId = __register_accept_recipient();
        address allocator = randomAddress();

        vm.startPrank(pool_manager2());
        vm.warp(allocationStartTime + 10);

        qvSimpleStrategy().addAllocator(allocator);


        vm.stopPrank();
        bytes memory allocateData = __generateAllocation(recipientId, maxVoiceCreditsPerAllocator);

        vm.startPrank(address(allo()));

        qvSimpleStrategy().allocate(allocateData, allocator);
        qvSimpleStrategy().allocate(allocateData, allocator);

        uint256 total = qvSimpleStrategy().getAllocator(allocator,recipientId);

        //expect to be maxVoiceCreditsPerAllocator*2 but it's maxVoiceCreditsPerAllocator*3
        assertEq(maxVoiceCreditsPerAllocator * 3,total);
    }
```

## Impact
 incorrect calculation

## Code Snippet
```solidity
    function _qv_allocate(
        Allocator storage _allocator,
        Recipient storage _recipient,
        address _recipientId,
        uint256 _voiceCreditsToAllocate,
        address _sender
    ) internal onlyActiveAllocation {
        // check the `_voiceCreditsToAllocate` is > 0
        if (_voiceCreditsToAllocate == 0) revert INVALID();

        // get the previous values
        uint256 creditsCastToRecipient = _allocator.voiceCreditsCastToRecipient[_recipientId];
        uint256 votesCastToRecipient = _allocator.votesCastToRecipient[_recipientId];

        // get the total credits and calculate the vote result
        uint256 totalCredits = _voiceCreditsToAllocate + creditsCastToRecipient; ;//@audit <--------- voiceCreditsCastToRecipient once.
        uint256 voteResult = _sqrt(totalCredits * 1e18);

        // update the values
        voteResult -= votesCastToRecipient;
        totalRecipientVotes += voteResult;
        _recipient.totalVotesReceived += voteResult;

        _allocator.voiceCreditsCastToRecipient[_recipientId] += totalCredits;//@audit <--------- voiceCreditsCastToRecipient twice.
        _allocator.votesCastToRecipient[_recipientId] += voteResult;

        // emit the event with the vote results
        emit Allocated(_recipientId, voteResult, _sender);
    }
```

## Tool used

Manual Review

## Recommendation
No need for redundant accumulation