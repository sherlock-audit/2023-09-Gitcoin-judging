Damaged Cornflower Turkey

medium

# Allocating and voting on 'address(0)' is possible in RFPCommitteeStrategy
Lack of `address(0)` makes it possible to allocate and vote on the `address(0)`, resulting in the poolManager having to withdraw the funds of that contract and deploying a new one while paying the percentFee and baseFee again.
## Vulnerability Detail
There is no `address(0)` check in `RFPCommitteeStrategy._allocate`.
```javascript
source:contracts/strategies/rfp-committee/RFPCommitteeStrategy.sol

function _allocate(bytes memory _data, address _sender) internal override onlyPoolManager(_sender) {
        if (acceptedRecipientId != address(0)) {
            revert RECIPIENT_ALREADY_ACCEPTED();
        }

        address voteCastedTo = votedFor[_sender];
        if (voteCastedTo != address(0)) {
            votes[voteCastedTo] -= 1;
        }

        address recipientId = abi.decode(_data, (address));

        votes[recipientId] += 1;
        votedFor[_sender] = recipientId;

        emit Voted(recipientId, _sender);

        if (votes[recipientId] == voteThreshold) {
            acceptedRecipientId = recipientId;

            Recipient storage recipient = _recipients[acceptedRecipientId];
            recipient.recipientStatus = Status.Accepted;

            _setPoolActive(false);

            emit Allocated(acceptedRecipientId, recipient.proposalBid, allo.getPool(poolId).token, address(0));
        }
    }
```

Moreover, `address(0)` will become the `acceptedRecipientId` if this check passes in `RFPCommitteeStrategy._allocate`:
- `if (votes[recipientId] == voteThreshold)`

This means that the poolManager will have no other choice than withdrawing the funds out of `RFPCommitteeStrategy.sol`.

The reason that the poolManager has to withdraw the funds out of `RFPComitteeStrategy` is:
- He does not want to distribute funds to `address(0)`.
- He can not change the `acceptedRecipientId` to someone else because of this check in `RFPCommitteeStrategy._allocate`:
	- `if (acceptedRecipientId != address(0)) {revert RECIPIENT_ALREADY_ACCEPTED();}`
This leaves the poolManager no other option than withdrawing the funds, creating a new pool again and paying the `percentFee` and `baseFee`(if set) again.

Here's a POC showcasing that voting and allocating to `address(0)` is possible:
```javascript
source: test/foundry/strategies/RFPCommitteeStrategy.t.sol

// put this in test/foundry/strategies/RFPCommitteeStrategy.t.sol
// run using:
// forge test --match-contract RFPCommitteeStrategy --match-test test_alloAndVoteZeroAddress -vvvv
    function test_alloAndVoteZeroAddress() public {
        // Register recipient and set milestones
        __register_recipient();
        __setMilestones();

        // Check that the voteTreshold == 2 
        assertEq(voteThreshold, 2);


        // Vote twice to reach voteTreshold which triggers allocation
        vm.startPrank(address(allo()));
        // First vote
        strategy.allocate(abi.encode(address(0)), address(pool_admin()));

        // Expect allocated emit
        vm.expectEmit();
        emit Allocated(address(0), 0, NATIVE, address(0));
        
        // Second vote
        strategy.allocate(abi.encode(address(0)), address(pool_admin()));
        vm.stopPrank();

        // Check current votes on address(0), should equal to 2
        uint256 votes = strategy.votes(address(0));
        assertEq(votes, 2);
    }
```

## Impact
Redploying the strategy, paying the `baseFee`, paying the percentFee.

## Code Snippet
https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/rfp-committee/RFPCommitteeStrategy.sol#L102-L138
## Tool used
Manual Review
## Recommendation
Add a zero address check in `RFPCommitteeStrategy._allocate`:
```diff
 function _allocate(bytes memory _data, address _sender) internal override onlyPoolManager(_sender){
// .. ommitted code
// add this to Line 116
+ if(recipientId == address(0)) revert();
// .. ommitted code
 }
```