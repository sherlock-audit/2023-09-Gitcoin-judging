Silly Carob Opossum

high

# QVSimpleStrategy allocator can use unlimited voice credits
Any allocator can use an unlimited number of voice credits by calling the `allocate` function multiple times with `_voiceCreditsToAllocate` value not greater than `maxVoiceCreditsPerAllocator`.

## Vulnerability Detail

The `QVBaseStrategy` contract does't clearly define the purpose of the `Allocator`'s property `voiceCredits`. From the definition, it seems that this value should determine the maximum number of votes that an allocator can use.

```solidity
/// @notice The details of the allocator
struct Allocator {
    // slot 0
    uint256 voiceCredits;
    // slots [1...n]
    mapping(address => uint256) voiceCreditsCastToRecipient;
    mapping(address => uint256) votesCastToRecipient;
}
```

But in `QVSimpleStrategy` contract this value is used as the number of votes that were used.

```solidity
function _allocate(bytes memory _data, address _sender) internal virtual override {
    ...
     if (!_hasVoiceCreditsLeft(voiceCreditsToAllocate, allocator.voiceCredits)) revert INVALID();
    ...
}

function _hasVoiceCreditsLeft(uint256 _voiceCreditsToAllocate, uint256 _allocatedVoiceCredits)
    internal
    view
    override
    returns (bool)
{
    return _voiceCreditsToAllocate + _allocatedVoiceCredits <= maxVoiceCreditsPerAllocator;
}
```

The value of `voiceCredits` property doesn't change anywhere and is always equal to 0. Therefore, the check if there are any voice credits left will always pass if `_voiceCreditsToAllocate` is not greater than `maxVoiceCreditsPerAllocator`.

## Impact

Any allocator can use an unlimited number of voice credits and influence the results of allocation. Since an external (not a pool manager), untrusted address can act as an allocator, this problem is considered high.

## POC

Add this test to `QVSimpleStrategyTest`, run with `forge test --mc QVSimpleStrategyTest --mt testPOC -v`.

```solidity
function testPOC() external {
    address recipientId = __register_accept_recipient();
    address allocator = randomAddress();

    vm.prank(pool_manager1());
    qvSimpleStrategy().addAllocator(allocator);

    vm.warp(allocationStartTime + 1);

    for (uint256 i; i < 100; i++) {
        bytes memory allocateData = __generateAllocation(recipientId, maxVoiceCreditsPerAllocator);
        vm.startPrank(address(allo()));
        qvSimpleStrategy().allocate(allocateData, allocator);
    }
}
```

## Code Snippet

https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/qv-base/QVBaseStrategy.sol#L139-L146
https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/qv-base/QVBaseStrategy.sol#L499-L534
https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/qv-simple/QVSimpleStrategy.sol#L103-L124
https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/qv-simple/QVSimpleStrategy.sol#L140-L151

## Tool used

Manual Review

## Recommendation

1. Clearly define the purpose of the property in the name.
```solidity
/// @notice The details of the allocator
struct Allocator {
    // slot 0
    uint256 allocatedVoiceCredits;
    // slots [1...n]
    mapping(address => uint256) voiceCreditsCastToRecipient;
    mapping(address => uint256) votesCastToRecipient;
}
```
2. Update it's value when the `_qv_allocate` function is called.
```solidity
function _qv_allocate(
    Allocator storage _allocator,
    Recipient storage _recipient,
    address _recipientId,
    uint256 _voiceCreditsToAllocate,
    address _sender
) internal onlyActiveAllocation {
    ...
    _allocator.voiceCredits += _voiceCreditsToAllocate;
    _allocator.voiceCreditsCastToRecipient[_recipientId] += totalCredits;
    _allocator.votesCastToRecipient[_recipientId] += voteResult;
    ...
}
```