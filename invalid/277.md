Little Cloth Coyote

high

# Votes Are Not Reset Between Milestones in RFPCommitteeStrategy, causing wrong vote calculation
## Summary
In RFPCommitteeStrategy, votes are not reset after each milestone, throwing off vote calculation for next milestone.

## Vulnerability Detail
To execute and complete the first `upcomingMilestone`, `acceptedRecipientId` must satisfy the `voteThreshold` condition. The system uses two mappings, `votes` and `votedFor`, to manage vote-related data. However, these mappings are not cleared after the completion of each milestone, leading to a situation where a vote recipient could become `acceptedRecipientId` prematurely, even if the `voteThreshold` for that specific milestone has not been met.
```solidity
    function _allocate(bytes memory _data, address _sender) internal override onlyPoolManager(_sender) {
        if (acceptedRecipientId != address(0)) {
            revert RECIPIENT_ALREADY_ACCEPTED();
        }

        // Check if the allocator has already casted the vote
        address voteCastedTo = votedFor[_sender];
        if (voteCastedTo != address(0)) {
            // remove the old vote to allow recasting of vote
            votes[voteCastedTo] -= 1;
        }

        // Decode the '_data'
        address recipientId = abi.decode(_data, (address));

        // Increment the votes for the recipient
        votes[recipientId] += 1;
        // Update the votedFor mapping
        votedFor[_sender] = recipientId;

        // Emit the event
        emit Voted(recipientId, _sender);

        // Check if the recipient has reached the vote threshold
        if (votes[recipientId] == voteThreshold) {
            // Set the accepted recipient
            acceptedRecipientId = recipientId;

            Recipient storage recipient = _recipients[acceptedRecipientId];
            recipient.recipientStatus = Status.Accepted;

            // Set the pool to inactive
            _setPoolActive(false);

            emit Allocated(acceptedRecipientId, recipient.proposalBid, allo.getPool(poolId).token, address(0));
        }
    }
```
Consider the following scenario:

1. A pool consists of 10 committees, and the `voteThreshold` is set to 6.
2. There are three potential candidates for votes: Alice, Bob, and Chad.
3. In the first milestone, Alice receives 6 votes, Bob receives 4 votes, and Chad gets 0 votes.
4. Following the conclusion of the first milestone (at which point votes should have been reset), the committees proceeded to cast their votes for the second milestone.
5. The first two committees who previously voted for Alice in last milestone, cast their votes for Bob, which results in Bob becoming the `acceptedRecipientId`.
6. Bob becomes the `acceptedRecipientId` even though only 2 of 10 committees voted and `voteThreshold` of 6 has not been met.

Side Note: 
`if (acceptedRecipientId != address(0))` check should be removed, as it prevents committees from voting beyond first milestone. Please refer to other issue I raised. 

## Impact
`acceptedRecipientId` can potentially be selected without meeting `voteThreshold`. Any subsequent milestone votes calculation are wrong.

## Code Snippet
https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/rfp-committee/RFPCommitteeStrategy.sol#L102
https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/rfp-simple/RFPSimpleStrategy.sol#L417
## Tool used

Manual Review

## Recommendation
I recommend using nested mappings to keep track of votes for each milestone.
```solidity
    /// @dev 'allocator' to 'recipientId'
-   mapping(address => address) public votedFor;
    /// @dev 'allocator' to 'milestone' to 'recipientId'
+   mapping(address => mapping (uint256 => address)) public votedFor;

    /// @notice This maps the recipient to the number of votes they have
    /// @dev 'recipientId' to 'votes'
-   mapping(address => uint256) public votes;
    /// @dev 'recipientId' to 'milestone' to 'votes'
+   mapping(address => mapping(uint256 => uint256)) votes;
```
```solidity
    function _allocate(bytes memory _data, address _sender) internal override onlyPoolManager(_sender) {
        if (acceptedRecipientId != address(0)) {
            revert RECIPIENT_ALREADY_ACCEPTED();
        }

        // Check if the allocator has already casted the vote
-       address voteCastedTo = votedFor[_sender];
+       address voteCastedTo = votedFor[_sender][upcomingMilestone];
        if (voteCastedTo != address(0)) {
            // remove the old vote to allow recasting of vote
-           votes[voteCastedTo] -= 1;
+           votes[voteCastedTo][upcomingMilestone] -= 1;
        }

        // Decode the '_data'
        address recipientId = abi.decode(_data, (address));

        // Increment the votes for the recipient
-       votes[recipientId] += 1;
+       votes[recipientId][upcomingMilestone] += 1;
        // Update the votedFor mapping
-       votedFor[_sender] = recipientId;
+       votedFor[_sender][upcomingMilestone] = recipientId;

        // Emit the event
        emit Voted(recipientId, _sender);

        // Check if the recipient has reached the vote threshold
-       if (votes[recipientId] == voteThreshold) {
+       if (votes[recipientId][upcomingMilestone] == voteThreshold) {
            // Set the accepted recipient
            acceptedRecipientId = recipientId;

            Recipient storage recipient = _recipients[acceptedRecipientId];
            recipient.recipientStatus = Status.Accepted;

            // Set the pool to inactive
            _setPoolActive(false);

            emit Allocated(acceptedRecipientId, recipient.proposalBid, allo.getPool(poolId).token, address(0));
        }
    }
```
