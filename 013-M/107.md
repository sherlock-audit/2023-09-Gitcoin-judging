Curved Chocolate Iguana

medium

# Anchor creation and ContractFactory deployments will fail on zkSync Era
Creation of new anchors and `ContractFactory` deployments will fail on zkSync Era.

## Vulnerability Detail
`CREATE3` fails on zkSync Era.

https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/core/Registry.sol#L350

https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/factory/ContractFactory.sol#L105

## Impact
It is not possible to create or update profiles and deploy new contracts using the  `ContractFactory` on zkSync Era.

## Code Snippet
```solidity
//SPDX-License-Identifier: Unlicense
pragma solidity ^0.8.0;

import {CREATE3} from "solady/src/utils/CREATE3.sol";

contract Anchor {
    uint256 public immutable profileId;

    constructor(uint256 profileId_) {
        profileId = profileId_;
    }
}

contract Cloner {
    constructor() {}

    function create3Anchor(uint256 profileId_, uint256 nonce_) external returns (address) {
        bytes32 salt = keccak256(abi.encodePacked(profileId_, nonce_));
        bytes memory creationCode = abi.encodePacked(type(Anchor).creationCode, abi.encode(profileId_));

        return CREATE3.deploy(salt, creationCode, 0);
    }
}

```

Calling `create3Anchor` will fail here.

You can try this contract on zkSync Era testnet :
https://goerli.explorer.zksync.io/address/0x67a1c4e70550c22a9E5A4F91Cb259DFc2008Cc39#contract

## Tool used

Manual Review

## Recommendation
`CREATE3` hole purpose is to make contracts deployments on the same addresses easy.

In zkSync Era case, contracts address will be different anyway due to the use of prefixes when computing `CREATE` and `CREATE2` addresses.

https://github.com/matter-labs/era-system-contracts/blob/main/contracts/Constants.sol#L79
https://github.com/matter-labs/era-system-contracts/blob/main/contracts/ContractDeployer.sol#L91

`CREATE3` can be replaced on zkSync Era deployments.

### Example Contract
```solidity
//SPDX-License-Identifier: Unlicense
pragma solidity ^0.8.0;

import {CREATE3} from "solady/src/utils/CREATE3.sol";

interface ContractDeployer {
    function getNewAddressCreate2(  
        address _sender,
        bytes32 _bytecodeHash,
        bytes32 _salt,
        bytes calldata _input
    ) external view returns (address newAddress);
}

contract Anchor {
    uint256 public immutable profileId;

    constructor(uint256 profileId_) {
        profileId = profileId_;
    }

    receive() external payable {}
    fallback() external payable {}
}

contract Cloner {
    event CreateAnchor(address indexed expected, address indexed actual, bytes32 indexed hash);

    bytes32 private _anchorHash;

    ContractDeployer private immutable _contractDeployer;

    constructor(bytes32 anchorHash_) {
        _anchorHash = anchorHash_;
        _contractDeployer = ContractDeployer(0x0000000000000000000000000000000000008006);
    }

    function createAnchor(uint256 profileId_, uint256 nonce_) external {
        bytes32 salt = keccak256(abi.encodePacked(profileId_, nonce_));

        address deployedAddress;
        address deploymentAddress = _contractDeployer.getNewAddressCreate2(address(this), _anchorHash, salt, abi.encode(profileId_));
        
        if (deploymentAddress.code.length > 0) {
            if (Anchor(payable(deploymentAddress)).profileId() != profileId_) revert('ANCHOR_ERROR');

            deployedAddress = deploymentAddress;
        } else {
            deployedAddress = address(new Anchor{salt: salt}(profileId_));
        }

        emit CreateAnchor(deploymentAddress, deployedAddress, _anchorHash);
    } 

    function deployContract(string memory contractName_, string memory version_, bytes memory creationCode_) external payable returns (address addr) {
        bytes32 salt = keccak256(abi.encodePacked(contractName_, version_));

        assembly {
            addr := create2(0, add(creationCode_, 32), mload(creationCode_), salt)
        }

        addr.call{value: msg.value}("");
    }

    function getAnchorCreationCode(uint256 profileId_) external view returns (bytes memory) {
        return abi.encodePacked(type(Anchor).creationCode, abi.encode(profileId_));
    } 
}
```
### Example Deployment Script
```javascript
import { Wallet, utils } from 'zksync-web3';
import * as ethers from 'ethers';
import { HardhatRuntimeEnvironment } from 'hardhat/types';
import { Deployer } from '@matterlabs/hardhat-zksync-deploy';

import dotenv from 'dotenv';
dotenv.config();

const PRIVATE_KEY = process.env.WALLET_PRIVATE_KEY || '';

if (!PRIVATE_KEY)
  throw '⛔️ Private key not detected! Add it to the .env file!';

export default async function (hre: HardhatRuntimeEnvironment) {
  const wallet = new Wallet(PRIVATE_KEY);
  const deployer = new Deployer(hre, wallet);
  const artifact = await deployer.loadArtifact('Cloner');
  const anchor = await deployer.loadArtifact('Anchor');

  const clonerContract = await deployer.deploy(artifact, [
    utils.hashBytecode(anchor.bytecode), // Pass the bytecode hash for the Anchor contract
  ]);

  const contractAddress = clonerContract.address;
  console.log(`${artifact.contractName} was deployed to ${contractAddress}`);

  if (process.env.NODE_ENV != 'test') {
    const contractFullyQualifedName = 'contracts/Cloner.sol:Cloner';

    const verificationId = await hre.run('verify:verify', {
      address: contractAddress,
      contract: contractFullyQualifedName,
      constructorArguments: [utils.hashBytecode(anchor.bytecode)],
      bytecode: artifact.bytecode,
    });
  } else {
    console.log(`Contract not verified, deployed locally.`);
  }
}
```