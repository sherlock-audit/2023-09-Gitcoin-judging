Brilliant Chambray Reindeer

medium

# RFPSimpleStrategy.sol#_distribute() - Milestones that are rejected can still be distributed
## Summary
There is a function function called `rejectMilestone` which the pool manager can call to set the `milestoneStatus` of a milestone to `Status.Rejected`. The comments clearly state "Reject pending milestone submmited by the acceptedRecipientId." The issue is there is no check inside `_distribute` to check if the milestone has been rejected, effectively making `rejectMilestone` useless.

## Vulnerability Detail
We can see that `rejectMilestone` sets the status of a milestone to `Status.Rejected`.
```javascript
    /// @notice Reject pending milestone submmited by the acceptedRecipientId.
    /// @dev 'msg.sender' must be a pool manager to reject a milestone. Emits a 'MilestoneStatusChanged()' event.
    /// @param _milestoneId ID of the milestone
    function rejectMilestone(uint256 _milestoneId) external onlyPoolManager(msg.sender) {
        // Check if the milestone is already accepted
        if (milestones[_milestoneId].milestoneStatus == Status.Accepted) revert MILESTONE_ALREADY_ACCEPTED();

        milestones[_milestoneId].milestoneStatus = Status.Rejected;

        emit MilestoneStatusChanged(_milestoneId, milestones[_milestoneId].milestoneStatus);
    }
```

This is the code inside `_distribute`.
```javascript
    /// @notice Distribute the upcoming milestone to acceptedRecipientId.
    /// @dev '_sender' must be a pool manager to distribute.
    /// @param _sender The sender of the distribution
    function _distribute(address[] memory, bytes memory, address _sender)
        internal
        virtual
        override
        onlyInactivePool
        onlyPoolManager(_sender)
    {
        // check to make sure there is a pending milestone
        if (upcomingMilestone >= milestones.length) revert INVALID_MILESTONE();

        IAllo.Pool memory pool = allo.getPool(poolId);
        Milestone storage milestone = milestones[upcomingMilestone];
        Recipient memory recipient = _recipients[acceptedRecipientId];

        // make sure has enough funds to distribute based on the proposal bid
        // by sending funds to the pool?
        if (recipient.proposalBid > poolAmount) revert NOT_ENOUGH_FUNDS();

        // Calculate the amount to be distributed for the milestone
        uint256 amount = (recipient.proposalBid * milestone.amountPercentage) / 1e18;

        // Get the pool, subtract the amount and transfer to the recipient
        poolAmount -= amount;
        _transferAmount(pool.token, recipient.recipientAddress, amount);

        // Set the milestone status to 'Accepted'
        milestone.milestoneStatus = Status.Accepted;

        // Increment the upcoming milestone
        upcomingMilestone++;

        // Emit events for the milestone and the distribution
        emit MilestoneStatusChanged(upcomingMilestone, Status.Accepted);
        emit Distributed(acceptedRecipientId, recipient.recipientAddress, amount, _sender);
    }
```

Nowhere inside `_distribute` do we check if the `upcomingMilestone` has been rejected or not. This can lead to an unwanted situation where the pool manager rejects a milestone,  accidently or not calls, `_distribute` and distributes the rewards for that milestone.

## Impact
Funds can be distributed for a rejected milestone. 
The code doesn't do what the comment states.

## Code Snippet
https://github.com/sherlock-audit/2023-09-Gitcoin/blob/6430c8004017e96ae2f5aac365bdefd0b6eeea72/allo-v2/contracts/strategies/rfp-simple/RFPSimpleStrategy.sol#L417-L450

## Tool used
Manual Review

## Recommendation
Add a check inside `_distribute` that checks if the `upcomingMilestone` has been rejected.
