Proud Honey Aardvark

high

# QVBaseStrategy:_qv_allocate: `totalVotesReceived` can be raised exponentially
When spending credits with `_qv_allocate()` one credit at a time  instead of all at once `allocator.voiceCreditsCastToRecipient[recipientId]` grows exponentially exceeding the amount of credits to give.
## Vulnerability Detail
Instead of just writing the value back in [QVBaseStrategy.sol#L529](https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/qv-base/QVBaseStrategy.sol#L529) the **new total** ([QVBaseStrategy.sol#L521](https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/qv-base/QVBaseStrategy.sol#L521)) **is added** back **on top of** the **old total value** ([QVBaseStrategy.sol#L517](https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/qv-base/QVBaseStrategy.sol#L517)).
When calling the function multiple times spending on credit at a time the value grows exponentially and since this value flows  into `totalVotesReceived`, `recipient.totalVotesReceived` itself also grows exponentially.

The relevant code and a Foundry Test highlighting the issue can be found below.

## Impact
Since `allocator.voiceCreditsCastToRecipient` flows into the `voteResult` and in `totalVotesReceived` as well as `totalRecipientVotes` a malicious pool-manager can first use bug #3 approve a recipient and then exponentially tilt the vote in favour of that recipient. Creating an unfair financial advantage [(see `_getPayout()`)](https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/qv-base/QVBaseStrategy.sol#L571). 
## Code Snippet
Affected code: [QVBaseStrategy.sol#L517C1-L529C78](https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/qv-base/QVBaseStrategy.sol#L517C1-L529C78)

The bug: [QVBaseStrategy.sol#L529](https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/qv-base/QVBaseStrategy.sol#L529)
```solidity
        _allocator.voiceCreditsCastToRecipient[_recipientId] += totalCredits;
```

Foundry Test highlighting the issue:
```solidity
    function test_allocate_BUG() public virtual {
        //address recipientId = __register_accept_recipient();
        vm.warp(registrationStartTime + 10);

        // register
        vm.startPrank(address(allo()));
        bytes memory data = __generateRecipientWithoutId(false);
        address recipientId1 = qvStrategy().registerRecipient(data, recipient1());
        address recipientId2 = qvStrategy().registerRecipient(data, recipient2());
        vm.stopPrank();
        
        address allocator = randomAddress();

        vm.startPrank(pool_manager2());
        vm.warp(allocationStartTime + 10);

        bytes memory spendAllCredits = __generateAllocation(recipientId1, 4);
        bytes memory spendOneCredit = __generateAllocation(recipientId2, 1);
        vm.stopPrank();
        vm.startPrank(address(allo()));

        // TEST STARTS HERE
        // spend credits all at once
        qvStrategy().allocate(spendAllCredits, allocator);

        // spend credits one at a time
        qvStrategy().allocate(spendOneCredit, allocator);
        qvStrategy().allocate(spendOneCredit, allocator);
        qvStrategy().allocate(spendOneCredit, allocator);
        qvStrategy().allocate(spendOneCredit, allocator);

        QVBaseStrategy.Recipient memory recipient_1 = qvStrategy().getRecipient(recipientId1);
        QVBaseStrategy.Recipient memory recipient_2 = qvStrategy().getRecipient(recipientId2);

        assertEq(recipient_1.totalVotesReceived, recipient_2.totalVotesReceived);
    }
```
Consider that the impact will increase with the amount of credits that can be spent, here only 4 credits where available:
```solidity
[FAIL. Reason: Assertion failed.] test_allocate_BUG() (gas: 480130)
Logs:
  Error: a == b not satisfied [uint]
        Left: 2000000000
       Right: 2828427124
```
## Tool used

Manual Review and Foundry Test

## Recommendation
Delete the `+` and make it
```solidity
_allocator.voiceCreditsCastToRecipient[_recipientId] = totalCredits;
```
in [QVBaseStrategy.sol#L529](https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/qv-base/QVBaseStrategy.sol#L529)