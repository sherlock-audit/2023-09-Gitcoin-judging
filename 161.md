Clever Metal Giraffe

high

# `RFPSimpleStrategy._distribute` may fail due to wrong check of enough funds
## Summary

When the accepted recipient submits a milestone, the pool manager calls `distribute` to distribute funds to the accepted recipient. `RFPSimpleStrategy._distribute` tries to ensure that the pool has enough funds to distribute based on the proposal bid. But it uses a wrong check. `RFPSimpleStrategy._distribute` may revert when the pool has enough funds.

## Vulnerability Detail

`RFPSimpleStrategy._distribute` checks whether `recipient.proposalBid > poolAmount` to ensure that the pool has enough funds, then the distributed funds are subtracted from `poolAmount`
https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/rfp-simple/RFPSimpleStrategy.sol#L432
```solidity
    function _distribute(address[] memory, bytes memory, address _sender)
        internal
        virtual
        override
        onlyInactivePool
        onlyPoolManager(_sender)
    {
        …

        // make sure has enough funds to distribute based on the proposal bid
        if (recipient.proposalBid > poolAmount) revert NOT_ENOUGH_FUNDS();

        // Calculate the amount to be distributed for the milestone
        uint256 amount = (recipient.proposalBid * milestone.amountPercentage) / 1e18;

        // Get the pool, subtract the amount and transfer to the recipient
        poolAmount -= amount;
        _transferAmount(pool.token, recipient.recipientAddress, amount);
        …
    }
```

`milestone.amountPercentage` is set in `RFPSimpleStrategy.setMilestones`. All milestone amount percentage totals to 1e18
https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/rfp-simple/RFPSimpleStrategy.sol#L227
```solidity
    function setMilestones(Milestone[] memory _milestones) external onlyPoolManager(msg.sender) {
        if (upcomingMilestone != 0) revert MILESTONES_ALREADY_SET();

        uint256 totalAmountPercentage;

        // Loop through the milestones and add them to the milestones array
        uint256 milestonesLength = _milestones.length;
        for (uint256 i; i < milestonesLength;) {
            totalAmountPercentage += _milestones[i].amountPercentage;
            milestones.push(_milestones[i]);

            unchecked {
                i++;
            }
        }

        // Check if the all milestone amount percentage totals to 1e18(100%)
        if (totalAmountPercentage != 1e18) revert INVALID_MILESTONE();

        emit MilestonesSet();
    }
```

Suppose that `poolAmount` is 10000 and `recipient.proposalBid` is 8000. And there are two milestones. Their percentages are both 5e17(50%).
* When the recipient calls `submitUpcomingMilestone` for the first time, and the pool manager calls `distribute`.
```solidity
// recipient.proposalBid > poolAmount is false. The check is passed.
amount = (recipient.proposalBid * milestone.amountPercentage) / 1e18 = 8000 * 5e17 / 1e18 = 4000
poolAmount -= amount; // poolAmount = 10000 - 4000 = 6000
```
* Afterwards, the recipient calls `submitUpcomingMilestone` again. The pool manager calls `distribute` as usual.
```solidity
recipient.proposalBid > poolAmount is true, revert NOT_ENOUGH_FUNDS() // 8000 > 4000
```

We can find out that the pool has enough funds(which is 4000). But `RFPSimpleStrategy._distribute` reverts.


## Impact

`RFPSimpleStrategy._distribute` may revert when the pool has enough funds.

## Code Snippet

https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/rfp-simple/RFPSimpleStrategy.sol#L432
https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/rfp-simple/RFPSimpleStrategy.sol#L227


## Tool used

Manual Review

## Recommendation

`RFPSimpleStrategy._distribute` should check `(recipient.proposalBid * milestone.amountPercentage) / 1e18` instead of `recipient.proposalBid`
```diff
    function _distribute(address[] memory, bytes memory, address _sender)
        internal
        virtual
        override
        onlyInactivePool
        onlyPoolManager(_sender)
    {
        …

-       // make sure has enough funds to distribute based on the proposal bid
-       if (recipient.proposalBid > poolAmount) revert NOT_ENOUGH_FUNDS();

        // Calculate the amount to be distributed for the milestone
        uint256 amount = (recipient.proposalBid * milestone.amountPercentage) / 1e18;

+       // make sure has enough funds to distribute based on the proposal bid
+       if (amount  > poolAmount) revert NOT_ENOUGH_FUNDS();

        // Get the pool, subtract the amount and transfer to the recipient
        poolAmount -= amount;
        _transferAmount(pool.token, recipient.recipientAddress, amount);
        …
    }
```
