Sneaky Amethyst Robin

high

# Unregistered recipients in DonationVotingMerkleDistributionBaseStrategy will have the status of the 0th recipient

Unregistered recipients will have the default status index of 0, which is a valid index that will apply to the initial recipient to register. As a result, all unregistered recipients will have the same status as the initial recipient and can bypass registration to have special access to protected logic.

## Vulnerability Detail

When a user calls `registerRecipient` for the first time, we set their `recipientToStatusIndexes` to the current `recipientsCounter` and we set their status at that index in the `statusesBitMap`. The problem with this is that if a user hasn't registered, when their recipient status is checked, it checks their index in the `recipientToStatusIndexes` mapping, which since the user doesn't have an index set in this mapping yet, it returns 0. The 0 index is of course a valid index in the `statusesBitMap`, so the returned status is that of the 0th registered recipient.

If the 0th registered recipient has been accepted, every address to interact with the protocol without registering will be marked as accepted and have special access like being allocated to. These unregistered users will also have the accepted status returned when their address is queried in `getRecipientStatus`, which they may use to convince the pool manager to include their address in distribution.

## Impact

All unregistered users have the same recipient status as the 0th recipient, giving them unintended special privilege to contract logic.

## Code Snippet

https://github.com/allo-protocol/allo-v2/blob/851571c27df5c16f6586ece2a1cb6fd0acf04ec9/contracts/strategies/donation-voting-merkle-base/DonationVotingMerkleDistributionBaseStrategy.sol#L819
```solidity
/// @notice Get recipient status
/// @param _recipientId ID of the recipient
/// @return status The status of the recipient
function _getUintRecipientStatus(address _recipientId) internal view returns (uint8 status) {
    // Get the column index and current row
    (, uint256 colIndex, uint256 currentRow) = _getStatusRowColumn(_recipientId);

    // Get the status from the 'currentRow' shifting by the 'colIndex'
    status = uint8((currentRow >> colIndex) & 15);

    // Return the status
    return status;
}

/// @notice Get recipient status 'rowIndex', 'colIndex' and 'currentRow'.
/// @param _recipientId ID of the recipient
/// @return (rowIndex, colIndex, currentRow)
function _getStatusRowColumn(address _recipientId) internal view returns (uint256, uint256, uint256) {
    // @audit unregistered recipients will have the same status index as the 0th registered recipient
    uint256 recipientIndex = recipientToStatusIndexes[_recipientId];

    uint256 rowIndex = recipientIndex / 64; // 256 / 4
    uint256 colIndex = (recipientIndex % 64) * 4;

    return (rowIndex, colIndex, statusesBitMap[rowIndex]);
}
```

## Tool used

Manual Review

## Recommendation

`recipientCounter` should start at 1 so that a recipient's status index is never the default value of 0.