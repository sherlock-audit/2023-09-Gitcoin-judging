Proper Fossilized Sardine

medium

# User must send extra funds to create a pool in Allo.sol
## Summary
In order to create a pool in `Allo.sol` a `baseFee` must be paid. However, the way the contract is currently coded if the user attempts to create a pool sending the exact required amount the transaction will revert.

## Vulnerability Detail

See the code block below, which can be found in `Allo::_createPool()`

```Solidity
if (baseFee > 0) {
    // To prevent paying the baseFee from the Allo contract's balance
    // If _token is NATIVE, then baseFee + _amount should be >= than msg.value.
    // If _token is not NATIVE, then baseFee should be >= than msg.value.
    // @audit - users have to send more msg.value than the baseFee. Should be ">" not ">="
    if ((_token == NATIVE && (baseFee + _amount >= msg.value)) || (_token != NATIVE && baseFee >= msg.value)) {
          revert NOT_ENOUGH_FUNDS();
     }
     _transferAmount(NATIVE, treasury, baseFee);
     emit BaseFeePaid(poolId, baseFee);
}
```

The if statement shown above requires the `msg.value` to be greater than the `baseFee`, meaning that the users have to send more value than required by the protocol. Running the test below in `Allo.t.sol` will further illustrate this.

```Solidity
function test_createPoolWithBaseFee() public {
        uint256 baseFee = 1e17;

        allo().updateBaseFee(baseFee);

        vm.expectEmit(true, false, false, true);
        emit BaseFeePaid(1, baseFee);

        vm.deal(address(pool_admin()), 1e18);

        vm.prank(pool_admin());
        // @audit - changed value being sent to the exact baseFee amount
        allo().createPoolWithCustomStrategy{value: baseFee}(
            poolProfile_id(), strategy, "0x", NATIVE, 0, metadata, pool_managers()
        );
}
```

The test above will revert with the expected error `NOT_ENOUGH_FUNDS()` even though the user sent the required baseFee amount.

## Impact

This will result in users having their transactions reverted when using the `Allo` contract correctly. Furthermore, the users will have to send more than the required amount to create a pool.

This issue has been listed as a Medium. The likelihood is high, however the impact is low since users could just send an extra wei to get their transactions through.

## Code Snippet

https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/core/Allo.sol#L469-L478

## Tool used

Manual Review & Foundry

## Recommendation

Remove the following line of code:

```Solidity
if ((_token == NATIVE && (baseFee + _amount >= msg.value)) || (_token != NATIVE && baseFee >= msg.value)) {
      revert NOT_ENOUGH_FUNDS();
}
```

and replace it with:

```Solidity
if ((_token == NATIVE && (baseFee + _amount > msg.value)) || (_token != NATIVE && baseFee > msg.value)) {
      revert NOT_ENOUGH_FUNDS();
}
```
