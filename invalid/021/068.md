Cuddly Pewter Shark

medium

# Incorrect calculation of fees in the `Allo._createPool()` function
## Summary
Incorrect calculation of fees in the `Allo._createPool()` function.

## Vulnerability Detail
The `Allo._createPool()` function verifies `msg.value` by the following condition: `(_token == NATIVE && (baseFee + _amount >= msg.value)) || (_token != NATIVE && baseFee >= msg.value)`:

```solidity

if (baseFee > 0) {
    // To prevent paying the baseFee from the Allo contract's balance
    // If _token is NATIVE, then baseFee + _amount should be >= than msg.value.
    // If _token is not NATIVE, then baseFee should be >= than msg.value.
    if ((_token == NATIVE && (baseFee + _amount >= msg.value)) || (_token != NATIVE && baseFee >= msg.value)) {
        revert NOT_ENOUGH_FUNDS();
    }
    _transferAmount(NATIVE, treasury, baseFee);
    emit BaseFeePaid(poolId, baseFee);
}

```

Please, pay special attention to the two `>=` signs, which indicates that a tx with `msg.value` == `baseFee`, for example, is invalid.
The comment above the condition states that such logic is wrong.

## PoC
```solidity
// SPDX-License-Identifier: AGPL-3.0-only
pragma solidity ^0.8.19;

import "forge-std/Test.sol";
import "../core/Allo.t.sol";

contract AuditTest is AlloTest {
    function test_audit_revertOnBaseFee() public {
        vm.startPrank(local());
        allo().updateBaseFee(100);
        allo().updatePercentFee(0);
        vm.stopPrank();

        vm.deal(pool_admin(), 1 ether);

        vm.startPrank(pool_admin());

        // ok
        allo().createPoolWithCustomStrategy{value: 101}(
            poolProfile_id(),
            address(new MockStrategy(address(allo()))),
            "0x",
            address(token),
            0,
            metadata,
            pool_managers()
        );

        // create mock strategy before vm.expectRevert
        address mock = address(new MockStrategy(address(allo())));

        // will revert
        vm.expectRevert(NOT_ENOUGH_FUNDS.selector);
        allo().createPoolWithCustomStrategy{value: 100}(
            poolProfile_id(),
            mock,
            "0x",
            address(token),
            0,
            metadata,
            pool_managers()
        );
    }
}
```

## Impact
Incorrect work of the `Allo.createPoolWithCustomStrategy()` and `Allo.createPool()` functions.

## Code Snippet
https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/core/Allo.sol#L473

## Tool used
Manual Review

## Recommendation
Replace the condition to `(_token == NATIVE && (baseFee + _amount > msg.value)) || (_token != NATIVE && baseFee > msg.value)`