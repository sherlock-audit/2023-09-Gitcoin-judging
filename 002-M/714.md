Mammoth Aquamarine Wallaby

medium

# M-02 Internal accounting and contract balance of strategy goes out of synch during `_fundPool` for Fee-On-Transfer tokens.
The reported balance of the internal accounting varialbe and the actual account balance will by out of sync when using Fee-on-transfer tokens

## Vulnerability Detail
In the Allo contract when a pool is created or funded the `_fundPool` function is called. The final call before the function emits the `PoolFunded` event is a call to the strategy to set the pool amount. The value of the variable that is set is based on either the `_amount` parameter sent in to the call as below

```solidity
uint256 amountAfterFee = _amount;
```
, or a calculation of the value minus the fees percentage.
```solidity
feeAmount = (_amount * percentFee) / getFeeDenominator();
amountAfterFee -= feeAmount;
```
This final value of `amountAfterFee` is sent into the call `_strategy.increasePoolAmount(amountAfterFee);` however it does not take into account that value was lost during the transfer which is inherent to Fee-on-transfer tokens.

## Impact
This will cause the a misalignment in the actual balance and reported balance in the strategy contract.

## Code Snippet
https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/core/Allo.sol#L502-L520
```solidity
    function _fundPool(uint256 _amount, uint256 _poolId, IStrategy _strategy) internal {
        uint256 feeAmount;
        uint256 amountAfterFee = _amount;

        Pool storage pool = pools[_poolId];
        address _token = pool.token;

        if (percentFee > 0) {
            feeAmount = (_amount * percentFee) / getFeeDenominator();
            amountAfterFee -= feeAmount;

            _transferAmountFrom(_token, TransferData({from: msg.sender, to: treasury, amount: feeAmount}));
        }

        _transferAmountFrom(_token, TransferData({from: msg.sender, to: address(_strategy), amount: amountAfterFee}));
        _strategy.increasePoolAmount(amountAfterFee);

        emit PoolFunded(_poolId, amountAfterFee, feeAmount);
    }
```

## PoC
Copy/Paste to a file called FeeERC20.sol in the `core` directory:
```solidity
// SPDX-License-Identifier: UNLICENSED
// slither-disable-next-line solc-version
pragma solidity ^0.8.17;

import "../../lib/openzeppelin-contracts/contracts/token/ERC20/ERC20.sol";
import "forge-std/console.sol";


contract FeeERC20 is ERC20 {
    uint256 public constant  INITIAL_SUPPLY = 1_000_000_000e18;
    address _owner;
    uint256 public fees;

    event Minted(uint256 fees);

    constructor(uint256 _fees) ERC20("FeeToken", "FT") {
        _owner = msg.sender;
        fees = _fees;
        emit Minted(fees);
    }
    //modifier
    modifier onlyOwner() {
        require(msg.sender==_owner,"Not the Owner!");
        _;
    }

    function mint(address account, uint256 amount) external onlyOwner {
        _mint(account, amount);
    }

    function transfer(address to,uint256 amount) public override returns (bool){
        uint256 newAmount = amount - fees;
        return super.transfer(to,newAmount);
    }

    function transferFrom(address src,address to,uint256 amount) public override returns (bool){
        //console.log("Starting transferFrom");
        //console.log("msg.sender is : ",msg.sender);
        //console.log("src is : ",src);
        //console.log("to is : ",to);
        //console.log("amount is : ", amount);
        uint256 newAmount = amount - fees;
        //console.log("newAmount is : ", newAmount);
        //console.log("Allowance is : ", allowance(src,to));
        return super.transferFrom(src,to,newAmount);
        //console.log("Completed transferFrom");
    }
    

}
```
Add the import to the test contract 
```solidity
import "../../../contracts/core/FeeERC20.sol";
```
Copy/Paste this test function in the `Allo.t.sol` file in the `test/foundry/core` directory:
 ```solidity
    function test_createPoolFeeOnTransfer() public {
        allo().addToCloneableStrategies(strategy);
        FeeERC20 feeToken = new FeeERC20(0.001 ether);
        feeToken.mint(pool_admin(),1000 ether);
        vm.startPrank(pool_admin());
        feeToken.approve(address(allo()), 100 ether);
        console.log("[+] Create a pool with the token set to be the FeeToken");
        uint256 poolId = allo().createPool(poolProfile_id(), strategy, "0x", address(feeToken), 100 ether, metadata, pool_managers());
        console.log("[+] The amount reported by the Strategy as the balance : ",MockStrategy(allo().getStrategy(poolId)).getPoolAmount());
        console.log("[+] The Actual FeeToken balance of strategy : ",feeToken.balanceOf(allo().getStrategy(poolId)));
        IAllo.Pool memory pool = allo().getPool(poolId);
        vm.stopPrank();
        assertEq(pool.profileId, poolProfile_id());
        assertNotEq(address(pool.strategy), address(strategy));
    }
 ```
 
 Run the forge test
 ```text
 forge test --match-contract AlloTest --match-test test_createPoolFeeOnTransfer -vv
 ```
### Output 
(Shows the misaligned balances)

```text
[PASS] test_createPoolFeeOnTransfer() (gas: 1326910)
Logs:
  [+] Create a pool with the token set to be the FeeToken
  [+] The amount reported by the Strategy as the balance :  99000000000000000000
  [+] The Actual FeeToken balance of strategy :  98999000000000000000

Test result: ok. 1 passed; 0 failed; 0 skipped; finished in 8.74ms
```
## Tool used

Manual Review

## Recommendation
Consider changing the call to the `_strategy.increasePoolAmount` function using the actual balance of the strategy contract.