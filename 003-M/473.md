Hot Zinc Hippo

high

# The total amount of credits per `allocator` can be exceed `maxVoiceCreditsPerAllocator` in `QVSimpleStrategy`.
When `_sender` allocates, `allocator.voiceCredits` is not updated, so the total amount of credits per `allocator` can exceed `maxVoiceCreditsPerAllocator` in `QVSimpleStrategy`.

## Vulnerability Detail
`QVSimpleStrategy._allocate` is as following.
```solidity
File: QVSimpleStrategy.sol
107:     function _allocate(bytes memory _data, address _sender) internal virtual override {
108:         (address recipientId, uint256 voiceCreditsToAllocate) = abi.decode(_data, (address, uint256));
109: 
110:         // spin up the structs in storage for updating
111:         Recipient storage recipient = recipients[recipientId];
112:         Allocator storage allocator = allocators[_sender];
113: 
114:         // check that the sender can allocate votes
115:         if (!_isValidAllocator(_sender)) revert UNAUTHORIZED();
116: 
117:         // check that the recipient is accepted
118:         if (!_isAcceptedRecipient(recipientId)) revert RECIPIENT_ERROR(recipientId);
119: 
120:         // check that the recipient has voice credits left to allocate
121:         if (!_hasVoiceCreditsLeft(voiceCreditsToAllocate, allocator.voiceCredits)) revert INVALID();
122: 
123:         _qv_allocate(allocator, recipient, recipientId, voiceCreditsToAllocate, _sender);
124:     }
```
On the other hand, `_hasVoiceCreditsLeft` called by this function is as following.
```solidity
File: QVSimpleStrategy.sol
144:     function _hasVoiceCreditsLeft(uint256 _voiceCreditsToAllocate, uint256 _allocatedVoiceCredits)
145:         internal
146:         view
147:         override
148:         returns (bool)
149:     {
150:         return _voiceCreditsToAllocate + _allocatedVoiceCredits <= maxVoiceCreditsPerAllocator;
151:     }
```
From the implementation of these functions, we can see that the amount of total credits per `allocator` cannot exceed `maxVoiceCreditsPerAllocator`.
So `_allocate` necessarily has to update `allocator.voiceCredits`. 
But the real implementation of `_allocate` didn't update `allocator.voiceCredits` anywhere.
As a result, the total amount of credits per allocator can exceed `maxVoiceCreditsPerAllocator`.

Meanwhile, this operation is implemented correctly in `HackathonQVStrategy._allocate`#L282.
```solidity
File: HackathonQVStrategy.sol
263:     function _allocate(bytes memory _data, address _sender) internal override {
264:         (address recipientId, uint256 nftId, uint256 voiceCreditsToAllocate) =
265:             abi.decode(_data, (address, uint256, uint256));
266: 
267:         // check that the sender can allocate votes
268:         if (nft.ownerOf(nftId) != _sender) {
269:             revert UNAUTHORIZED();
270:         }
271: 
272:         // spin up the structs in storage for updating
273:         Recipient storage recipient = recipients[recipientId];
274:         Allocator storage allocator = allocators[_sender];
275: 
276:         if (!_hasVoiceCreditsLeft(voiceCreditsToAllocate, voiceCreditsUsedPerNftId[nftId])) {
277:             revert INVALID();
278:         }
279: 
280:         _qv_allocate(allocator, recipient, recipientId, voiceCreditsToAllocate, _sender);
281: 
282:         voiceCreditsUsedPerNftId[nftId] += voiceCreditsToAllocate;
283: 
284:         TmpRecipient memory tmp = TmpRecipient({recipientId: address(0), voteRank: 0, foundRecipientAtIndex: 0});
285: 
```


## Impact
The total amount of credits per allocator can exceed `maxVoiceCreditsPerAllocator`.

## Code Snippet
https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/qv-simple/QVSimpleStrategy.sol#L107-L124

## Tool used
Manual Review

## Recommendation
By the end of `QVSimpleStrategy._allocate`, the following operation has to be added.
```solidity
    _allocator.voiceCredits += voiceCreditsToAllocate;
```