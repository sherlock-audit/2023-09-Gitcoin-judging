Atomic Ultraviolet Mole

medium

# ````_distribute()```` of ````RFPSimpleStrategy```` would revert with ````NOT_ENOUGH_FUNDS()```` even if their is enough ````poolAmount```` remaining
## Summary
````_distribute()```` function of ````RFPSimpleStrategy```` contract incorrectly requires ````recipient.proposalBid <= poolAmount```` for each ````milestone````. It would cause ````_distribute()```` failing even if their is enough ````poolAmount```` remaining.

## Vulnerability Detail
Let's look at the implementation of ````_distribute()````, please pay attention on L432 and L438, ````poolAmount```` decreases in each ````milestone````, but ````recipient.proposalBid```` always keep fixed.
```solidity
File: contracts\strategies\rfp-simple\RFPSimpleStrategy.sol
417:     function _distribute(address[] memory, bytes memory, address _sender)
418:         internal
419:         virtual
420:         override
421:         onlyInactivePool
422:         onlyPoolManager(_sender)
423:     {
424:         // check to make sure there is a pending milestone
425:         if (upcomingMilestone >= milestones.length) revert INVALID_MILESTONE();
426: 
427:         IAllo.Pool memory pool = allo.getPool(poolId);
428:         Milestone storage milestone = milestones[upcomingMilestone];
429:         Recipient memory recipient = _recipients[acceptedRecipientId];
430: 
431:         // make sure has enough funds to distribute based on the proposal bid
432:         if (recipient.proposalBid > poolAmount) revert NOT_ENOUGH_FUNDS();//@audit incorrect
433: 
434:         // Calculate the amount to be distributed for the milestone
435:         uint256 amount = (recipient.proposalBid * milestone.amountPercentage) / 1e18;
436: 
437:         // Get the pool, subtract the amount and transfer to the recipient
438:         poolAmount -= amount;
439:         _transferAmount(pool.token, recipient.recipientAddress, amount);
440: 
441:         // Set the milestone status to 'Accepted'
442:         milestone.milestoneStatus = Status.Accepted;
443: 
444:         // Increment the upcoming milestone
445:         upcomingMilestone++;
446: 
447:         // Emit events for the milestone and the distribution
448:         emit MilestoneStatusChanged(upcomingMilestone, Status.Accepted);
449:         emit Distributed(acceptedRecipientId, recipient.recipientAddress, amount, _sender);
450:     }

```

Now, let's consider the case below:
(1) before first distribution, the states are
```solidity
poolAmount = 10 ETH
recipient.proposalBid = 8 ETH
milestones[2] = [{amountPercentage: 50%}, {amountPercentage: 50%}]
```
(2) after first distribution, states change to
```solidity
poolAmount = 6 ETH
recipient.proposalBid = 8 ETH
milestones[2] = [{amountPercentage: 50%}, {amountPercentage: 50%}]
```

(3) during second distribution, it would revert due to ````recipient.proposalBid(8 ETH) > poolAmount(6 ETH)````
```solidity
432:         if (recipient.proposalBid > poolAmount) revert NOT_ENOUGH_FUNDS();
```
but actually ````poolAmount(6 ETH)```` is enough to pay the second ````milestone(4 ETH)````.

## Impact
To make ````_distribute()```` work, pool owner or managers must deposit far more fund than it should be.

## Code Snippet
https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/rfp-simple/RFPSimpleStrategy.sol#L432

## Tool used

Manual Review

## Recommendation
```diff
File: contracts\strategies\rfp-simple\RFPSimpleStrategy.sol
417:     function _distribute(address[] memory, bytes memory, address _sender)
418:         internal
419:         virtual
420:         override
421:         onlyInactivePool
422:         onlyPoolManager(_sender)
423:     {
424:         // check to make sure there is a pending milestone
425:         if (upcomingMilestone >= milestones.length) revert INVALID_MILESTONE();
426: 
427:         IAllo.Pool memory pool = allo.getPool(poolId);
428:         Milestone storage milestone = milestones[upcomingMilestone];
429:         Recipient memory recipient = _recipients[acceptedRecipientId];
430: 
431:         // make sure has enough funds to distribute based on the proposal bid
-432:         if (recipient.proposalBid > poolAmount) revert NOT_ENOUGH_FUNDS();
+432:         if (upcomingMilestone == 0 && recipient.proposalBid > poolAmount) revert NOT_ENOUGH_FUNDS();
433: 
434:         // Calculate the amount to be distributed for the milestone
435:         uint256 amount = (recipient.proposalBid * milestone.amountPercentage) / 1e18;
436: 
437:         // Get the pool, subtract the amount and transfer to the recipient
438:         poolAmount -= amount;
439:         _transferAmount(pool.token, recipient.recipientAddress, amount);
440: 
441:         // Set the milestone status to 'Accepted'
442:         milestone.milestoneStatus = Status.Accepted;
443: 
444:         // Increment the upcoming milestone
445:         upcomingMilestone++;
446: 
447:         // Emit events for the milestone and the distribution
448:         emit MilestoneStatusChanged(upcomingMilestone, Status.Accepted);
449:         emit Distributed(acceptedRecipientId, recipient.recipientAddress, amount, _sender);
450:     }

```