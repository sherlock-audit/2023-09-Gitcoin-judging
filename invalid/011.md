Interesting Shamrock Lemur

high

# Reentrancy Vulnerability in _distribute function

The `_distribute` function within the `QVBaseStrategy.sol` abstract contract presents a critical vulnerability that exposes the smart contract to the risk of a reentrancy attack. This vulnerability arises from the sequence of operations within the function that allows an attacker, particularly a malicious `PoolManager`, to withdraw all pool funds before the `paidOut` flag is set to `true`.

## Vulnerability Detail

The vulnerability stems from the order of execution within the [`_distribute` function](https://github.com/allo-protocol/allo-v2/blob/8a41a342a0de7a2d5d7dbc5395d1da44cb811348/contracts/strategies/qv-base/QVBaseStrategy.sol#L436). Currently, after transferring tokens to the recipient's address, the `paidOut[recipientId]` flag is set to `true`. This is done in the following [two sloc](https://github.com/allo-protocol/allo-v2/blob/main/contracts/strategies/qv-base/QVBaseStrategy.sol#L456-L458). This sequence of operations creates a window of opportunity for a malicious actor to exploit the contract. A malicious `PoolManager` can call the `_distribute` function and initiate a reentrancy attack before his `paidOut` is updated to indicate that his has been paid. Let see a step by step attack.

1. A pool is created using `QVBaseStrategy.sol` as a base strategy for the main strategy with token as NATIVE.
2. The `distribute(address[] calldata _recipientIds) external` function logically uses `_distribute` function with the `recipientId` of the `PoolManager` only.
3. The malicious `PoolManager` calls `distribute` function.
4. When `transferAmount` is called, it uses the safeTransferETH function.
5. When sending ether, it calls the malicious `PoolManager` receive contract function. This function calls distribute.
6. This send ETH again...
7. After draining funds, `paidOut[PoolManagerId]` flag is set to `true` (too late!).

This critical vulnerability can manifest when a strategy contract is created by inheriting from the `QVBaseStrategy.sol` abstract contract, which is the intended use of this smart contract explained in the [sequence diagram](https://github.com/allo-protocol/allo-v2/tree/main/contracts/strategies/qv-base#sequence-diagram). The strategy contract utilizes the `_distribute` function to distribute tokens, exposing the vulnerability. In this context, a malicious `PoolManager` operating the strategy contract can exploit the vulnerability to initiate a reentrancy attack, potentially leading to the unauthorized withdrawal of all pool funds. This scenario underscores the importance of promptly addressing and mitigating the vulnerability to ensure the security of strategy contracts built upon this abstract contract.

## Impact

If successfully exploited, a malicious `PoolManager` could drain the entire pool's funds. This type of attack could result in significant financial losses to the pool participants, as all the assets within the pool could be siphoned off before any legitimate distribution occurs. 

## Code Snippet

```solidity
   /// @notice Distribute the tokens to the recipients
    /// @dev The '_sender' must be a pool manager and the allocation must have ended
    /// @param _recipientIds The recipient ids
    /// @param _sender The sender of the transaction
    function _distribute(address[] memory _recipientIds, bytes memory, address _sender)
        internal
        virtual
        override
        onlyPoolManager(_sender)
        onlyAfterAllocation
    {
        uint256 payoutLength = _recipientIds.length;
        for (uint256 i; i < payoutLength;) {
            address recipientId = _recipientIds[i];
            Recipient storage recipient = recipients[recipientId];

            PayoutSummary memory payout = _getPayout(recipientId, "");
            uint256 amount = payout.amount;

            if (paidOut[recipientId] || !_isAcceptedRecipient(recipientId) || amount == 0) {
                revert RECIPIENT_ERROR(recipientId);
            }

            IAllo.Pool memory pool = allo.getPool(poolId);
            _transferAmount(pool.token, recipient.recipientAddress, amount);

            paidOut[recipientId] = true;

            emit Distributed(recipientId, recipient.recipientAddress, amount, _sender);
            unchecked {
                ++i;
            }
        }
    }
```

## Tool used

Manual Review

## Recommendation

Change the function to send ether at the end:
```solidity
    /// @notice Distribute the tokens to the recipients
    /// @dev The '_sender' must be a pool manager and the allocation must have ended
    /// @param _recipientIds The recipient ids
    /// @param _sender The sender of the transaction
    function _distribute(address[] memory _recipientIds, bytes memory, address _sender)
        internal
        virtual
        override
        onlyPoolManager(_sender)
        onlyAfterAllocation
    {
        uint256 payoutLength = _recipientIds.length;
        for (uint256 i; i < payoutLength;) {
            address recipientId = _recipientIds[i];
            Recipient storage recipient = recipients[recipientId];

            PayoutSummary memory payout = _getPayout(recipientId, "");
            uint256 amount = payout.amount;

            if (paidOut[recipientId] || !_isAcceptedRecipient(recipientId) || amount == 0) {
                revert RECIPIENT_ERROR(recipientId);
            }

            IAllo.Pool memory pool = allo.getPool(poolId);
            paidOut[recipientId] = true;

            emit Distributed(recipientId, recipient.recipientAddress, amount, _sender);

            _transferAmount(pool.token, recipient.recipientAddress, amount);

            unchecked {
                ++i;
            }
        }
    }
```