Mammoth Aquamarine Wallaby

medium

# M-01 Extra ETH needs to be sent as msg.value than is actually needed to create a pool when a base fee is set.
Extra ETH is needed to be sent than needed when creating a Pool and the extra dust is not refunded.

## Vulnerability Detail
Confirmed with sponsor, the code enforces that the msg.value must be greater than fees or fees plus the native token amount to send to the pool, should this value be exactly equivalent to the value required, the code will revert. A secondary issue is that there is no check to calculate a refund for the sender should the msg.value be higher than required.

## Impact
The user has to send more ETH than needed, and the extra ETH dust is not refunded in the `_createPool` function when a base fee is set.

## Code Snippet
https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/core/Allo.sol#L469-L478

```solidity
if (baseFee > 0) {
            // To prevent paying the baseFee from the Allo contract's balance
            // If _token is NATIVE, then baseFee + _amount should be >= than msg.value.
            // If _token is not NATIVE, then baseFee should be >= than msg.value.
            if ((_token == NATIVE && (baseFee + _amount >= msg.value)) || (_token != NATIVE && baseFee >= msg.value)) {
                revert NOT_ENOUGH_FUNDS();
            }
            _transferAmount(NATIVE, treasury, baseFee);
            emit BaseFeePaid(poolId, baseFee);
        }
```

## PoC
Copy/Paste this test function in the `Allo.t.sol` file in the `test/foundry/core` directory:
```solidity
function test_createPoolWithBaseFeePoC() public {
        uint256 baseFee = 1e17;

        allo().updateBaseFee(baseFee);

        vm.expectEmit(true, false, false, true);
        emit BaseFeePaid(1, baseFee);

        vm.deal(address(pool_admin()), 1e18);
        console.log("[+] Allo base fee is set to : ",baseFee);
        console.log("");
        console.log("Pool admin balance before creating a pool : ",pool_admin().balance);
        console.log("");
        console.log("## FAIL SCENARIO ##");
        console.log("Create pool exact fee needed : ",baseFee);
        console.log("[-] This should revert, will only succeed if reverted with NOT_ENOUGH_FUNDS");
        console.log("");
        vm.expectRevert(NOT_ENOUGH_FUNDS.selector);
        vm.prank(pool_admin());
        allo().createPoolWithCustomStrategy{value: baseFee}(
            poolProfile_id(), strategy, "0x", NATIVE, 0, metadata, pool_managers()
        );

        console.log("## SUCCESS SCENARIO ##");
        console.log("Create pool fee needed plus 1 wei : ",baseFee + 1 wei);
        console.log("[+] This should not revert");
        vm.prank(pool_admin());
        allo().createPoolWithCustomStrategy{value: baseFee + 1 wei}(
            poolProfile_id(), strategy, "0x", NATIVE, 0, metadata, pool_managers()
        );
        console.log("");
        console.log("## FINAL BALANCES ##");
        console.log("[+] Pool admin balance after creating a pool : ",pool_admin().balance);
        console.log("[+] Extra 1 wei is not returned");
        console.log("[+] Extra wei is now dust in Allo contract");
        console.log("[+] Allo balance is : ",address(allo()).balance);
    }
```
Run the forge test 
```text
forge test --match-contract AlloTest --match-test test_createPoolWithBaseFeePoC -vv
```
## Output
```text
Running 1 test for test/foundry/core/Allo.t.sol:AlloTest
[PASS] test_createPoolWithBaseFeePoC() (gas: 768662)
Logs:
  [+] Allo base fee is set to :  100000000000000000
  
  Pool admin balance before creating a pool :  1000000000000000000
  
  ## FAIL SCENARIO ##
  Create pool exact fee needed :  100000000000000000
  [-] This should revert, will only succeed if reverted with NOT_ENOUGH_FUNDS
  
  ## SUCCESS SCENARIO ##
  Create pool fee needed plus 1 wei :  100000000000000001
  [+] This should not revert
  
  ## FINAL BALANCES ##
  [+] Pool admin balance after creating a pool :  899999999999999999
  [+] Extra 1 wei is not returned
  [+] Extra wei is now dust in Allo contract
  [+] Allo balance is :  1

Test result: ok. 1 passed; 0 failed; 0 skipped; finished in 10.38ms
```
## Tool used

Manual Review

## Recommendation
The check for the base fees should be changed to be as below, this includes correcting the comments.
```solidity
if (baseFee > 0) {
            // To prevent paying the baseFee from the Allo contract's balance
            // If _token is NATIVE, then msg.value should be >= than baseFee + _amount.
            // If _token is not NATIVE, then msg.value should be >= than baseFee.
            if ((_token == NATIVE && (msg.value < baseFee + _amount)) || (_token != NATIVE && msg.value < baseFee)) {
                revert NOT_ENOUGH_FUNDS();
            }
            _transferAmount(NATIVE, treasury, baseFee);
            emit BaseFeePaid(poolId, baseFee);
        }
```
Secondly it can be checked whether all the value sent in the msg.value was used, and any unused funds refunded to the caller.