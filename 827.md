Helpful Bubblegum Spider

high

# funding pools with Fee-on-Transfer will lead to an incorrect increase in poolAmount value
## Summary
funding pools with Fee-on-Transfer will lead to an incorrect increase in poolAmount value

## Vulnerability Detail
anyone that calls Allo#fundPool() specifies an amount that will be transferred into the contract:
https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/core/Allo.sol#L339
```solidity
    function fundPool(uint256 _poolId, uint256 _amount) external payable nonReentrant {
        // if amount is 0, revert with 'NOT_ENOUGH_FUNDS()' error
        if (_amount == 0) revert NOT_ENOUGH_FUNDS();

        // Call the internal fundPool() function
        _fundPool(_amount, _poolId, pools[_poolId].strategy);
    }
```
https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/core/Allo.sol#L502
```solidity
    function _fundPool(uint256 _amount, uint256 _poolId, IStrategy _strategy) internal {
        uint256 feeAmount;
        uint256 amountAfterFee = _amount;

        Pool storage pool = pools[_poolId];
        address _token = pool.token;

        if (percentFee > 0) {
            feeAmount = (_amount * percentFee) / getFeeDenominator();
            amountAfterFee -= feeAmount;

            _transferAmountFrom(_token, TransferData({from: msg.sender, to: treasury, amount: feeAmount}));
        }

        _transferAmountFrom(_token, TransferData({from: msg.sender, to: address(_strategy), amount: amountAfterFee}));
        _strategy.increasePoolAmount(amountAfterFee); // @audit poolAmount will be increased by an incorrect amount for feeontransfer tokens
        emit PoolFunded(_poolId, amountAfterFee, feeAmount);
    }
```
The problem is that the poolAmount will be increased without accounting for the Fee of fee-on-Transfer tokens:
BaseStrategy#increasePoolAmount()
https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/BaseStrategy.sol#L153
```solidity
    function increasePoolAmount(uint256 _amount) external override onlyAllo {
        _beforeIncreasePoolAmount(_amount);
        poolAmount += _amount;
        _afterIncreasePoolAmount(_amount);
    }
```

The _transferAmountFrom call will transfer amountAfterFee of token to the strategy but the strategy will not receive that many tokens because of the fee on transfer.

## Impact

There will be a difference in the poolAmount and the actual token amount held by the contract.
This will have unwanted consequences depending on the strategy.
For example QVBaseStrategy calculates the payout with poolAmount:
https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/qv-base/QVBaseStrategy.sol#L559
```solidity
    function _getPayout(address _recipientId, bytes memory)
        internal
        view
        virtual
        override
        returns (PayoutSummary memory)
    {
        Recipient memory recipient = recipients[_recipientId];

        // Calculate the payout amount based on the percentage of total votes
        uint256 amount;
        if (!paidOut[_recipientId] && totalRecipientVotes != 0) {
            amount = poolAmount * recipient.totalVotesReceived / totalRecipientVotes; // @audit here
        }
        return PayoutSummary(recipient.recipientAddress, amount);
    }
```

This function is called when distributing tokens, if there is less tokens in the contract than expected this will lead to incorrect amounts being distributed:
https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/qv-base/QVBaseStrategy.sol#L448


## Code Snippet
https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/core/Allo.sol#L339
## Tool used

Manual Review

## Recommendation
disallow fee-on-transfer tokens or adjust the code to increase the poolAmount correctly