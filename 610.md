Modern Pearl Kangaroo

medium

# Possible Denial of Service (DoS) on Fee-on-Transfer Token
## Summary

According to the context Q&A section on the contest page:

> Are there any FEE-ON-TRANSFER tokens interacting with the smart contracts?

> Yes. When funding a pool on Allo.sol

The `Allo._fundPool()` function may not support deflationary tokens. After transferring the tokens into the pool, the total amount of tokens in the pool will be less than `Base.poolAmount` due to fees on transfer.

## Vulnerability Detail

In the `Allo._fundPool()` function, this function is used to transfer the amount to the distribution strategy and increase the pool amount at lines 516 and 517, respectively.

https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/core/Allo.sol#L502-L520

```solidity
function _fundPool(uint256 _amount, uint256 _poolId, IStrategy _strategy) internal {
    uint256 feeAmount;
    uint256 amountAfterFee = _amount;

    Pool storage pool = pools[_poolId];
    address _token = pool.token;

    if (percentFee > 0) {
        feeAmount = (_amount * percentFee) / getFeeDenominator();
        amountAfterFee -= feeAmount;

        _transferAmountFrom(_token, TransferData({from: msg.sender, to: treasury, amount: feeAmount}));
    }

    _transferAmountFrom(_token, TransferData({from: msg.sender, to: address(_strategy), amount: amountAfterFee}));
    _strategy.increasePoolAmount(amountAfterFee);

    emit PoolFunded(_poolId, amountAfterFee, feeAmount);
}
```

When funding the fee-on-transfer token to the pool, the token burns or diverts a small portion, causing the pool to end up with slightly less than what the sender initially provided.

By funding the deflationary token to the pool, the `BaseStrategy.increasePoolAmount()` function increases the value of the `poolAmount` variable based on `amountAfterFee` which is slightly greater than the sender gave to the pool, resulting in the `poolAmount` being slightly greater than the total amount of tokens in the pool.

https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/BaseStrategy.sol#L153-L157

```solidity
function increasePoolAmount(uint256 _amount) external override onlyAllo {
    _beforeIncreasePoolAmount(_amount);
    poolAmount += _amount;
    _afterIncreasePoolAmount(_amount);
}
```

This may result in a DOS during the distribution phrase. For example:

The `QVBaseStrategy._getPayout()` function is used to calculate the payout amount for distributing the tokens based on the `poolAmount` and the percentage of total votes.

https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/qv-base/QVBaseStrategy.sol#L559-L574

```solidity
function _getPayout(address _recipientId, bytes memory)
    internal
    view
    virtual
    override
    returns (PayoutSummary memory)
{
    Recipient memory recipient = recipients[_recipientId];

    // Calculate the payout amount based on the percentage of total votes
    uint256 amount;
    if (!paidOut[_recipientId] && totalRecipientVotes != 0) {
        amount = poolAmount * recipient.totalVotesReceived / totalRecipientVotes;
    }
    return PayoutSummary(recipient.recipientAddress, amount);
}
```

The `QVBaseStrategy._distribute()` function is used to distribute the tokens to the recipients by calculating the amount of payout at line 448.

However, since funding the pool using a fee-on-transfer token can result in the `poolAmount` being slightly greater than the balance of tokens in the pool, the `_distribute()` function may revert due to the transfer amount exceeding the balance at line 456.

https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/qv-base/QVBaseStrategy.sol#L436-L465

```solidity
function _distribute(address[] memory _recipientIds, bytes memory, address _sender)
    internal
    virtual
    override
    onlyPoolManager(_sender)
    onlyAfterAllocation
{
    uint256 payoutLength = _recipientIds.length;
    for (uint256 i; i < payoutLength;) {
        address recipientId = _recipientIds[i];
        Recipient storage recipient = recipients[recipientId];

        PayoutSummary memory payout = _getPayout(recipientId, "");
        uint256 amount = payout.amount;

        if (paidOut[recipientId] || !_isAcceptedRecipient(recipientId) || amount == 0) {
            revert RECIPIENT_ERROR(recipientId);
        }

        IAllo.Pool memory pool = allo.getPool(poolId);
        _transferAmount(pool.token, recipient.recipientAddress, amount);

        paidOut[recipientId] = true;

        emit Distributed(recipientId, recipient.recipientAddress, amount, _sender);
        unchecked {
            ++i;
        }
    }
}
```

## Impact

Possible DOS when funding the pool with a fee-on-transfer token.

## Code Snippet

https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/core/Allo.sol#L516-L517

## Tool used

Visual Studio Code / Manual Review

## Recommendation

We suggest increasing the pool amount according to the value of the tokens after the transfer rather than the value before the token transfer.