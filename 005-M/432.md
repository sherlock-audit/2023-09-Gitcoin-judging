Atomic Ultraviolet Mole

high

# ````_registerRecipient()```` of ````RFPSimpleStrategy```` always reverts while ````useRegistryAnchor```` enabled
In ````_registerRecipient()```` function of ````RFPSimpleStrategy```` contract, The ````recipientAddress```` is not decoded from ````_data```` parameter while ````useRegistryAnchor```` is enabled, the default 0 ````recipientAddress```` would cause ````_registerRecipient()```` function to revert always, no recipients can be registered. As the ````useRegistryAnchor```` is set only in constructor, therefore, all ````RFPSimpleStrategy```` instances with ````useRegistryAnchor```` enabled become useless.

## Vulnerability Detail
Let's look at the implementation of ````_registerRecipient()````, please pay attention on L321 and L327~L332, ````recipientAddress```` is keeping as the default zero address, then the function would revert on L362.
```solidity
File: contracts\strategies\rfp-simple\RFPSimpleStrategy.sol
314:     function _registerRecipient(bytes memory _data, address _sender)
315:         internal
316:         override
317:         onlyActivePool
318:         returns (address recipientId)
319:     {
320:         bool isUsingRegistryAnchor;
321:         address recipientAddress;
322:         address registryAnchor;
323:         uint256 proposalBid;
324:         Metadata memory metadata;
325: 
326:         // Decode '_data' depending on the 'useRegistryAnchor' flag
327:         if (useRegistryAnchor) {//@audit missing recipientAddress
328:             /// @custom:data when 'true' -> (address recipientId, uint256 proposalBid, Metadata metadata)
329:             (recipientId, proposalBid, metadata) = abi.decode(_data, (address, uint256, Metadata));
330: 
331:             // If the sender is not a profile member this will revert
332:             if (!_isProfileMember(recipientId, _sender)) revert UNAUTHORIZED();
333:         } else {
334:             //  @custom:data when 'false' -> (address recipientAddress, address registryAnchor, uint256 proposalBid, Metadata metadata)
335:             (recipientAddress, registryAnchor, proposalBid, metadata) =
336:                 abi.decode(_data, (address, address, uint256, Metadata));
337: 
338:             // Check if the registry anchor is valid so we know whether to use it or not
339:             isUsingRegistryAnchor = registryAnchor != address(0);
340: 
341:             // Ternerary to set the recipient id based on whether or not we are using the 'registryAnchor' or '_sender'
342:             recipientId = isUsingRegistryAnchor ? registryAnchor : _sender;
343: 
344:             // Checks if the '_sender' is a member of the profile 'anchor' being used and reverts if not
345:             if (isUsingRegistryAnchor && !_isProfileMember(recipientId, _sender)) revert UNAUTHORIZED();
346:         }
347: 
348:         // Check if the metadata is required and if it is, check if it is valid, otherwise revert
349:         if (metadataRequired && (bytes(metadata.pointer).length == 0 || metadata.protocol == 0)) {
350:             revert INVALID_METADATA();
351:         }
352: 
353:         if (proposalBid > maxBid) {
354:             // If the proposal bid is greater than the max bid this will revert
355:             revert EXCEEDING_MAX_BID();
356:         } else if (proposalBid == 0) {
357:             // If the proposal bid is 0, set it to the max bid
358:             proposalBid = maxBid;
359:         }
360: 
361:         // If the recipient address is the zero address this will revert
362:         if (recipientAddress == address(0)) revert RECIPIENT_ERROR(recipientId);
363: 
364:         // Get the recipient
365:         Recipient storage recipient = _recipients[recipientId];
366: 
367:         if (recipient.recipientStatus == Status.None) {
368:             // If the recipient status is 'None' add the recipient to the '_recipientIds' array
369:             _recipientIds.push(recipientId);
370:             emit Registered(recipientId, _data, _sender);
371:         } else {
372:             emit UpdatedRegistration(recipientId, _data, _sender);
373:         }
374: 
375:         // update the recipients data
376:         recipient.recipientAddress = recipientAddress;
377:         recipient.useRegistryAnchor = isUsingRegistryAnchor ? true : recipient.useRegistryAnchor;
378:         recipient.proposalBid = proposalBid;
379:         recipient.recipientStatus = Status.Pending;
380:     }

```



## Impact
Registering recipient is a core step of ````RFPSimpleStrategy```` , this issue causes all ````RFPSimpleStrategy```` instances with ````useRegistryAnchor```` enabled become useless.

## Code Snippet
https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/rfp-simple/RFPSimpleStrategy.sol#L314

## Tool used

Manual Review

## Recommendation
Passing and decoding ````recipientAddress```` in all cases.