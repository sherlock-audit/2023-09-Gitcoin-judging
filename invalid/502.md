Damaged Cornflower Turkey

medium

# Creating a pool with a baseFee set will always lead to overspending
When someone creates a pool with a `baseFee` is set, he will always have to overspend due to not being able to send the exact amount required to create a pool.
## Vulnerability Detail
In the `_createPool` function, `baseFee + _amount >= msg.value` is validated. This means the `msg.value` always needs to be more than `baseFee + _amount`. This leads to always having a portion of the funds being sent to `Allo.sol`.

I wrote a POC to showcase this:
```diff
file: test/utils/MockStrategy.sol

// Add this to the end of the MockStrategy.sol, otherwise funding with NATIVE token will always fail.
+    receive() external payable{}
```

This first POC will showcase that creating a pool with the exact amount of funds required, will revert:
```javascript
source: allo-v2/test/foundry/core/Allo.t.sol

    // forge test --match-contract AlloTest --match-test test_createPoolWithNativeToken -vvvv
    function test_createPoolWithNativeToken() public {
		// Set baseFee to 1e18 
        uint256 baseFee = 1e18;
        allo().updateBaseFee(baseFee);

		// Give the admin 10 * 1e18 native tokens
        vm.deal(address(pool_admin()), 10 * 1e18);

        vm.prank(pool_admin());
        // set msg.value to 10e18
        // 1e18 is for the baseFee
        // 9e18 is for the amount to fund the pool
        // this will revert with -> NOT_ENOUGH_FUNDS() error
        vm.expectRevert(NOT_ENOUGH_FUNDS.selector);
        allo().createPoolWithCustomStrategy{value: 10e18}(
            poolProfile_id(), strategy, "0x", NATIVE, 9e18, metadata, pool_managers()
        );
    }
```

Now, let's do the same but this time we have to make sure that `msg.value > baseFee + _amount`, otherwise it will revert like shown in the previous POC:
```javascript
source: allo-v2/test/foundry/core/Allo.t.sol

    // put this in test/foundry/core/Allo.t.sol
    // forge test --match-contract AlloTest --match-test test_createPoolWithNativeTokenWhileOverspending -vvv
    function test_createPoolWithNativeTokenWhileOverspending() public {
		// Set baseFee to 1e18
        uint256 baseFee = 1e18;
        allo().updateBaseFee(baseFee);

		// Give the admin 10 * 1e18 native tokens
        vm.deal(address(pool_admin()), 10 * 1e18);
        console.log("Balance of admin before: %s", pool_admin().balance);
        console.log("Balance of Allo.sol before: %s", address(allo()).balance);
        console.log("Balance of treasury before: %s ", allo_treasury().balance);
        console.log("Balance of strategy before: %s ", strategy.balance);

        vm.prank(pool_admin());
        // set msg.value to 10e18
        // 1e18 for baseFee
        // 8e18 for amount since amount cant be the same as msg.value + baseFee
        // this will lead to (msg.value - baseFee - _amount = 1e18) being sent to the Allo.sol contract
        allo().createPoolWithCustomStrategy{value: 10e18}(
            poolProfile_id(), strategy, "0x", NATIVE, 8e18, metadata, pool_managers()
        );
        // Admin will have an empty balance
        console.log("---------------------------\n  Balance of admin after: %s", pool_admin().balance);
        // Allo.sol will have the remaining value of msg.value as balance
        // 1e18
        console.log("Balance of Allo.sol after: %s", address(allo()).balance);
        // Treasury will have the baseFee + percentFee as balance
        console.log("Balance of treasury after: %s", allo_treasury().balance);
        // Treasury will have _amount - percentFee as balance
        console.log("Balance of strategy after: %s", strategy.balance);
    }
```

As you can see, the remaining `msg.value` will be sent to `Allo.sol`, in this case, `1e18`.
The owner of `Allo.sol` will have to call `Allo.recoverFunds()`.

The same logic applies to a person that creates a pool using non-native tokens while a `baseFee` is set. This person has to overspend on his `msg.value` as shown below:

```javascript
source: allo-v2/test/foundry/core/Allo.t.sol

	// put this in test/foundry/core/Allo.t.sol
	// forge test --match-contract AlloTest --match-test test_createPoolWithNonNativeTokenWhileOverspending -vvv
    function test_createPoolWithNonNativeTokenWhileOverspending() public {
		// Set baseFee to 1e18
        uint256 baseFee = 1e18;
        allo().updateBaseFee(baseFee);

		// Give the admin 2 * 1e18 native tokens
        vm.deal(address(pool_admin()), 2 * 1e18);
        console.log("Native Token Balance Admin Before: %s", pool_admin().balance);
        console.log("Native Token Balance Allo.sol Before: %s", address(allo()).balance);
        console.log("Native Token Balance Treasury Before: %s ", allo_treasury().balance);
        console.log("Native Token Balance Strategy Before: %s ", strategy.balance);

        vm.prank(pool_admin());
        // set msg.value to 2e18 since msg.value cant be the same as baseFee when using non native tokens
        // 1e18 for baseFee
        // for the sake of this example, we will fund with 100 * 1e18 non native tokens. these have been distributed
        // in the setUp() function
        // this will lead to (msg.value - baseFee - _amount) being sent to the Allo.sol contract -> 1e18
        allo().createPoolWithCustomStrategy{value: 2e18}(
            poolProfile_id(), strategy, "0x", address(token), 100 * 1e18, metadata, pool_managers()
        );
        // Admin will have an empty balance native balance
        console.log("------------------------------\n  Native Token Balance Admin After: %s", pool_admin().balance);
        // Allo.sol will have the remaining value of msg.value as balance -> 1e18
        console.log("Native Token Balance Allo.sol After: %s", address(allo()).balance);
        // Treasury will have the baseFee as native token as balance -> 1e18
        console.log("Native Token Balance Treasury After: %s", allo_treasury().balance);
        // Treasury will have the percentFee as non native token as balance -> 1e16
        console.log("Non Native Token Balance Treasury After: %s", token.balanceOf(address(allo_treasury())));
        // Treasury will have _amount - percentFee as non native token balance
        console.log("Non Native Token Balance Strategy After: %s", token.balanceOf(address(strategy)));
    }
```

Additionally, due to not being able to specify the amount being recovered in `recoverFunds`, the owner needs to be wary of the following scenario:
- poolCreator1 wants his funds recovered. 
- Just before the owner of Allo.sol calls recoverFunds, poolCreator2 overspends while creating a pool. 
- owner calls `recoverFunds`, not knowing poolCreator2 had funds in `Allo.sol`
- poolCreator2 wants his funds recovered. 
- This is not possible since the funds got sent to poolCreator1 because the `recoverFunds` function sweeps the whole balance of the `Allo.sol` contract.
```javascript
function recoverFunds(address _token, address _recipient) external onlyOwner {	    uint256 amount = _token == NATIVE ? address(this).balance :                 IERC20Upgradeable(_token).balanceOf(address(this));
    _transferAmount(_token, _recipient, amount);
    }
```
## Impact
Overspending happens every time a pool is created with the `baseFee` set, aka, very often. 
This, in combination with the fact that `recoverFunds` does not specify an amount to be recovered, is just a matter of time before one of the poolcreators who have overspent will lose their money.

## Code Snippet
https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/core/Allo.sol#L469-L475
## Tool used
Manual Review
## Recommendation
Remove the `=` operand in `Allo._createPool`
```diff
L473:
- if ((_token == NATIVE && (baseFee + _amount >= msg.value)) || (_token != NATIVE && baseFee >= msg.value)) {
+ if ((_token == NATIVE && (baseFee + _amount > msg.value)) || (_token != NATIVE && baseFee > msg.value)) {
```