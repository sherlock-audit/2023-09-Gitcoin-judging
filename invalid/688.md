Square Ultraviolet Bat

medium

# To create a new pool, an amount greater than the base fee must be paid
To create a pool, it is required to transfer an amount of native currency that exceeds the `Allo.baseFee` within the `Allo` smart contract.

## Vulnerability Detail
A member or profile owner can create a pool using either the `Allo.createPool` or `Allo.createPoolWithCustomStrategy` functions within the `Allo` smart contract. When calling one of these functions, it is necessary to provide the `Allo.baseFee` amount in the native currency of the network. The profile owner must pay more due to an incorrect if statement.
```solidity
if (baseFee > 0) {
            // To prevent paying the baseFee from the Allo contract's balance
            // If _token is NATIVE, then baseFee + _amount should be >= than msg.value.
            // If _token is not NATIVE, then baseFee should be >= than msg.value.
            if ((_token == NATIVE && (baseFee + _amount >= msg.value)) || (_token != NATIVE && baseFee >= msg.value)) {
                revert NOT_ENOUGH_FUNDS();
            }
            _transferAmount(NATIVE, treasury, baseFee);
            emit BaseFeePaid(poolId, baseFee);
        }
```

To create a pool, it is necessary to transfer the `baseFee + 1` amount of native currency in order to prevent the `NOT_ENOUGH_FUNDS` error. A test scenario:
```solidity
function testRevert_createPoolWithBaseFee() public {
        uint256 baseFee = 1e18;

        allo().updateBaseFee(baseFee);
        vm.deal(address(pool_admin()), 1e18 + 1);

        // create pool with 1e18 amount when baseFee = 1e18 [error]
        vm.expectRevert(NOT_ENOUGH_FUNDS.selector);
        vm.prank(pool_admin());
        allo().createPoolWithCustomStrategy{value: 1e18}(
            poolProfile_id(), strategy, "0x", NATIVE, 0, metadata, pool_managers()
        );

        // create pool with 1e18 + 1 amount when baseFee = 1e18 [success]
        vm.expectEmit(true, false, false, true);
        emit BaseFeePaid(1, baseFee);
        vm.prank(pool_admin());
        allo().createPoolWithCustomStrategy{value: 1e18 + 1}(
            poolProfile_id(), strategy, "0x", NATIVE, 0, metadata, pool_managers()
        );
    }
```

## Impact
The assertion that only the `Allo.baseFee` needs to be paid to create a pool is incorrect. If `msg.value` exceeds the  `Allo.baseFee`, the `msg.value - baseFee` amount will remain in the Allo smart contract

## Code Snippet
https://github.com/allo-protocol/allo-v2/blob/0b881ef4a0013d2809374c9ea69f4cf1288dfe62/contracts/core/Allo.sol#L473-L474

## Tool used

Manual Review

## Recommendation
Consider reverting with a 'NOT_ENOUGH_FUNDS' error if `baseFee != msg.value` instead of `baseFee >= msg.value`.

```diff
-if ((_token == NATIVE && (baseFee + _amount >= msg.value)) || (_token != NATIVE && baseFee >= msg.value)) {
+if ((_token == NATIVE && (baseFee + _amount != msg.value)) || (_token != NATIVE && baseFee != msg.value)) {
                revert NOT_ENOUGH_FUNDS();
            }
```