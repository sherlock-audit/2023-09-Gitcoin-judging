Mysterious Lava Lynx

high

# Approved allocator can send as many votes as he wants to an accepted recipient
In `QVSimpleStrategy.sol` an approved allocator can allocate votes to an accepted recipient. 

## Vulnerability Detail

The problem here is that an allocator can allocate as many votes as he wants since after he gave a recipient votes his `voiceCredits` doesn't **increase**.  

Allocator's struct: 
```solidity
   /// @notice The details of the allocator
    struct Allocator {
        uint256 voiceCredits;
        mapping(address => uint256) voiceCreditsCastToRecipient;
        mapping(address => uint256) votesCastToRecipient;
    }
```
When an allocator wants to allocate votes to a recipient he calls `allocate` on the `Allo.sol` contract, which then will call `_allocate` in `QVSimpleStrategy`:

```solidity
function _allocate(bytes memory _data, address _sender) internal virtual override {

        (address recipientId, uint256 voiceCreditsToAllocate) = abi.decode(_data, (address, uint256));

        // spin up the structs in storage for updating
        Recipient storage recipient = recipients[recipientId];
        Allocator storage allocator = allocators[_sender];

        // check that the sender can allocate votes
        if (!_isValidAllocator(_sender)) revert UNAUTHORIZED();

        // check that the recipient is accepted
        if (!_isAcceptedRecipient(recipientId)) revert RECIPIENT_ERROR(recipientId);

        // check that the recipient has voice credits left to allocate
        if (!_hasVoiceCreditsLeft(voiceCreditsToAllocate, allocator.voiceCredits)) revert INVALID();

        _qv_allocate(allocator, recipient, recipientId, voiceCreditsToAllocate, _sender);
    }
```
`_hasVoiceCreditsLeft` is used to check if an allocator has voiceCredits left. In `_allocate` above `_hasVoiceCreditsLeft` is used the following way: 

**first** param is the arbitrary amount an allocator wants to allocate.
**second** param is the `allocator.voiceCredits`

So in order for an allocator to have `voiceCredits`, the sum between `voiceCreditsToAllocate` and `allocator.voiceCredits` **shouldn't** be bigger than the `maxVoiceCreditsPerAllocator`

```solidity
function _hasVoiceCreditsLeft(uint256 _voiceCreditsToAllocate, uint256 _allocatedVoiceCredits)
        internal
        view
        override
        returns (bool)
    {
        return _voiceCreditsToAllocate + _allocatedVoiceCredits <= maxVoiceCreditsPerAllocator;
    }
```

Then `_qv_allocate` is called in `QVBaseStrategy.sol`:

```solidity
  function _qv_allocate(
        Allocator storage _allocator,
        Recipient storage _recipient,
        address _recipientId,
        uint256 _voiceCreditsToAllocate,
        address _sender
    ) internal onlyActiveAllocation {
        // check the `_voiceCreditsToAllocate` is > 0
        if (_voiceCreditsToAllocate == 0) revert INVALID();

        // get the previous values
        uint256 creditsCastToRecipient = _allocator.voiceCreditsCastToRecipient[_recipientId];
        uint256 votesCastToRecipient = _allocator.votesCastToRecipient[_recipientId];

        // get the total credits and calculate the vote result
        uint256 totalCredits = _voiceCreditsToAllocate + creditsCastToRecipient;
        uint256 voteResult = _sqrt(totalCredits * 1e18);

        // update the values
        voteResult -= votesCastToRecipient;
        totalRecipientVotes += voteResult;
        _recipient.totalVotesReceived += voteResult;

        _allocator.voiceCreditsCastToRecipient[_recipientId] += totalCredits;
        _allocator.votesCastToRecipient[_recipientId] += voteResult;

        // emit the event with the vote results
        emit Allocated(_recipientId, voteResult, _sender);
    }
```

`_voiceCreditsToAllocate` is an arbitrary amount an allocator wishes to allocate to a recipient, as you can see the votes a recipient will receive are calculated like that: 

```solidity
uint256 totalCredits = _voiceCreditsToAllocate + creditsCastToRecipient;

uint256 voteResult = _sqrt(totalCredits * 1e18);
``` 

Meaning `_voiceCreditsToAllocate` is the arbitrary amount and `creditsCastToRecipient` is the already allocated votes to a recipient by the allocator. After that the `voteResult` is calculated. The problem here is that the allocator's `voiceCredits` are **not** increased by the `_voiceCreditsToAllocate`, which means that the `_hasVoiceCreditsLeft` check will always pass and allocator will have infinite amount of voiceCredits to allocate.

## Impact
Allocator can give an infinite amount of votes to a recipient

## Code Snippet

https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/qv-base/QVBaseStrategy.sol#L506-L534

https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/qv-simple/QVSimpleStrategy.sol#L144-L151

## Tool used

Manual Review

## Recommendation
Increase the `voiceCredits` of the allocator with the amount he wishes to allocate a recipient