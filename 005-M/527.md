Energetic Berry Llama

high

# `RFPSimpleStrategy::_registerRecipient()` will always revert if `useRegistryAnchor` is "true"
`useRegistryAnchor` variable is set for every strategy during initialization. `_registerRecipient()` function will always revert in the `RFPSimpleStrategy` contract if the `useRegistryAnchor` is set "true". This issue doesn't happen in other strategy contracts, it is specific to `RFPSimpleStrategy` contract because the encoded data doesn't include `recipientAddress` variable in this contract.

## Vulnerability Detail
`_registerRecipient()` function takes two input parameters, `data` and `sender`. In a high-level overview this function:

* decodes the `data` input
    
* verifies if the `sender` is authorized to call this function
    
* updates the recipient's data with the decoded input.
    

The data to input for this function is custom, and it differs according to `useRegistryAnchor` is enabled or not. This custom data is:

* If `useRegistryAnchor` is "true": `(address recipientId, uint256 proposalBid, Metadata metadata)`
    
* If `useRegistryAnchor` is "false": `(address recipientAddress, address registryAnchor, uint256 proposalBid, Metadata metadata)`
    

As you can see above, `recipientAddress` variable is never decoded in this function if the `useRegistryAnchor` is "true".

Here is the `_registerRecipient()` function: [https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/rfp-simple/RFPSimpleStrategy.sol#L310C1-L380C6](https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/rfp-simple/RFPSimpleStrategy.sol#L310C1-L380C6)

```solidity
    /// @custom:data when 'useRegistryAnchor' is 'true' -> (address recipientId, uint256 proposalBid, Metadata metadata)
    ///              when 'useRegistryAnchor' is 'false' -> (address recipientAddress, address registryAnchor, uint256 proposalBid, Metadata metadata)
    /// @param _sender The sender of the transaction
    /// @return recipientId The id of the recipient
    function _registerRecipient(bytes memory _data, address _sender)
        internal
        override
        onlyActivePool
        returns (address recipientId)
    {
        bool isUsingRegistryAnchor;
        address recipientAddress;
        address registryAnchor;
        uint256 proposalBid;
        Metadata memory metadata;

        // Decode '_data' depending on the 'useRegistryAnchor' flag
327.--> if (useRegistryAnchor) {
            /// @custom:data when 'true' -> (address recipientId, uint256 proposalBid, Metadata metadata)
329.-->     (recipientId, proposalBid, metadata) = abi.decode(_data, (address, uint256, Metadata)); //@audit this data doesn't include recipientAddress

            // If the sender is not a profile member this will revert
            if (!_isProfileMember(recipientId, _sender)) revert UNAUTHORIZED();
        } else {
            // I skipped this "else" part for brevity. Not important for this submission
        }

        // Check if the metadata is required and if it is, check if it is valid, otherwise revert
        if (metadataRequired && (bytes(metadata.pointer).length == 0 || metadata.protocol == 0)) {
            revert INVALID_METADATA();
        }

        if (proposalBid > maxBid) {
            // If the proposal bid is greater than the max bid this will revert
            revert EXCEEDING_MAX_BID();
        } else if (proposalBid == 0) {
            // If the proposal bid is 0, set it to the max bid
            proposalBid = maxBid;
        }

        // If the recipient address is the zero address this will revert
362.-->    if (recipientAddress == address(0)) revert RECIPIENT_ERROR(recipientId); //@audit it will always revert right here if useRegistryAnchor is true.

        // Get the recipient
        Recipient storage recipient = _recipients[recipientId];

        if (recipient.recipientStatus == Status.None) {
            // If the recipient status is 'None' add the recipient to the '_recipientIds' array
            _recipientIds.push(recipientId);
            emit Registered(recipientId, _data, _sender);
        } else {
            emit UpdatedRegistration(recipientId, _data, _sender);
        }

        // update the recipients data
        recipient.recipientAddress = recipientAddress;
        recipient.useRegistryAnchor = isUsingRegistryAnchor ? true : recipient.useRegistryAnchor;
        recipient.proposalBid = proposalBid;
        recipient.recipientStatus = Status.Pending;
    }
```

* If `useRegistryAnchor` is "true", the function will execute the "*if*" part of the function. At this point, `recipientAddress` is still 0.
    
* The function will continue to execute but it will revert in line 362, since the `recipientAddress` is never decoded and is still 0.

## Impact
The function will always revert if the `useRegistryAnchor` is enabled and no recipient will be registered.

## Code Snippet
https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/rfp-simple/RFPSimpleStrategy.sol#L327C2-L329C100

```solidity
      if (useRegistryAnchor) {
            /// @custom:data when 'true' -> (address recipientId, uint256 proposalBid, Metadata metadata)
            (recipientId, proposalBid, metadata) = abi.decode(_data, (address, uint256, Metadata));
```

https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/rfp-simple/RFPSimpleStrategy.sol#L362
```solidity        
      if (recipientAddress == address(0)) revert RECIPIENT_ERROR(recipientId);
```

## Coded PoC
You can use the protocol's exact test setup to prove this PoC.  
\- Copy the code snippet below and paste it in the `RFPSimpleStrategy.t.sol` test file.  
\- Run `forge test --match-test testRevert_registerRecipient_withUseRegistryAnchor_RECIPIENT_ERROR`

```solidity
    //@audit revert when useRegistryAnchor is true even though the sender is owner or member.
    function testRevert_registerRecipient_withUseRegistryAnchor_RECIPIENT_ERROR() public {
        RFPSimpleStrategy testStrategy = new RFPSimpleStrategy(address(allo()), "RFPSimpleStrategy");
        vm.prank(address(allo()));
        
        // initialize with useRegistryAnchor = true
        testStrategy.initialize(1337, abi.encode(maxBid, true, metadataRequired));

        // Get the registry anchor address and create metadata for function input 
        address anchor = poolProfile_anchor();
        Metadata memory metadata = Metadata({protocol: 1, pointer: "metadata"});

        // Create the custom data for the function
        bytes memory data = abi.encode(anchor, 1e18, metadata);

        // This will revert even though the sender is owner or member.
        // It will revert with RECIPIENT_ERROR.
        vm.expectRevert(abi.encodeWithSelector(RECIPIENT_ERROR.selector, anchor));
        vm.prank(address(allo()));
        testStrategy.registerRecipient(data, pool_admin());
    }
```

The test result after running is below:

```solidity
Running 1 test for test/foundry/strategies/RFPSimpleStrategy.t.sol:RFPSimpleStrategyTest
[PASS] testRevert_registerRecipient_withUseRegistryAnchor_RECIPIENT_ERROR() (gas: 2579906)
Test result: ok. 1 passed; 0 failed; 0 skipped; finished in 9.79ms
 
Ran 1 test suites: 1 tests passed, 0 failed, 0 skipped (1 total tests)
```

## Tool used

Manual Review

## Recommendation
Include `recipientAddress` to the custom data.