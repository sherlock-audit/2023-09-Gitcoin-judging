Furry Cider Panda

high

# Allocator can significantly increase the number of votes of a certain recipient by calling Allo.allocate/batchAllocate multiple times

If the allocator only votes once for a certain recipient, the current implementation of [[QVBaseStrategy._qv_allocate](https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/qv-base/QVBaseStrategy.sol#L517-L530)](https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/qv-base/QVBaseStrategy.sol#L517-L530) is fine. However, if the allocator votes multiple times for a certain recipient, the total number of votes for the recipient can be greatly increased. These increased votes are not equal to the number that should be increased, but far exceed it.

## Vulnerability Detail

```solidity
File: contracts\strategies\qv-base\QVBaseStrategy.sol
506:     function _qv_allocate(
507:         Allocator storage _allocator,
508:         Recipient storage _recipient,
509:         address _recipientId,
510:         uint256 _voiceCreditsToAllocate,
511:         address _sender
512:     ) internal onlyActiveAllocation {
513:         // check the `_voiceCreditsToAllocate` is > 0
514:         if (_voiceCreditsToAllocate == 0) revert INVALID();
515: 
516:         // get the previous values
517:->       uint256 creditsCastToRecipient = _allocator.voiceCreditsCastToRecipient[_recipientId];
518:         uint256 votesCastToRecipient = _allocator.votesCastToRecipient[_recipientId];
519: 
520:         // get the total credits and calculate the vote result
521:->       uint256 totalCredits = _voiceCreditsToAllocate + creditsCastToRecipient;
522:         uint256 voteResult = _sqrt(totalCredits * 1e18);
523: 
524:         // update the values
525:         voteResult -= votesCastToRecipient;
526:         totalRecipientVotes += voteResult;
527:         _recipient.totalVotesReceived += voteResult;
528: 
529:->       _allocator.voiceCreditsCastToRecipient[_recipientId] += totalCredits;
530:         _allocator.votesCastToRecipient[_recipientId] += voteResult;
531: 
532:         // emit the event with the vote results
533:         emit Allocated(_recipientId, voteResult, _sender);
534:     }
```

L517, `creditsCastToRecipient` is the previous credit, which is read from `_allocator.voiceCreditsCastToRecipient[_recipientId]`.

L521, `totalCredits` is equal to the previous credit plus the credits to be allocated.

L529, accumulate `totalCredits` to `_allocator.voiceCreditsCastToRecipient[_recipientId]`. The problem is here, it should be `=` instead of `+=`. **This resulted in the previous credit being increased twice**.

Consider the following scenario:

As a allocator, bob wants to vote for one recipient(B).

1.  bob votes for B with 9 VoiceCredits by calling `Allo.allocate`:
    
    ```flow
    creditsCastToRecipient = _allocator.voiceCreditsCastToRecipient[B] = 0
    totalCredits = 9 + 0 = 9
    B.totalVotesReceived = 3
    _allocator.voiceCreditsCastToRecipient[B] = 9(9+0)
    ```
    
2.  bob votes for B with 16 VoiceCredits by calling `Allo.allocate`:
    
    ```flow
    creditsCastToRecipient = _allocator.voiceCreditsCastToRecipient[B] = 9
    totalCredits = 16 + 9 = 25
    B.totalVotesReceived = 5
    _allocator.voiceCreditsCastToRecipient[B] = 34(9 + 25) //normally this should be 25, but now there are 9 more.
    ```
    
3.  bob votes for B with 15 VoiceCredits by calling `Allo.allocate`:
    
    ```flow
    creditsCastToRecipient = _allocator.voiceCreditsCastToRecipient[B] = 34
    totalCredits = 15 + 34 = 49
    B.totalVotesReceived = 7
    _allocator.voiceCreditsCastToRecipient[B] = 64(49 + 15)
    ```
    

We can see that 7 votes should consume 49 credits, but bob only used `9 + 16 + 15 = 40` credits. So how to maximize `B.totalVotesReceived`? 
Suppose `maxVoiceCreditsPerAllocator` is 100e18,
1.  bob votes for B with 99e18 VoiceCredits by calling `Allo.allocate`.
2.  bob votes for B with 1wei VoiceCredits by calling `Allo.allocate` as many times as possible. 

The more times `Allo.allocate` is called, the larger `B.totalVotesReceived` becomes. The greater the number of votes, the more funds will be distributed.

## Impact

Allocator can significantly increase the number of votes of a certain recipient by calling `Allo.allocate`/`batchAllocate` multiple times, causing the recipient to receive more funds.

## Code Snippet

https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/qv-base/QVBaseStrategy.sol#L517-L530

## Tool used

Manual Review

## Recommendation

```fix
File: contracts\strategies\qv-base\QVBaseStrategy.sol
506:     function _qv_allocate(
......
529:-        _allocator.voiceCreditsCastToRecipient[_recipientId] += totalCredits;
529:+        _allocator.voiceCreditsCastToRecipient[_recipientId] = totalCredits;
530:         _allocator.votesCastToRecipient[_recipientId] += voteResult;
......
534:     }
```