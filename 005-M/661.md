Energetic Berry Llama

high

# `QVBaseStrategy::_qv_allocate()` updates the `allocator.voiceCreditsCastToRecipient` incorrectly, which results in more votes for the recipient
## Summary
`QVBaseStrategy::_qv_allocate()` updates the `allocator.voiceCreditsCastToRecipient` incorrectly. This leads to more voiceCredits for the recipient, which leads to more votes for the recipient and the complete miscalculation of distribution amounts.

## Vulnerability Detail
In quadratic voting, each allocator has a `voiceCredit` and they allocate their credits to the recipients. If an allocator spreads their credits to different recipients, the same amount of credit results in more votes. But if the allocator wants to give all of their credits to only one person, the same amount of credits creates fewer votes. That's the nature of the quadratic voting.

The vote calculations are made with `sqrt` of the `voiceCredit` amounts. Allocators allocate their `voiceCredit`s to a recipient and the recipient vote count is derived from the `voiceCredit`.

Now let's check the `_qv_allocate()` function.  
[https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/qv-base/QVBaseStrategy.sol#L506C1-L534C6](https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/qv-base/QVBaseStrategy.sol#L506C1-L534C6)

```solidity
    function _qv_allocate(
        Allocator storage _allocator,
        Recipient storage _recipient,
        address _recipientId,
        uint256 _voiceCreditsToAllocate,
        address _sender
    ) internal onlyActiveAllocation {
        // check the `_voiceCreditsToAllocate` is > 0
        if (_voiceCreditsToAllocate == 0) revert INVALID();

        // get the previous values
        uint256 creditsCastToRecipient = _allocator.voiceCreditsCastToRecipient[_recipientId];
        uint256 votesCastToRecipient = _allocator.votesCastToRecipient[_recipientId];

        // get the total credits and calculate the vote result
        uint256 totalCredits = _voiceCreditsToAllocate + creditsCastToRecipient;
        uint256 voteResult = _sqrt(totalCredits * 1e18);

        // update the values
        voteResult -= votesCastToRecipient;
        totalRecipientVotes += voteResult;
        _recipient.totalVotesReceived += voteResult;

-->     _allocator.voiceCreditsCastToRecipient[_recipientId] += totalCredits; //@audit creditsCastToRecipient is added twice in here. 'totalCredits' alredy includes creditsCastToRecipient, it is never deducted. 
        _allocator.votesCastToRecipient[_recipientId] += voteResult; //@audit this one is true because previos vote count (votesCastToRecipient) is deducted from the 'voteResult'

        // emit the event with the vote results
        emit Allocated(_recipientId, voteResult, _sender);
    }
```

The function:

1. Gets the already cast voice credits and vote counts up to this point.
    
2. Adds the additional voice credit to the previous one to get `totalCredits`
    
3. Calculates the corresponding vote count to the `totalCredits`.
    
4. Deducts the previous vote count from the `voteResult` to find the exact additional vote count.
    
5. Updates the state.
    

The reason for the vulnerability is that the state update for the `allocator.voiceCreditsCastToRecipient` is incorrect. It adds the `totalCredits` to the `allocator.voiceCreditsCastToRecipient` but the `totalCredits` already includes `voiceCreditsCastToRecipient`. Voice credit amounts are never deducted as the vote counts did.

## Coded PoC

Note: I added a simple view function to make the testing easier. It makes working with the foundry much simpler instead of trying to manage the tupples. You can see the additional function here.

```solidity
    // Function to make testing easier
    function getAllocatorCastedPerRecipient(address _allocator, address _recipient) external view returns (uint256) {
        Allocator storage allocator = allocators[_allocator];
        uint256 x = allocator.voiceCreditsCastToRecipient[_recipient];
        return x;
    } 
```

You can use the protocol's own setup to prove the PoC below  
\- Copy the snippet below and paste it into the `QVSimpleStrategy.t.sol` test file.  
\- Run with `forge test --match-test test_allocate_voiceCreditsCastToRecipient_Calculation_Wrong`

```solidity
//@audit-issue - Test that every allocation will increase voice count exponantially
    function test_allocate_voiceCreditsCastToRecipient_Calculation_Wrong() public {
        address recipientId = __register_accept_recipient();
        address allocator = randomAddress();

        vm.startPrank(pool_manager2());
        
        // Add the allocator
        qvSimpleStrategy().addAllocator(allocator);
        
        // Start allocation period
        vm.warp(allocationStartTime + 10);

        // Create allocation data with 10 voice credits.
        bytes memory allocateData = __generateAllocation(recipientId, 10);

        vm.stopPrank();
        vm.startPrank(address(allo()));

        // I added a view function to make test easier. getAllocatorCastedPerRecipient() is a view function for the test purpose only.
        // Allocate 10 voice credits for the first time and see voiceCreditsCastToRecipient is also 10.
        qvSimpleStrategy().allocate(allocateData, allocator);
        assertEq(qvSimpleStrategy().getAllocatorCastedPerRecipient(allocator, recipientId), 10);

        // Allocate 10 more. But voiceCreditsCastToRecipient is now 30
        qvSimpleStrategy().allocate(allocateData, allocator);
        assertEq(qvSimpleStrategy().getAllocatorCastedPerRecipient(allocator, recipientId), 30);

        // Allocate 10 more. Now voiceCreditsCastToRecipient is going to be 70
        qvSimpleStrategy().allocate(allocateData, allocator);
        assertEq(qvSimpleStrategy().getAllocatorCastedPerRecipient(allocator, recipientId), 70);

        // Normally, allocating to the same recipient should decrease the vote counts in quadratic voting.
        // But because of this issue, allocating to the same recipient with small amounts will extremely increase the vote count.
    }
```

You can find the test results below:

```solidity
Running 1 test for test/foundry/strategies/QVSimpleStrategy.t.sol:QVSimpleStrategyTest
[PASS] test_allocate_voiceCreditsCastToRecipient_Calculation_Wrong() (gas: 337382)
Test result: ok. 1 passed; 0 failed; 0 skipped; finished in 11.83ms
 
Ran 1 test suites: 1 tests passed, 0 failed, 0 skipped (1 total tests)
```

## Impact
Vote calculation is wrong. Normally, allocating to the same recipient should decrease the vote counts in quadratic voting. But because of this issue, allocating to the same recipient with small credits will extremely increase the vote count, and break the whole system.

## Code Snippet
https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/qv-base/QVBaseStrategy.sol#L529

```solidity
        _allocator.voiceCreditsCastToRecipient[_recipientId] += totalCredits;
```

## Tool used

Manual Review

## Recommendation
Change this:
```solidity
        _allocator.voiceCreditsCastToRecipient[_recipientId] += totalCredits;
```

To this:
```solidity
        _allocator.voiceCreditsCastToRecipient[_recipientId] = totalCredits;
```
