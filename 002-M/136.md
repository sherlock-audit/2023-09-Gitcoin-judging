Rapid Lead Cricket

medium

# FEE-ON-TRANSFER usage could DOS a pool
According to sponsor answer regarding sherlock doc : 
```solidity
Are there any FEE-ON-TRANSFER tokens interacting with the smart contracts?
Yes. When funding a pool on Allo.sol
```
However variables used for accounting on Allo protocol does not used ballanceBefore and balanceAfter pattern so if fee-on-transfer token would be used it will break the account of the protocol.

## Vulnerability Detail

In the codebase, the usage of `safeTransfer` and `safeTransferFrom` assume that the receiver receives the exact transferred amount, specifically when pool creator is funding a pool only treasury fees are decremented : 

Allo.sol
```solidity
     /// @notice Fund a pool.
    /// @dev Deducts the fee and transfers the amount to the distribution strategy.
    function _fundPool(uint256 _amount, uint256 _poolId, IStrategy _strategy) internal {
        uint256 feeAmount;
        uint256 amountAfterFee = _amount;

        Pool storage pool = pools[_poolId];
        address _token = pool.token;

        if (percentFee > 0) {
            //E _amount * percentFee / 1e18
            //E ex : amount * 10 / 100 = 0.1*amount
            feeAmount = (_amount * percentFee) / getFeeDenominator();
            amountAfterFee -= feeAmount;

            _transferAmountFrom(_token, TransferData({from: msg.sender, to: treasury, amount: feeAmount}));
        }

        _transferAmountFrom(_token, TransferData({from: msg.sender, to: address(_strategy), amount: amountAfterFee}));
        _strategy.increasePoolAmount(amountAfterFee);

        emit PoolFunded(_poolId, amountAfterFee, feeAmount);
    }
```
Transfer.sol : 
```solidity
    function _transferAmountFrom(address _token, TransferData memory _transferData) internal returns (bool) {
        uint256 amount = _transferData.amount;
        if (_token == NATIVE) {
            // Native Token
            if (msg.value < amount) revert AMOUNT_MISMATCH();

            SafeTransferLib.safeTransferETH(_transferData.to, amount);
        } else {
            SafeTransferLib.safeTransferFrom(_token, _transferData.from, _transferData.to, amount);
        }
        return true;
    }
```

BaseStrategy.sol : 
```solidity
    function increasePoolAmount(uint256 _amount) external override onlyAllo {
        _beforeIncreasePoolAmount(_amount); //E hook 
        poolAmount += _amount;
        _afterIncreasePoolAmount(_amount); //E hook 
    }
```
As we can see fees that should be taken by the protocol are decremented but `poolAmount` is incremented by amount on `increasePoolAmount` function , not taking any fee-on-transfer that could be applied.
According to `[this link](https://github.com/d-xo/weird-erc20#fee-on-transfer)` some tokens take a transfer fee (e.g. STA, PAXG), some do not currently charge a fee but may do so in the future (e.g. USDT, USDC).
So to take a concrete example , if the token charge transfer fee, the payment amount is 100 ETH(after protocol fees deducted)  and 1 ETH is charged as fee during transfer, strategy contract will only receive 99 ETH, but the wrong value payment 100 ETH is used to update the accounting `poolAmount`.
For now it's not a big problem but when distribution happen, there will be a problem as `_distribute()` function rely directly on `poolAmount` to send tokens to recipients that deserve it , for example in QVBaseStrategy : 
```solidity
function _distribute(address[] memory _recipientIds, bytes memory, address _sender)
        internal
        virtual
        override
        onlyPoolManager(_sender)
        onlyAfterAllocation //E if (block.timestamp < allocationEndTime) revert
    {
        uint256 payoutLength = _recipientIds.length;
        for (uint256 i; i < payoutLength;) {
            address recipientId = _recipientIds[i];
            //E mapping address => Recipient Struct
            Recipient storage recipient = recipients[recipientId];

            //E return PayoutSummary(recipient.recipientAddress, amount);
            PayoutSummary memory payout = _getPayout(recipientId, "");
            //E amount = poolAmount * recipient.totalVotesReceived / totalRecipientVotes
            uint256 amount = payout.amount;

            //E if alreadyPaid or not accepted or amount == 0 => revert
            //E accepted if recipients[_recipientId].recipientStatus == Status.Accepted;
            if (paidOut[recipientId] || !_isAcceptedRecipient(recipientId) || amount == 0) {
                revert RECIPIENT_ERROR(recipientId);
            }

            IAllo.Pool memory pool = allo.getPool(poolId);
            _transferAmount(pool.token, recipient.recipientAddress, amount);

            paidOut[recipientId] = true;

            emit Distributed(recipientId, recipient.recipientAddress, amount, _sender);
            unchecked {
                ++i;
            }
        }
    }
```
If `poolAmount` does not represent real tokens on the strategy contract, we can easily imagine that first distributions will work until there is no more token on the contract to send regarding calculation of rewarded amount.

**The same is true for all merkle,RFP and QV strategy.**

## Impact

Distribution will be DOS and recipient won't get there rewarded tokens or will get less than they should get until there is no more funds to distribute last recipients who will see there distribution locked on the strategy.

## Code Snippet

https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/core/Allo.sol#L517
https://github.com/sherlock-audit/2023-09-Gitcoin/blob/main/allo-v2/contracts/strategies/BaseStrategy.sol#L155

## Tool used

Manual Review

## Recommendation

If you want to use fee on transfer token, use the `amountBefore`/`amountAfter` pattern matching for your accounting strategy